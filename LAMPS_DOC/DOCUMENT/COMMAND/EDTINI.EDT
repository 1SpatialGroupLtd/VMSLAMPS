! EDT startup file for runoff - intended for use in DOC's command file
! environment
!
! define a macro to insert contents requirement
!
FIND =CON
I;I;.!**
I;I;.!** include a contents page
I;I;.!**
I;I;.PAGE
I;I;.SAVE
I;I;.REQUIRE  "filename.RNT"
I;I;.RESTORE
I;I;.!**
I;s/filename// .-3
DEF MACRO CON
!
! and another for the index
!
FIND =IND
I;I;.!**
I;I;.!** include an index
I;I;.!**
I;I;.PAGE
I;I;.REQUIRE  "filename.RNX"
I;I;.!**
I;s/filename// .-2
DEF MACRO IND
!
! define macros for handling the different header levels
!
FIND =HL1
I;I;
I;I;.TP 10
I;I;.P 0
I;I;^*--------------------------------------------------------------------------------\*
I;I;.HL 1 ^^^*header\*
I;I;
I;I;.LM +10
I;I;
I;I;.P 0
I;s/header// .-5
DEF MACRO HL1
!
FIND =HL2
I;I;
I;I;.TP 10
I;I;.P 0
I;I;^*----------------------------------------------------------------------\*
I;I;.HL 2 ^^^*header\*
I;I;
I;I;.P 0
I;s/header// .-3
DEF MACRO HL2
!
FIND =HL3
I;I;
I;I;.TP 10
I;I;.BLANK
I;I;.HL 3 ^^^*header\*
I;I;
I;I;.P 0
I;s/header// .-3
DEF MACRO HL3
!
FIND =ENDHL1
I;I;
I;I;.LM -10
DEF MACRO ENDHL1
!
FIND =MAIN				! back to main filespace
!
SET NONUMBERS				! no line numbering
SET SCREEN 80				! full screen line width
SET NOTRUNCATE				! wrap lines
SET WRAP 78				! automatically split lines
!
! Normal punctuation is not defined as a word delimitor
!
SET ENTITY WORD " 	
=+-*/@#$%_\/"
!
! define the keys F17 through F20 to be header level controls
!
DEFINE KEY FUNCTION 31 AS "EXT HL1."	! F17
DEFINE KEY FUNCTION 32 AS "EXT HL2."	! F18
DEFINE KEY FUNCTION 33 AS "EXT HL3."	! F19
DEFINE KEY FUNCTION 34 AS "EXT ENDHL1."	! F20
!
SET MODE CHANGE				! goto screen mode
