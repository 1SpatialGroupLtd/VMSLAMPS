	LOGICAL FUNCTION HEIGHT_POINTS(
     &	NOD_I,NOD_R,NOD_L,ARM_I,ARM_L,LNK_I,LNK_R,LNK_L,PUP_I,
     &	PDN_I,NET_I,STN_I,STN_R,STL_I,STL_L,MNX_I,MNX_R,PNT_I,PNT_R,
     &	MAX_PNTS,MAX_NODS,MAX_ARMS,MAX_LNKS,MAX_STNS,MAX_STLS)
C
***	MODULE	HEIGHT_POINTS
***	IDENT	13FE89
C	
C Copyright Laser Scan Laboratories, Cambridge, England.
C 	
C Created						ACM 12-Jan-1989
C
C Description
C	Interpolates a height for every river/ridgeline point.
C
	IMPLICIT NONE
C	
C Parameters
        INCLUDE 'LSL$CMNI3D:CONSTANTS.PAR'
        INCLUDE 'LSL$CMNI3D:I3DMSG.PAR'
C
C Common blocks
	INCLUDE 'LSL$CMNI3D:CONTROL.CMN'
C
C Include CB structure file so as to have assess to IFF_ABSENT parameter
	INCLUDE 'LSL$CMNIFF:CB.STR'
C
C Dynamically allocated array declarations
	INCLUDE 'LSL$CMNI3D:VM_ARG_DECL.SRC'
C 
C Workspace
	INTEGER		ILNK,INOD,IPNT		! LNK,NOD,PNT indices
	INTEGER		STA_PNT,FIN_PNT		! link start/fin pnts
	INTEGER		IPNT1,IPNT2		! consec heighted pnts
	REAL		L			! len from 1st htd pt
	REAL		LEN			! len betw consc htd pts
	REAL		HT1,HT2			! consec hts along link
C
C **********************************************************************
C
	HEIGHT_POINTS = .FALSE.
C
C Ensure that all link ends have height = corresponding node height, and
C that all nodes are heighted.
C
	DO 10 ILNK = 1,NUM_LNKS
C	   IF (LNK_L(LNK_DEL,ILNK)) GOTO 10
C
	   INOD = LNK_I(LNK_ND1,ILNK)
	   IF (NOD_I(NOD_ZZZ,INOD).EQ.IFF_ABSENT) GOTO 999
	   IPNT = LNK_I(LNK_STA,ILNK)
	   PNT_R(PNT_ZZZ,IPNT) = NOD_R(NOD_ZZZ,INOD)
C
	   INOD = LNK_I(LNK_ND2,ILNK)
	   IF (NOD_I(NOD_ZZZ,INOD).EQ.IFF_ABSENT) GOTO 999
	   IPNT = LNK_I(LNK_FIN,ILNK)
	   PNT_R(PNT_ZZZ,IPNT) = NOD_R(NOD_ZZZ,INOD)
10	CONTINUE
C
C Interpolate between heighted points along each link
C
	DO 50 ILNK = 1,NUM_LNKS			! for each link
C	   IF (LNK_L(LNK_DEL,ILNK)) GOTO 50
	   STA_PNT = LNK_I(LNK_STA,ILNK)	! first point in link
	   FIN_PNT = LNK_I(LNK_FIN,ILNK)	! last point in link
	   HT2 = PNT_R(PNT_ZZZ,STA_PNT)
	   IPNT2 = STA_PNT
C
C Start here to linear interpolate each section between heighted points
C
C Find length between consecutive heighted points
C
20	   IF (IPNT2.EQ.FIN_PNT) GOTO 50	! at last point yet?
	   HT1 = HT2
	   IPNT1 = IPNT2
C
	   IPNT = IPNT1
	   LEN = 0
C
30	   IF (IPNT.EQ.FIN_PNT) GOTO 50		! at last point yet?
	   IPNT = PNT_I(PNT_NXT,IPNT)		! next point along link
	   LEN = LEN + PNT_R(PNT_LEN,IPNT)
	   IF (PNT_I(PNT_ZZZ,IPNT).EQ.IFF_ABSENT) GOTO 30
C
	   HT2 = PNT_R(PNT_ZZZ,IPNT)
	   IPNT2 = IPNT
C
C Interpolate between consecutive heighted points
C
	   IPNT = IPNT1
	   L = 0
C
40	   IPNT = PNT_I(PNT_NXT,IPNT)
	   IF (IPNT.EQ.IPNT2) GOTO 20		! end of section?
	   L = L + PNT_R(PNT_LEN,IPNT)
	   PNT_R(PNT_ZZZ,IPNT) = (L*HT2 + (LEN-L)*HT1)/LEN
	   GOTO 40	   
C
50	CONTINUE
C
	HEIGHT_POINTS = .TRUE.
	GOTO 1000
C
999	CALL LSL_PUTMSG(I3D__INTRNLERR,1,'HEIGHT_POINTS')
C
1000	RETURN
	END
