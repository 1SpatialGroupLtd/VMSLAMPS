	LOGICAL FUNCTION FILEOUT_CMD()
C
***	MODULE	FILEOUT_CMD
***	IDENT	26FE91
C
C	Copyright (C), Laser-Scan Laboratories Ltd., Cambridge, England
C
C Created by					ALB 28-Apr-1988
C
C Function to get output file-spec from FILEOUT command. If argument is
C missing the user is prompted.  
C
C Function returns .TRUE. if all is well, otherwise .FALSE.
C
	IMPLICIT NONE
C
C Parameters :
	INCLUDE 'LSL$DTMCREATESYS:MESPAR.PAR'
C
C Common:
	INCLUDE	'LSL$CMNLSL:TXTC.CMN'
	INCLUDE 'LSL$CMNI3D:CONTROL.CMN'
	INCLUDE 'LSL$CMNIFF:IFFHAN.CMN'			! IFF common
C
C Workspace
C
	BYTE		CH		! a character
	LOGICAL		BLANK		! blank line?
	LOGICAL		OK		! for error returns
	LOGICAL		IERR		! for error returns
	INTEGER		CUR_IFF		! current IFF file number being opened
	INTEGER		LUN		! LUN for input IFF file 
C
C Functions
C
	LOGICAL		GETFILNAM ! get file-spec
	LOGICAL		RDCHS	! read char. skipping spaces
	LOGICAL		TTRSTR	! read from TT:
C
C ......................................................................
C
	FILEOUT_CMD=.TRUE.		! assume success
C
C ......................................................................
C Check whether the maximum number of output IFF files have already been opened
C
	IF (NUM_FILEOUT.GE.MAX_FILEOUT) THEN
	   CALL WRITEF('%S Specifying command FILEOUT',ERROR)
	   CALL WRITEF('Maximum of %I0 files already opened.',
     &		MAX_FILEOUT)
	   FILEOUT_CMD = .FALSE.
	   GOTO 999
	ENDIF
C
C Look for filename, taking appropriate action if none found
C
	BLANK=RDCHS(CH)			! empty line ...
	IF (BLANK.OR.CH.EQ.'!') THEN	! or comment?
C
C Display warning/error for no filename present
C
	   IF (TTIN) THEN
	      CALL WRITEF('%S You must specify a file-spec argument'//
     &		' to the FILEOUT command',WARNING)
	   ELSE
	      CALL WRITEF('%S You must specify a file-spec argument'//
     &		' to the FILEOUT command',ERROR)
	   ENDIF	   
C
C If terminal input, prompt for filename
C
	   IF (TTIN) THEN
	      CALL WRITEF('For example FILEOUT SW555_MOD.IFF')
	      OK=TTRSTR(,,'Read from FILEOUT file? ',IERR)
	      IF (OK) THEN
	         CALL BSLN			! reset decode ptr
	      ELSE
	         CALL LSL_PUTMSG(OK)		! problem with input
	         CALL LSL_ADDMSG(IERR)
	         FILEOUT_CMD=.FALSE.
	         GOTO 999
	      ENDIF
	      IF (TXTPTR.EQ.0) THEN
	   	 FILEOUT_CMD=.FALSE.
		 GOTO 999		! empty line
	      ENDIF
	   ELSE
	      GOTO 999  ! someone got their indirect file wrong !!
	   ENDIF
	ENDIF
C
C ......................................................................
C Try interpreting the file-spec
C
	CUR_IFF = NUM_FILEOUT + 1
	CALL BSCH				! move back one chr
	OK=  GETFILNAM(	FILEOUT_FNAME(CUR_IFF),	! filename
     &			FILEOUT_LEN(CUR_IFF),	! filename length
     &			'LSL$IF:IFF.IFF',	! default
     &			.TRUE.,			! allow version numbers
     &			%REF('!') )		! termination character
C
C Deal with any error in interpreting file-spec
C
	IF (.NOT.OK) THEN
	   CALL WRITEF('%S Unable to interpret output file-spec',ERROR)
	   CALL LSL_PUTMSG(OK)
	   FILEOUT_CMD=.FALSE.
	   GOTO 999
	ENDIF
	NUM_FILEOUT = NUM_FILEOUT + 1
	HAD_FILEOUT = .TRUE.	! got a output IFF file (remember)
C
999	CONTINUE
C
	RETURN
	END
C
