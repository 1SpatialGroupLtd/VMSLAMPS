C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1989-02-15 20:38:48.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION INTERPOLATE_NODE_HEIGHTS(
     &		UP_DIST,DN_DIST,UP_HT,DN_HT,
     &		N_UP_LINKS,N_DN_LINKS,
     &	NOD_I,NOD_R,NOD_L,ARM_I,ARM_L,LNK_I,LNK_R,LNK_L,PUP_I,
     &	PDN_I,NET_I,STN_I,STN_R,STL_I,STL_L,MNX_I,MNX_R,PNT_I,PNT_R,
     &	MAX_PNTS,MAX_NODS,MAX_ARMS,MAX_LNKS,MAX_STNS,MAX_STLS)
C
***	MODULE	INTERPOLATE_NODE_HEIGHTS
***	IDENT	13FE89
C
C Copyright Laser Scan Laboratories, Cambridge, England.
C 
C Created						ACM 08-Jan-1989
C
C Description
C	Interpolates a height for each intermediate node along path
C	given by links up and down stream from node at start of paths
C	in PUP_I and PDN_I.
C
C Algorithm
C	Linear interpolation along path length.
C
C ----------------------------------------------------------------------
	IMPLICIT NONE
C
C Parameters
	INCLUDE 'LSL$CMNI3D:CONSTANTS.PAR'
	INCLUDE 'LSL$CMNI3D:I3DMSG.PAR'
C
C Common blocks
	INCLUDE 'LSL$CMNI3D:CONTROL.CMN'
C
C Data in
	REAL		UP_HT,DN_HT		! heights found
	REAL		UP_DIST,DN_DIST		! path dist to hts found
	INTEGER		N_UP_LINKS		! num links upstream
	INTEGER		N_DN_LINKS		! num links downstream
C
C Dynamically assigned array argument declarations
	INCLUDE 'LSL$CMNI3D:VM_ARG_DECL.SRC'
C
C Workspace
	INTEGER		INOD,ILNK,JLNK,IPTH	! array indices
	INTEGER		N_LINKS		! num links in up/down path
	INTEGER		FLOW_CODE	! flow code
	REAL		UP_WT,DN_WT	! up/down height weights
	REAL		DIST		! dist along path from root node
	REAL		REST		! rest of dist along path
	REAL		RT_HT		! root node height
C
C ----------------------------------------------------------------------
C
	INTERPOLATE_NODE_HEIGHTS = .FALSE.
C
C First interpolate a height for node at start of up & down paths
C
	IF (UP_DIST.LT.COORD_TOL.OR.DN_DIST.LT.COORD_TOL) THEN
	   CALL LSL_PUTMSG(I3D__INTRNLERR,1,'INTERPOLATE_NODE_HEIGHTS')
	   GOTO 999
	ENDIF
	UP_WT = 1/UP_DIST
	DN_WT = 1/DN_DIST
	RT_HT = (UP_WT*UP_HT+DN_WT*DN_HT)/(UP_WT+DN_WT)
	ILNK = PUP_I(PUP_LNK,1)
	JLNK = PDN_I(PUP_LNK,1)
	IF (LNK_I(LNK_ND1,ILNK).EQ.LNK_I(LNK_ND1,JLNK).OR.
     &	    LNK_I(LNK_ND1,ILNK).EQ.LNK_I(LNK_ND2,JLNK)) THEN
	   INOD = LNK_I(LNK_ND1,ILNK)
	ELSE
	   INOD = LNK_I(LNK_ND2,ILNK)
	ENDIF
	NOD_R(NOD_ZZZ,INOD) = RT_HT
C
C Interpolate a height for other nodes along up path
C
	FLOW_CODE = UPHILL
	N_LINKS = N_UP_LINKS
	GOTO 20
C
10	FLOW_CODE = DOWNHILL
	N_LINKS = N_DN_LINKS
C
C Height nodes at far end of all links but last (because a point must be
C heighted before the farthest point along last link, otherwise it could
C not be the last link)
C
20	DIST = 0				! zero dist from root
	DO 30 IPTH = 1,N_LINKS-1
	   IF (FLOW_CODE.EQ.UPHILL) THEN
	      ILNK = PUP_I(PUP_LNK,IPTH)
	   ELSE
	      ILNK = PDN_I(PDN_LNK,IPTH)
	   ENDIF
	   IF (LNK_I(LNK_ND1,ILNK).EQ.INOD) THEN
	      INOD = LNK_I(LNK_ND2,ILNK)
	   ELSE
	      INOD = LNK_I(LNK_ND1,ILNK)
	   ENDIF
	   DIST = DIST+LNK_R(LNK_LEN,ILNK)
	   IF (FLOW_CODE.EQ.UPHILL) THEN
	      REST = UP_DIST-DIST
	      NOD_R(NOD_ZZZ,INOD) = (DIST*UP_HT+REST*RT_HT)/UP_DIST
	   ELSE
	      REST = DN_DIST-DIST
	      NOD_R(NOD_ZZZ,INOD) = (DIST*DN_HT+REST*RT_HT)/DN_DIST
	   ENDIF
30	CONTINUE
C
	IF (FLOW_CODE.EQ.UPHILL) GOTO 10
C
	INTERPOLATE_NODE_HEIGHTS = .TRUE.
999	RETURN
	END
