$!
$! * This file is part of the LAMPS distribution, released as a software
$! * preservation project to archive digital history for future historians.
$! * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
$! *
$! * Timestamp of this file for the 2002 release was: 1990-08-03 12:57:24.000000000 +0100
$! *
$! * This program is free software: you can redistribute it and/or modify
$! * it under the terms of the GNU General Public License as published by
$! * the Free Software Foundation, version 3.
$! *
$! * This program is distributed in the hope that it will be useful, but
$! * WITHOUT ANY WARRANTY; without even the implied warranty of
$! * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
$! * General Public License for more details.
$! *
$! * You should have received a copy of the GNU General Public License
$! * along with this program. If not, see http://www.gnu.org/licenses/.
$!
$! LSLSEARCHLISTS_GENERATE.COM - generate standard LSL searchlists
$!
$! Copyright Laser-Scan Ltd, Cambridge, England
$! version for LSLINSTALL				PGH	03-Aug-1990
$!
$! Scans LSL$PUBLIC_ROOT and builds up a file called LSDEFNS_SEARCHLISTS.NEW,
$! containing definitions of LSL$EXE, LSL$COM, etc. This is then renamed
$! as LSDEFNS_SEARCHLISTS.COM which is called at startup time from LSDEFNS.COM.
$!
$	ws="write sys$output"
$	wc="write command"
$	ws "LSL$SEARCHLISTS_GENERATE.COM (V2.0) - generate file of searchlist definitions"
$	ws " "
$	ws "Creating LSDEFNS_SEARCHLISTS.COM in SYS$COMMON:[SYSMGR]"
$	ws " "
$	create sys$common:[sysmgr]lsdefns_searchlists.new ! so that it is in standard text file format
$	open/append command sys$common:[sysmgr]lsdefns_searchlists.new
$!
$	ws "$! LSDEFNS_SEARCHLISTS.COM - define LSL searchlists."
$	wc "$! LSDEFNS_SEARCHLISTS.COM - define LSL searchlists."
$	ws "$!"
$	wc "$!"
$	ws "$! WARNING - This file is automatically generated by a command procedure"
$	wc "$! WARNING - This file is automatically generated by a command procedure"
$	ws "$! Do not edit it. Instead go @LSL$COM:LSLSEARCHLISTS_GENERATE.COM."
$	wc "$! Do not edit it. Instead go @LSL$COM:LSLSEARCHLISTS_GENERATE.COM."
$	ws "$!"
$	wc "$!"
$!
$	directory_type="COM"
$	logical_name="COM"
$	site_dir="[LSL.COM]"
$	gosub search_for_directories
$	site_dir="[LSL.EXE]"
$	directory_type="EXE"
$	logical_name="EXE"
$	gosub search_for_directories
$	site_dir="[LSL.LIB]"
$	directory_type="LIB"
$	logical_name="LIBRARY"
$	gosub search_for_directories
$	site_dir="[LSL.HELP]"
$	directory_type="HELP"
$	logical_name="HELP"
$	gosub search_for_directories
$	site_dir="[LSL.LOOKUP]"
$	directory_type="LOOKUP"
$	logical_name="LOOKUP"
$	gosub search_for_directories
$	site_dir="[LSL.UIL]"
$	directory_type="UIL"
$	logical_name="UIL"
$	gosub search_for_directories
$	site_dir="[LSL.UID]"
$	directory_type="UID"
$	logical_name="UID"
$	gosub search_for_directories
$	site_dir="[LSL.CDL]"
$	directory_type="CDL"
$	logical_name="CDL"
$	gosub search_for_directories
$	site_dir="[LITES2.CMD]"
$	directory_type="CMD"
$	logical_name="LITES2CMD"
$	gosub search_for_directories
$!
$	ws "$!"
$	wc "$!"
$	ws "$	EXIT"
$	wc "$	EXIT"
$	close command
$	purge/noconfirm sys$common:[sysmgr]lsdefns_searchlists.new
$	rename/noconfirm sys$common:[sysmgr]lsdefns_searchlists.new -
			 sys$common:[sysmgr].com
$	exit
$!
$! subroutine to generate search list for a particular directory type
$!
$search_for_directories:
$	ws "$!"
$	wc "$!"
$	ws "$! Set up search list for public tree .''directory_type directories"
$	wc "$! Set up search list for public tree .''directory_type directories"
$	ws "$!"
$	wc "$!"
$	ws "$	Define/System	LSL$''logical_name'	-"
$	wc "$	Define/System	LSL$''logical_name'	-"
$	ws "		LSL$SITE_ROOT:''site_dir',-"
$	wc "		LSL$SITE_ROOT:''site_dir',-"
$	ws "		LSL$''logical_name'_PUBLIC"
$	wc "		LSL$''logical_name'_PUBLIC"
$	ws "$!"
$	wc "$!"
$	ws "$	Define/System	LSL$''logical_name'_PUBLIC	-"
$	wc "$	Define/System	LSL$''logical_name'_PUBLIC	-"
$	directory/out=lsdefns_searchlists.tmp/col=1/nohead/notrail -
		lsl$public_root:[*]'directory_type'.dir
$	open/read listing lsdefns_searchlists.tmp
$	read/end=none listing line
$	goto alter
$next_line:
$	read/end=no_more listing line
$	previous_line=previous_line+",-"
$	ws previous_line
$	wc previous_line
$alter:	line=line-"]''directory_type'.DIR;1"
$	line="		"+line+".''directory_type']"
$	previous_line=line
$	goto next_line
$no_more:
$	ws previous_line
$	wc previous_line
$	goto done
$none:
$	ws "		NL:"		! if no such directories
$	wc "		NL:"		! if no such directories
$done:
$	close listing
$	delete lsdefns_searchlists.tmp;*
$	return
