	.TITLE DPSDUMVEC
	.IDENT /20SE02/
; 
; Copyright Laser Scan Ltd., Cambridge, England
; Author Paul Hardy				PGH	20-Sep-2002
; Modified for Alpha (and warning)		PGH	17-Oct-2002
;
; DPS dummy transfer vector
;
; to satisfy dangling references left when Compaq removed the
; Display PostScript client libraries.

	.LIBRARY	/LSL$LIBRARY:LSLMACLIB/


	.PSECT		TRANSFER,EXE,NOWRT

; macro to define a numbered entry in a transfer vector

	.MACRO		DUMVEC,DUMNUM
	.TRANSFER	DPSDUM'DUMNUM
	.MASK		DPSDUM'DUMNUM
	JMP		L^DPSDUM'DUMNUM+2	; to actual routine
	.ENDM

; macro to define a numbered entry point dummy

	.MACRO		DUMENT,DUMNUM
	.ENTRY		DPSDUM'DUMNUM, ^M<R2>
	JMP		DPSUNS			; join common code
	.ENDM

; define a transver vector with 1000 entries

INDEX=0
	.REPEAT		1000
INDEX=INDEX+1
	DUMVEC		\INDEX
	.ENDR

; now define 1000 dummy routines

	CODE_PSECT	DPSDUMMY

INDEX=0
	.REPEAT		1000
INDEX=INDEX+1
	DUMENT		\INDEX
	.ENDR

; common code to signal error as Unsupported

DPSUNS:	PUSHL		#3658			; SS$_UNSUPPORTED
	CALLS		#1,G^LIB$SIGNAL
	RET

; now some empty space to pad out, to satisfy image activator.

	.BLKB	^X100000

	.END
