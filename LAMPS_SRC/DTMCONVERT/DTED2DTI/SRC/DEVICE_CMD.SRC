	LOGICAL FUNCTION DEVICE_CMD(IS_MOUNTED)
C
***	MODULE	DEVICE_CMD
***	IDENT	21JN88
C
C	Copyright (C), Laser-Scan Laboratories Ltd., Cambridge, England
C
C	Include MESPAR.PAR from 'LSL$DTMCONVERTSYS' now.FJB 21-Jun-1988
C	Created						FJB 15-Feb-1988
C
C Function to get input device name from DEVICE command. If argument is
C missing the user is prompted. When argument read, input device is
C checked for existence and the tapedeck opened for reading.
C
C Function returns .TRUE. if all is well, otherwise .FALSE.
C
	IMPLICIT NONE
C
C Parameters:
	INCLUDE 'LSL$CMNLSL:READSTR.PAR'
C                                                                     
C Common:
	INCLUDE	'LSL$CMNLSL:FILENAME.CMN'
	INCLUDE 'LSL$CMNLSL:MTIVCM.CMN'
C
C For messages:
	INCLUDE	'LSL$CMNLSL:TXTC.CMN'
     	INCLUDE 'LSL$CMNLSL:LSLLIBMSG.PAR'
	INCLUDE 'LSL$DTMCONVERTSYS:MESPAR.PAR'
C
C
	INCLUDE 'LSL$DTMCONVERTSYS:BLOCKY.CMN'
	INCLUDE 'LSL$CMNDTED2DTI:OPTIONS.CMN'                 
C
C W/s
	BYTE		CH	! a character
	INTEGER*4	IERR		! for error return	
	INTEGER*4	OK		! for error return	
	LOGICAL		DEVMNT_OK	! check tape mounted ok
C
	LOGICAL		IS_MOUNTED     	! no fatal tape errors
C	
C Functions
	LOGICAL		RDCHS	! read char. skipping spaces
	LOGICAL	  	TTRSTR	! read from TT:
	INTEGER*4	READSTR		! read string          
	INTEGER*4	LIB$GETDVI      ! device details
C
C
C System parameters
	INCLUDE		'($DCDEF)/NOLIST'
	INCLUDE 	'($DVIDEF)/NOLIST'
	INCLUDE		'($MTDEF)/NOLIST'
	INCLUDE 	'($SSDEF)/NOLIST'
C
C
C
C
C ......................................................................
C
	DEVICE_CMD = .TRUE.		! assume success
C
C ......................................................................
C
C If the user hasn't given a device name prompt for one
	IF (RDCHS(CH)) THEN			! empty line
	   IF (TTIN) THEN
	      CALL WRITEF('%S You must specify a tapedeck name argument'//
     &		' to the DEVICE command',WARNING)
	   ELSE
	      CALL WRITEF('%S You must specify a tapedeck name argument'//
     &		'to the DEVICE command',ERROR)          
	   ENDIF	   
	   CALL WRITEF('For example DEVICE MSA0:')
C
C Go back for another line
	   GOTO 10  
	ENDIF
C
C Read the name
	CALL BSCH
	DEVLEN = READSTR(DEVICE,,ON_EOL,,OK)
	IF (.NOT.OK) THEN
	   CALL WRITEF('%S reading tapedeck name',ERROR)
	   CALL LSL_PUTMSG(OK)
	   GOTO 10
	ENDIF                                       
C
C Check if input is a mag tape unit
C
	OK=LIB$GETDVI(DVI$_DEVCLASS,,DEVICE,,,)
	IF (.NOT.OK) THEN
	   CALL EXPAND('%S%S is not a known ',WARNING,DEVICE(:DEVLEN))
	   CALL WRITAP('magnetic tape device')
	   CALL LSL_PUTMSG(OK)
	   GOTO 10
	ENDIF
C
C
C Check tape mounted /FOREIGN
	OK = DEVMNT_OK()
	IF(.NOT. OK)THEN
	   IS_MOUNTED = .FALSE.                        
	   GOTO 10
	ENDIF
C
	GOTO 20
C
C Return with our tail down
10	DEVICE_CMD = .FALSE.
C
C ......................................................................
C
20	RETURN
	END
