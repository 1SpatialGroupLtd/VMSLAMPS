	LOGICAL FUNCTION RDGRID( RESULT )
C
***	IDENT	21JN88
***	MODULE	RDGRID
C
C	Copyright LaserScan Laboratories Ltd., Cambridge, England
C
C Include MESPAR.PAR from 'LSL$DTMCONVERTSYS' now.FJB 21-Jun-1988
C Amended for DTI format messages		FJB 14-April-1988
C Author Clarke Brunt, 12 February 1985
C
C To read a fixed format number from TXTC the current text buffer
C returning TRUE if an error occurs, FALSE if it all works
C N.B. used instead of RDFANG when not reading sexagesimal angle
C
C The different form is:
C			NNNNNNNH	where H is hemisphere
C
	IMPLICIT NONE
C
C For messages:
	INCLUDE 'LSL$DTMCONVERTSYS:MESPAR.PAR'   ! ERROR/WARNING messages
C
C we call
C
	LOGICAL		RDCH	! read a character
	LOGICAL		RDLONG	! read a number (int*4)
C
C and we return
C
	INTEGER*4	RESULT
C
C and workspace is
C
	BYTE		CH	! a character
	INTEGER*2	DCP1,DCP2	! decode pointer saves
C
C
	RDGRID = .TRUE.		! be pessimistic
	CALL DCPSAV( DCP1 )	! decode pointer on entry
C
C we want to concern ourselves with the next few characters alone, so
C we set a window from this position onwards
C
	CALL SETWIN( ,7 )		! 7 digits
C
C and read the number
C
	IF ( RDLONG(RESULT) ) THEN
	   CALL WRITEF('%S Number blank/missing',ERROR)
	   GOTO 999
	ENDIF
C
	CALL SETWIN( , )		! unset our window constraints
C
C Now check the decode pointer
C
	CALL DCPSAV( DCP2 )
	IF ( (DCP2-DCP1).NE.7 ) THEN
	   CALL TTWSTR('$S Error reading number',ERROR)
	   GOTO 999
	ENDIF
C
C now we must inspect what hemisphere the number is in, to decide
C whether it should be positive or negative
	IF ( RDCH(CH) ) THEN
	   CALL EXPAND('%S No hemisphere character found ',WARNING)
	   CALL WRITAP('in number (found ''%C'')',CH)
	   GOTO 999
	ENDIF
C
C is it a negative type hemisphere, then?
C
	IF ( (CH.EQ.'S') .OR. (CH.EQ.'W') ) THEN
	   RESULT = -RESULT		! was negative
	ELSE IF ( (CH.NE.'N') .AND. (CH.NE.'E') ) THEN
	   CALL EXPAND('%S Unrecognised hemisphere letter ',WARNING)
	   CALL WRITAP('in number (letter ''%C'')',CH)
	   GOTO 999
	ENDIF                                        
C
C so we have a sensible (?) number - return it to the user
	RDGRID = .FALSE.
C
999	RETURN                 
	END
