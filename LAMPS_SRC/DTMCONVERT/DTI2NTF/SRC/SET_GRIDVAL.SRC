	INTEGER FUNCTION SET_GRIDVAL(FWIDTH)
C
***	MODULE SET_GRIDVAL
****	IDENT 07JL93
C
C Copyright (C) Laser-Scan Ltd, Cambridge, England
C
C Author SG
C
CMOD	Use LOG10(MAXVAL) to calculate field widths	JB 11-Oct-1991
C	Calculate min/max correctly
C	Add BYTE values					JB 28-Oct-1991
C	Disallow BIT datatype
C	Use Z_MULT to set field widths as for the actual data
C
C	Rearrange some messages, and do not take LOG(0)	JB 28-Jun-1993
C
C This function determines the MAX and minimum values in all the dti files
C
	IMPLICIT NONE
C
	INCLUDE 'LSL$CMNLSL:FILENAME.CMN'
	INCLUDE 'LSL$CMNLSL:LSLLIBMSG.PAR'
	INCLUDE 'LSL$CMNDTI:DTIHDR.CMN'
	INCLUDE 'HERE:DTI2NTFMSG.PAR'
	INCLUDE 'HERE:LUNS.PAR'
	INCLUDE 'HERE:MESSAGE.PAR'
	INCLUDE 'HERE:NTFDATA.PAR'
C
	INCLUDE 'HERE:FILENAME.CMN'
	INCLUDE 'HERE:COMMANDS.CMN'
	INCLUDE 'HERE:SECTION.CMN'
	INCLUDE 'HERE:NTFDATA.CMN'
C
C Argument
	INTEGER	FWIDTH
C
C Local
	INTEGER		OPEN_DTI
C
	INTEGER		OK
	INTEGER		LOOP
	REAL		MINVAL,MAXVAL
	LOGICAL 	IS_SPACE
	LOGICAL		HAD_VALUES		! Read any DTI values?
C
C------------------------------------------------------------------------------
	OK = LSL__NORMAL			! assume success
C
C Use given value if there and warn if less than 4 (the usual value)
	IF (HAD_FWIDTH) THEN
	   CALL WRITEF(I_MESG//'Using supplied field width of %I', FIELD_WIDTH)
	   IF (FIELD_WIDTH.LT.4) CALL WRITEF
     &			(I_AMESG//'it may not be correct for the input data')
	   FWIDTH = FIELD_WIDTH
	   GOTO 999
	ENDIF
C
	HAD_VALUES = .FALSE.			! not had any numbers yet
C
	FWIDTH = 0				!ignore input FWIDTH
	Z_MAX  = 0
	Z_MIN  = 0
C
C------------------------------------------------------------------------------
C For each file, open it and find max and minimum
C
	DO 100 LOOP = 1,DTI_FNUM
	   OK = OPEN_DTI(LOOP, .TRUE.)
C
	   IF (.NOT.OK) THEN
C
C If only one DTI file, or last file read with still no DTI values read 
C then exit, otherwise attempt to continue
C
	      CALL WRITEF(E_MESG//'Unable to open DTI file for min-max info')
C
	      IF (.NOT.HAD_VALUES. AND. LOOP.EQ.DTI_FNUM) GOTO 999
C
	      IF (DTI_FNUM.NE.1) OK = .FALSE.		! downgrade error
C
	      CALL WRITEF(I_AMESG//'disregarding error and continuing')
	      GOTO 99					! get next
	   ENDIF
C
C DTI opened
C
	   IF (DATA_TYPE(DTI_LUN).EQ.DTI_BIT) THEN
	      OK = DTI2NTF__BADDTITYPE
	      CALL LSL_PUTMSG(OK)
	      CALL WRITEF(E_MESG//'Unsupported DTI data type: BIT')
C
	      IF (DTI_FNUM.EQ.1 .OR.
     &		 (.NOT.HAD_VALUES .AND. LOOP.EQ.DTI_FNUM) ) THEN
		OK = .FALSE.
		GOTO 999
	      ENDIF
C
	      CALL WRITEF(I_AMESG//'disregarding error and continuing')
	      GOTO 99
	   ENDIF
C
C ....................................................................
C get the maximum and minimum values, convert to real for convenience
C
	   IF    (DATA_TYPE(DTI_LUN).EQ.DTI_REAL) THEN
	      MINVAL = MIN_RVALUE(DTI_LUN)
	      MAXVAL = MAX_RVALUE(DTI_LUN)
	      Z_MULT = 0.001
	      HAD_VALUES = .TRUE.
C
	   ELSEIF(DATA_TYPE(DTI_LUN).EQ.DTI_LONG) THEN
	      MINVAL = REAL( MIN_LVALUE(DTI_LUN) )
	      MAXVAL = REAL( MAX_LVALUE(DTI_LUN) )
	      Z_MULT = 1.0
	      HAD_VALUES = .TRUE.
C
	   ELSEIF(DATA_TYPE(DTI_LUN).EQ.DTI_WORD .OR.
     &		  DATA_TYPE(DTI_LUN).EQ.DTI_BYTE) THEN
	      MINVAL = REAL( MIN_WVALUE(DTI_LUN) )
	      MAXVAL = REAL( MAX_WVALUE(DTI_LUN) )
	      Z_MULT = 1.0
	      HAD_VALUES = .TRUE.
	   ENDIF
C
C ....................................................................
C minimise and MAXVALise
C
	   Z_MAX = MAX(Z_MAX, MAXVAL/Z_MULT)
	   Z_MIN = MIN(Z_MIN, MINVAL/Z_MULT)
C
	   IF (MINVAL.EQ.0.0 .AND. MAXVAL.EQ.0.0) THEN	! no min-max info
	      CALL WRITEF(E_MESG//'No min-max information in DTI file')
C
	      IF (.NOT.HAD_VALUES .AND. LOOP.EQ.DTI_FNUM) THEN
		OK = .FALSE.
		GOTO 999
	      ENDIF
C
	      CALL WRITEF(I_AMESG//'disregarding error and continuing')
	      GOTO 99					! get next
	   ENDIF
C ....................................................................
C close the DTI file
C
99	   CALL CLOSE_DTI(LOOP)
C
100	CONTINUE
C
C-------------------------------------------------------------------------------
C Check if had any DTI values
C
	IF (.NOT.HAD_VALUES) THEN
	   CALL WRITEF(E_MESG//'No min-max information in DTI file')
	   OK = .FALSE.
	   GOTO 999
	ENDIF
C
C calculate the field width needed for the data
C
	MAXVAL = MAX( ABS(Z_MAX), ABS(Z_MIN) )
C
	IF (MAXVAL.EQ.0) THEN
	   CALL WRITEF(E_MESG//'No min-max information in DTI file')
	   OK = .FALSE.
	   GOTO 999
	ENDIF
C
	FWIDTH = 1 + NINT(LOG10(MAXVAL))	!make it human readable
	IF (Z_MIN.LT.0.0) FWIDTH = FWIDTH+1
C
C-------------------------------------------------------------------------------
C
999	SET_GRIDVAL = OK
C
	RETURN
	END
C
