C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1993-07-01 18:17:14.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	INTEGER FUNCTION READ_DTI_COL(DTI_COL)
C
***	MODULE	READ_DTI_COL
***	IDENT	01JL93
C
C Copyright (C) Laser-Scan Ltd, Cambridge, England
C
C Author 						SG 03-Sep-1991
C
C This function will read the data for the output GRID records, from the input 
C DTI file
C
C	.-----------------{ PSEUDO CODE }---------------.
C	|						|
C	| for each row in the DTI_COL			|
C	|   read data from dti file			|
C	|     (use appropriate read_dti_.. function)	|
C	|   convert the data into reals			|
C	|   store in common block			|
C	`-----------------------------------------------'
C
	IMPLICIT NONE
C
C includes
	INCLUDE 	'LSL$CMNLSL:LSLLIBMSG.PAR'
	INCLUDE		'LSL$CMNDTI:DTIHDR.CMN'
	INCLUDE		'LSL$CMNDTI:DTIPAR.PAR'
	INCLUDE		'HERE:DTI2NTFMSG.PAR'
	INCLUDE		'HERE:SECTION.CMN'
	INCLUDE		'HERE:NTFDATA.CMN'
C
	INCLUDE 	'HERE:LUNS.PAR'
	INCLUDE		'HERE:NTFDATA.PAR'
	INCLUDE 	'HERE:MESSAGE.PAR'
C
C parameters
	INTEGER		DTI_COL
C
C functions
	INTEGER	DTI_READ_BYTE_DATA
	INTEGER	DTI_READ_WORD_DATA
	INTEGER	DTI_READ_LONG_DATA
	INTEGER	DTI_READ_REAL_DATA
C
C workspace
	INTEGER		OK
	INTEGER		DTI_ROW
C
	BYTE		BYTE_DATA		!unsigned integer
	INTEGER*2	UNSIGNED_WORD_DATA	
	EQUIVALENCE(BYTE_DATA, UNSIGNED_WORD_DATA)
	INTEGER*2	WORD_DATA
	INTEGER*4	LONG_DATA
	REAL*4		REAL_DATA
C
C --------------------------------------------------------------------------
C
	OK = LSL__NORMAL				!assume success
	DTI_ROW = 0					!initialise row count
C
C	......................................................................
C	for each row in the column
C
	DO WHILE (OK.AND.(DTI_ROW.LT.YMAX))
	  DTI_ROW = DTI_ROW+1				!next row
C
C	  ......................................................................
C	  read and convert data from DTI file
C
	  IF ( DATA_TYPE(DTI_LUN).EQ.DTI_BYTE ) THEN
	    OK = DTI_READ_BYTE_DATA(DTI_LUN, BYTE_DATA, DTI_COL, DTI_ROW)
	    REAL_DATA = FLOAT(UNSIGNED_WORD_DATA)		!convert
	    IF (BYTE_DATA.EQ.DTI_NULL_BYTE) REAL_DATA = 0.0	!change if null
C
	  ELSEIF ( DATA_TYPE(DTI_LUN).EQ.DTI_WORD ) THEN
	    OK = DTI_READ_WORD_DATA(DTI_LUN, WORD_DATA, DTI_COL, DTI_ROW)
	    REAL_DATA = FLOAT(WORD_DATA)			!convert
	    IF (WORD_DATA.EQ.DTI_NULL_WORD) REAL_DATA = 0.0	!change if null
C
	  ELSEIF ( DATA_TYPE(DTI_LUN).EQ.DTI_LONG ) THEN
	    OK = DTI_READ_LONG_DATA( DTI_LUN, LONG_DATA, DTI_COL, DTI_ROW)
	    REAL_DATA = FLOAT(LONG_DATA)			!convert
	    IF (WORD_DATA.EQ.DTI_NULL_LONG) REAL_DATA = 0.0	!change if null
C
	  ELSEIF ( DATA_TYPE(DTI_LUN).EQ.DTI_REAL ) THEN
	    OK = DTI_READ_REAL_DATA( DTI_LUN, REAL_DATA, DTI_COL, DTI_ROW)
	    IF (REAL_DATA.EQ.DTI_NULL_REAL) REAL_DATA = 0.0	!change if null
C
	  ELSEIF ( DATA_TYPE(DTI_LUN).EQ.DTI_BIT ) THEN		! shouldn't get 
	    OK = DTI2NTF__BADDTITYPE				! here
	    CALL LSL_PUTMSG(OK)
	    CALL WRITEF(E_MESG//'Unsupported DTI data type : BIT')
	    GOTO 999
C
	  ELSE
	    OK = DTI2NTF__BADDTITYPE
	    CALL LSL_PUTMSG(OK)
	    CALL WRITEF(E_MESG//'Unsupported DTI data type')
	    GOTO 999
	  ENDIF
C
C	  ......................................................................
C	  check that return value was valid
C
	  IF (OK) THEN
	    COLUMN_DATA(DTI_ROW) = REAL_DATA		!remember data
C
	  ELSE
	    CALL LSL_PUTMSG(OK)
	    CALL WRITEF(E_MESG//'Unable to read DTI coordinate')
	    CALL WRITEF(I_AMESG//'ROW %I, COLUMN %I', DTI_COL, DTI_ROW)
	    GOTO 999
	  ENDIF
C
	END DO
C
C --------------------------------------------------------------------------
C
999	READ_DTI_COL = OK
C
	RETURN
	END
C
