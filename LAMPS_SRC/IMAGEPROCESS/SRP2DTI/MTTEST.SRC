	LOGICAL FUNCTION MTTEST( FILE )
C
***	MODULE MTTEST
***	IDENT 23NO84
C
	IMPLICIT NONE
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England
C Author S. Hancock
C
C tests given file name for being a tape specification
C
C argument
	CHARACTER*(*)	FILE			! file name
C
	INTEGER*4	DVCLASS			! to distinguish disc
						! and tape
	INTEGER*4	RETL
	INTEGER*4	ITMLST(4)		! item list for GETDVI
	INTEGER*2	ITM1(2)
	INTEGER*4	L,L1
	LOGICAL*4	STAT
	LOGICAL*4	SYS$GETDVI

	EQUIVALENCE	(ITM1(1),ITMLST(1))

C
	INCLUDE '($SSDEF)'
	INCLUDE '($DCDEF)'
	INCLUDE '($DVIDEF)'
C
	MTTEST = .FALSE.			! assume not tape
C remove trailing spaces, tabs & nulls
	L = INDEX(FILE,' ') - 1
	IF(L.LT.0) L=LEN( FILE)
	L1 = INDEX(FILE, CHAR(9)) - 1
	IF(L1 .GE. 0 .AND. L1 .LT. L) L=L1
	L1 = INDEX(FILE, CHAR(0)) - 1
	IF(L1 .GE. 0 .AND. L1 .LT. L) L=L1
C tape or disc?
	ITM1(1)	= 4
	ITM1(2) = DVI$_DEVCLASS

	ITMLST(2) = %LOC(DVCLASS)
	ITMLST(3) = %LOC(RETL)
	ITMLST(4) = 0				! terminate item list

	STAT = SYS$GETDVI( ,,FILE(1:L),ITMLST,,,,)
	IF( STAT .AND. DVCLASS .EQ. DC$_TAPE ) MTTEST = .TRUE.
100	RETURN
C
	END
