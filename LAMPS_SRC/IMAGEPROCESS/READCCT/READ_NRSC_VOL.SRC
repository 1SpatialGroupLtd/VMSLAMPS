        LOGICAL FUNCTION READ_NRSC_VOL()
C
***	MODULE	READ_NRSC_VOL
***	IDENT	30MR87
C
C 	Copyright Laser-Scan Laboratories Ltd.,Cambridge,England
C 	Author: D R Catlow
C
C	Routine for to decode a RAE CCT volume (VOL 1) record.
C
	IMPLICIT NONE
C
	INCLUDE 'HERE:READCCT.CMN'
	INCLUDE 'HERE:TAPE_RECORDS.CMN'
	INCLUDE 'HERE:TAPE.CMN'
C
C	Locals
	INTEGER*4	I		!counter
	INTEGER*4	DAY_OF_WEEK	!index into DAYS
	INTEGER*4	ABS_TIME	!absolute binary time
	INTEGER*4	IERR		!function return
C
	CHARACTER*23	DATE		!date
	CHARACTER*3	DAYS(7)		!days of week
C	
	DATA		DAYS/'Mon','Tue','Wed','Thu','Fri','Sat','Sun'/
C
C	Functions
	INTEGER*4	OPEN_FILES	!to open header file
	INTEGER*4	FLWSEL		!to select file on specified LUN
C
C--------------------------------------------------------------------------
C
	COUNT_VOL1=COUNT_VOL1+1	!increment counter for this
C						!record type
C
	IF(COUNT_VOL1.NE.WHICH_SCENE)THEN
	  SKIP_SCENE=.TRUE.	!don't bother doing any decoding of this scene
	ELSE
	  SKIP_SCENE=.FALSE.	!ie. this is the scene we are interested in
	ENDIF
C
	READ_NRSC_VOL=.TRUE.
C
	IF(SKIP_SCENE)GOTO 999
C
C---------------------------------------------------------------------------
C
C	The character information is encoded as 8 bit ASCII
C	so mask out in order to interprete correctly.
C	The first byte has already been done
C
	DO 100 I=1,LABEL_RECORD_LENGTH
	   MTBUF(I)=MIN(127,MTBUF(I)+128)
100	CONTINUE
C	   	
	IF(HAD_DIAG)THEN
	  CALL WRITEF
     &    ('*** NRSC VOL Record               %I6',COUNT_VOL1)
	ENDIF
C
	IF((HAD_PRINT.OR.HAD_LIST).AND.DECODE_HEADER)THEN
C
	  IERR=FLWSEL(2)
	  IF(.NOT.IERR)THEN
	    CALL LSL_PUTMSG(IERR)
	    READ_NRSC_VOL=.FALSE.
	    GOTO 999
	  ENDIF
C
	  CALL FLWRTF
     & ('-------------------------------------------------------------')
	  CALL FLWRTF
     & ('               *** NRSC VOLUME HEADER RECORD ***')
	  CALL FLWRTF
     & ('-------------------------------------------------------------')
	  CALL FLWRTF('Volume Serial Number : %A6', MTBUF(5))
	  CALL FLWRTF('Owner Identification : %A14',MTBUF(38))
	ENDIF
C	That's all there is!
C
C------------------------------------------------------------------------
C	Output any relevant details to the HDR text file
	IF(HAD_HDR)THEN
C
C	First open the text file
	  IF(.NOT.OPEN_FILES(.FALSE.,.TRUE.))THEN
	    READ_NRSC_VOL=.FALSE.
	    GOTO 999
	  ENDIF
C
	  IERR=FLWSEL(1)
	  IF(.NOT.IERR)THEN
	    CALL LSL_PUTMSG(IERR)
	    READ_NRSC_VOL=.FALSE.
	    GOTO 999
	  ENDIF
C
	  CALL FLWRTF('!Keyword values required for HIPS header')
C
	  CALL EXPAND
     &    ('ORIG_NAME=NRSC RAE FARNBOROUGH  !CCT Creation Agency')
	  CALL FLWLIN
C
	  CALL EXPAND('SEQ_NAME=%A6',MTBUF(5))
	  CALL APPEND('                 !Volume Identification')
	  CALL FLWLIN
C
	  CALL EXPAND('ORIG_DATE=                    ')    
          CALL APPEND('  !Date and time CCT was created')
	  CALL FLWLIN
C
	  CALL EXPAND('SEQ_HISTORY="-D Created by READCCT ')
C
	  CALL LIB$DATE_TIME(DATE)		!get date 
	  CALL SYS$BINTIM(DATE,ABS_TIME)	!convert to binary time
	  CALL LIB$DAY_OF_WEEK(ABS_TIME,DAY_OF_WEEK)	!day of week
	  CALL APPEND('%S ',DAYS(DAY_OF_WEEK))
	  CALL APPEND('%S ',DATE(4:6))
	  CALL APPEND('%S ',DATE(1:2))
	  CALL APPEND('%S ',DATE(13:20))
	  CALL APPEND('GMT %S"',DATE(8:11))
C
	  CALL FLWLIN
C
	  CALL FLWRTF('SEQ_DESCR=')
	  CALL FLWRTF('GRID_INTERVAL_X=')
	  CALL FLWRTF('GRID_INTERVAL_Y=')
C
	  CALL FLWRTF('!')
	  CALL FLWRTF('!NRSC Header Information')
	  CALL FLWRTF('!')
	ENDIF
C
C---------------------------------------------------------------------------
999	RETURN
C
	END
