        LOGICAL FUNCTION READ_VOLUME()
C
***	MODULE	READ_VOLUME
***	IDENT	12MY87
C
C 	Copyright Laser-Scan Laboratories Ltd.,Cambridge,England
C 	Author: D R Catlow
C
C	Routine for READCCT to decode a volume record.
C
C	The volume directory is used to determine satellite type.
C	SPOT or TM, and spectral mode ie. panchromatic or multispectral.
C	This is safe, since the volume record must be first record
C	associated with a scene.
C
	IMPLICIT NONE
C
	INCLUDE 'HERE:READCCT.CMN'
	INCLUDE 'HERE:TAPE.CMN'		!Values relating to the tape records
	INCLUDE 'HERE:TAPE_RECORDS.CMN'
C
C	Local
	INTEGER*4	IERR		!function return
	INTEGER*4	ABS_TIME	!absolute binary time
	INTEGER*4	DAY_OF_WEEK	!index into DAYS
	INTEGER*2	MONTH_INDEX	!which month
	CHARACTER*3	MONTHS(12)	!months
	CHARACTER*3	DAYS(7)		!days of week
	CHARACTER*23	DATE		!date
C	
	DATA            MONTHS/'Jan','Feb','Mar','Apr','May','Jun',
     &                         'Jul','Aug','Sep','Oct','Nov','Dec'/
C
	DATA		DAYS/'Mon','Tue','Wed','Thu','Fri','Sat','Sun'/
C
C	Function
C	Function
	LOGICAL		RDINT		!to read an integer*2
	INTEGER*4	OPEN_FILES	!to open a text file
	INTEGER*4	FLWSEL		!select file opened on required LUN
C
C----------------------------------------------------------------------------
C----------------------------------------------------------------------------
C
	COUNT_VOLUME_DESCR=COUNT_VOLUME_DESCR+1	!increment counter for this
C						!record type
	IF(COUNT_VOLUME_DESCR.NE.WHICH_SCENE)THEN
	  SKIP_SCENE=.TRUE.	!don't bother doing any decoding of this scene
	ELSE
	  SKIP_SCENE=.FALSE.	!ie. this is the scene we are interested in
	ENDIF
C
	READ_VOLUME=.TRUE.
C
	IF(SKIP_SCENE)GOTO 999
C
C---------------------------------------------------------------------------
C
	NUMBER_CALIB_RECORDS=0	!initialise calib. counter
C
	LANDSAT       =.FALSE.
	SPOT          =.FALSE.
	PANCHROMATIC  =.FALSE.
	MULTISPECTRAL =.FALSE.
C
C	Determine which satellite we are dealing with.
	IF(MTBUF(61).EQ.'L')LANDSAT=.TRUE.
	IF(MTBUF(61).EQ.'S'.AND.MTBUF(62).EQ.'P')SPOT=.TRUE.
C
C	Signal Failure if we don't have either Landsat or SPOT
	IF((.NOT.SPOT).AND.(.NOT.LANDSAT))THEN
	  READ_VOLUME=.FALSE.
	  GOTO 999
	ENDIF
C
C	Determine what type of SPOT data (ie. panchromatic
C	or multispectral).
	IF(SPOT)THEN
	  IF(MTBUF(65).EQ.'P')PANCHROMATIC =.TRUE.
	  IF(MTBUF(65).EQ.'X')MULTISPECTRAL=.TRUE.
	ENDIF
C
C---------------------------------------------------------------------------
C	Output any relevant details to the HDR text file
	IF(HAD_HDR)THEN
C
C	First open the text file
	  IF(.NOT.OPEN_FILES(.FALSE.,.TRUE.))THEN
	    READ_VOLUME=.FALSE.
	    GOTO 999
	  ENDIF
C
	  IERR=FLWSEL(1)
	  IF(.NOT.IERR)THEN
	    CALL LSL_PUTMSG(IERR)
	    READ_VOLUME=.FALSE.
	    GOTO 999
	  ENDIF
C
	  CALL FLWRTF('!Keyword values required for HIPS header')
	  CALL SETAUX(MTBUF,MAX_RECORD_LENGTH)
C
	  CALL EXPAND('ORIG_NAME=%A8',MTBUF(141))
	  CALL APPEND('                          !CCT Creation Agency')
	  CALL FLWLIN
C
	  CALL EXPAND('SEQ_NAME=%A16',MTBUF(61))
	  CALL APPEND('                   !Logical volume identification')
	  CALL FLWLIN
C
	  CALL EXPAND('ORIG_DATE=Mon ')		!day - always MONDAY!
C
	  CALL SETWIN(117,2)
	  IF(RDINT(MONTH_INDEX))THEN
            READ_VOLUME=.FALSE.
	    GOTO 999
	  ENDIF
C
	  CALL APPEND('%S ',MONTHS(MONTH_INDEX))	!month
	  CALL APPEND('%A2 ',MTBUF(119))		!day
	  CALL APPEND('%A2:',MTBUF(121))		!hour
	  CALL APPEND('%A2:',MTBUF(123))		!minute
	  CALL APPEND('%A2 ',MTBUF(125))		!second	  
	  CALL APPEND('EST %A4',MTBUF(113))		!year
C
	  CALL APPEND('      !date and time CCT was created')
	  CALL FLWLIN
C
C
	  CALL EXPAND('SEQ_HISTORY="-D Created by READCCT ')
C
	  CALL LIB$DATE_TIME(DATE)		!get date 
	  CALL SYS$BINTIM(DATE,ABS_TIME)	!convert to binary time
	  CALL LIB$DAY_OF_WEEK(ABS_TIME,DAY_OF_WEEK)	!day of week
	  CALL APPEND('%S ',DAYS(DAY_OF_WEEK))
	  CALL APPEND('%S ',DATE(4:6))
	  CALL APPEND('%S ',DATE(1:2))
	  CALL APPEND('%S ',DATE(13:20))
	  CALL APPEND('GMT %S"',DATE(8:11))
C
	  CALL FLWLIN
C
	  CALL FLWRTF('SEQ_DESCR=')
	  CALL FLWRTF('GRID_INTERVAL_X=')
	  CALL FLWRTF('GRID_INTERVAL_Y=')
C	  
	ENDIF
C
C---------------------------------------------------------------------------
C	Print out contents of volume record if PRINT qualifier is
C	present.  This is only useful for debugging.
C	
	IF((HAD_PRINT.OR.HAD_LIST).AND.DECODE_VOLUME_DESCR)THEN
C	record details to the terminal or to temporary file
C	for subsequent spooling.
	  IERR=FLWSEL(2)
	  IF(.NOT.IERR)THEN
	    CALL LSL_PUTMSG(IERR)
	    READ_VOLUME=.FALSE.
	    GOTO 999
	  ENDIF
C
	  CALL FLWRTF
     & ('-------------------------------------------------------------')
	  IF(LANDSAT)CALL FLWRTF
     & ('           *** LANDSAT TM VOLUME DESCRIPTOR ***')
	  IF(SPOT)   CALL FLWRTF
     & ('              *** SPOT VOLUME DESCRIPTOR ***')
	  CALL FLWRTF
     & ('-------------------------------------------------------------')
	  IF(PANCHROMATIC)CALL FLWRTF
     & ('Spectral Mode                              : Panchromatic')
	  IF(MULTISPECTRAL)CALL FLWRTF
     & ('Spectral Mode                              : Multispectral')
	  CALL FLWRTF
C
     & ('Tape Identification                        : %A16',MTBUF(61))
	  CALL FLWRTF
     & ('Tape Creation Date                         : %A4 %A2 %A2',
     &    MTBUF(113),MTBUF(117),MTBUF(119))
	  CALL FLWRTF
     & ('Tape Creation Time                         : %A2 %A2 %A2',
     &    MTBUF(121),MTBUF(123),MTBUF(125))
	  CALL FLWRTF
     & ('Generating Country                         : %A12',MTBUF(129))
	  CALL FLWRTF
     & ('Generating Agency                          : %A8',MTBUF(141))
	  CALL FLWRTF
     & ('Generating Facility                        : %A12',MTBUF(149))
	  CALL FLWRTF
     & ('-------------------------------------------------------------')
	  CALL FLWRTF
     & ('Number of Volumes in Set                   : %A2',MTBUF(93))
	  CALL FLWRTF
     & ('Number of First Volume in Set              : %A2',MTBUF(95))
	  CALL FLWRTF
     & ('Number of Last Volume in Set               : %A2',MTBUF(97))
	  CALL FLWRTF
     & ('Number of This Volume                      : %A2',MTBUF(99))
	  CALL FLWRTF
     & ('Number of File Pointer Records:            : %A2',MTBUF(163))
C
	ENDIF
C
	IF(.NOT.DECODE_VOLUME_DESCR)GOTO 999	!don't decode the rest of
C						!the information
C---------------------------------------------------------------------------
C
999	RETURN
C
	END
