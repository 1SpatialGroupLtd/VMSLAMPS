        LOGICAL FUNCTION READ_NRSC_IMAGE()
C
***	MODULE	READ_NRSC_IMAGE
***	IDENT	30MR87
C
C 	Copyright Laser-Scan Laboratories Ltd.,Cambridge,England
C 	Author: D R Catlow
C
C	Routine for READCCT to decode a RAE CCT image data record
C
C	The byte image data is transferred directly to a
C	DTI file using DTILIB routine DTI_COPY_BYTE_DATA.
C
	IMPLICIT NONE
C
***	COMMON/LSL$CMNDTI:DTIHDR/
C
***	COMMON/HERE:READCCT/		!SPOTIN variables
***	COMMON/HERE:TAPE/		!Values relating to the tape records
***	COMMON/HERE:TAPE_RECORDS/
C
***	PARAMETER/LSL$CMNDTI:DTILIBMSG/ !DTILIB error returns
C
C	Locals
	INTEGER*4	IERR			!function return
	INTEGER*4	ERROR		        !system error code
C
C	Functions
	INTEGER*4	DTI_COPY_BYTE_DATA	!to transfer an array
	INTEGER*4	OPEN_FILES		!to open DTI file
C
C-------------------------------------------------------------------------
C
	READ_NRSC_IMAGE=.TRUE.		!set to signal success
C
	COUNT_IMAGE=COUNT_IMAGE+1	!increment counter for this
C
	IF(HAD_DIAG)THEN
	  CALL WRITEF
     &    ('*** NRSC Image Data Record Number %I6',COUNT_IMAGE)
	ENDIF
C	
	IF(.NOT.DECODE_IMAGE)GOTO 999	!no further action
C
	IF(SKIP_SCENE)GOTO 999
C
C-------------------------------------------------------------------------
C	Are we interested in this band
	IF(.NOT.THIS_BAND(BAND_NUMBER))GOTO 999
C
	IF(.NOT.HAD_DTI)GOTO 999	!don't actually transfer the data
C
C	Open an output DTI file if necessary
	IF(.NOT.DTI_OPEN)THEN
	  IF(.NOT.OPEN_FILES(.TRUE.,.FALSE.))THEN
	     READ_NRSC_IMAGE=.FALSE.
	     GOTO 999
	  ENDIF
	ENDIF
C
C	Increment line count
	LINE_NUMBER=LINE_NUMBER+1
C
C	Are we interested in this line
	IF(LINE_NUMBER.LT.FIRST_LINE.OR.LINE_NUMBER.GT.LAST_LINE)GOTO 999
C
C	Which LUN is the output file opened on.
C	We may have multiple files opened.
	DTI_OUT_LUN=BAND_NUMBER
	IF(DTI_OUT_LUN.LE.0.OR.DTI_OUT_LUN.GT.7)THEN
	  READ_NRSC_IMAGE=.FALSE.
	  GOTO 999
	ENDIF
C
C	Transfer data to the output file
	IERR=DTI_COPY_BYTE_DATA (DTI_OUT_LUN, MTBUF(FIRST_SAMPLE),
     &                           .TRUE.,
     &                           COUNT_COLUMNS,1,
     &			         1,HOW_MANY_PIXELS,ERROR)
C
	IF(.NOT.IERR)THEN
	  CALL LSL_PUTMSG(IERR)
	  IF(IERR.EQ.DTI__SYSERR)CALL LSL_PUTMSG(ERROR)
	  READ_NRSC_IMAGE=.FALSE.
	  GOTO 999
	ENDIF
C
C	Increment DTI column count
	COUNT_COLUMNS=COUNT_COLUMNS+1
C
C-------------------------------------------------------------------------
999	RETURN
C
	END


