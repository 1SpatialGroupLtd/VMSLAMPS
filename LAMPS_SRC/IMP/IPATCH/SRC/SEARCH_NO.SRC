C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1986-08-11 10:35:30.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION SEARCH_NO
C
***	IDENT	11AU86
***	MODULE	SEARCH_NO
C
C Copyright (C) LaserScan Laboratories Ltd, Cambridge, England
C
C Author TJ Ibbs, 25 July 1986
C
C to look for the next NO entry
C - returns FALSE if we find one, TRUE if we don't
C
C - note allows wildcards in the arguments
C
	IMPLICIT NONE
C
C **********************************************************************
C common files
C
	INCLUDE	'HERE:ENTRY.CMN'		! for entry data
C
C functions
C
	LOGICAL	       	READ_WILD_INT2		! read a number
	LOGICAL	       	READ_WILD_HEX2		! read a hex number
	LOGICAL	       	READ_WILD_HEX4
	LOGICAL		SEARCH_SIMPLE		! do our search
	LOGICAL		CHECK_EOL		! check eol for rubbish
C
C workspace
C
	INTEGER		NO_WANTED(3)		! values required
	LOGICAL		NO_WILD(3)		! are they wild?
	INTEGER		COUNT			! number of args
C
	INTEGER		I			! count for DO loop
C
C **********************************************************************
C assume the search works
C
	SEARCH_NO = .FALSE.
C
C no arguments yet
C
	COUNT = 0
C
C try to read the layer number
C
	IF ( READ_WILD_INT2( NO_WANTED(1),NO_WILD(1),.FALSE. ) ) GOTO 1900
C
	COUNT = 1
C
C and check for a flag and pointer
C
	IF ( READ_WILD_HEX2( NO_WANTED(2),NO_WILD(2) ) ) GOTO 1900
 	COUNT = 2
C
	IF ( READ_WILD_HEX4( NO_WANTED(3),NO_WILD(3) ) ) GOTO 1900
	COUNT = 3
C
C and check the end of line for rubbish
C
1900	IF ( CHECK_EOL() ) GOTO 9000
C
C **********************************************************************
C so find the next NO entry
C
2000	IF ( SEARCH_SIMPLE( %REF('NO') ) ) GOTO 8000
C
C if we have no arguments to match, then this is success
C
	IF ( COUNT.EQ.0 ) GOTO 9000
C
C so read the entry in
C
	CALL READ_ENTRY
C
C otherwise, check the values of the arguments
C
	DO 3000 I = 1,COUNT
	   IF ( NO_WILD(I) ) GOTO 3000		! don't care about value
	   IF ( I.EQ.1 .AND. NO_WANTED(I).NE.LAYER     ) GOTO 2000
	   IF ( I.EQ.2 .AND. NO_WANTED(I).NE.MISC_INT2 ) GOTO 2000
	   IF ( I.EQ.3 ) THEN
	      IF ( IELEN.LT.4 ) GOTO 2000	! didn't have a pointer
	      IF ( NO_WANTED(I).NE.POINTER ) GOTO 2000
	   ENDIF
3000	CONTINUE
C
C so we have matched - return with success
C
	GOTO 9000
C
C **********************************************************************
C tell the user if we can't find it
C
8000	SEARCH_NO = .TRUE.
	CALL EXPAND('%%IPATCH-W-SEARCH, cannot find NO')
C
	DO 81000 I = 1,COUNT
	   IF ( NO_WILD(I) ) THEN
	      CALL APPEND(' *')
	   ELSE
	      IF ( I.EQ.1 ) THEN
		 CALL APPEND(' %N',NO_WANTED(I))
	      ELSEIF ( I.EQ.2 ) THEN
		 CALL APPEND(' %P0%X4',NO_WANTED(I))
	      ELSE
		 CALL APPEND(' %P0%X8',NO_WANTED(I))
	      ENDIF
	   ENDIF
81000	CONTINUE
C
	CALL TTWLIN
C
C **********************************************************************
C and return
C
9000	RETURN
	END
