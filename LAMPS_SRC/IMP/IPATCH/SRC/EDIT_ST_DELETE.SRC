C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1987-12-20 16:02:50.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION EDIT_ST_DELETE
C
***	IDENT	20DE87
***	MODULE	EDIT_ST_DELETE
C
C Copyright (C) LaserScan Laboratories Ltd, Cambridge, England
C
C Author TJ Ibbs, 1 August 1986
C
C to delete some values in the current ST, ZS, or CB entry
C
C - returns TRUE if we fail to delete anything, or if the
C   deletion should be ignored
C
	IMPLICIT NONE
C
C **********************************************************************
C parameter files
C
	INCLUDE	'HERE:IPATCHMSG.PAR'
C
C common files
C
	INCLUDE	'LSL$CMNLSL:CMDCOM.CMN'	! for command details
  	INCLUDE	'HERE:ENTRY.CMN'	! for entry details
C
C workspace
C
	INTEGER		BOT,TOP		! the range to delete
	INTEGER		L_NPTS		! long copy of NPTS
C
C **********************************************************************
C assume the worst
C
	EDIT_ST_DELETE = .TRUE.
C
C firstly, work out what we're being asked to do
C - note that our caller has already made sure that we have SOME
C   arguments to use
C
	BOT = INTARG(1)
	IF ( CMDICT.EQ.1 ) THEN
	   TOP = BOT
	ELSE
	   TOP = INTARG(2)
	ENDIF
C
C check that the numbers make sense
C
	IF ( IECODE.EQ.'CB' ) THEN
	   L_NPTS = CBH.NROW
	ELSE
	   L_NPTS = NPTS
	ENDIF
C
	IF ( BOT.LT.1 .OR. BOT.GT.L_NPTS ) THEN
	   CALL LSL_PUTMSG( IPATCH__INTRANGE, BOT, 1, L_NPTS )
	   GOTO 9000
	ELSEIF ( TOP.LT.1 .OR. TOP.GT.L_NPTS ) THEN
	   CALL LSL_PUTMSG( IPATCH__INTRANGE, TOP, 1, L_NPTS )
	   GOTO 9000
	ELSEIF ( TOP.LT.BOT ) THEN
	   CALL LSL_PUTMSG( IPATCH__ORDER, BOT, TOP )
	   GOTO 9000
	ENDIF
C
C **********************************************************************
C so do the deletion
C - if we are simply shortening the array, then that's all we need do,
C   but otherwise we shall have to move it around
C
	IF ( TOP.NE.L_NPTS ) THEN		! not just shortening it
	   IF ( IECODE.EQ.'ST' ) THEN
	      CALL VIOMV3(STBUF(1,TOP+1),(L_NPTS-TOP+1)*2*4,STBUF(1,BOT))
	   ELSEIF ( IECODE.EQ.'ZS' ) THEN
	      CALL VIOMV3(ZSBUF(1,TOP+1),(L_NPTS-TOP+1)*3*4,ZSBUF(1,BOT))
	   ELSE
	      CALL VIOMV3(CBD(TOP*CBH.NCOL+1),
     &			  (L_NPTS-TOP+1)*CBH.NCOL*4,
     &			  CBD((BOT-1)*CBH.NCOL+1) )

	   ENDIF
	ENDIF
C
C and adjust the array size
C
	L_NPTS = L_NPTS - (TOP-BOT+1)
	IF ( IECODE.EQ.'CB' ) THEN
	   CBH.NROW = L_NPTS
	ELSE
	   NPTS = L_NPTS
	ENDIF
C
C and if we finish, then that's success
C
	EDIT_ST_DELETE = .FALSE.
C
C **********************************************************************
C and that's it
C
9000	RETURN
	END
