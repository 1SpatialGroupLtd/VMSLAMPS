	LOGICAL FUNCTION SEARCH_NF
C
***	IDENT	11AU86
***	MODULE	SEARCH_NF
C
C Copyright (C) LaserScan Laboratories Ltd, Cambridge, England
C
C Author TJ Ibbs, 25 July 1986
C
C to look for the next NF entry
C - returns FALSE if we find one, TRUE if we don't
C
C - note allows wildcards in the arguments
C
	IMPLICIT NONE
C
C **********************************************************************
C common files
C
	INCLUDE	'HERE:ENTRY.CMN'		! for entry data
C
C functions
C
	LOGICAL	       	READ_WILD_INT2		! read a number
	LOGICAL		SEARCH_SIMPLE		! do our search
	LOGICAL		CHECK_EOL		! check eol for rubbish
C
C workspace
C
	INTEGER		FSN_WANTED,ISN_WANTED	! values required
	LOGICAL		FSN_WILD,ISN_WILD	! is it wild?
	INTEGER		COUNT			! number of args
C
C **********************************************************************
C assume the search works
C
	SEARCH_NF = .FALSE.
C
C no arguments yet
C
	COUNT = 0
C
C try to read the FSN
C
	IF ( READ_WILD_INT2( FSN_WANTED, FSN_WILD, .TRUE. ) ) GOTO 1900
C
	COUNT = 1
C
C and then the ISN
C
	IF ( READ_WILD_INT2( ISN_WANTED, ISN_WILD, .TRUE. ) ) GOTO 1900
C
	COUNT = 2
C
C and check for rubbish after that
C
1900	IF ( CHECK_EOL() ) GOTO 9000
C
C **********************************************************************
C so find the next NF entry
C
2000	IF ( SEARCH_SIMPLE( %REF('NF') ) ) GOTO 8000
C
C if we have no arguments to match, then this is success
C
	IF ( COUNT.EQ.0 ) GOTO 9000
C
C so read the entry in
C
	CALL READ_ENTRY
C
C otherwise, check the values of the arguments
C
	IF ( COUNT.GE.1 .AND. .NOT.FSN_WILD
     &			.AND. L_FSN.NE.FSN_WANTED ) GOTO 2000
C
	IF ( COUNT.EQ.2 .AND. .NOT.ISN_WILD
     &			.AND. L_ISN.NE.ISN_WANTED ) GOTO 2000
C
C so we have matched - return with success
C
	GOTO 9000
C
C **********************************************************************
C tell the user if we can't find it
C
8000	SEARCH_NF = .TRUE.
	CALL EXPAND('%%IPATCH-W-SEARCH, cannot find NF')
C
	IF ( COUNT.GE.1 ) THEN
	   IF ( FSN_WILD ) THEN
	      CALL APPEND(' *')
	   ELSE
	      CALL APPEND(' %N',FSN_WANTED)
	   ENDIF
	ENDIF
C
	IF ( COUNT.EQ.2 ) THEN
	   IF ( ISN_WILD ) THEN
	      CALL APPEND(' *')
	   ELSE
	      CALL APPEND(' %N',ISN_WANTED)
	   ENDIF
	ENDIF
C
	CALL TTWLIN
C
C **********************************************************************
C and return
C
9000	RETURN
	END
