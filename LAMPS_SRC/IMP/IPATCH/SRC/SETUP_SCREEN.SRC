	SUBROUTINE SETUP_SCREEN
C
***	IDENT	06AU86
***	MODULE	SETUP_SCREEN
C
C Copyright (C) LaserScan Laboratories Ltd, Cambridge, England
C
C Author TJ Ibbs, 6 August 1986
C
C sets up the status area on the screen
C - the status area is considered to be lines 1 to 5
C
	IMPLICIT NONE
C
C ***********************************************************************
C parameter files
C
	INCLUDE	'HERE:IPATCHMSG.PAR'
	INCLUDE	'HERE:USEFUL.PAR'	! for ESC, CSI
C
C common files
C
	INCLUDE	'HERE:SWITCHES.CMN'
C
	CHARACTER*80	BLANKS
	PARAMETER	(BLANKS=' ')
C
C functions
C
	LOGICAL		TTINIT
C
C workspace
C
 	INTEGER		I		! for DO loops
	LOGICAL		OK		! function return
C
C remember whether we have initialised our binary output
C
	LOGICAL		DONE_INIT_TERM
	DATA		DONE_INIT_TERM /.FALSE./
	SAVE		DONE_INIT_TERM
C
C ***********************************************************************
C if we haven't done it before, initialise the binary output
C to the terminal
C
	IF ( .NOT.DONE_INIT_TERM ) THEN
	   OK = TTINIT()
	   IF ( .NOT.OK ) THEN
	      CALL LSL_PUTMSG( OK )
	      CALL LSL_ADDMSG( IPATCH__CRESTATUS )
	      STATUS_LINE = .FALSE.
	      GOTO 9000
	   ELSE
	      DONE_INIT_TERM = .TRUE.
	   ENDIF
	ENDIF
C
C clear the screen - move to 1,1 and erase from there
C
	CALL BINOUT( CSI // '1;1H' // CSI // 'J' )
C
C for each of the status lines, highlight with reverse video for 80 chars
C
	DO 100 I = 1,5
      	   CALL BINOUT( CSI // '7m' //
     &			CSI // CHAR(ICHAR('0')+I) // ';1H' // BLANKS )
100	CONTINUE
C
C define the scrolling region as from line 6 downwards
C
	CALL BINOUT( CSI // '6;r' )
C
C set ordinary video
C
	CALL BINOUT( CSI // '0m' )
C
C and move to the last line of the scroll area
C
	CALL BINOUT(CSI // '24;1H' )
C
9000	RETURN
	END
