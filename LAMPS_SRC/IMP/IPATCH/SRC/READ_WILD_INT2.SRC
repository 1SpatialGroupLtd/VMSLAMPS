C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1986-11-27 10:29:26.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION READ_WILD_INT2( VALUE, IS_WILD, UNSIGNED )
C
***	IDENT	27NO86
***	MODULE	READ_WILD_INT2
C
C Copyright (C) LaserScan Laboratories Ltd, Cambridge, England
C
C Author TJ Ibbs, 26 July 1986
C
C to read a (possibly wildcarded) short integer
C - returns TRUE if nothing there (ie not a number, not a wildcard)
C
	IMPLICIT NONE
C
C arguments
C
	INTEGER*2	VALUE		! the number to read
	LOGICAL		IS_WILD		! true if was wildcarded
	LOGICAL		UNSIGNED	! true if it is 0-65535
C
C **********************************************************************
C parameter files
C
	INCLUDE	'LSL$CMNLSL:LSLLIBMSG.PAR'
	INCLUDE	'HERE:IPATCHMSG.PAR'
 	INCLUDE	'HERE:USEFUL.PAR'	! for ranges
C
C common files
C
	INCLUDE	'LSL$CMNLSL:EXCEPTION.CMN'
C
C functions
C
	LOGICAL		RDCHS		! read a character
	LOGICAL		RDINT		! read a short integer
	LOGICAL	      	RDLONG		! read a long  integer
C
C workspace
C
	BYTE		ICH		! character
	LOGICAL		ERROR		! function result
	INTEGER		NUMBER		! to read into
	INTEGER*2	NUM
	EQUIVALENCE	(NUM,NUMBER)
C
C and the wildcard
C
	BYTE		WILD
	PARAMETER	(WILD='*')
C
C **********************************************************************
C assume that we fail
C
	READ_WILD_INT2 = .TRUE.
	NUMBER = 0
C
C is there anything on the line?
C
	IF ( RDCHS(ICH) ) THEN
	   IS_WILD = .FALSE.
	   GOTO 9000
	ENDIF
C
C was it a wildcard?
C
	IF ( ICH.EQ.WILD ) THEN
	   IS_WILD = .TRUE.
	   READ_WILD_INT2 = .FALSE.
	   GOTO 9000
	ELSE
	   IS_WILD = .FALSE.
	ENDIF
C
C try to read an integer - read the appropriate length
C
	CALL BSCH
	IF ( UNSIGNED ) THEN
	   ERROR = RDLONG(NUMBER)
	ELSE
	   ERROR = RDINT(NUMBER)
	ENDIF
C
	IF ( ERROR ) THEN
	   IF ( ERRNUM.EQ.LSL__HADEXCP ) THEN
	      CALL LSL_PUTMSG(ERRNUM)
	      CALL LSL_ADDMSG(LSL_EXCP)
	   ENDIF
	   GOTO 9000
	ENDIF
C
C so we have a value - check its range
C
	IF ( UNSIGNED .AND. (NUMBER.LT.0.OR.NUMBER.GT.MAXU2) ) THEN
	   CALL LSL_PUTMSG( IPATCH__INTRANGE, NUMBER,0,MAXU2 )
	ELSE
	   VALUE = NUM
	   READ_WILD_INT2 = .FALSE.		! success
	ENDIF
C
C and return
C
9000	RETURN
	END
