	LOGICAL FUNCTION EDIT_CP_BOTH
C
***	IDENT	11AU86
***	MODULE	EDIT_CP_BOTH
C
C Copyright (C) LaserScan Laboratories Ltd, Cambridge, England
C
C Author TJ Ibbs, 8 August 1986
C
C to change both the LH and RH sides of a CP
C - returns TRUE if it fails
C
	IMPLICIT NONE
C
C **********************************************************************
C common files
C
	INCLUDE	'HERE:ENTRY.CMN'		! for CP
	INCLUDE	'HERE:CPEDIT.CMN'
C
C functions
C
	LOGICAL		CHECK_EOL	! check for rubbish at eol
	LOGICAL		PROMPT_CP	! prompt for new point
C
C workspace
C
	REAL		T$CP(4,4)	! temporary CP
	INTEGER		I,J	    	! for DO loops
C
C **********************************************************************
C assume that we fail
C
	EDIT_CP_BOTH = .TRUE.
C
C check that there is no data at the end of line
C
	IF ( CHECK_EOL() ) GOTO 9999
C
C note what sort of edit this is
C
	TYPE_OF_EDIT = CP_BOTH
C
C transfer the old values to T$CP
C - this is because PROMPT_CP won't change values in the output that
C   the user has not altered
C
	DO 100 I = 1,4
	   DO 50 J = 1,4
	      T$CP(J,I) = CP(J,I)
50	   CONTINUE
100	CONTINUE
C
C prompt for a new value for each of the original numbers
C
	CALL WRITEF(
     &	'Now give a new value for each of the original (left) control points')
C
	DO 200 I = 1,4
	   IF ( PROMPT_CP( I, T$CP(1,I) ) ) GOTO 9999
200	CONTINUE
C
C prompt for a new value for each of the target numbers
C
	CALL WRITEF(
     &	'And give a new value for each of the target (right) control points')
C
	DO 300 I = 1,4
	   IF ( PROMPT_CP( I, T$CP(3,I) ) ) GOTO 9999
300	CONTINUE
C
C so we have new values for all four corners - place them back into
C the CP entry
C
 	DO 450 I = 1,4
	   DO 400 J = 1,4
	      CP(J,I) = T$CP(J,I)
400	   CONTINUE
450	CONTINUE
C
	EDIT_CP_BOTH = .FALSE.		! success
C
9999	RETURN
	END
