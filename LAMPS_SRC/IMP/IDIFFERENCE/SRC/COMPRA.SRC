	LOGICAL FUNCTION COMPRA( ABSENT )
C
***	IDENT	03FE87
***	MODULE	COMPRA
C
C Copyright LaserScan Laboratories Ltd., Cambridge, England
C Modified by TJ Ibbs for current IDIFFERENCE, 9 December 1986
C
C Reads and compares the RA entries in the input files. If they are
C different then it will output them.
C
C This function also calls the routine that calculates the tolerance
C to be used in comparing point features - this tolerance is determined
C from the ranges of the two files
C
C The function returns TRUE if the RA entries are different,
C and sets ABSENT to TRUE if they are not there (in one file or
C the other)
C
	IMPLICIT NONE
C
***	COMMON/HERE:CMPIFF/
***	COMMON/HERE:FILES/
***	COMMON/HERE:PARAM/
C
C Arguments
C
	LOGICAL		ABSENT		! do we have two RA entries?
C
C Local variables:
C
	INTEGER*2	IELEN
	INTEGER		I,J		! loop counter
	REAL		RA(4,2)		! range array
	LOGICAL		HAD_RA(2)
	LOGICAL		ALREADY
C
C
C assume that they match, and are both present
C
	COMPRA = .FALSE.
	ABSENT = .FALSE.
C
	ALREADY = .FALSE.
C
C first, read in the RA entries
C
	DO 10 I = 1, 2
 	   CALL IFFSEL(LUN(I))		! select an input file
	   CALL IFFRWD			! go back to its start
	   CALL IFFNXC('RA',IELEN)	! find the RA
	   IF (IELEN.EQ.-1) THEN
	      HAD_RA(I) = .FALSE.	! no such entry
	      IF (FULL) THEN
		 CALL PREHDR(' ',ALREADY)
		 CALL FLWRTF('RA (RAnge)' //
     &			     ' entry missing in file %N',I)
	      ENDIF
	      GOTO 10
	   ELSE
	      HAD_RA(I) = .TRUE.	! found the RA
	   ENDIF
	   CALL EIHRR(RA(1,I),4,1)	! read the entry
10	CONTINUE
C
C if they don't both have the entry, we're finished
C (note that for RAnge entries we don't worry about whether both
C  files have the RA missing or not)
C
	IF ( .NOT.HAD_RA(1) .OR.  .NOT.HAD_RA(2) ) THEN
	   ABSENT = .TRUE.		! note their absence
	   COMPRA = .TRUE.		! so we failed
	   GOTO 60			! so return
	ENDIF
C
C otherwise, we can calculate the overall range, and thus get the
C tolerance we will compare point features with
C
	CALL GET_RANGE( RA )
C
C and now see if the entries are different
C
	DO 20 I = 1,4
	   IF  ( ABS(RA(I,1)-RA(I,2)).GT.REAL_TOL ) GOTO 30
20	CONTINUE
	GOTO 60
C
C now print out the mismatched RAs
C
30	COMPRA = .TRUE.
	IF (FULL) THEN
	   CALL PREHDR(' ',ALREADY)
	   DO 50 J = 1, 2
	      IF ( HAD_RA(J) ) THEN
	         CALL FLWRTF('RA (RAnge) entry in file %N',J)
	         CALL FLWRTF(' %F12.4 %F12.4 %F12.4 %F12.4',
     &			       RA(1,J),RA(2,J),RA(3,J),RA(4,J))
	      ENDIF
50	   CONTINUE
	ENDIF
C
60	RETURN
	END
