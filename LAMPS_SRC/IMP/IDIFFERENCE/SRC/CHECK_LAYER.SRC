	SUBROUTINE CHECK_LAYER
C
***	IDENT	18DE87
***	MODULE	CHECK_LAYER
C
C Copyright LaserScan Laboratories Ltd., Cambridge, England
C
C Author TJ Ibbs, 8 December 1986
C - abstracted from CMPIFF main source file
C
C This routine compares the contents of a layer which is present in
C both input IFF files
C
	IMPLICIT NONE
C
***	COMMON/HERE:CMPIFF/		! our own common block
***	COMMON/HERE:PARAM/		! command line qualifiers
***	COMMON/HERE:OVRLAY/		! layer specific data
C
C and use the following for workspace
C
	INTEGER		I		! just for DO loops
C
C *********************************************************************
C clear the NF array - it has a total length of:
C
C	4 bytes/longword * NFBIG longwords/file * 2 files
C
	CALL VIOCLR( NFPOSN(1,1), 4*2*NFBIG )
C
C get the next layer number, and which files it is in
C
	CALL GETNOV(.FALSE.)
C
C if we processing according to OS protocol, we must ignore
C certain layers
C
	IF ( OS ) THEN
	   IF ( LAYER.EQ.0 .OR. LAYER.EQ.11
     &			   .OR. LAYER.EQ.32 ) THEN
	      IF (FULL) THEN
		 CALL FLWRTF
	         CALL FLWRTF('Ignoring layer %^W%N',LAYER)
	      ENDIF
	   GOTO 9000		! return at once
	   ENDIF
	ENDIF
C
C report the layer number?
C
	IF (FULL) THEN
	   CALL FLWRTF
	   CALL FLWRTF('Comparing layer %^W%N',LAYER)
	ENDIF
C
C now gather the FSN pointers for all features in this overlay
C
	DO 700 I = 1,2
C
C	 GETNOV will have set LAYER_PRESENT for whether the overlay
C	 is present in both files - note that it is present in at
C	 least one of them!
C
	   IF (LAYER_PRESENT(I)) THEN
	      MAXNF(I) = -65536
	      MINNF(I) =  65536
	      CALL READIN( I )
    	   ENDIF
C
700	CONTINUE
C
C if the layer is present in both files, then we must compare it
C if it is absent from one, then we must count it as present/absent
C
	IF ( LAYER_PRESENT(1) .AND. LAYER_PRESENT(2) ) THEN
	   CALL DIFFNF
	ELSEIF ( .NOT.LAYER_PRESENT(1) ) THEN
	   IF (FULL) CALL FLWRTF(
     &			'Layer %^W%N not present in file 1',LAYER)
	   LAYEXTR = LAYEXTR+1		! it is EXTRA in file 2
	ELSEIF ( .NOT.LAYER_PRESENT(2) ) THEN
	   IF (FULL) CALL FLWRTF(
     &			'Layer %^W%N not present in file 2',LAYER)
	   LAYMISS = LAYMISS+1		! it is MISSING in file 2
	ENDIF
C
C now perform any required output to the plot files
C
	IF (PLOT) THEN
	   DO 8000 I = 1,3
	      CALL WRPLOT(I)
8000	   CONTINUE
	ENDIF
C
9000	RETURN
	END
