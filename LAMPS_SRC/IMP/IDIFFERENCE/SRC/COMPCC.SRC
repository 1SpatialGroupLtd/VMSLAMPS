	LOGICAL FUNCTION COMPCC
C
***	IDENT	11DE86
***	MODULE	COMPCC
C
C Copyright LaserScan Laboratories Ltd., Cambridge, England
C Author : J Price Williams, 19 September 1986
C Modified by TJ Ibbs, 9 December 1986, for current IDIFFERENCE
C
C	Reads and compares the CC entries in the input files
C	- if they are different then it will output them
C	
	IMPLICIT NONE
C
***	COMMON/HERE:CMPIFF/
***	COMMON/HERE:FILES/
***	COMMON/HERE:PARAM/
C
C Local variables:
C
	INTEGER*2	IELEN
	INTEGER		I,J			! loop counter
	REAL		CC(10,2,2)		! CC array
	LOGICAL		HAD_CC(2)
	LOGICAL		ALREADY
C
C
C assume that they match
C
	COMPCC = .FALSE.
C
	ALREADY = .FALSE.
C
C and read them in from each file
C
	DO 10 I = 1, 2
	   CALL IFFSEL(LUN(I))		! select an input file
	   CALL IFFRWD			! start at the start
	   CALL IFFNXC('CC',IELEN)	! find the CC
	   IF (IELEN.EQ.-1) THEN
	      HAD_CC(I) = .FALSE.	! no CC to be found
	      IF (FULL) THEN
		 CALL PREHDR(' ',ALREADY)
		 CALL FLWRTF('CC (Calibration Coefficients)' //
     &			     ' entry missing in file %N',I)
	      ENDIF
	      GOTO 10
	   ELSE
	      HAD_CC(I) = .TRUE.		! got the CC
	   ENDIF
	   CALL EIHRR(CC(1,1,I),20,1)		! read the CC
10	CONTINUE
C
C were they in both files?
C
	IF ( .NOT.HAD_CC(1) .AND. .NOT.HAD_CC(2) ) GOTO 9999
	IF ( .NOT.HAD_CC(1) .OR.  .NOT.HAD_CC(2) ) THEN
	   COMPCC = .TRUE.
	   GOTO 9999
	ENDIF
C
C are the entries the same?
C
	DO 20 I = 1, 10
	   IF ( ABS(CC(I,1,1) - CC(I,1,2)).GT.REAL_TOL .OR.
     &	        ABS(CC(I,2,1) - CC(I,2,2)).GT.REAL_TOL
     &	      ) GOTO 30
20	CONTINUE
	GOTO 9999
C
C print them out, as they are different
C
30	COMPCC = .TRUE.
	IF (FULL) THEN
	   CALL PREHDR(' ',ALREADY)
	   DO 50 J = 1, 2
	      IF ( HAD_CC(J) ) THEN
	         CALL FLWRTF(
     &		   'CC (Calibration Coefficients) in file %N',J)
	         DO 40 I = 1,10
	            CALL FLWRTF('  %E  %E',CC(I,1,J),CC(I,2,J))
40	         CONTINUE
	      ENDIF
50	   CONTINUE
	ENDIF
C
9999	RETURN
	END
