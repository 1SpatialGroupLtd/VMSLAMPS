C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1987-09-29 17:26:04.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL*4 FUNCTION DESCHK()
C
***	MODULE	DESCHK
***	IDENT	29SE87
C
C	Copyright Laser Scan Laboratories, Cambridge, England.
C
CMOD	Check on projection zone replace by check on all
C	projection parameters in MD2PAR			TJH 29-Sep-1987
C	Author						TJH 06-Feb-1986
C
C Routine to check the contents of the Map Descriptor attribute
C arrays set up in /MDESC/ to ensure that all the input files share
C common characteristics. Routine will issue warning messages only
C and will not terminate IMERGE execution. It is left up to the user to
C interpret the significance of the warnings. 
C 
C Function returns one of 2 condition codes:
C
C	SS$_NORMAL		-   all is well
C	IMERGE__CONFMD		-   conflicting map descriptors
C
	IMPLICIT NONE
C	
	LOGICAL		HAD_WARNING  ! already warned about this file?
	INTEGER*4	I	     ! loop counter
	INTEGER*4	IFIL	     ! file number
C
	INCLUDE '($SSDEF)'
	INCLUDE 'LSL$CMNIMERGE:PARAMS.PAR'
	INCLUDE 'LSL$CMNIMERGE:IMERGEMSG.PAR'
	INCLUDE 'LSL$CMNIMERGE:COMMAND.CMN'
	INCLUDE 'LSL$CMNIMERGE:DESCK.CMN'
C
C ......................................................................
C
C Assume the best
	DESCHK=SS$_NORMAL
C
C If only one input file there is no point in going on
	IF (NUMPAR.EQ.1.OR.MDTYPE.NE.2) GOTO 40		! return
C
C Check map projection
	DO 30 IFIL=2,NUMPAR
	   HAD_WARNING=.FALSE.
	   IF (FLPROJ(IFIL).NE.FLPROJ(1)) THEN
	      DESCHK=IMERGE__CONFLMD
	      CALL LSL_PUTMSG(DESCHK)
	      CALL LSL_ADDMSG(IMERGE__CONFLPROJ
     &				,INFILE(IFIL)(:LENFIL(IFIL)))
	      HAD_WARNING=.TRUE.
	   ENDIF
C
C Check projection dependent parameters
	   DO 10 I=1,15
	      IF (FLPAR(IFIL,I).NE.FLPAR(1,I)) THEN
	         DESCHK=IMERGE__CONFLMD
	         IF (.NOT.HAD_WARNING) CALL LSL_PUTMSG(DESCHK)
	         CALL LSL_ADDMSG(IMERGE__CONFLMDPAR
     &				,INFILE(IFIL)(:LENFIL(IFIL)))
	         GOTO 20
	      ENDIF
10	   CONTINUE
C
C Check spheroid
20	   IF (FLSPHE(IFIL).NE.FLSPHE(1)) THEN
	      DESCHK=IMERGE__CONFLMD
	      IF (.NOT.HAD_WARNING) CALL LSL_PUTMSG(DESCHK)
	      CALL LSL_ADDMSG(IMERGE__CONFLSPHERE
     &				,INFILE(IFIL)(:LENFIL(IFIL)))
	   ENDIF
C
C Check units
	   IF (FLUNIT(IFIL).NE.FLUNIT(1)) THEN
	      DESCHK=IMERGE__CONFLMD
	      IF (.NOT.HAD_WARNING) CALL LSL_PUTMSG(DESCHK)
	      CALL LSL_ADDMSG(IMERGE__CONFLUNIT
     &				,INFILE(IFIL)(:LENFIL(IFIL)))
	   ENDIF
30	CONTINUE
40	RETURN
	END
