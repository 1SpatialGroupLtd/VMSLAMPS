C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1988-09-05 11:09:40.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION RDANG(SECS,FLG)
C
***	MODULE RDANG
***	IDENT 05SE88
C
C	Function that reads an angle in ddd mm ss.ss format
C	and returns the angle in seconds (as a R*8)
C
C	Function returns .FALSE. if valid angle returned, and .TRUE.
C	otherwise. The reason for invalid angles are returned in FLG.
C
C	Values of FLG  -  see READANG.PAR
C
	IMPLICIT	NONE
C
***	PARAMETER /HERE:ITRANSMSG/
***	PARAMETER /LSL$CMNLSL:READANG/
C
C arguments (output)
C
	REAL*8		SECS	! angle returned in seconds of arc
	INTEGER*4	FLG
C
C functions
C
	LOGICAL		LSL_RDDBLE_CMPT
	LOGICAL		RDCHS
C
C working storage
C
	REAL*8		DEG,MIN,SEC	! elements of angle
	REAL*8		SIGN
	LOGICAL		HADSIGN
	LOGICAL		HADTERM
	LOGICAL		HADHEMI
	LOGICAL		HAD_DOT
	LOGICAL		DUMMY_SIGN
	BYTE		TERM
	REAL		TOTANG
C
C initialise
C
	RDANG=.TRUE.			! assume failure
	FLG=READANG_EOL			! with blank line
	DUMMY_SIGN=.FALSE.		! LSL_RDDBLE_CMPT doesn't test
	HADSIGN=.FALSE.
	HADTERM=.FALSE.
	HADHEMI=.FALSE.
	SECS=0.0
	DEG=0.0
	MIN=0.0
	SEC=0.0
	SIGN=+1.0
C
C read next character from buffer
C
	IF (RDCHS(TERM)) THEN
	   GOTO 999			! end of line
	ELSE
	   IF (TERM.GE.'0'.AND.TERM.LE.'9') THEN	! numeric
	      CALL BSCH
	   ELSEIF (TERM.EQ.'-'.OR.TERM.EQ.'+') THEN	! sign
	      HADSIGN=.TRUE.
	      IF (TERM.EQ.'-') SIGN=-SIGN
	   ELSE
	      CALL BSCH			! put character back in buffer
	      FLG=READANG_ILLEGCHAR	! invalid character
	      CALL LSL_PUTMSG(ITRANS__UNEXPCH,TERM)
	      GOTO 999
	   ENDIF
	ENDIF
C
C read degrees
C
	HAD_DOT=.FALSE.
	IF (LSL_RDDBLE_CMPT(DEG,DUMMY_SIGN,HAD_DOT)) GOTO 800	! eol
C
C see if there is a terminator
C
	IF (RDCHS(TERM)) THEN		! end of line and end of angle
	   GOTO 800
	ELSE
	   IF (TERM.GE.'0'.AND.TERM.LE.'9') THEN	! minutes
	      IF (HAD_DOT) THEN			! decimal degrees!!!
	         FLG=READANG_ILLEGDECPT		! invalid format
	         CALL LSL_PUTMSG(ITRANS__ILLEGDECPT)
	         CALL BSCH			! put it back
	         GOTO 999
	      ELSE
	         CALL BSCH
	      ENDIF
	   ELSE					! angle terminated
	      HADTERM=.TRUE.
	      GOTO 800
	   ENDIF
	ENDIF
C
C read the minutes (cannot in fact be eol!)
C
	HAD_DOT=.FALSE.
	IF (LSL_RDDBLE_CMPT(MIN,DUMMY_SIGN,HAD_DOT)) GOTO 800	! eol
C
C see if there is a terminator
C
	IF (RDCHS(TERM)) THEN		! end of line and end of angle
	   GOTO 800
	ELSE
	   IF (TERM.GE.'0'.AND.TERM.LE.'9') THEN	! minutes
	      IF (HAD_DOT) THEN			! decimal minutes!!!
	         FLG=READANG_ILLEGDECPT		! invalid format
	         CALL LSL_PUTMSG(ITRANS__ILLEGDECPT)
	         CALL BSCH			! put it back 
	         GOTO 999
	      ELSE
	         CALL BSCH
	      ENDIF
	   ELSE					! angle terminated
	      HADTERM=.TRUE.
	      GOTO 800
	   ENDIF
	ENDIF
C
C read seconds (cannot in fact be end of line)
C	
	HAD_DOT=.FALSE.
	IF (LSL_RDDBLE_CMPT(SEC,DUMMY_SIGN,HAD_DOT)) GOTO 800	! eol
C
C check terminating character
C
	 IF (RDCHS(TERM)) THEN
	   GOTO 800			! eol
	ELSE
	   IF (TERM.GE.'0'.AND.TERM.LE.'9') THEN
	      CALL BSCH			! number
	   ELSE
	      HADTERM=.TRUE.
	   ENDIF
	ENDIF
C
C now sort out angle
C
  800	CONTINUE
	HADHEMI=HADTERM.AND.
     &	        (TERM.EQ.'E'.OR.TERM.EQ.'W'.OR.
     &	         TERM.EQ.'N'.OR.TERM.EQ.'S'.OR.
     &	         TERM.EQ.'e'.OR.TERM.EQ.'w'.OR.
     &	         TERM.EQ.'n'.OR.TERM.EQ.'s')
C
C put terminating character back in buffer, if it is not hemisphere
C
	IF (HADTERM.AND..NOT.HADHEMI) CALL BSCH
C
C check for hemisphere and sign
C
	IF (HADHEMI.AND.HADSIGN) THEN
	   FLG=READANG_SGNANDHEMI
	   CALL LSL_PUTMSG(ITRANS__SGNANDHEMI)
	   GOTO 999
	ENDIF
C
C check for valid angular components
C
	IF (.NOT.HADHEMI) THEN
	   TOTANG=360.0
	ELSE
	   IF (TERM.EQ.'E'.OR.TERM.EQ.'W'.OR.TERM.EQ.'e'.OR.TERM.EQ.'w')
     &	                        THEN
	      TOTANG=180.0
	   ELSE
	      TOTANG=90.0
	   ENDIF
	ENDIF
C
	IF ((HADHEMI.AND.DEG.GT.TOTANG).OR.		! 180 or 90 OK
     &	    (.NOT.HADHEMI.AND.DEG.GE.TOTANG).OR.	! 360 too big
     &	    (DEG.EQ.TOTANG.AND.(MIN.NE.0.0.OR.SEC.NE.0.0)).OR.
     &	    (MIN.GE.60.0).OR.
     &	    (SEC.GE.60.0)) THEN
	   FLG=READANG_ILLEGANGVAL
	   CALL LSL_PUTMSG(ITRANS__ILLEGANGVAL)
	   GOTO 999
	ENDIF
C
C get hemishpere
C
	IF (.NOT.HADHEMI) THEN
	   FLG=READANG_ANGLE
	ELSEIF (TERM.EQ.'E'.OR.TERM.EQ.'e'.OR.
     &	        TERM.EQ.'W'.OR.TERM.EQ.'w') THEN
	   FLG=READANG_LONGITUDE			! longitude
	ELSE
	   FLG=READANG_LATITUDE				! latitude
	ENDIF
C
C get sign from hemisphere
C
	IF (HADHEMI.AND.
     &	   (TERM.EQ.'W'.OR.TERM.EQ.'w'.OR.
     &	    TERM.EQ.'S'.OR.TERM.EQ.'s')) SIGN=-SIGN
C
C construct angle and return
C
	SECS=SIGN*(DEG*3600.0+MIN*60+SEC)
	RDANG=.FALSE.
C
  999	CONTINUE
	RETURN
	END
