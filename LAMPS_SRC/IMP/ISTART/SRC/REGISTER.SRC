C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1988-03-01 13:49:14.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
        SUBROUTINE REGISTER
C
***	MODULE	REGISTER
***	IDENT	01MR86
C
C 	Copyright Laser-Scan Laboratories Ltd.,England
C
CMOD	Option to generate no registration marks added	TJH 01-Mar-1988
C	Created						TJH 29-Sep-1986
C
C Routine to generate as IFF features to act as registration marks for
C each control point. Registration marks may either be:
C      1)  single point symbols, or,
C      2) "L" shaped ticks (default).
C
	IMPLICIT NONE
C
C Parameters:
C
	INCLUDE	'LSL$CMNLSL:LSLLIBMSG.PAR'
	INCLUDE	'LSL$CMNISTART:ISTARTMSG.PAR'
C
	INTEGER*4 X,Y			! coordinate array indices
	PARAMETER (X=1)
	PARAMETER (Y=2)
C
C
C Word:
	INTEGER*2	IDATA(4)	! for FS entry
	DATA 		IDATA/4*0/
	INTEGER*2	ISEQ		! for NF internal sequence no.
C
C Longword:
	INTEGER		I		! loop variable
C
C Real:
	REAL		TIKPOS(2,3)	! reg. tick coordinate(s)
	REAL		SYMPOS(2)	! reg. cross coordinate(s)
	REAL		TIKLEN		! length of tick side
C
C Common:
	INCLUDE 'LSL$CMNISTART:COMMAND.CMN'
	INCLUDE 'LSL$CMNISTART:IFF_ENT.CMN'
	INCLUDE 'LSL$CMNIFF:IFFHAN.CMN'
C
C ......................................................................
C
C
C The user had the option to specify the feature code to be used for
C the registration mark features. 
C
	IF (HAD_FC) THEN
	  IDATA(1)=USERFC		! supplied via /FEATURE_CODE
	ENDIF
C
C Set the graphical type of the registration features to accord with 
C their tick (linear graphical type) or cross (symbol graphical type)
C status
C
	IF (TICKS) THEN
	  IDATA(3)='0000'X		! linear
	ELSE
	  IDATA(3)='4000'X		! symbol
	ENDIF
C
C ......................................................................
C
C Either write:   "L"   S H A P E D   T I C K S
C
	IF (TICKS) THEN
C
C Calculate the length of the tick "arms" to be 5% of map X-axis
C
	  TIKLEN=(CPARRY(X,3)-CPARRY(X,2))/20.0	
C
C Define the 3 coordinates for each tick in turn
	  DO 60 I=1,4
C
C First set the mid-point of the tick onto the control point position.
C
	    TIKPOS(X,2)=CPARRY(X,I)
	    TIKPOS(Y,2)=CPARRY(Y,I)
C
C Then define the 2 other points for each tick depending on which
C control point it represents
C
	    GOTO (10,20,30,40),I
C
C Top left:
10	    TIKPOS(X,1)=CPARRY(X,1)+TIKLEN
	    TIKPOS(Y,1)=CPARRY(Y,1)
	    TIKPOS(X,3)=CPARRY(X,1)
	    TIKPOS(Y,3)=CPARRY(Y,1)-TIKLEN
	    GOTO 50
C
C Bottom left:
20	    TIKPOS(X,1)=CPARRY(X,2)
	    TIKPOS(Y,1)=CPARRY(Y,2)+TIKLEN
	    TIKPOS(X,3)=CPARRY(X,2)+TIKLEN
	    TIKPOS(Y,3)=CPARRY(Y,2)
	    GOTO 50
C
C Bottom right:
30	    TIKPOS(X,1)=CPARRY(X,3)-TIKLEN
	    TIKPOS(Y,1)=CPARRY(Y,3)
	    TIKPOS(X,3)=CPARRY(X,3)
	    TIKPOS(Y,3)=CPARRY(Y,3)+TIKLEN
	    GOTO 50
C
C Top right:
40	    TIKPOS(X,1)=CPARRY(X,4)
	    TIKPOS(Y,1)=CPARRY(Y,4)-TIKLEN
	    TIKPOS(X,3)=CPARRY(X,4)-TIKLEN
	    TIKPOS(Y,3)=CPARRY(Y,4)
C
C Generate an IFF feature for the tick
C
50	    ISEQ=0
	    CALL IFFNF(I,ISEQ)
	    CALL IFFFS(IDATA)
	    CALL IFFTH(0)
	    CALL IFFST(TIKPOS,3,1)
	    CALL IFFEF
60	  CONTINUE

	ELSEIF (SYMBOLS) THEN
C
C ......................................................................
C
C or: 	R E G I S T R A T I O N    S Y M B O L S
C
	  DO 70 I=1,4
C
C For each control point in turn...
C 
	    SYMPOS(X)=CPARRY(X,I)
	    SYMPOS(Y)=CPARRY(Y,I)
C
C Generate an IFF feature for the cross symbol
C
	    ISEQ=0
	    CALL IFFNF(I,ISEQ)
	    CALL IFFFS(IDATA)
	    CALL IFFTH(0)
	    CALL IFFST(SYMPOS,1,1)
	    CALL IFFEF
70	  CONTINUE	
	ENDIF
	RETURN
	END
