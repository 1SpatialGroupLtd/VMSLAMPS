C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1986-06-20 13:44:32.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE SMOOTH(P,NP,T,NT,MAXP,SMOOTH_TOL,LOOP,IW,RW,
     &			  CENTRELINE)
C
***	IDENT	09AP86
***	MODULE	SMOOTH
C
C	Copyright : Laser-Scan Laboratories Ltd., Cambridge, England
C	Created   : ACM					Nov 1985
C
	IMPLICIT NONE
C
C local parameters
	REAL		HUG		! max dist of curve
	PARAMETER(	HUG = 1.0)	! from guide pts
C
C parameters in
	INTEGER		NP		! No of pts in input line P
	INTEGER		MAXP		! max No of pts in P or T
	REAL		P(2,MAXP)	! line to be smoothed
	REAL		SMOOTH_TOL	! lateral tolerance for Bezier
	LOGICAL		LOOP		! line is closed loop?
	LOGICAL		CENTRELINE	! centre-line?
C
C parameters out
	INTEGER		NT		! No of pts in output line T
	REAL		T(2,MAXP)	! smoothed line
C
C workspace
	INTEGER		I,J,K		! misc integers
	INTEGER		IW(NP)		! workspace for THIN routine
	REAL		RW(MAXP)	! workspace for BEZIER routine
	REAL		LEN
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C	Creates smooth curve T through points in P
C
C	P must have .GE. 2 pts
C
C	*** Uses P as workspace ***
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C if P has only 2 pts it must be a straight line and T = P
	IF (NP.EQ.2) THEN
10	  T(1,1) = P(1,1)
	  T(2,1) = P(2,1)
	  T(1,2) = P(1,2)
	  T(2,2) = P(2,2)
	  NT = 2
	  GOTO 40
	END IF
C
C if centreline then use original points as guide points
	IF (CENTRELINE) GOTO 30
C
C if link has length < 9 then use original pts as guide pts
	LEN = 0
	DO 20 I = 2,NP
20	LEN = LEN+ABS(P(1,I)-P(1,I-1))+ABS(P(2,I)-P(2,I-1))
	IF (LEN.LE.8) GOTO 30
C
C remove consecutive coincident pts & interpolate segments longer than 1
	CALL SPCPTS(P,NP,T,NT,MAXP,LOOP)
	IF (NT.LT.3) GOTO 40
C
C obtain guide pts
	CALL AVPTS(T,NT,P,NP,LOOP)
	CALL THIN(P,NP,T,NT,SMOOTH_TOL,IW)
	DO 25 I = 1,NT
	  P(1,I) = T(1,I)
	  P(2,I) = T(2,I)
25	CONTINUE
	NP = NT
	IF (NP.EQ.2) GOTO 10
C
C interpolate guide pts
30	CALL BEZIER(P,NP,T,NT,MAXP,SMOOTH_TOL,HUG,LOOP,RW)
C
40	END
