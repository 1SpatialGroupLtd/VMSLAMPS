        LOGICAL FUNCTION FLUSH_LINE(THIS_FSN,THIS_ISN)
C
***     IDENT	01MR95
***     MODULE  FLUSH_LINE
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
CMOD    Only output ONEPTST message when there is one	ST	28-Feb-1995
C	point in whole feature not just this ST as it
C	may be a valid ST 1 0 entry (ie invisible move)
C	Mod 1472.
CMOD	Last edited					 DM	03-Dec-1986
C	Created 					RJH	27-Oct-1986
C
C	IMP IFILTER Program - Output line string to IFF file
C
C Description
C
C
        IMPLICIT NONE
C
C Included Parameter Files
C
***	PARAMETER/LSL$CMNIFILT:IFILTMSG/	! message definitions
C
C Local Parameters
C
	INTEGER*4	MAXST		! max. no. of ST points
	PARAMETER	(MAXST = 200)
C
C Arguments
C
	INTEGER*2	THIS_FSN	! FSN for this feature
	INTEGER*2	THIS_ISN	! ISN for this feature
C
C Common Blocks
C
***	COMMON/LSL$CMNIFILT:CONTROL/	! for POINTS_IN_FEAT
***	COMMON/LSL$CMNIFILT:LINES/	! input and output line strings
***	COMMON/LSL$CMNIFF:IFFHAN/	! current position in IFF file
C
C Functions called
C
	LOGICAL		ST_OUT		! outputs a ST entry
C
C Local variables
C
	INTEGER*4	FIRST		! first point to output
	INTEGER*4	LAST		! last point to output
	INTEGER*2	NUM_PTS		! number of points to output
	INTEGER*2	IENDS		! pen up/pen down flag
	LOGICAL		OK		! everything OK ?
C
C Initialisation
C
	FIRST = 1
	NUM_PTS = 0
	IENDS = 0
C
C Output the next ST entry
C
100	FIRST = FIRST + NUM_PTS
	NUM_PTS = MIN(NUM_OUT-(FIRST-1),MAXST)
	IF (NUM_PTS .GT. 0) THEN
	   OK = ST_OUT(LINE_OUT(1,FIRST),NUM_PTS,IENDS)
	ENDIF
C
C Repeat if enough ST's have not yet been output
C
	IENDS = 1
	IF (OK .AND. (NUM_PTS .EQ. MAXST)) GO TO 100
C
C Warn if there was only one point 
C
	IF (OK) THEN
	   IF (POINTS_IN_FEAT .EQ. 1)
     &	      CALL LSL_PUTMSG(IFILT__ONEPTST,THIS_FSN,THIS_ISN)
	ENDIF
C
C Assign function value
C
	FLUSH_LINE = OK
C
C Return
C
	RETURN
        END
