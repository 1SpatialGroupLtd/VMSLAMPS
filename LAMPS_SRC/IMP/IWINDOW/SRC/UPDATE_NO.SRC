C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1988-05-23 14:44:54.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION UPDATE_NO(SUPPRESS)
C
***	IDENT	23MY88
***	MODULE	UPDATE_NO
C
C	Copyright LaserScan Laboratories Ltd., Cambridge, England
C
C	Created					RJH	02-Dec-1986
C
C	IMP IWINDOW Program - Update layer information and NO entry
C
C Description
C
C	It is expected that the input IFF file is currently selected,
C	possibly with an entry in hand.
C
	IMPLICIT NONE
C
C Included Parameter Files
C
***	PARAMETER/LSL$CMNIWIN:IWINMSG/	! message definitions
***	PARAMETER/LSL$CMNIWIN:LUN/	! unit numbers
***	PARAMETER/LSL$CMNIWIN:MAX/	! maximum things
C
C Arguments
C
	LOGICAL		SUPPRESS	! current layer suppressed ?
C
C Common Blocks
C
***	COMMON/LSL$CMNIFF:IFFHAN/	! current position in IFF file
***	COMMON/LSL$CMNIWIN:COPY/	! transfer for output
***	COMMON/LSL$CMNIWIN:PARAMS/	! control parameters
C
C Functions called
C
	LOGICAL		COPY_TC		! copy TC entries
C
C Local Variables
C
	INTEGER*4	I		! loop variable
	LOGICAL		OK		! everything OK ?
C
C initialisation
C
	OK = .TRUE.
C
C firstly output data about this layer, if it is wanted
C
	IF (HADLOG) THEN
	   IF (SUPPRESS) THEN
	      CALL FLWRTF('Layer %^W%N:  clipping suppressed',LAYER)
	   ELSE
	      CALL EXPAND('Layer %^W%N: %^L%N feature%m deleted,',
     &		 LAYER,DELNF)
	      CALL FLWAPP(' %N clipped and %N created',
     &	         OCLIPD,CRENF(LAYIND))
	   ENDIF
	   DO 106 I=1,NUMFC
	      IF (OFCSUP(I).GT.0) THEN
	         CALL EXPAND('          Suppressed clipping ')
	         CALL FLWAPP('%N feature%m with feature code %^W%N',
     &						OFCSUP(I),FCTAB(I))
	      ENDIF
106	   CONTINUE
	   IF (OTEXTS.GT.0)
     &	      CALL FLWRTF('          %N external text%m kept', OTEXTS)
	   IF (.NOT.CLPGRD.AND.OGRIDS.GT.0)
     &	      CALL FLWRTF('          %N grid%m unchecked', OGRIDS)
	   CALL FLWRTF(' ')
	ENDIF
C
C Update the totals
C
	TEXTS  = TEXTS+OTEXTS
	TOTDEL = TOTDEL+DELNF
	TOTCLP = TOTCLP+OCLIPD
C
C If there were any TC or CH entries before it then they should be kept
C
	IF (HADTC) THEN
	   IF (COPY_TC()) GOTO 900
	ENDIF
C
C now, we are about to write the EO, but we must go back to the
C NO and copy over the EO pointer, so that it points to here
C correctly
C
	CALL IFFSEL(OUTLUN)		! select the output file
	CALL IFFUNO(LPOSNO)		! update NO entry
	IF (IERCD.NE.0) GOTO 500
	CALL IFFEO			! output the EO
	CALL IFFSEL(INLUN)		! back to the input file
	GOTO 999
C
C Error messages
C
500	CALL LSL_PUTMSG(IWIN__ERRPOSNO,LPOSNO)
C
C Error exit
C
900	OK = .FALSE.
C
C Assign function value
C
999	UPDATE_NO = (.NOT. OK)
C
C Return
C
	RETURN
	END
