	LOGICAL FUNCTION CLIP(X1,Y1,Z1,M1,X2,Y2,Z2,M2,NEEDZ)
C
***	MODULE	CLIP
***	IDENT	26SE88
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Authors many and varied but mainly Paul Hardy based on algorithms
C by P. Woodsford and P.Cross, with tidies by R.Fairbairns
C
C Created July 1980
C
C copied into CLIP directory by TJ Ibbs 19 Feb 82, for use by
C the map clipping routine OSCLIP.
C
C copied from old OSCLIP, amended to interpolate height for IMP IWINDOW
C by R J Hulme, 22-May-1986
C
C to clip a straight line to rectangular boundaries.
C function value is TRUE if no visible result.
C
	IMPLICIT NONE
C
	REAL X1,Y1,Z1,X2,Y2,Z2	! coords of ends of line
	INTEGER M1,M2		! 0,1=> end is unchanged,changed
	LOGICAL	NEEDZ		! true if to interpolate height
C                        
	INCLUDE	'LSL$CMNIFF:CB.STR'	! record structure definition
C
***	COMMON/LSL$CMNIWIN:CLIP/	! contains boundaries
C
	PARAMETER X=1,Y=2,Z=3	! for clarity
C w/s
	REAL P(3,2)		! our copies of the points
	REAL D(3)		! DX,DY,DZ is line vector
	REAL R			! temp variable
	INTEGER M(2)		! copy of ends info
	INTEGER DEP,INDEP	! X or Y depending which axis considered
	INTEGER HIGH,LOW	! 1 or 2 to indicate ends of line
	INTEGER Z1_INT		! new Z1 as an integer
	INTEGER Z2_INT		! new Z2 as an integer
	EQUIVALENCE	(P(Z,1),Z1_INT)
	EQUIVALENCE	(P(Z,2),Z2_INT)
C
C set everything up
	CLIP=.FALSE.			! assume visible result
	M(1)=0				! neither end moved yet
	M(2)=0
	IF(.NOT.ENCLIP) GOTO 40		! clipping disabled
	P(X,1)=X1			! take copy of args
	P(Y,1)=Y1
	P(Z,1)=Z1
	P(X,2)=X2
	P(Y,2)=Y2
	P(Z,2)=Z2
	D(X)=X2-X1			! calculate vector
	D(Y)=Y2-Y1
	IF (NEEDZ) D(Z)=Z2-Z1
C
C now loop for both axes
	DO 30 DEP=X,Y
	   INDEP=3-DEP			! independent axis
	   LOW=1			! guess which end lowest
	   IF(D(INDEP).LT.0.0) LOW=2	! upside down
	   HIGH=3-LOW			! the other end
CCCC	   IF(P(INDEP,HIGH).LT.CLIPLO(INDEP)) GOTO 50	! out the bottom
CCCC	   IF(P(INDEP,LOW ).GT.CLIPHI(INDEP)) GOTO 50	! out the top
	   IF(P(INDEP,HIGH).LE.CLIPLO(INDEP)) GOTO 50	! out the bottom
	   IF(P(INDEP,LOW ).GE.CLIPHI(INDEP)) GOTO 50	! out the top
	   IF(D(INDEP).EQ.0.0) GOTO 30	! parallel to frame this axis
C
C here to consider low end
	   R=P(INDEP,LOW)-CLIPLO(INDEP)	! distance from frame
	   IF(R.GE.0.0) GOTO 20		! inside frame
C
C clip low end
	   P(DEP,LOW)=P(DEP,LOW)-R*D(DEP)/D(INDEP) ! similar triangles
	   P(INDEP,LOW)=CLIPLO(INDEP)	! push onto frame
	   M(LOW)=1			! moved this end
C
C now lets consider the high end
20	   R=CLIPHI(INDEP)-P(INDEP,HIGH) ! distance from frame
	   IF(R.GE.0.0) GOTO 30		! inside frame
C
C clip high end
	   P(DEP,HIGH)=P(DEP,HIGH)+R*D(DEP)/D(INDEP) ! similar triangles
	   P(INDEP,HIGH)=CLIPHI(INDEP)	! push onto frame
	   M(HIGH)=1			! moved this end
C
30	   CONTINUE			! loop for both axes
C
C interpolate height for the ends clipped
	IF (M(1) .EQ. 1) THEN
	   IF (NEEDZ) THEN
	      CALL INTERP_Z(X1,Y1,Z1,P(X,1),D)
	   ELSE
	      Z1_INT = IFF_ABSENT
	   ENDIF
	ENDIF
	IF (M(2) .EQ. 1) THEN
	   IF (NEEDZ) THEN
	      D(X) = -D(X)
	      D(Y) = -D(Y)
	      D(Z) = -D(Z)
	      CALL INTERP_Z(X2,Y2,Z2,P(X,2),D)
	   ELSE
	      Z2_INT = IFF_ABSENT
	   ENDIF
	ENDIF
C
C copy args back
	X1=P(X,1)
	Y1=P(Y,1)
	Z1=P(Z,1)
	X2=P(X,2)
	Y2=P(Y,2)
	Z2=P(Z,2)
40	M1=M(1)
	M2=M(2)
	RETURN
C
C here with all invisible
50	CLIP=.TRUE.
	GOTO 40
C
C
	END
