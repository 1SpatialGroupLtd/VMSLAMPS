	SUBROUTINE CALC_AREA(X,Y,VRTX)
C
***	IDENT 07MY91
***	MODULE CALC_AREA
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Loosely based on POLYGONS library AREA	RJH	20-Jun-1988
C
C	Polygon Clipping - Calculate polygon area
C
C Description
C
C	Calculates the signed area of a polygon, through repeated calls
C
C	Final area is calculated by repeating the call with the last 
C	point as argument. 
C
C	Area is +ve if the string coords. are in anticlockwise order, 
C	-ve if clockwise.
C 
	IMPLICIT NONE
C
C Arguments
C
	REAL*8		X		! current X
	REAL*8		Y		! current Y
	INTEGER*4	VRTX		! current vertex number
C
C Common Blocks
C
	INCLUDE 'HERE:AREA_CALC.CMN'	! area calculation
C
C Local Variables
C
	REAL		XOFF		! X offset from local origin
	REAL		YOFF		! Y offset from local origin
C
C First point
C
	IF (VRTX .EQ. 1) THEN
	   XORIG = X
	   YORIG = Y
	   CUM_AREA = 0.0
C
C Second point
C
	ELSEIF (VRTX .EQ. 2) THEN
	   LAST_XOFF = X - XORIG
	   LAST_YOFF = Y - YORIG
	   LAST_VRTX = VRTX
C
C Third point
C
	ELSEIF (VRTX .NE. LAST_VRTX) THEN
	   XOFF = X - XORIG
	   YOFF = Y - YORIG
	   CUM_AREA = CUM_AREA + XOFF * YOFF +
     &	              (LAST_XOFF - XOFF) * (LAST_YOFF + YOFF) +
     &	              (-LAST_XOFF) * LAST_YOFF
	   LAST_XOFF = XOFF
	   LAST_YOFF = YOFF
	   LAST_VRTX = VRTX
C
C Calculate area
C
	ELSE
	   CUM_AREA = CUM_AREA / 2.0
	ENDIF
C
C Return
C
	RETURN
	END
