	LOGICAL FUNCTION REJECT(COMAND,VALUE)
C
***	IDENT   30SE86
***	MODULE  REJECT
C
C	Copyright LaserScan Laboratories Ltd, Cambridge, England
C
C
C	Author: Clarke Brunt
C	Modified: D Catlow for SELAC operation
C	Modified: A.T. Timms for ISELAC 
C
C Tests whether a feature is to be rejected because of its VALUE
C for the attribute associated with the command COMAND.
C Returns true if to be rejected.
C
C Summary of logic: If no restrictions, then FALSE.
C If outside an 'include' range, and not already in another,
C (INCLUDE TRUE) then mark for exclusion (SKIP TRUE).
C If inside an 'exclude' range, return TRUE immediately
C If inside an 'include' range, mark for inclusion (INCLUDE TRUE &
C							SKIP FALSE)
C If falls out of bottom of loop, return SKIP
C
	IMPLICIT NONE
C
	INCLUDE 'LSL$CMNISELAC:ATTRIB.CMN'
C
C arguments passed
	INTEGER*4	COMAND		! command number
	REAL            VALUE		! value of attribute
C
	INTEGER		I		! loop counter
	LOGICAL*1	SKIP		! marked for rejection?
	LOGICAL*1	INCLUDE		! in an included range?
C
	REJECT = .TRUE.
	SKIP = .FALSE.
	INCLUDE = .FALSE.
C
C....................................................................
C
	DO 10 I = 1,NLIM(COMAND)
	  IF ((HADLOW(I,COMAND).AND.VALUE.LT.LLIM(I,COMAND)) .OR.
     &	       ( HADUP(I,COMAND).AND.VALUE.GT.ULIM(I,COMAND)) ) THEN
	    IF (.NOT.EXCLUDE(I,COMAND).AND..NOT.INCLUDE) SKIP = .TRUE.
	  ELSE
	    IF (EXCLUDE(I,COMAND)) GOTO 999		! RETURN
	    SKIP = .FALSE.
	    INCLUDE = .TRUE.
	  ENDIF
10	CONTINUE
C
C....................................................................
	REJECT = SKIP
C
C....................................................................
999	RETURN
	END

