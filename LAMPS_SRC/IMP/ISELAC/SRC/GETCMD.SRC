	LOGICAL*4 FUNCTION GETCMD()
C
***	MODULE GETCMD
***	IDENT 15OC86
C
C	Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C
C	Author  : A.T. Timms
C	Created : 29 September 1986
C
C Command decoding function for IMP utility ISELAC. Based on IPATCH 
C PROCESS.SRC as IPATCH is the only other command driven IMP utility.
C
C It returns FALSE if a fatal error has occurred, and TRUE otherwise
C
	IMPLICIT NONE
C
C Common blocks
	INCLUDE 'LSL$CMNISELAC:ATTRIB.CMN'	! for HADGO,DOEXIT etc..
	INCLUDE 'LSL$CMNISELAC:IFFOK.CMN' 	! IFF error stuff
C
C parameter files
	INCLUDE	'LSL$CMNLSL:LSLLIBMSG.PAR'	! LSLLIB error codes
C
C functions
C LSLLIB functions
	LOGICAL		RDCHS		! read a (significant) char
	INTEGER		RDCOMM		! read a command
C
C local functions
	LOGICAL		READ_PROMPT	! read line with prompt
	LOGICAL		SELDEC		! process command
C
C workspace
        BYTE		ICH
C
C....................................................................
C assume the worst
	GETCMD = .FALSE.
C
C....................................................................
C we start by prompting the user for a command...
C (note that ^Z will be treated as EXIT)
1000	IF ( .NOT. READ_PROMPT( 'ISELAC> ', 'EXIT' ) ) GOTO 9999
C
C....................................................................
C Start at the beginning of the line returned, and read the first
C character from it. If there is nothing on the line (or only spaces
C or tabs), then ignore it and prompt again
	CALL BSLN
	IF ( RDCHS(ICH) ) GOTO 1000
C
C....................................................................
C Check whether the first character is of significance, or whether
C it may be assumed to be the start of an ordinary command.
C
C we recognise the single character command:
C
C		!	- comment - ignore this line
C
	IF ( ICH.EQ.'!' ) GOTO 1000
C
C....................................................................
C Otherwise, this should be an ordinary command.
C
C Call SELDEC to process the command. It returns TRUE if we are
C to EXIT because of an error.
 	OK = SELDEC() 
	IF (.NOT.OK) GOTO 9999			! error
	IF (DOEXIT.OR.HADGO) GOTO 9000		! normal exit or go !
C....................................................................
C get another command
	GOTO 1000
C
C....................................................................
C successful exit arrives here
9000	GETCMD = .TRUE.			! all went well
C
C....................................................................
C errors join us here
9999	RETURN
	END
