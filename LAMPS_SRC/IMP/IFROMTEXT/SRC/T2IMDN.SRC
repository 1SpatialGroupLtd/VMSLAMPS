C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1986-12-08 20:23:18.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION T2IMDN(MDNUM)
C
***	IDENT	08DE86
***	MODULE	T2IMDN
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
CMOD	Documentation completed			RJH	15-MAY-1984
C	Created					RJH	06-APR-1984
C
C Description
C
C    This function is designed for use by the IFROMTEXT program, and its
C purpose is to read the next map descriptor (MD) code in the input
C file. If not found on the current line, an MD code is expected on the
C next non-blank line.
C    The function takes the value false if successful, and true if
C i) the MD code was in error, ii) an illegal separator was encountered,
C or iii) an input file read error occurred.
C    If the function was successful, the argument returned is the
C command table sequence number for the MD code read. If unsuccessful,
C the argument will be set to zero.
C    Communication with other routines is via the common block T2IPAR.
C
	IMPLICIT NONE
C
C Arguments
C
	INTEGER*2	MDNUM		! integer to be read
C
C Common blocks
C
***	COMMON/LSL$CMNIFROMTEXT:IFROMMSG.PAR/	
***	COMMON/LSL$CMNIFROMTEXT:T2IPAR/		! global variables
C
C Functions called
C
	INTEGER*4	RDCOMM		! CMDLIB - reads a command
	LOGICAL		T2ISEP		! skips separators
	LOGICAL		RDCHS		! VIOLIB - reads a character
C
C Command Table
C
	INTEGER		T2IMCD_CMD_TABLE
	EXTERNAL	T2IMCD_CMD_TABLE
C
C Local variables
C
	LOGICAL		NEWLIN		! new line ?
	LOGICAL		EOLN		! end of line ?
	BYTE		NXTCH		! next character
C
C Start of function
C
	MDNUM = 0			! assume error in MD code
	NEWLIN = .FALSE.		! assume new line not read in
C
C Check separator is valid
C
	IF (.NOT. T2ISEP()) THEN
C
C Get ready to read MD code
C
	   EOLN = RDCHS(NXTCH)		! get next non-blank character
	   IF (EOLN) THEN		! check if end of line
	      CALL T2ILIN(.TRUE.)	! read in next input line
	      NEWLIN = .TRUE.		! new line was read in
	   ELSE
	      CALL BSCH			! back to start of MD code
	   ENDIF	! (EOLN)
C
C Read the MD code
C
	   IF (.NOT. DONE) THEN			! check can read
	      MDNUM = RDCOMM(T2IMCD_CMD_TABLE)	! read MD code
	      IF (MDNUM .LE. 0) THEN		! check if error
		 CALL LSL_PUTMSG(IFROM__UNEXPMDENT,CURLIN)
		 IF (NEWLIN) THEN		! check if new line
		    CALL BSLN			! back to start of it
		    CALL T2ITST			! test for IFF code
		    IF (GOTNXT) CALL LSL_PUTMSG(IFROM__ENDASS,CURLIN-1)
		 ENDIF 	! (NEWLIN)
	      ENDIF	! (MDNUM .LE. 0)
	   ENDIF	! (.NOT. DONE)
	ENDIF	! (.NOT. T2ISEP())
C
C Assign function value
C
	T2IMDN = (MDNUM .LE. 0)
C
C Return
C
	RETURN
	END
