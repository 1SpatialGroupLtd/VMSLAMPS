	LOGICAL*4 FUNCTION READ_NO()
C
***	MODULE READ_NO
***	IDENT 17JL98
C
C	Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C
C	Author  : A.T. Timms 				15-Jan-1987
C
CMOD	Stopped writing lines to out file twice		07-Dec-1988
C
C	Function to read NO entry of IFF file. If we have had the 
C	/LAYER qualifier then we may want to ignore this layer in which 
C	case find the next EO. 
C
C.......................................................................
C
	IMPLICIT NONE
C
C Common blocks
	INCLUDE 'LSL$CMNIFF:IFFHAN.CMN'		! IFF stuff
	INCLUDE 'LSL$CMNICHECK:ICHKCMD.CMN'	! ICHECK commands
	INCLUDE 'LSL$CMNICHECK:ICHKFIL.CMN'	! ICHECK files
	INCLUDE 'LSL$CMNICHECK:ICHKFT.CMN'	! ICHECK features
	INCLUDE 'LSL$CMNICHECK:ICHECKMSG.PAR'	! ICHECK messages
C
	INTEGER  SS$_NORMAL
	EXTERNAL SS$_NORMAL		! normal successful completion
C
C Workspace 
	INTEGER		I		! loop variable
	INTEGER*2	IELEN		! length of entry in hand
C
	CHARACTER*10	DO_WHAT		! 'ignored' or 'processed'
C
	LOGICAL		PROC_LAYER	! Process layer ?
C
C.......................................................................
C Assume success
C
	READ_NO = %LOC(SS$_NORMAL)
C
C and assume we process this layer.
C
	PROC_LAYER = .TRUE.
	DO_WHAT = 'processed'
C
C.......................................................................
C If this is the first layer we encounter and have a marker file then
C we have to create a layer 1 in the marker file.
C
	IF (FIRST_LAY.AND.HAD_MARK) THEN
	  CALL IFFSEL(MRKLUN)
	  NOPOS=LPOSE			! remember NO position
	  CALL IFFNO(1,0,0)		! write dummy NO entry
	  CALL IFFSEL(INLUN)
	  FIRST_LAY = .FALSE.		! not first layer again !
	  MRK_FSN = 0			! no features yet !
	  IF (.NOT.HAD_BASE) BASE_FC = 0! initialise BASE_FC
	ENDIF
C.......................................................................
C Read the NO entry
C
	CALL EIHRI(LAYER,1,1)			! read layer no.
C
C...................................................................
C /LAYER specified ? if so are we interested in it ?
C
	IF (LAYER.EQ.0) THEN			! Ignore layer 0
	  PROC_LAYER = .FALSE.			! so we don't process
	  DO_WHAT = 'ignored  '
	ENDIF
C
	IF (HAD_LAY) THEN			! yes /LAYER
	  DO 10 I=1,LAY_NUM	
	    IF (LAYER.EQ.LAYNO(I)) THEN
	      PROC_LAYER = .TRUE.
	      DO_WHAT = 'processed'
	      GOTO 20 	! yes specified
	    ENDIF
10	  CONTINUE
	  PROC_LAYER = .FALSE.			! no so we don't process
	  DO_WHAT = 'ignored  '
	ENDIF
C
C...................................................................
C Tell the world where we are ?
C
20	IF (HAD_LOG) THEN			! tell the world ?
	  READ_NO = ICHECK__LAYER
	  CALL LSL_PUTMSG(READ_NO,LAYER,DO_WHAT)
	ENDIF
C
	IF (HAD_OUT.OR.HAD_PRINT) THEN	! write to the world
	  CALL FLWSEL(OUTLUN)
     	  CALL FLWRTF('Layer %^W%N will be %S',LAYER,DO_WHAT)
	ENDIF
C
C...................................................................
C If Layer is not one of the specified ones jump to next EO.
C
	IF (.NOT.PROC_LAYER) THEN
	  CALL IFFNXC('EO',IELEN)
	  IF (IELEN.EQ.-1) THEN 	! Unexpected end of file
	    READ_NO = ICHECK__UNEXPEOF	! say so
	    CALL LSL_PUTMSG(READ_NO)
	  ENDIF
	ENDIF
C
C...................................................................
C
999	RETURN
   	END
