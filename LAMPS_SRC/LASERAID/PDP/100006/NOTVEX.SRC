	LOGICAL FUNCTION NOTVEX(NXTPNT,LSTPNT,VEXPNT,THISPT,THATPT)
C
C Copyright Laser-Scan Laboratories Ltd, Cambridge, England
C
C Author Paul Hardy
C
C Written 6.10.78
C Last edited 19/3/79
C
C checks for possible intersection between the vectors
C (NXT->LST) and (THIS->THAT) returning false if it is
C appropriate to construct a point VEXPNT.
C
***	COMMON/PARAMS/
C
***	COMMON/LKPARM/
***	COMMON/LKFOLL/
C
	INTEGER NXTPNT(2),LSTPNT(2),THISPT(2),THATPT(2),VEXPNT(2)
C
C w/s
	REAL VEC(2,3),COSALF,SINBET,SINGAM,SINDEL,H
	REAL DOT11,DOT22,DOT33,SM12
C
C statement function definitions
	DOT(N,M)=VEC(X,N)*VEC(X,M)+VEC(Y,N)*VEC(Y,M)
	CROSS(N,M)=VEC(X,N)*VEC(Y,M)-VEC(Y,N)*VEC(X,M)
C
C initial values
	NOTVEX=.TRUE.
C set up vectors
	VEC(X,1)=LSTPNT(X)-NXTPNT(X)
	VEC(Y,1)=LSTPNT(Y)-NXTPNT(Y)
	VEC(X,2)=THATPT(X)-THISPT(X)
	VEC(Y,2)=THATPT(Y)-THISPT(Y)
C check angle between them
	DOT11=DOT(1,1)
	DOT22=DOT(2,2)
	SM12=SQRT(DOT11*DOT22)
	COSALF=DOT(1,2)/SM12
	IF(ABS(COSALF).GT.0.9) GOTO 999  ! too linear
C VEC(3) is the gap of length H3
	VEC(X,3)=FLOAT(THISPT(X)) - FLOAT(LSTPNT(X))
	VEC(Y,3)=FLOAT(THISPT(Y)) - FLOAT(LSTPNT(Y))
	DOT33=DOT(3,3)
	H3=SQRT(DOT33)
	IF(H3.LE.5.) GOTO 999  ! too near
	IF(H3.GE.1000.) GOTO 999  ! too far away
C check gap for linearity with ends
	SINBET=CROSS(1,3)/SQRT(DOT11*DOT33)
	IF(ABS(SINBET).LT.0.1) GOTO 999  ! linear with first vector
	SINGAM=CROSS(3,2)/SQRT(DOT33*DOT22)
	IF(ABS(SINGAM).LT.0.1) GOTO 999  ! linear with second vector
C check for intersection within vectors
	IF(SINBET*SINGAM.LT.0.0) GOTO 999  ! inflexion
C
C here with reasonable vertex to calculate by sine rule
	SINDEL=CROSS(1,2)/SM12
	D=H3*SINGAM/SINDEL
C D is distance from LSTPNT in the direction of VEC(1) to the vertex
	IF(D.GT.GAPSIZ) GOTO 999
	H1=SQRT(DOT11)  ! length of VEC(1)
	R=D/H1
C offset from LSTPNT to get VEXPNT
	VEXPNT(X)=LSTPNT(X)+R*VEC(X,1)
	VEXPNT(Y)=LSTPNT(Y)+R*VEC(Y,1)
	NOTVEX=.FALSE.
C
999	CONTINUE
C	CALL WRITEF('NV%N A%F H%F B%F G%F V %N,%N',NOTVEX,COSALF,
C     &    H3,SINBET,SINGAM,VEXPNT(X),VEXPNT(Y))
	RETURN
	END
