;
; * This file is part of the LAMPS distribution, released as a software
; * preservation project to archive digital history for future historians.
; * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
; *
; * Timestamp of this file for the 2002 release was: 1980-02-21 11:59:12.000000000 +0000
; *
; * This program is free software: you can redistribute it and/or modify
; * it under the terms of the GNU General Public License as published by
; * the Free Software Foundation, version 3.
; *
; * This program is distributed in the hope that it will be useful, but
; * WITHOUT ANY WARRANTY; without even the implied warranty of
; * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
; * General Public License for more details.
; *
; * You should have received a copy of the GNU General Public License
; * along with this program. If not, see http://www.gnu.org/licenses/.
;
	.TITLE	FLFSST
	.ENABL	LC
	.IDENT	/JAN 79/

; Copyright Laser-Scan Laboratories Ltd., Cambridge, England
; Author  R. Fairbairns		Date as .IDENT
; Adapted for FLF by M.C.Fulford 19/Oct/78

	.PSECT

; Overlaid code to provide assistance to subroutine FLDSST in picking
; up SSTs entered in resident module DEBUG

; basic interface from the resident code
; NB call is by  JSR  R5, FLFSST
FLFSST::MOV	R0, SSTREG	; dump registers
	MOV	#SSTREG+2, R0
	MOV	R1, (R0)+
	MOV	R2, (R0)+
	MOV	R3, (R0)+
	MOV	R4, (R0)+
	MOV	(SP)+, (R0)+	; R5 before call
	MOV	SP, (R0)+	; SP before call
	MOV	R5, -(SP)	; preserve our link
	MOV	$SEQC, (R0)+	; OTS line number at entry to SST
	MOV	$NAMC, @R0	; OTS trace-back link
	CALL	FLDSST		; hand over to FORTRAN program to field SST
	RTS	PC		; back to resident code (TITs command only!)


; footle subroutine to read absolute store address
; Call  I = RDWD(A);  A is incremented to next word boundary

RDWD::	MOV	@2(R5), R1
	BIT	#1, R1
	BNE	1$		; treat odd addresses as byte
	MOV	(R1)+, R0
	BR	2$
1$:	MOVB	(R1)+, R0
2$:	MOV	R1, @2(R5)
	RTS	PC


	.PSECT	DEBUG, D,OVR,GBL
SSTTYP:	.BLKW	1		; type of SST
SSTREG:	.BLKW	7		; registers 0-6
SSTLNO:	.BLKW	1		; line number dump
SSTTBK:	.BLKW	1		; trace-back link
SSTPC:	.BLKW	1		; PC
SSTPS:	.BLKW	1		; PS
MMSR0:	.BLKW	1		; memory management
MMSR1:	.BLKW	1		;  status registers


	.PSECT	SSTCMD, D,OVR,GBL

; command table for what to do after a collapse print-out

$$CMCT = 0

SSTCM::	CMD	REP		; repeat
	CMD	DLP		; dump to LP
	CMD	DMP		; dump to TI
	CMD	EOS		; close down tidily
	CMD	QUI		; close down abruptly
	CMD	<CON,C>,E	; continue


	.END
