C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1980-02-21 12:55:38.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE TRSFRM(COEFF,XIN,YIN,XOUT,YOUT,NORDER)
	REAL COEFF(10,2),XIN,YIN,XOUT,YOUT
	INTEGER NORDER
C
	DOUBLE PRECISION V(10),XD,YD
C
	NVARS = ((NORDER+1)*(NORDER+2))/2
	XD = XIN
	YD = YIN
	CALL POLGEN(V,XD,YD,NORDER)
	XD = 0.0D0
	YD = 0.0D0
	DO 100 I=1,NVARS
	  XD = XD+COEFF(I,1)*V(I)
	  YD = YD+COEFF(I,2)*V(I)
100	  CONTINUE
	XOUT = XD
	YOUT = YD
	RETURN
C
	END

	SUBROUTINE FIT(NPTS,SHOULD,SAID,COEFF,NORDER,NGAP)
	INTEGER NPTS,NORDER
	REAL SHOULD(2,1),SAID(2,1),COEFF(10,1)
C gets SHOULD as a function of SAID.
	DOUBLE PRECISION A(10,10),B(10,2),V(10)
	DOUBLE PRECISION XD,YD,XD1,YD1
C
	NVARS = ((NORDER+1)*(NORDER+2))/2
C clear arrays
	DO 20 I=1,NVARS
	  B(I,1) = 0.0D0
	  B(I,2) = 0.0D0
	  DO 10 J=1,NVARS
	    A(I,J) = 0.0D0
10	    CONTINUE
20	  CONTINUE
C form sums
	DO 60 I=1,NPTS
	  IF (MOD(I,NGAP) .NE. 0) GOTO 60
	  XD = SAID(1,I)
	  YD = SAID(2,I)
	  XD1 = SHOULD(1,I)
	  YD1 = SHOULD(2,I)
	  IF (XD.GT.1.0D29 .OR. XD1.GT.1.0D29) GOTO 60
	  CALL POLGEN(V,XD,YD,NORDER)
	  DO 40 J=1,NVARS
	    B(J,1) = B(J,1)+V(J)*XD1
	    B(J,2) = B(J,2)+V(J)*YD1
	    DO 35 K=1,NVARS
	      A(J,K) = A(J,K)+V(J)*V(K)
35	      CONTINUE
40	    CONTINUE
60	  CONTINUE
C solve equations
	CALL MA01A(A,B,NVARS,2,0,10,10)
C return results
	DO 100 I=1,10
	  COEFF(I,1) = 0.0
	  COEFF(I,2) = 0.0
	  IF (I.GT.NVARS) GOTO 100
	  COEFF(I,1) = B(I,1)
	  COEFF(I,2) = B(I,2)
100	  CONTINUE
C
	RETURN
	END


	SUBROUTINE POLGEN(V,X,Y,NF)
	IMPLICIT DOUBLE PRECISION (A-H),(O-Z)
	DIMENSION V(21)
C Subroutine to generate in V ; 1 X Y XX XY YY XXX XXY XYY YYY ....
C terms up to order nf.
	V(1)=1.0
	K=1
	DO 1 I=1,NF
	N1=K+1
	N2=K+I
	DO 2 J=N1,N2
2	V(J)=V(J-I)*X
	V(K+I+1)=V(K)*Y
1	K=K+I+1
	RETURN
	END
