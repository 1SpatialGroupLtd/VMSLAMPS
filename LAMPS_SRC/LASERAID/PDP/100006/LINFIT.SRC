C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1980-02-21 11:26:54.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION LINFIT(MBUF,IB,IT,IW,IU,IV,OFFNSS,A0,A1)
C
C Fits a line to the points held in MBUF between IB and IT
C with IU as the abcissa and IV as the ordinate
C ( or vice-versa ?)
C anyway IU is X and IV is Y
C and IW is the weight and a point is ignored
C completely and utterly if its weight is less than zero,
C otherwise it is weighted by a function of IW.
C
C The answer is returned in A1 and A0
C
C Result is .TRUE. if a line was fitted,
C          .FALSE. if one couldn't be
C
C	Last edited 3/Oct/78
C
	REAL A0,A1
	INTEGER IB,IT,IW,IU,IV
	INTEGER MBUF(3,1)
C
C
	REAL S1,SU,SV,SUV,SUU,RMOD,RW
C
C
	LINFIT = .FALSE.
C
C Zero totals
C
	S1 = 0.
	SU = 0.
	SV = 0.
	SUU = 0.
	SUV = 0.
C
C Accumulate totals to make coefficients
C
	DO 200 I=IB,IT
	IF (MBUF(IW,I) .LE. 0) GOTO 200
	RU = MBUF(IU,I)
	RV = MBUF(IV,I)
	RW = MBUF(IW,I)/OFFNSS
	RW = 2.0/(RW*RW+1)
	S1 = S1+RW
	SU = SU+RU*RW
	SV = SV+RV*RW
	SUU = SUU+RU*RU*RW
	SUV = SUV+RU*RV*RW
200	CONTINUE
C
C We have now set up a set of simultaneous equations
C and we will solve them by a simple matrix inversion.
C
	RMOD = S1*SUU - SU*SU
	IF (RMOD .EQ. 0) GOTO 900
	LINFIT = .TRUE.
	A0 = (SUU*SV - SU*SUV)/RMOD
	A1 = (-SU*SV + S1*SUV)/RMOD
C
C And that is all
C
900	RETURN
C
	END
