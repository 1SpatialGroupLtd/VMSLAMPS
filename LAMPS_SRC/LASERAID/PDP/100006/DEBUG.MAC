;
; * This file is part of the LAMPS distribution, released as a software
; * preservation project to archive digital history for future historians.
; * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
; *
; * Timestamp of this file for the 2002 release was: 1980-02-21 11:58:48.000000000 +0000
; *
; * This program is free software: you can redistribute it and/or modify
; * it under the terms of the GNU General Public License as published by
; * the Free Software Foundation, version 3.
; *
; * This program is distributed in the hope that it will be useful, but
; * WITHOUT ANY WARRANTY; without even the implied warranty of
; * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
; * General Public License for more details.
; *
; * You should have received a copy of the GNU General Public License
; * along with this program. If not, see http://www.gnu.org/licenses/.
;
	.TITLE	DEBUG
	.ENABL	LC
	.IDENT	/21MY79/

; Copyright Laser-Scan Laboratories Ltd., Cambridge, England
; Author  R. Fairbairns		Date as .IDENT
; Adapted by M.C.Fulford for FLF 19/Oct/78


	.MCALL	SVDB$S

; SST routines

; Only memory management routine ever executed when TITS present

; Memory management SST
MMSST:	MOV	(SP)+, MMSR1	; preserve instruction backup register
	MOV	(SP)+, 2(SP)	; vpc -> pc
	MOV	(SP)+, MMSR0	; preserve 'general' status register
	MOV	#"MM, SSTTYP	; SST type
	BR	COMSST		; join common code

; Non-RSX EMT SST - pro tem, treat as a reserved instruction
EMTSST:	TST	(SP)+		; ignore low order

; reserved instruction and IOT
RISST:	MOV	#"RI, SSTTYP
	BR	COMSST

; Odd address
OASST:	MOV	#"OA, SSTTYP
	BR	COMSST

; T-bit trap (eh?) or execution of a BPT instruction
BPTSST:	MOV	#"BK, SSTTYP

; common SST code - preserve registers
COMSST:	MOV	(SP), SSTPC
	MOV	2(SP), SSTPS
	MOV	(SP)+, (SP)	; preserve PC for possible return
	JSR	R5, FLFSST	; decode the SST (in overlay)
	RTS	PC		; continue (if FLFSST so deems)

; debugging assistance
BREAK::	BPT
	RETURN


; Set up SST table
; SST table
SSTTBL:	.WORD OASST
	.WORD MMSST
	.WORD BPTSST
	.WORD RISST
	.WORD RISST
	.WORD RISST
	.WORD 0
	.WORD 0

SETSST::SVDB$S	#SSTTBL ,#8.
	RETURN

	.PSECT	DEBUG, D,OVR,GBL
SSTTYP:	.BLKW	1		; type of SST
SSTREG:	.BLKW	7		; registers 0-6
SSTLNO:	.BLKW	1		; line number dump
SSTTBK:	.BLKW	1		; trace-back link
SSTPC:	.BLKW	1		; PC
SSTPS:	.BLKW	1		; PS
MMSR0:	.BLKW	1		; memory management
MMSR1:	.BLKW	1		;  status registers

	.PSECT

	.END
