C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1980-02-21 12:39:06.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE ONTRAK(FSTX,FSTY,PNTX,PNTY,NMIN,NMAX,HLIM2,NSEG)
C
C Copyright Laser-Scan Laboratories Ltd,Cambridge,England
C
C Author M.C.Fulford
C
C	Created 31.viii.77
C	Last edited 18/viii/78
C
C Looks at line element [FST, PNT] to see if we are backtracking
C along the already digitised line. Checks the direction of
C the line element w.r.t. the MP line and
C checks if PNT is closer than HLIM to the portion of the
C MP track between points NMIN & NMAX.
C NSEG is set to the segment the point is on, zero if none.
C
***	COMMON/PARAMS/
C
	REAL FSTX,FSTY		! 1st point of the line element
	REAL PNTX,PNTY		! last point of it
	INTEGER NMIN,NMAX	! limits of track
	REAL HLIM2		! closeness squared
	INTEGER NSEG		! the segment
C
***	COMMON/MPEP/
C
	REAL FP(2)	! vector along line element
	REAL AB(2)	! vector between 2 MPs
	REAL AP(2)	! vector between MP & PNT
	REAL ABAB	! length of AB squared
	REAL ABAP	! component of AP along AB
	REAL Q		!     "      "  " across "
C
	IF (NMAX-1 .LT. NMIN) GOTO 250	! CHECK LINE EXISTS
	FP(X) = PNTX-FSTX
	FP(Y) = PNTY-FSTY
	DO 200 I = NMIN,NMAX-1
	  NSEG = I
	  AB(X) = MP1(X,I+1) - MP1(X,I)
	  AB(Y) = MP1(Y,I+1) - MP1(Y,I)
C check line is anti-parallel to MP track
	  IF (FP(X)*AB(X)+FP(Y)*AB(Y).GE.0) GOTO 200	! if not
C yes - apply further checks
	  AP(X) = PNTX - MP1(X,I)
	  AP(Y) = PNTY - MP1(Y,I)
	  ABAB = AB(X)*AB(X) + AB(Y)*AB(Y)
C is point within segment ?
	  ABAP = AP(X)*AB(X) + AP(Y)*AB(Y)
	  IF (ABAP.LE.0 .OR.
     *        ABAP.GT.ABAB) GOTO 200
C yes - is it on, or near the line ?
	  Q = AP(X)*AB(Y) - AP(Y)*AB(X)
	  IF (Q*Q .LT. ABAB*HLIM2) GOTO 900 ! with NSEG set
C no
200	  CONTINUE
C seems OK
250	NSEG = 0
C
900	RETURN
	END
