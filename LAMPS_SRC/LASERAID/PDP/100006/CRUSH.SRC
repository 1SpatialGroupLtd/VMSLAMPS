	SUBROUTINE CRUSH(EPS,LIM,DIS)
C
C Author Alan Wylie
C
C Copyright Laserscan Laboratories Limited,Cambridge,England
C
C Created 26/Mar/80
C
C Last edited 23/Apr/80
C
C Subroutine to remove superfluous points from chains,in order
C to speed up junction recognition.
C
***	COMMON/PARAMS/
***	COMMON/LINKS/
***	COMMON/RSE/
C
C arguments:
	REAL EPS		! max perp distance
	REAL LIM		! cosine of max angle
	REAL DIS		! maximum distance between points
C
C workspace:
	INTEGER I		! for each chain
	INTEGER THISPT		! point to be added if tests fail
	INTEGER NEXTPT		! point after THISPT
	INTEGER TESTPT		! point stepped between
C				! LASTPT and NEXTPT
	INTEGER LASTPT		! last compacted point
	INTEGER COUNT		! number of points skipped
	INTEGER VLN(2)		! vector LASTPT to NEXTPT
	INTEGER VLT(2)		! vector LASTPT to TESTPT
	INTEGER EXL,EYL		! ENCBUF(X,LASTPT),etc...
	REAL FVX,FVY		! FLOAT(VLT(X)),etc...
	INTEGER PD		! perp distance from TESTPT to VLN
C
C code:
	DO 50 I=1,MAXCHN		! for each chain
	   THISPT=CHAIN(I)
C is it a real chain ?
	   IF (THISPT.LE.0.OR.NXT(THISPT).LE.0) GOTO 50
10	   COUNT=-1
	   LASTPT=THISPT
20	   COUNT=COUNT+1
	   THISPT=NXT(THISPT)
	   NEXTPT=NXT(THISPT)
	   IF (NEXTPT.LE.0) GOTO 40	! at the end ?
	   TESTPT=LASTPT
	   EXL=ENCBUF(X,LASTPT)
	   EYL=ENCBUF(Y,LASTPT)
	   VLN(X)=ENCBUF(X,NEXTPT)-EXL
	   VLN(Y)=ENCBUF(Y,NEXTPT)-EYL
	   FVX=FLOAT(VLN(X))
	   FVY=FLOAT(VLN(Y))
	   IF ((FVX*FVX+FVY*FVY).GT.DIS*DIS) GOTO 40
30	      TESTPT=NXT(TESTPT)
C Have we tested all intermediate points ?,if so try next TESTPT
	      IF (TESTPT.EQ.NEXTPT) GOTO 20
	      VLT(X)=ENCBUF(X,TESTPT)-EXL
	      VLT(Y)=ENCBUF(Y,TESTPT)-EYL
	      COS=ABS(VECCOS(VLN,VLT))
C Is the angle too great ?
	      IF (COS.LT.LIM) GOTO 40
	      FVX=FLOAT(VLT(X))
	      FVY=FLOAT(VLT(Y))
	      PD=SQRT((FVX*FVX+FVY*FVY)*(1-COS*COS))
C Is the distance too great ?
	      IF (PD.LT.EPS) GOTO 30
C put in a new point if distance or angle too great
C i.e. cosine too small. Isn't cosine an awkward function
40	   NXT(LASTPT)=THISPT
	   LENGTH(I)=LENGTH(I)-COUNT
	   IF (NEXTPT.GT.0) GOTO 10
50	CONTINUE
C
	RETURN
	END
