	.TITLE PROFLIE
	.IDENT /12OCT77/
	.DSABL GBL
	.ENABL LC
 	.MCALL ASTX$S,DIR$,MRKT$
;
; Profiling routines for FORTRAN IV - PLUS programs.
; Replaces the usual traceback routines and provides a
; few more services.
;
; Author M.C.Fulford
;

; External globals
	.GLOBL  $OTSV

; Parameters
	ENTSIZ = 6	; size of entries in table:-
;			word 1 - name of routine in RAD50 (1st half)
;			word 2 - second half of name
;			word 3 - time spent in routine (in ticks)
;
	TABNUM = 50.	; max number of routines profilable
;
	TABSIZ = TABNUM*ENTSIZ

	.PSECT	TIMING,GBL,OVR,D
TIMING:	.WORD 0		; number of ticks per AST
TIMERR:	.WORD 0		; error code
SIZE:	.WORD TABNUM
TABLE:	.RAD50 /$$$$$$/	;  first entry is the null one
	.WORD 0
	.BLKW TABSIZ-ENTSIZ	; hash table

	.PSECT

ENTRY:	.WORD TABLE	; current routine (originally the null one)
MKT:	.WORD 0		; >0 => mark time in progress

MKTIM:	MRKT$ ,1,1,MRKAST


; The replacement for the OTS tracing routine.
; Performs as the standard version, but sets up profile info
; as well

NAM$::	MOV  @#$OTSV,R3
	MOV  (R3)+,-(SP)	; save sequence number ...
	MOV  @R3,-(SP)		; ... & previous trace entry
	MOV  SP,@R3		; new trace entry
	CLR  -(R3)		; clear sequence number
; find a counter to increment on clock ticks
	CLR  R0			; select null
	TST  TIMING		; want a profile ?
	BLE  30$		; if not
; look up name in the hash table
	MOV  4(SP),R2
	MOV  6(SP),R3
	MOV  R2,R1		; form # value from name
	XOR  R3,R1
	CLR  R0
	DIV  #TABNUM,R0
	MUL  #ENTSIZ,R1		; # value's in R1 now
	MOV  R1,R0		; remember original value
;
10$:	TST  TABLE(R0)		; zero entry ?
	BNE  12$
	TST  TABLE+2(R0)
	BEQ  18$		; if yes
12$:	CMP  TABLE(R0),R2	; name match ?
	BNE  14$
	CMP  TABLE+2(R0),R3
	BEQ  30$		; yes
14$:	ADD  #ENTSIZ,R0		; move to next entry
	CMP  R0,#TABSIZ		; over the end ?
	BLT  16$		; no
	CLR  R0			; yes, cycle round
16$:	CMP  R0,R1		; back to initial entry ?
	BNE  10$		; no - keep looking
	CLR  R0			; yes - select the null entry
	BR   30$		; since the table's full
; zero entry found, insert new entry
18$:	MOV  R2,TABLE(R0)
	MOV  R3,TABLE+2(R0)
	CLR  TABLE+4(R0)
; come here with entry to be used in R0
30$:	MOV  ENTRY,-(SP)	; save the old one
	ADD  #TABLE+4,R0
	MOV  R0,ENTRY		; new counter's address
	TST  TIMING		; profile wanted ?
	BLE  40$
	TST  MKT		; marking time ?
	BNE  40$		; yes
	MOV  TIMING,MKTIM+M.KTMG
	DIR$ #MKTIM,ERROR
	INC  MKT
; enter traced routine
40$:	JSR  PC,@R4
;
; come here on exit from a traced routine
	MOV  (SP)+,R0		; get saved entry
	BEQ  80$		; there wasn't one (?)
	MOV  R0,ENTRY		; back to old counter
80$:	MOV  @#$OTSV,R4		; remove the topmost trace entry
	MOV  (SP)+,2(R4)
	MOV  (SP)+,@R4
	CMP  (SP)+,(SP)+
	RTS  PC			; actually return


; Report use of wronf FORTRAN
$NAM::	TRAP 337

; AST for mark time

MRKAST:	CLR  MKT		; mark time finished
	INC  @ENTRY		; one more tick
	TST  TIMING		; profiling wanted ?
	BEQ  5$			; no
	MOV  TIMING,MKTIM+M.KTMG
	DIR$ #MKTIM,ERROR
	INC  MKT
5$:	TST  (SP)+		; remove event flag number
	ASTX$S			; exit from the AST

; Error routine
ERROR:	BPT
	RTS  PC

	.END
