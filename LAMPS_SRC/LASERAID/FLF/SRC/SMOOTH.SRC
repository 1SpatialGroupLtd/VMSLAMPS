	SUBROUTINE SMOOTH
C
***	MODULE	SMOOTH
***	IDENT	18MR83
C
C Copyright Laser-Scan Laboratories Limited, Cambridge, England.
C
C	Rehashed				 DM	18-Mar-1983
C	Tidied					AJW	01-Jun-1981
C	INCLUDES to *** COMMONS			MCF	24-Oct-1980
C	Created					MCF	18-Dec-1978
C
C Smooths the chains.
C
**	IF VAX
	IMPLICIT NONE
**	ENDC
C
***	PARAMETER/PARAMS/
C
***	COMMON/RSE/
***	COMMON/LINKS/
C
	INTEGER*2 J
	INTEGER*2 M,N			! pointers into current chain
	REAL      LX,LY,MX,MY,NX,NY	! coords of 3 consecutive points
C
C ......................................................................
C
	DO 20 J=1,MAXCHN
	   N=CHAIN(J)
	   IF (N.LE.0.OR.LENGTH(J).LT.3) GO TO 20  ! ignore short chains
C
	   LX=ENCBUF(X,N)		! get 3 consecutive pnts (L,M,N)
	   LY=ENCBUF(Y,N)
	   M=NXT(N)			! middle point is special
	   MX=ENCBUF(X,M)
	   MY=ENCBUF(Y,M)
	   N=NXT(M)
C
C ... move point M to the mid-point of the line joining the mid-points
C ... of (L)---(M) and (M)---(N). Repeat for the whole chain.
10	   NX=ENCBUF(X,N)			! (L)---(N) is the chord
	      NY=ENCBUF(Y,N)
	      ENCBUF(X,M)=0.25*(LX + 2*MX + NX)	! move the middle point
	      ENCBUF(Y,M)=0.25*(LY + 2*MY + NY)	! (but retain MX and MY)
	      M=N
	      N=NXT(N)				! prepare to get new N
	      IF (N.LE.0) GO TO 20		! (get next chn if none)
	      LX=MX				! shuffle down old M ...
	      LY=MY
	      MX=NX				! and old N
	      MY=NY
	   GO TO 10				! get new N
C
20	CONTINUE
	RETURN
	END
