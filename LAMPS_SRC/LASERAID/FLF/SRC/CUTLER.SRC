C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1983-05-03 12:38:34.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE CUTLER(IU,IV,HOLE,RIPPLE)
C
***	MODULE	CUTLER
***	IDENT	03MY83
C
**	IF VAX
	  IMPLICIT NONE
**	ENDC
C
C Copyright Laser-Scan Laboratories Ltd,Cambridge,England
C
CMOD	K(U)=ENC(U) message removed (since
CMOD	no-one understands it!			AJW	03-May-1983
CMOD	also, some tidies			AJW	03-May-1983
CMOD	Additional commentary			WJJ	20-May-1982
CMOD	Tidied					AJW	10-Apr-1981
CMOD	Created					MCF	19-Jul-1977
C
C A routine for making forks out of data points.
C Works similarly to GROW, but attempts to identify
C converging & diverging lines as it grows the chains.
C The width information is used to do this.
C
C All current chains are held in V order in NXTCHN(I). All line
C  encounters on one scan line are examined in V order, and compared 
C  with the current chains by their edges. If both edges match, the
C  encounter is added to the chain. If one edge matches, we may have
C  divergence or convergence of lines. There may also be the start of
C  new chains or the termination of current ones.
C
C
***	PARAMETER/PARAMS/
C
C arguments
	INTEGER IU,IV		! axes relative to scan direction
	INTEGER HOLE		! maximum hole in line (gap in U dirn.
				!  of edges to chain across)
	INTEGER RIPPLE		! maximum sideways ripple on line (gap
				!  in V dirn. of edges to chain across)
C
***	COMMON/LINKS/		! data structure
***	COMMON/RSE/		! raw scan encounters
***	COMMON/FORKS/		! more structure
C
C external functions
	LOGICAL NEBIT
	INTEGER ADDCHN,ADDFRK
C
C workspace
	INTEGER EDGE		! -1,1 => leading,trailing edge of 
				!  current encounter is being examined
	INTEGER CHN		! current chain
	INTEGER ENC		! current encounter
	INTEGER LSTENC		! end of scan line
	INTEGER FST		! first encounter on scan line
	INTEGER LST		! first encounter on next line
	INTEGER WAY		! direction of line

C Note: ENC runs from FST to LSTENC by WAY while examining a scan-line

	INTEGER JUNCTN		! type of junction:-
C		<0 => divergence, |JUNCTN| is the other encounter
C		=0 => continuation of line
C		>0 => convergence, JUNCTN  is the other chain
	INTEGER VENC		! V-coord of encounter's edge
	INTEGER VCHN		! V-coord of chain's edge
	INTEGER DV		! difference
	INTEGER IDU
	INTEGER I,K,N
	INTEGER NELO,NEHI
	INTEGER NU
	INTEGER IW
	INTEGER K1,K2
	INTEGER NEWCHN
	LOGICAL*1 NMD		! No More Data
C
C
C clear data structures
	DO 10 I = 1,MAXCHN
	  ENDFRK(1,I) = 0
	  ENDFRK(2,I) = 0
	  NXTCHN(I) = 0
10	CONTINUE
C
	NXTCHN(0) = -1
C
	DO 20 I = 1,MAXFRK
	  FRKTYP(I) = 0
20	CONTINUE
C
	RETRY = .TRUE.
	ENC = 0
	LSTENC = 0		! LSTENC=ENC => start new scan-line
	CHN = 0
	LST = 1			! first encounter of next scan-line
	NMD = .FALSE.		! assume there will be some data
	NEHI = -1		! no data yet
C
C set up for leading edge (black -> white)
30	EDGE = -1
C
C get the next encounter
40	IF (NMD) GOTO 320	!  the end
C
C end of scan line's data ?
	IF (ENC .NE. LSTENC) GOTO 110
C
C Yes
C no more to do, sort out the next line
	FST = LST		! LST is first encounter of next line
	WAY = 0			! dirn of scan-line is not determined
50	LST = LST+1		! look at next encounter
60	IF (LST .LE. NEHI) GOTO 70	! check that it is there
C
C need some more data
	NMD = .NOT.NEBIT(NELO,NEHI)	! get more encounters
	IF (NMD) GOTO 80	! if no more data, process what we have
	GOTO 60			! see if there is another encounter yet
C
C here with the next encounter, does it belong on this line
70	IF ((ENCBUF(W,LST).AND.NEGMUL) .EQ. 0) GOTO 80	! j if new line
	IF (WAY .NE. 0) GOTO 50	! jump if we know the line direction
C set WAY on the basis of the V coords of the first two encounters
	WAY = -1		! assume dirn is backwards
	IF (ENCBUF(IV,LST) .GT. ENCBUF(IV,FST))WAY = 1
	GOTO 50
C
C here when we have an encounter from the next scan-line
C set up ENC, LSTENC to cycle through encounter in V order
80	IF (WAY .EQ. 0) WAY = 1	! only one encounter on line
	ENC = FST
	IF (WAY .LT. 0) ENC = LST-1
	LSTENC = FST+LST-1 - ENC
C
C de-activate any chains that are overlapped by more recent ones,
C or that are simply lagging behind the point of interest.
C	[remove overlapped ones]
C this code will appear when I find the piece of paper
C that I wrote it on!
C
C remove lagging chains
C run through all current chains, comparing their last point's U value
C  with that of this scan-line. If it is more than HOLE behind use
C  REMCHN to remove the chain from the list.
	N = NXTCHN(0)
	NU = ENCBUF(IU,ENC)
90	K = N
	IF (K .LE. 0) GOTO 100
	IDU = ENCBUF(IU,CHAIN(K))-NU
	N = NXTCHN(K)
	IF (ABS(IDU) .GT. HOLE) CALL REMCHN(K)
	GOTO 90
C
C Now prepare to run through the chains alongside the scan-line
100	CHN = NXTCHN(0)		! begin at the beginning
	JUNCTN = 0		! forget pending junctions
	EDGE = -1		! start afresh
C
C ready to start a new scan line
	GOTO 120
C
C extract next encounter and throw away if it is an edge
110	ENC = ENC+WAY
120	IW = ENCBUF(W,ENC).AND.NEGPOS
	IF (IW .EQ. NEGNEG .OR.
     *      IW .EQ. NEGPOS) GOTO 40		! skip edges
C
C compare the chosen edges of the current encounter and
C the current chain.
130	IF (CHN .LE. 0) GOTO 250	! jump if run out of chains
	K = CHAIN(CHN)			! get last encounter of chain
CCC	IF (ENCBUF(IU,K).EQ.ENCBUF(IU,ENC))	! oddity if the U values
CCC  &      CALL WRITEF('K(U) = ENC(U) !')	! of chain and line =
	VCHN = ENCBUF(IV,  K)+((ENCBUF(W,  K).AND.NEGWID)*EDGE)/2
	VENC = ENCBUF(IV,ENC)+((ENCBUF(W,ENC).AND.NEGWID)*EDGE)/2
	DV = VCHN - VENC		! difference in V
C check for too large ripple
	IF (DV .GT. RIPPLE) GOTO 250	! chain came first
	IF (DV .LT.-RIPPLE) GOTO 190	! encounter came first
C
C edges (nearly) correspond
C
	IF (EDGE) 140,310,150		! two lines
C
C leading edges match => start of junction (or simple link)
140	EDGE = 1		! look at trailing edge now
	JUNCTN = 0
	GOTO 130		! look at other side of encounter
C
C trailing edges match => end of junction, so see what sort
150	IF (JUNCTN) 170,160,180
C
C straightforward link
C we can add to the chain directly, since it will not
C be looked at again in this scan line's processing.
160	NXT(ENC) = CHAIN(CHN)
	CHAIN(CHN) = ENC
	LENGTH(CHN) = LENGTH(CHN)+1
	CHN = NXTCHN(CHN)
	GOTO 30		! look at next encounter
C
C divergence
170	NEWCHN = NXTCHN(CHN)
	CALL REMCHN(CHN)		! remove stem chain
	K1 = ADDCHN(IV,-JUNCTN)		! create branch chains
	K2 = ADDCHN(IV,ENC)
C create a fork if the two new chains were created
	IF (K1.NE.0 .AND. K2.NE.0) CALL ADDFRK(1,CHN,-K1,-K2)
	CHN = NEWCHN
	GOTO 30
C
C convergence
180	NEWCHN = NXTCHN(CHN)
	CALL REMCHN(JUNCTN)
	CALL REMCHN( CHN )
	K1 = ADDCHN(IV,ENC)
C again, check that chain was created
	IF (K1.NE.0) CALL ADDFRK(2,-K1,JUNCTN,CHN)
	CHN = NEWCHN
	GOTO 30
C
C Chain's edge came first
C
190	IF (EDGE) 200,310,210
C leading edge => hole in a line
C ignore hole and it will go away as the chain will
C be de-activated when it lags behind too much.
200	GOTO 240
C
C trailing edge => possible convergence
210	IF (JUNCTN) 220,230,240
C we thought we were diverging though
220	NEWCHN = NXTCHN(CHN)
	CALL REMCHN(CHN)
	CALL ADDCHN(IV,-JUNCTN)
	CHN = NEWCHN
	JUNCTN = 0
	EDGE = -1
	GOTO 130
C
C store the convergence information
230	JUNCTN = CHN
240	CHN = NXTCHN(CHN)
	GOTO 130
C
C Encounter's edge came first
C
250	IF (EDGE) 260,310,270
C
C leading edge => new line appearing
260	CALL ADDCHN(IV,ENC)
	GOTO 40
C
C trailing edge => possible divergence
270	IF (JUNCTN) 300,290,280
C
C just a minute, weren't we converging ?
280	CALL REMCHN(JUNCTN)
	CALL ADDCHN(IV,ENC)
	JUNCTN = 0
	GOTO 30
C
C store the divergence information
290	JUNCTN = -ENC
300	GOTO 40
C
C Various errors
C
310	CALL WRITEF('CUTLER - EDGE %N !',EDGE)
	GOTO 320
C
C Exit
C
320	RETURN
	END
