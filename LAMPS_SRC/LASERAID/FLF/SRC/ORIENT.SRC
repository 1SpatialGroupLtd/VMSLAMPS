C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1985-10-25 11:03:32.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE ORIENT(CHNEND,POSITN,ANGLE)
C
***	MODULE	ORIENT
***	IDENT	25OC85
C
C Copyright Laser-Scan Laboratories Limited, Cambridge, England.
C
C Author: D. Meader
C
C	Created 				 DM	16-Oct-1985
C
C Calculates the position and orientation of a 'square' point feature.
C
**	IF VAX
	IMPLICIT NONE
**	ENDC
C
***	PARAMETER/PARAMS/
	PARAMETER PI=   3.141593
	PARAMETER PIBY2=PI/2.0
C
	INTEGER	  CHNEND	! pointer to the end of the feature
	REAL	  POSITN(2)	! centre of gravity of the feature
	REAL	  ANGLE		! mean orientation of the feature
C
***	COMMON/LINKS/
***	COMMON/LKFOLL/
***	COMMON/RSE/
C
	INTEGER	  I,J		! temporary variables
	REAL	  ANG(4)	! orientation of each side
	REAL	  POSBUF(2,4)	! the four corners of the feature
C
C function:
	REAL	  ANGNRM	! normalises angle to -PI < angle <= PI
C
C ......................................................................
C
C ... first find the centre of gravity.
	POSITN(X)=0.0
	POSITN(Y)=0.0
	J=CHNEND
	DO 10 I=1,4		! assume feature has exactly 4 points
	   POSBUF(X,I)=FLOAT(ENCBUF(X,J))
	   POSBUF(Y,I)=FLOAT(ENCBUF(Y,J))
	   POSITN(X)=POSITN(X) + POSBUF(X,I)
	   POSITN(Y)=POSITN(Y) + POSBUF(Y,I)
	   J=NXT(J)
10	CONTINUE
	POSITN(X)=START(X) + POSITN(X)/4.0
	POSITN(Y)=START(Y) + POSITN(Y)/4.0
C
C ... rotate successive sides by 180, 270, 0 and 90 degrees (because of
C ... the way CHNPIX happens to analyse the data), then find the mean
C ... orientation.
	ANG(1)=ATAN2(POSBUF(Y,1)-POSBUF(Y,2),POSBUF(X,1)-POSBUF(X,2))
	ANG(2)=ATAN2(POSBUF(X,3)-POSBUF(X,2),POSBUF(Y,2)-POSBUF(Y,3))
	ANG(3)=ATAN2(POSBUF(Y,4)-POSBUF(Y,3),POSBUF(X,4)-POSBUF(X,3))
	ANG(4)=ATAN2(POSBUF(X,4)-POSBUF(X,1),POSBUF(Y,1)-POSBUF(Y,4))
	ANGLE=ANG(1) +
     &	      (ANGNRM(ANG(2) - ANG(1)) +
     &	       ANGNRM(ANG(3) - ANG(1)) +
     &	       ANGNRM(ANG(4) - ANG(1)))
     &	      /4.0		! mean orientation
	IF (ANGLE.LT.0.0) ANGLE=ANGLE + PI
	ANGLE=MOD(ANGLE,PIBY2)	! normalise to range 0 <= ANGLE <= PIBY2
	RETURN
	END
	REAL FUNCTION ANGNRM(ANGLE)
C
***	MODULE	ANGNRM
***	IDENT	25OC85
C
C Copyright Laser-Scan Laboratories Limited, Cambridge, England.
C
C Author: D. Meader
C
C	Created 				 DM	25-Oct-1985
C
C Normalises ANGLE (radians) to the range -PI < ANGLE <= PI.
C
**	IF VAX
	IMPLICIT NONE
**	ENDC
C
	PARAMETER PI=   3.141593
	PARAMETER TWOPI=2.0*PI
C
	REAL	  ANGLE
C
C ......................................................................
C
	ANGNRM=ANGLE
10	IF (ANGNRM.LE.-PI) GO TO 30
20	IF (ANGNRM.LT.PI) GO TO 40
	ANGNRM=ANGNRM - TWOPI
	GO TO 20
30	ANGNRM=ANGNRM + TWOPI
	GO TO 10
40	RETURN
	END
