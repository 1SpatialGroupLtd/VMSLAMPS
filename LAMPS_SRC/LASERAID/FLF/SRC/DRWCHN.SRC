	SUBROUTINE DRWCHN(NCHN,IU,IV)
C
***	MODULE	DRWCHN
***	IDENT	06SE85
C
C Copyright Laser-Scan Laboratories Limited, Cambridge, England.
C
CMOD	Colour added				 DM	06-Sep-1985
CMOD	One-point chains rehashed; tidied	 DM	24-Jul-1985
CMOD	Draws one point chains			AJW	05-Jul-1983
CMOD	CLOCHN handling generalised		 DM	23-May-1983
CMOD	Closes chains for junctions, sanitised	 DM	12-Jul-1982
CMOD	Tidied					AJW	12-Feb-1981
CMOD	Created					MCF
C
C To draw the chains on the Tektronix.
C
**	IF VAX
	IMPLICIT NONE
**	ENDC
C
C common blocks:
***	PARAMETER/PARAMS/
***	COMMON/LINKS/
***	COMMON/RSE/
***	COMMON/SCPARM/
C
C arguments:
	INTEGER NCHN
	INTEGER IU,IV
C
C workspace:
	INTEGER III,KKK,DDD
C
C ......................................................................
C
C ... all chains or a specific one ?
	III=MAX(1,NCHN)			! NCHN=0 ==> all chains
	CALL TXCOL(3)
C
10	KKK = CHAIN(III)		! get LAST point in chain
	   IF (KKK.LE.0) GO TO 30	! ignore null chains
	   CALL TXMTO(ENCBUF(X,KKK),ENCBUF(Y,KKK))
	   KKK=NXT(KKK)			! penultimate point in chain
	   IF (KKK.GT.0) GO TO 20	! (if any)
C
C ... one point chain - draw a 'plus' slightly less than one pitch wide.
	   DDD=(IPIT-1)/2
	   CALL TXMBY(0,-DDD)
	   CALL TXLBY(0,2*DDD)
	   CALL TXMBY(-DDD,-DDD)
	   CALL TXLBY(2*DDD,0)
	   GO TO 30
C
C ... go along chain from last point to first point.
20	   CALL TXLTO(ENCBUF(X,KKK),ENCBUF(Y,KKK))
	      KKK = NXT(KKK)
	   IF (KKK.GT.0) GO TO 20	! at start (i.e. finished) ?
	   IF (CLOCHN(III))
     &	      CALL TXLTO(ENCBUF(X,CHAIN(III)),ENCBUF(Y,CHAIN(III)))
C
C ... finished with this chain.
30	   IF (NCHN.NE.0) GO TO 40	! did we want all chains ?
	   III=III+1			! yes - get next one
	IF (III.LE.MAXCHN) GO TO 10	! run out of chains ?
C
40	CALL TXCOL(1)
	CALL TXEND
	RETURN
	END
