	SUBROUTINE FAX(SLAVE,NWID,NPIT)
C
***	MODULE	FAX
***	IDENT	30MY86
C
C Copyright Laser-Scan Laboratories Limited, Cambridge, England.
C
CMOD	Only call CSRD if HADLDI; tidied	 DM	30-May-1986
CMOD	Uses MAX(CRX,CRY) for scaling		 DM	27-Jun-1985
CMOD	TXORGN(500,350)				AJW	26-Mar-1982
CMOD	Tidied					AJW	28-May-1981
C	Author					MCF
C
C Draws a facsimile of the area around the cursor on the Tektronix.
C N.B. Must take care not to upset track following.
C
**	IF VAX
	IMPLICIT NONE
**	ENDC
C
C parameters:
***	PARAMETER/PARAMS/
C
C arguments:
	LOGICAL*1 SLAVE
	INTEGER NWID,NPIT
C
C common blocks:
***	COMMON/MASTER/
***	COMMON/SCPARM/
***	COMMON/SCCORR/
C
C workspace:
	INTEGER   I
	INTEGER   ICH,ICX,ICY
	INTEGER   ID(2)
	INTEGER   IDIRSV
	INTEGER   IMODSV
	INTEGER   IPITSV
	INTEGER   IWIDSV
	REAL      SCTX
	REAL      XC,YC
	REAL      XS,YS
C
C ......................................................................
C
C set up for drawing on Tek.
	CALL RDABO
	CALL TXCLR
C
	IF (HADLDI) CALL CSRD(XC,YC)
	CALL DISMOD(0)
	SCTX=680.0/(NWID*WAR*MAX(CRX,CRY))
	CALL TXSCLE(SCTX)
	CALL TXORGN(500,350)
C
C remember various parameters.
	IMODSV=IMOD
	IDIRSV=IDIR
	IWIDSV=IWID
	IPITSV=IPIT
C
C set up for scans in line mode.
	IMOD=1
	IWID=NWID
	IPIT=NPIT
C
C perform two scans, one in each direction.
	DO 10 I=1,2
	   ID(I)=IWID*WAR*CR(3-I)
	   ID(3-I)=0
	   IDIR=0
	   IF (IABS(ID(2)).LT.IABS(ID(1))) IDIR=1
	   XS=XC - ID(X)/2.0
	   YS=YC - ID(Y)/2.0
	   CALL MOVTO(XS,YS)
	   CALL SCANBY(ID(X),ID(Y),1)
	   CALL EDGDMP(ID)
	   CALL SCSET(0)
10	CONTINUE
C
C restore state
	IMOD=IMODSV
	IDIR=IDIRSV
	IWID=IWIDSV
	IPIT=IPITSV
C
	IF (.NOT.SLAVE) GO TO 20
	CALL TXCUR
	CALL TXLOC(ICH,ICX,ICY)
	CALL CSWT(XC+ICX,YC+ICY)
C
20	CALL TXORGN(0,0)
	CALL TXSCLE(1.0)
	CALL TXMTO(0,600)
	CALL TXEND
C
	RETURN
	END
