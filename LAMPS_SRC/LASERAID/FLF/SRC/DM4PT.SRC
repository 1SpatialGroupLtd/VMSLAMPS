	LOGICAL*4 FUNCTION DM4PT
C
***	MODULE	DM4PT
***	IDENT	23JN83
C
C Copyright Laser-Scan Laboratories Limited, Cambridge, England.
C
C Author: D. Meader
C
C	Created	from WJJ4PT			 DM	03-Jun-1983
C
C Performs a four-point transformation.
C This routine is more expensive than WJJ4PT, but has the following
C advantages:
C   (1) The point distribution is checked for validity.
C   (2) The matrix A set up by WJJ4PT is not really suitable for SOLVE.
C
	REAL	  CPTS(4,4)	! CPSET entry - corner points
	REAL	  X,Y		! CPUSE entry - coords to transform
C
C use common area to preserve coefficients.
	REAL*8	  CPCOEF(4,2)
	COMMON/CPCOM/CPCOEF	! to hold the transform coeffs
C
	INTEGER	  I,J
	LOGICAL*4 CPSET,CPUSE		! function names
	REAL	  XX			! saved X
	REAL*8	  A(4,4)		! temporary matrix for SOLVE
	REAL*8	  CRIT			! criterion for rejection of fit
	REAL*8	  XM,YM,X2M,XYM,Y2M	! 'measured' X,Y,X**2,X*Y,Y**2
	REAL*8	  XT,YT			! 'true' X,Y (map space)
C
C ......................................................................
C
	ENTRY CPSET(CPTS)
C
C ... zero all the arrays.
	DO 20 J=1,4
	   CPCOEF(J,1)=0.0D0
	   CPCOEF(J,2)=0.0D0
	   DO 10 I=1,4
	      A(I,J)=0.0D0
10	   CONTINUE
20	CONTINUE
C
C ... set up the matrix equation AC=B, where B is CPCOEF initially.
	DO 30 I=1,4
	   XM=CPTS(1,I)				! measured values
	   YM=CPTS(2,I)
	   X2M=XM*XM				! for efficiency
	   XYM=XM*YM
	   Y2M=YM*YM
	   XT=CPTS(3,I)				! 'true' values
	   YT=CPTS(4,I)
C
	   CPCOEF(1,1)=CPCOEF(1,1) + XT		! r.h.s. of equation
	   CPCOEF(1,2)=CPCOEF(1,2) + YT
	   CPCOEF(2,1)=CPCOEF(2,1) + XT*XM
	   CPCOEF(2,2)=CPCOEF(2,2) + YT*XM
	   CPCOEF(3,1)=CPCOEF(3,1) + XT*YM
	   CPCOEF(3,2)=CPCOEF(3,2) + YT*YM
	   CPCOEF(4,1)=CPCOEF(4,1) + XT*XYM
	   CPCOEF(4,2)=CPCOEF(4,2) + YT*XYM
C
	   A(2,1)=A(2,1) + XM			! coefficient matrix A
	   A(3,1)=A(3,1) + YM
	   A(4,1)=A(4,1) + XYM
	   A(2,2)=A(2,2) + X2M
	   A(4,2)=A(4,2) + X2M*YM
	   A(3,3)=A(3,3) + Y2M
	   A(4,3)=A(4,3) + XM*Y2M
	   A(4,4)=A(4,4) + X2M*Y2M
30	CONTINUE
C
C ... finish setting up the coefficient matrix A.
	A(1,1)=4.0D0
	A(1,2)=A(2,1)
	A(3,2)=A(4,1)
	A(1,3)=A(3,1)
	A(2,3)=A(4,1)
	A(1,4)=A(4,1)
	A(2,4)=A(4,2)
	A(3,4)=A(4,3)
C
C ... now call SOLVE to give us the transform array C in CPCOEF.
	CALL SOLVE(A,CPCOEF,4,2,0,4,4)
C
C ... analyse the eigenvalues of A (now triangular).
C ... examine the first and last eigenvalues; if the fit is good these
C ... will have 'similar' magnitudes (one day I may get round to proving
C ... this, until then take my word for it !!)
C ... return .TRUE. if we don't believe these CPs are any good.
C ... N.B. we are more tolerant than in FIT, since we must allow fairly
C ... weird corner points to be used if required.
	CRIT=1.0D6			! rather arbitrary, this !
	CPSET=DABS(A(1,1)/A(4,4)).GT.CRIT
C
	RETURN
C
C ......................................................................
C
	ENTRY CPUSE(X,Y)
C
C ... X & Y contain the coord to be transformed.
C ... CPCOEF contains the transform matrix set up by CPSET
C
	XX=CPCOEF(1,1) + X*CPCOEF(2,1) + Y*CPCOEF(3,1) +
     &	   X*Y*CPCOEF(4,1)
	Y= CPCOEF(1,2) + X*CPCOEF(2,2) + Y*CPCOEF(3,2) +
     &	   X*Y*CPCOEF(4,2)
	X=XX
	CPUSE=.FALSE.				! we always succeed
C
	RETURN
	END
