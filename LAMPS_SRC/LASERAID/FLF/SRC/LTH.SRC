	SUBROUTINE LTH(HLPFIL,KEYWRD,CONTXT)
C
***	MODULE	LTH
***	IDENT	28JA86
C
C Copyright Laser-Scan Laboratories Limited, Cambridge, England.
C
C Author: D. Meader
C
CMOD	Conditional compilation removed		 DM	28-Jan-1986
CMOD	Command table PNTCM (POINT_MODE) added	 DM	11-Oct-1985
C	Created 				 DM	18-Sep-1985
C
C Laseraid Terminal Help facility.
C This routine is call-compatible with the earlier PDP and VAX
C implementations, but uses the VAX/VMS HELP utility.
C
	IMPLICIT NONE
C
	CHARACTER*(*) HLPFIL	! help file name
	INTEGER*2 KEYWRD	! the argument to '?' (or DOLLS if ??)
	INTEGER*2 CONTXT	! RAD50 mnemonic for the mode we're in
C
***	COMMON/EXPC/
C
	BYTE	  COMAND(3)	! KEYWRD translated from RAD50
	CHARACTER*14 MODES(9)	! mode names
	INTEGER*2 DOLLS		! '$$$' in RAD50
	INTEGER*2 R50MOD(8)	! possible RAD50 mode names
	INTEGER*4 I		! temporary variable
	INTEGER*4 MODLEN(9)	! length of each entry in MODES
C
C function:
	INTEGER*4 LIB$PUT_OUTPUT
	EXTERNAL  LIB$PUT_OUTPUT
C
	DATA DOLLS/3R$$$/
	DATA MODES/'MAIN_MODE','HELP_MODE','CONFIRM_MODE',
     &		   'CHECK_MODE','CALIBRATE_MODE','LOCATE_MODE',
     &		   'MEASURE_MODE','POINT_MODE','INVALID_MODE'/
	DATA MODLEN/9,9,12,10,14,11,12,10,12/
	DATA R50MOD/3RMAI,3RLNK,3RYNO,3RCHK,3RCOR,3RLOC,3RMEA,3RPNT/
C
C ......................................................................
C
C ... first determine which mode we're in.
	DO 10 I=1,8
	   IF (CONTXT.EQ.R50MOD(I)) GO TO 20
10	CONTINUE
C
C ... then decode the argument to '?'.
20	COMAND(1)=0			! if ?? then show all help
	IF (KEYWRD.NE.DOLLS) CALL R50ASC(3,KEYWRD,COMAND)
C
C ... and finally pass it all on to the help facility.
	CALL EXPAND('%S %A3',MODES(I)(:MODLEN(I)),COMAND)
	CALL LBR$OUTPUT_HELP(LIB$PUT_OUTPUT,72,EXPDSC,HLPFIL,0,)
	RETURN
	END
