	SUBROUTINE ONTRAK(FSTX,FSTY,PNTX,PNTY,NMIN,NMAX,HLIM2,NSEG)
C
***	MODULE	ONTRAK
***	IDENT	01JN81
C
**	IF VAX
	  IMPLICIT NONE
**	ENDC
C
C Copyright Laser-Scan Laboratories Ltd,Cambridge,England
C
C	Tidied					AJW	01-Jun-1981
C	Created					MCF	31-Aug-1977
C
C	Looks at line element [FST, PNT] to see if we are
C	backtracking along the already digitised line. Checks the
C	direction of the line element w.r.t. the MP line and checks
C	if PNT is closer than HLIM to the portion of the MP track
C	between points NMIN & NMAX. NSEG is set to the segment the
C	point is on, zero if none. 
C
C Parameters:
***	PARAMETER/PARAMS/
C
C arguments:
	REAL      FSTX,FSTY	! 1st point of the line element
	REAL      PNTX,PNTY	! last point of it
	INTEGER   NMIN,NMAX	! limits of track
	REAL      HLIM2		! closeness squared
	INTEGER   NSEG		! the segment
C
C common block:
***	COMMON/MPEP/
C
C workspace:
	INTEGER   I		! do loop counter
C
	REAL      FP(2)		! vector along line element
	REAL      AB(2)		! vector between 2 MPs
	REAL      AP(2)		! vector between MP & PNT
	REAL      ABAB		! length of AB squared
	REAL      ABAP		! component of AP along AB
	REAL      Q		!     "      "  " across "
C
	IF (NMAX-1 .LT. NMIN) GOTO 20	! check line exists
	FP(X) = PNTX-FSTX
	FP(Y) = PNTY-FSTY
	DO 10 I = NMIN,NMAX-1
	  NSEG = I
	  AB(X) = MP1(X,I+1) - MP1(X,I)
	  AB(Y) = MP1(Y,I+1) - MP1(Y,I)
C check line is anti-parallel to MP track
	  IF (FP(X)*AB(X)+FP(Y)*AB(Y).GE.0) GOTO 10	! if not
C yes - apply further checks
	  AP(X) = PNTX - MP1(X,I)
	  AP(Y) = PNTY - MP1(Y,I)
	  ABAB = AB(X)*AB(X) + AB(Y)*AB(Y)
C is point within segment ?
	  ABAP = AP(X)*AB(X) + AP(Y)*AB(Y)
	  IF (ABAP.LE.0 .OR.
     &        ABAP.GT.ABAB) GOTO 10
C yes - is it on, or near the line ?
	  Q = AP(X)*AB(Y) - AP(Y)*AB(X)
	  IF (Q*Q .LT. ABAB*HLIM2) GOTO 30 ! with NSEG set
C no
10	  CONTINUE
C seems OK
20	NSEG = 0
C
30	RETURN
	END
