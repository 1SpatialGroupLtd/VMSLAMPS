	SUBROUTINE DRWBUF(NMPTS,RESCAL,DRXMIN,DRYMIN,DRSCAL)
C
***	MODULE	DRWBUF
***	IDENT	09AP86
C
C Copyright Laser-Scan Laboratories Limited, Cambridge, England.
C
CMOD	Draw junction points in different colour DM	09-Apr-1986
CMOD	Extra argument (number of MPs to draw)	 DM	24-Sep-1985
CMOD	Colour added				 DM	06-Sep-1985
CMOD	Tidied					AJW	12-Feb-1981
C	Created					PGH
C
**	IF VAX
	IMPLICIT NONE
**	ENDC
C
***	PARAMETER/PARAMS/
C
***	COMMON/MPEP/
***	COMMON/BLG/
C
C arguments:
	INTEGER	  NMPTS			! number of master pnts to draw
	LOGICAL*1 RESCAL  		! rescale the drawing if true
	REAL      DRXMIN,DRYMIN		! minima
	REAL      DRSCAL		! scale
C
C workspace:
	REAL      DRYMAX,DRXMAX
	REAL      XRANGE,YRANGE
	REAL      P(2)
	INTEGER   I
	INTEGER   IX,IY
	INTEGER	  MINMP
C
C ......................................................................
C
	IF (NMP(1).LE.0) GO TO 90		! too small
	MINMP=MAX(1,NMP(1)-NMPTS+1)		! earliest master ...
	IF (NMPTS.LT.1) MINMP=1			! point to draw
	IF (.NOT.RESCAL) GO TO 50		! do not scale
C
	DRXMIN=MP1(X,MINMP)
	DRXMAX=DRXMIN
	DRYMIN=MP1(Y,MINMP)
	DRYMAX=DRYMIN
C
	IF (NMP(1).LT.MINMP+1) GO TO 20
	DO 10 I=MINMP+1,NMP(1)
	   IF (MP1(X,I).GT.DRXMAX) DRXMAX=MP1(X,I)
	   IF (MP1(Y,I).GT.DRYMAX) DRYMAX=MP1(Y,I)
	   IF (MP1(X,I).LT.DRXMIN) DRXMIN=MP1(X,I)
	   IF (MP1(Y,I).LT.DRYMIN) DRYMIN=MP1(Y,I)
10	CONTINUE
C
C check for any backlogged points
20	IF (INDEX(1).LE.0) GO TO 40
	DO 30 I=1,INDEX(1)
	   P(X)=MP1(X,NMP(1)) + XBLG(1,I)
	   P(Y)=MP1(Y,NMP(1)) + YBLG(1,I)
	   IF (P(X).GT.DRXMAX) DRXMAX=P(X)
	   IF (P(Y).GT.DRYMAX) DRYMAX=P(Y)
	   IF (P(X).LT.DRXMIN) DRXMIN=P(X)
	   IF (P(Y).LT.DRYMIN) DRYMIN=P(Y)
30	CONTINUE
C
C evaluate scale and origin
40	XRANGE=(DRXMAX-DRXMIN)/800.0
	YRANGE=(DRYMAX-DRYMIN)/650.0
	DRSCAL=MAX(XRANGE,YRANGE)
	IF (DRSCAL.LT.1.0E-5) DRSCAL=1.0
	DRSCAL=1.0/DRSCAL
	CALL TXORGN(0,0)
	CALL TXSCLE(1.0)
C
C draw it
50	DO 60 I=MINMP,NMP(1)
	   CALL TXCOL(5)
	   IX=(MP1(X,I)-DRXMIN)*DRSCAL + 100
	   IY=(MP1(Y,I)-DRYMIN)*DRSCAL + 50
	   IF (I.EQ.MINMP) CALL TXMTO(IX,IY)
	   CALL TXLTO(IX,IY)
	   IF (MPTYPE(I).GT.0) CALL TXCOL(7)	! junction
	   CALL DRWX(IX,IY,6)
60	CONTINUE
C
C and backlog if any
	IF (INDEX(1).LE.0) GO TO 80
	CALL TXCOL(5)
	DO 70 I=1,INDEX(1)
	   P(X)=MP1(X,NMP(1)) + XBLG(1,I)
	   P(Y)=MP1(Y,NMP(1)) + YBLG(1,I)
	   IX=(P(X)-DRXMIN)*DRSCAL + 100
	   IY=(P(Y)-DRYMIN)*DRSCAL + 50
	   CALL TXLTO(IX,IY)
	   CALL DRWX(IX,IY,4)
70	CONTINUE
C
80	CALL TXCOL(1)
	CALL TXEND
90	RETURN
	END
