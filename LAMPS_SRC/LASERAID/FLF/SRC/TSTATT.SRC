C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1982-11-03 11:29:24.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL*4 FUNCTION TSTATT(NX,NY,NCHN,EPS,FSTOFF,LSTOFF,MINPNT,
     &                          MINRAT,R2MIN)
C
***	MODULE	TSTATT
***	IDENT	03NO82
C
**	IF VAX
	  IMPLICIT NONE
**	ENDC
C
C Copyright Laser-Scan Laboratories Ltd,Cambridge,England
C
C	Tidied					AJW	02-Feb-1981
C	Created					MCF	11-Dec-1978
C
C Find the position along chain NCHN where point NX,NY is
C nearest to it (within tolerance EPS). Result is "point found"
C & details are in MINPNT & MINRAT:
C
C	MINPNT is nearest point of chain
C	nearest point is MINRAT between
C		     MINPNT & MINPNT+1
C
C The numbering of the points is from FIRST to LAST.
C
C parameters:
***	PARAMETER/PARAMS/
C
C arguments:
	INTEGER   NX,NY		! the point
	INTEGER   NCHN		! the chain
	REAL      EPS		! tolerance
	INTEGER   FSTOFF	! first margin
	INTEGER   LSTOFF	! last margin
	INTEGER   MINPNT	! nearest point
	REAL      MINRAT	! position along segment( 0.0 -> 1.0)
	REAL      R2MIN		! minimum distance squared
C
C common blocks:
***	COMMON/RSE/
***	COMMON/LINKS/
C
C local w/s:
	INTEGER   THSPNT	! point being considered
	INTEGER   I,J,K
	REAL      ALPHA		! ratio   "       "
	REAL      R2		! distance
	REAL      DX,DY,DX1,DY1	! coordinates
C
C
C
	TSTATT=.FALSE.
	R2MIN =EPS*EPS
	IF (LENGTH(NCHN).LT.2) GOTO 50	! silly chain
C look at all segments within margins
	DO 40 I=FIRST(NCHN)+FSTOFF,LAST(NCHN)-1-LSTOFF
	  J     =NODE(I)
	  K     =NODE(I+1)
	  DX    =FLOAT(ENCBUF(X,K))-FLOAT(ENCBUF(X,J))
	  DY    =FLOAT(ENCBUF(Y,K))-FLOAT(ENCBUF(Y,J))
C
	  IF (DX.EQ.0.AND.DY.EQ.0) GOTO 40	! repeated point
	    DX1   =FLOAT(NX)-FLOAT(ENCBUF(X,J))
	    DY1   =FLOAT(NY)-FLOAT(ENCBUF(Y,J))
	    R2    =DX*DX + DY*DY
	    ALPHA =(DX*DX1 + DY*DY1)/R2
	    THSPNT=I-FIRST(NCHN)+1
	    IF (ALPHA.GE.1.0) GOTO 10
	    IF (ALPHA.LE.0.0) GOTO 20
C within segment
	    DX1   =ALPHA*DX-DX1
	    DY1   =ALPHA*DY-DY1
	    GOTO 30
C off ends
10	    THSPNT=THSPNT+1
	    DX1   =DX-DX1
	    DY1   =DY-DY1
20	    ALPHA =0.0
C remember if nearest
30	    R2    =DX1*DX1 + DY1*DY1
	    IF (R2.GT.R2MIN) GOTO 40
	    R2MIN =R2
	    MINPNT=THSPNT
	    MINRAT=ALPHA
	    TSTATT=.TRUE.
40	  CONTINUE
C
50	RETURN
	END
