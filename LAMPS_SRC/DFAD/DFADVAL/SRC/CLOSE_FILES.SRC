	LOGICAL FUNCTION CLOSE_FILES()
C
***	MODULE CLOSE_FILES
***	IDENT 13JA88
C
C Copyright Laser-Scan Laboratories Ltd, Cambridge, England
C Author: J.M. Billing			13 Jan 1988
C
C Function to close any open files
C Returns .TRUE. if successful
C
	IMPLICIT NONE
C
	INCLUDE 'LSL$DFADVAL:DFADVAL.CMN'
	INCLUDE 'LSL$DFADVAL:DFADVALMSG.PAR'
C
C w/s variables
	INTEGER	OK
	INTEGER IERR
C
C functions
	INTEGER	FLWCLO
	INTEGER	FLWSPL
C
C Assume success
C
	CLOSE_FILES = .TRUE.
C
C Close IFF input file
C
	IF (INOPN) CALL IFFCLO(INLUN)
C
C Close Rule file
C
	IF (RULEOPN) THEN
	   RULEOPN = .FALSE.
	   CLOSE (UNIT = RULELUN, IOSTAT = OK)
	   IF (OK.NE.0) THEN
	      CALL LSL_PUTMSG(DV__UNCLORULE)
	      CLOSE_FILES = .FALSE.
	   ENDIF
	ENDIF
C
C Close list file
C
	IF (LISTOPN) THEN
	   LISTOPN = .FALSE.
C
C If printer option, close and spool it to output, otherwise just close
C
	   IF (PRINTER) THEN
	      OK = FLWSPL(LISTLUN, IERR)
	   ELSE
	      OK = FLWCLO(LISTLUN, IERR)
	   ENDIF
C
	   IF (.NOT.OK) THEN
	      CALL LSL_PUTMSG(OK)
	      CALL LSL_ADDMSG(IERR)
	      CLOSE_FILES = .FALSE.
	   ENDIF
	ENDIF
C
C Close LITES2 file
C
	IF (LITES2OPN) THEN
	   LITES2OPN = .FALSE.
	   OK = FLWCLO (LITES2LUN, IERR)
	   IF (.NOT.OK) THEN
	      CALL LSL_PUTMSG(OK)
	      CALL LSL_ADDMSG(IERR)
	      CLOSE_FILES = .FALSE.
	   ENDIF
	ENDIF
C
C Exit
C
	RETURN
	END
