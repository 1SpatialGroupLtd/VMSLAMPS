	SUBROUTINE PLACE(STABOX,MIDBOX,FINBOX,NBOXES)
C
***	IDENT	11JL84
***	MODULE	PLACE
C
C	Copyright Laser-Scan Laboratories Ltd, Cambridge, England.
C
C	Author:	Andrew Morris
C	Created: June 1984
C
C Finds which of from 1 to 3 of the four sub-boxes at level DEP
C are touched or crossed by the current line segment.
C
C STABOX gives first box intersected
C MIDBOX gives any box passed accross when 3 boxes intersected
C FINBOX gives last box intersected
C NBOXES gives how many boxes are intersected.
C
C The basis for the algorithm used here is that a segment crosses
C a box if and only if the line containing the segment crosses the
C box and the box around the segment overlaps the box.
C
C
	IMPLICIT	NONE
C
***	COMMON/HERE:INTCHK/
C
C Parameters in are in COMMON/INTCHK/
C
C Parameters out
	INTEGER		STABOX,MIDBOX,FINBOX
	INTEGER		NBOXES		! Number of boxes intersected
C
C Workspace
	INTEGER		ONBOX(0:3)	! Boxes intersected
	REAL		DXPQ,DYPQ
	INTEGER		IXMIN,IYMIN,IXMAX,IYMAX
	INTEGER		AX,AY		! Box axes/seg line int coords
C
C functions
	INTEGER		MIN0,MAX0
C
C Find box coords of seg line intersection with box axes
	DXPQ = BOXQX-BOXPX
	DYPQ = BOXQY-BOXPY
	IF (DYPQ.NE.0) THEN
	   AX = (BOXPX+(WID-BOXPY)*DXPQ/DYPQ)/WID
	ELSE
	   AX = 2
	END IF
	IF (DXPQ.NE.0) THEN
	   AY = (BOXPY+(WID-BOXPX)*DYPQ/DXPQ)/WID
	ELSE
	   AY = 2
	END IF
C
	IXMIN = MIN0(PIX,QIX)
	IXMAX = MAX0(PIX,QIX)
	IYMIN = MIN0(PIY,QIY)
	IYMAX = MAX0(PIY,QIY)
C
	NBOXES = 0
	IF (IXMIN.LE.0) THEN
	   IF ((IYMIN.LE.0).AND.((AX.LE.0).OR.(AY.LE.0))) THEN
	      NBOXES = 1
	      ONBOX(1) = 0
	   END IF
	   IF ((IYMAX.GT.0).AND.((AX.LE.0).OR.(AY.GT.0))) THEN
	      NBOXES = NBOXES+1
	      ONBOX(NBOXES) = 1
	   END IF
	END IF
	IF (IXMAX.GT.0) THEN
	   IF ((IYMIN.LE.0).AND.((AX.GT.0).OR.(AY.LE.0))) THEN
	      NBOXES = NBOXES+1
	      ONBOX(NBOXES) = 2
	   END IF
	   IF ((IYMAX.GT.0).AND.((AX.GT.0).OR.(AY.GT.0))) THEN
	      NBOXES = NBOXES+1
	      ONBOX(NBOXES) = 3
	   END IF
	END IF
C
C Find which are start, mid and fin boxes
	GOTO (10,30,20) NBOXES
C
C WARNING -if there is a path to this line, something is wrong!
	GOTO 40	! DEBUG
C
C Here if segment intersects exactly one box
10	STABOX = ONBOX(1)
	FINBOX = STABOX
	GOTO 40		! return
C
C Here if segment intersects exactly three boxes
20	IF (ONBOX(1).EQ.0) THEN
	   IF (ONBOX(3).EQ.3) THEN
	      MIDBOX = ONBOX(2)
	      ONBOX(2) = ONBOX(3)
	   ELSE
	      MIDBOX = ONBOX(1)
	      ONBOX(1) = ONBOX(3)
	   END IF
	ELSE
	   MIDBOX = 3
	END IF
C
30	IF (ONBOX(1)/2.NE.ONBOX(2)/2) THEN	! boxes horiz.
	   IF ((PIX.LT.QIX).EQV.(ONBOX(1).LT.ONBOX(2))) THEN
	      STABOX = ONBOX(1)
	      FINBOX = ONBOX(2)
	   ELSE
	      STABOX = ONBOX(2)
	      FINBOX = ONBOX(1)
	   END IF
	ELSE	! boxes vertical
	   IF ((PIY.LT.QIY).EQV.(ONBOX(1).LT.ONBOX(2))) THEN
	      STABOX = ONBOX(1)
	      FINBOX = ONBOX(2)
	   ELSE
	      STABOX = ONBOX(2)
	      FINBOX = ONBOX(1)
	   END IF
	END IF
C
40	RETURN
	END
