	LOGICAL FUNCTION OPEN_FADT()
C
***	MODULE OPEN_FADT
***	IDENT 13JU88
C
C Copyright Laser-Scan Laboratories Ltd, Cambridge, England.
C Author   : J.M. Billing			13 Jan 1988
C
C Modified : J.M. Billing			13 June 1988
C To perform full error checking/reporting in FORTRAN file operations
C
C Function to open indexed fadt file for read or to create new fadt file
C depending on value in COMMAND (1,2,3 for create,edit,list).
C Returns .TRUE. if succeeds
C
	IMPLICIT NONE
C
	INCLUDE 'LSL$FADTINPUT:FADTINPUT.CMN'
	INCLUDE 'LSL$FADTINPUT:FADTINPUTMSG.PAR'
C
C w/s variables
	CHARACTER*3	FILE_STATUS
	INTEGER		OK
C
C functions
	LOGICAL		COPY_OLD_FADT
C
C Assume success
C
	OPEN_FADT = .TRUE.
C
C Open new or old file depending on command
C
	IF (COMMAND.EQ.1) THEN
	   FILE_STATUS = 'NEW'
C
C Check if file exists
C
	   OPEN (	UNIT = FADTLUN,
     &			FILE = FADTFILE (1:FADTLEN),
     &			ORGANIZATION = 'INDEXED',
     &			ACCESS = 'KEYED',
     &			RECORDTYPE = 'FIXED',
     &			FORM = 'UNFORMATTED',
     &			RECL = 5,
     &			KEY = (1:2:INTEGER),
     &			STATUS = 'OLD',
     &			IOSTAT = OK)
C
C Tell user if open it successfully 
C
	   IF (OK.EQ.0) THEN
	      CALL FORM_MSG('FADT file with that specification exists'//
     &			 '. Creating new file anyway.',2)
	      CLOSE (FADTLUN)
	   ENDIF
C
C Set up status for old file
C
	ELSE
	   IF ((.NOT.WANT_LIST).AND.(.NOT.LISTOPN)) THEN
	      IF (.NOT.COPY_OLD_FADT()) THEN
C
C Deal with errors while copying existing file
C
	         OPEN_FADT = .FALSE.
	         GOTO 20
	      ENDIF
	   ENDIF
C
C Set file status to indicate this is an existing file
C
	   FILE_STATUS = 'OLD'
	ENDIF
C
C Open file
C
	OPEN (	UNIT = FADTLUN,
     &		FILE = FADTFILE (1:FADTLEN),
     &		ORGANIZATION = 'INDEXED',
     &		ACCESS = 'KEYED',
     &		RECORDTYPE = 'FIXED',
     &		FORM = 'UNFORMATTED',
     &		RECL = 5,
     &		KEY = (1:2:INTEGER),
     &		STATUS = FILE_STATUS,
     &		IOSTAT = OK)
C
C Check for errors
C
	IF (OK.EQ.0) GOTO 20
C
C Deal with errors while opening FADT file
C
	IF (HAD_LNM) THEN
C
C If using logical names, just output ordinary error messages
C
	   CALL WRITEF()
	   CALL LSL_PUTMSG(FADTIN__UNOPNFADT)
C
C Retrieve and output the fortran error message
C
	   CALL ERRSNS(,,,,OK)
	   CALL LSL_ADDMSG(OK)
	ELSE
C
C Deal with error while opening file under control of forms
C
	   CALL FORM_MSG('Unable to open FADT file',5)
	ENDIF
C
C Indicate failure to open file
C
	OPEN_FADT = .FALSE.
C
C Exit
C
20	RETURN
	END 
