C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1992-07-28 12:18:26.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE RDMAG
C
***	IDENT	28JL92
***	MODULE	RDMAG
C
C	Copyright LaserScan Laboratories Ltd., Cambridge, England
C
C Author TJ Ibbs,  1 March 1983
C
C to read from a DLMS DFAD (Digital Land Mass Simulation
C Digital Feature Analysis Data) mag tape, and assemble the block read
C into the buffer in /DFAD/ for later use.
C
C The tape is written by a Univac 1108 computer. This means that the
C basic word length is 36 bits, and that stripes are written in the
C opposite order to that expected by the VAX. Thus we must read each
C block, reverse the order of the stripes, and split the whole thing up
C into manageable portions (ie 18 bit chunks, which we can store in
C 32 bit longwords).
C
C It also means that the words on the tape are in 1s complement form.
C This has 2 main effects - firstly, we get two zeroes (+ and -),
C and secondly we must add 1 to a negative 1s complement number to
C convert it to 2s complement (which the VAX uses).
C We do no such conversion in here, because we don't know for which
C words this must be done.
C
C Before the data proper, we may find an ICL header block (27 stripes),
C but for the moment we shall not do anything about this
C
C The tape proper starts with a header block of 6 36bit words (ie 27
C stripes on the tape), then lots of data blocks, each of 602 36bit
C words (ie 2709 stripes on the tape) and we finish with a 6 36bit word
C trailer block
C
C For CHANGE 3 tapes, after the manuscript header, we get DSI and ACC
C blocks. The DSI block is 648 stripes long, the ACC block is 2700
C stripes long, where both are composed of 8 bit bytes, so must not
C be restructured into DFABUF!
C
C Also note that blocks of 5 and 601 words may be found, on tapes
C straight from the Univac (ie 23 or 2705 stripes)
C
C Henceforward, 'word' shall mean a 36bit word, and 'byte' a standard
C 8bit byte ('longword' will mean a VAX 32 bit longword)
C
C The optional ICL header block is of variable length
C
C The header/trailer blocks are 6 words long:
C	words 1-4 contain bitcoded info,
C	word 5 contains a checksum,
C	word 6 is zero
C
C The data blocks are 602 words long:
C	words 1-600 contain data,
C	word 601 contains a checksum,
C	word 602 is zero
C
C
	IMPLICIT NONE
C
C
C
C common blocks
***     PARAMETER/HERE:DFAD2IMSG/
***     PARAMETER/LSL$CMNLSL:LSLLIBMSG/ 
***	COMMON/HERE:STATE/		! what state we are in - BLKTYP
***	COMMON/HERE:PARAMS/		! for DIAG
***	COMMON/LSL$CMNLSL:MTIVCM/	! input buffering
***	COMMON/LSL$CMNDLMS:DFAD/	! DFAD storage, BYTBUF too
C
C functions in use
C
	INTEGER		MTIRDB		! read a block off magtape
	INTEGER*4	LIB$EXTZV	! extract a bitfield (0 extend)
C
C workspace
C
	INTEGER*4	RETVAL		! result from MTIRDB, etc
	INTEGER*4	IERR		! for widening errors into
	INTEGER*4	CHUNK(4)	! for Univac word assembly
	INTEGER*4	CHKSUM(2)	! for checksum assembly
	INTEGER		BASE		! count of longwords
	INTEGER		I,J		! DO loop count
	INTEGER		CHPTR		! for copying chunks
	INTEGER		DMPPTR		! for outputting block dump
	INTEGER		CHKTOP		! when to stop checksumming
C
C and we must unset our checksum...
C
	CHKSUM(1) = 0
	CHKSUM(2) = 0
C
C and unset any idea of what sort of block this might be
C
	BLKTYP = -1			! unknown/error block
C
C first, let us actually read that block...
C
	RETVAL = MTIRDB( BYTBUF, INLEN, BYTCNT )
C
	IF (.NOT.RETVAL) THEN  
	   IF (RETVAL.EQ.LSL__EOF)THEN
	      BLKTYP = TAPEMK		! we read a tapemark
	   ELSE
              CALL LSL_PUTMSG(DFAD2I__TAPRDBERR,MTINBL) 
C                                       !error reading new blk 
              CALL LSL_PUTMSG(RETVAL)   !off tape (#MTINBL)
              IERR = MTIERR		! widen the error
              IF(RETVAL.EQ.LSL__SYSERR)CALL LSL_PUTMSG( IERR )
	   ENDIF
	   RETURN			! and back to main program
	ENDIF
C
C otherwise we have the block safe in our hands
C
C NOTE - the mystical value 'A2'X is how HDDR looks when it is
C 4 into 3 packed into 8 bit stripes. This value comes at the start
C of an ICL header
C
	IF (BYTCNT.EQ.648) THEN
C
	   BLKTYP = DSIHED		! DSI block found
C
	ELSEIF (BYTCNT.EQ.2700) THEN
C
	   BLKTYP = ACCHED		! ACC block found
C
	ELSEIF (BYTCNT.EQ.2709.OR.BYTCNT.EQ.2705) THEN
C
	   CHKTOP = 600*2		! word 601 is checksum
	   BLKTYP = FTRBLK		! so it's a feature block
C
	ELSEIF (BYTCNT.EQ.27.OR.BYTCNT.EQ.23) THEN
C
	   CHKTOP = 4*2			! word 5 is checksum
	   BLKTYP = MANHED		! and it is a header
C
C	 but if first block, check for ICL header just in case
C
	   IF (MTINBL.EQ.1.AND.BYTBUF(1).EQ.'A2'X) BLKTYP = ICLHED
C
	ELSE
C
C	 an unknown block length - if it is the first block
C	 then check if it is an ICL header
C
	   IF (MTINBL.EQ.1.AND.BYTBUF(1).EQ.'A2'X) THEN
	      BLKTYP = ICLHED			! was a header
	   ELSE
	      BLKTYP = -1			! unknown block size
	   ENDIF
C
	ENDIF
C
C if we are skipping to a particular manuscript, we shall only
C process this block (below) if it is a manuscript header, since
C data blocks are being ignored
C
	IF (SKIP.AND.BLKTYP.NE.MANHED) THEN
	   RETURN			! and leave it at that
	ENDIF
C
C So let's dump it if wanted and if block is of known type
C
	IF (DUMP.AND.(BLKTYP.NE.-1)) THEN
	   CALL FLWSEL(DUMPLUN)
	   CALL FLWRTF
	   CALL FLWRTF
	   CALL FLWRTF('.Block %N, %N  longwords of data',
     &					MTINBL,(BYTCNT+3)/4)
	   CALL FLWRTF
C
C Now dump contents of byte buffer as hexadecimal longwords
C
	   DO 10 CHPTR = 1, ((BYTCNT+3)/4), 4
	      CALL EXPAND
C
	      DO 5 DMPPTR = CHPTR, MIN((CHPTR+3), ((BYTCNT+3)/4) )
	         CALL APPEND('%P0%X8  ', INTBUF(DMPPTR))
5	      CONTINUE
C
	      CALL FLWSTR
10	   CONTINUE
	ENDIF
C
C now, should we abandon processing at this level?
C We do so if it is not written with 36 bit words, or if we don't know
C what it is
C
	IF ( BLKTYP.EQ.DSIHED .OR. BLKTYP.EQ.ACCHED .OR.
     &	     BLKTYP.EQ.ICLHED .OR. BLKTYP.EQ.-1 ) THEN
	   IF (BLKTYP.EQ.-1) THEN
	      CALL LSL_PUTMSG(DFAD2I__BLKLENERR, BYTCNT) !unrecognised 
	   ENDIF                            ! block length BYTCNT
	   RETURN	                    ! to deal with the problem
	ENDIF
C
C So by this stage we actually DO want to do things with it -
C let us proceed
C
C Univac have the unfortunate habit (apart from using 36 bit words) of
C regarding the most significant stripe (in VAX terms) as the least
C significant. Thus we must reverse the order of the stripes in the
C buffer - each set of 1,2,3,4,... must become a set of ...,4,3,2,1
C
C Note that thus we reverse blocks of 9 stripes (2 words) which has the
C additional result of reversing the order of the words
C
C Ensure capability to deal with 'CHANGE 2' data.
	BYTCNT = (((BYTCNT-1)/9)+1)*9
C
	CALL SWAP( BYTBUF, BYTCNT, .FALSE. ) ! do the swapping, no DIAG
C
C so we now have our buffer containing stripes in a sensible order,
C but words in reversed pairs.
C
C we must now abstract 18 bit CHUNKs to insert into the buffer in
C /DFAD/ for future use, elsewhere in the program
C
C the sequence is really quite simple, and relies on the fact that
C 9*8 = 2*36 = 72  -  ie 9 bytes are equivalent to 2 Univac words
C and we can ensure that we unpack every 9 bytes into 4 CHUNKs
C
C word 2 comes first...
C chunk3 <-                byte1, byte2, bottom 2 bits of byte3
C chunk4 <-  top 6 bits of byte3, byte4, bottom 4 bits of byte5
C
C words 1 comes after it...
C chunk1 <-  top 4 bits of byte5, byte6, bottom 6 bits of byte7
C chunk2 <-  top 2 bits of byte7, byte8, byte9
C
C Note that when we extract our 18 bits, we do NOT sign extend
C
C as we read words, we add them to the checksum. When we read the
C checksum off the tape (last but one word, the last word is zero),
C we compare it with our calculated value. Thus we must not add the
C last two words into our calculated checksum - we work out the value
C that DFALEN will have when we reach the checksum,zero pair of words.
C
C we start with nowt in the buffer
C
	DFALEN = 0			! zero 1/2 word pointer
C
	DO 500 BASE = 1,BYTCNT,9
C
C	 get the second word of 36 bits from the buffer
C
	   CHUNK(3) = LIB$EXTZV( 0, 18, BYTBUF(BASE)  )	! low 1/2
	   CHUNK(4) = LIB$EXTZV( 2, 18, BYTBUF(BASE+2) )! high 1/2
C
C	 and get the first 36 bit word from the buffer
C
	   CHUNK(1) = LIB$EXTZV( 4, 18, BYTBUF(BASE+4) )! low 1/2
	   CHUNK(2) = LIB$EXTZV( 6, 18, BYTBUF(BASE+6) )! high 1/2
C
C	 we must now consider our checksum - we add the words we've
C	 worked out to the checksum, unless they are the last two
C	 words of the block, which are always checksum value and zero
C
	   IF ( DFALEN.EQ.CHKTOP ) THEN
C
C	    we have our checksum calculated - compare it with the value
C	    we've obtained from adding up all those words off tape
C
	      IF ( CHKSUM(1).NE.CHUNK(1) .OR.
     &		   CHKSUM(2).NE.CHUNK(2) ) THEN
		 CALL LSL_PUTMSG(
     &		    DFAD2I__CHKSUMERR, MTINBL ) !checksum incorrect in 
C                                               !    block MTINBL
		 CALL LSL_PUTMSG(               !        1/2 words:
     &		    DFAD2I__CHKWORDS,CHUNK(2), CHUNK(1)      ! recorded
     &                              , CHKSUM(2), CHKSUM(1) ) !   calc'd
	      ENDIF
C
C	    and we have finished reading everything of meaning in this
C	    block - we have no need to transfer the checksum to the
C	    buffer (and especially there's no point in making sure its
C	    not negative!), so we skip out to the bottom of the loop
C
	      GOTO 500			! finished assembling buffer
C
	   ELSE
C
C	    we must add these words to the checksum we are calculating
C
	      CALL CHECK( CHUNK(1), CHKSUM )
	      CALL CHECK( CHUNK(3), CHKSUM )
C
	   ENDIF
C
C	 and copy them over to the waiting buffer
C
	   DO 400 CHPTR = 1,4
C
	      DFABUF( DFALEN+CHPTR ) = CHUNK( CHPTR )
400	   CONTINUE
C
	   DFALEN = DFALEN + 4		! increase our chunk count
C
500	CONTINUE		! and loop for the next pair of words
C
C
C	IF ( DIAG_FULL ) THEN
C
C	 - describe our buffer's start...
C
C	   CALL WRITEF
C	   CALL WRITEF('.Block %N, %N ''words'' of data',
C     &					MTINBL,(DFALEN+1)/2)
C	   CALL WRITEF('.First few words are:')
C
C	   DO 1200 CHPTR = 1,12,4
C	      CALL WRITEF(' %P0%X5 / %X5,   %X5 / %X5',
C     &		   DFABUF(CHPTR+1), DFABUF(CHPTR), DFABUF(CHPTR+3),
C     &		   DFABUF(CHPTR+2) )
C1200	   CONTINUE
C
C	   CALL WRITEF
C	ENDIF
C
C So that's all over, and we have our buffer in hand.
C Point to the first word as the next one to be read...
C
	DFAPTR = 1
C
	RETURN
	END
