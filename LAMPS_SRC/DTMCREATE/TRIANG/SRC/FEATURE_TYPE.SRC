	LOGICAL	FUNCTION FEATURE_TYPE(LAYER,FC,ITYP,RIDPTS,RIVPTS)
C
***	MODULE	FEATURE_TYPE
***	IDENT	26JL88
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England
C
C	Created 					TJH 28-Jul-1988
C
C Arguments (in)
	INTEGER*2 LAYER		! layer number
	INTEGER*2 FC		! feature code
C
C Arguments (out)
	INTEGER	  ITYP		! see /COORDS/ comments
	LOGICAL	  RIDPTS	! .T. if ridgeline
	LOGICAL	  RIVPTS	! .T. if river
C
C W/s
	LOGICAL	  HAD_MATCH	! .T. if match found in bitmap
	LOGICAL	  OK		! for error reporting
C
	INCLUDE 'LSL$CMNTRIANG:SELECTIONS.CMN'
C
C Functions:
	LOGICAL		BITMATCH	! test for match in bitmap
C
C ......................................................................
C
	FEATURE_TYPE=.FALSE.
C
C Initialise
	RIDPTS=.FALSE.
	RIVPTS=.FALSE.
	ITYP=1
C
C See if the current layer is marked as a breakline
	OK=BITMATCH(BREKOV,LAYER,HAD_MATCH)
	IF (.NOT.OK) GOTO 10
	IF (HAD_MATCH) THEN
	   ITYP=ISIGN(ITYP,-1)
	ENDIF	
C
C and repeat for feature code breakline assignments
	OK=BITMATCH(BREKFC,FC,HAD_MATCH)
	IF (.NOT.OK) GOTO 10
	IF (HAD_MATCH) THEN
	   ITYP=ISIGN(ITYP,-1)
	ENDIF	
C
C Now see if the current layer is marked for cliff features
	OK=BITMATCH(OVCLC,LAYER,HAD_MATCH)
	IF (.NOT.OK) GOTO 10
	IF (HAD_MATCH) THEN
	   ITYP=-3
	ENDIF	
C
C and repeat for feature code cliff assignments
	OK=BITMATCH(FCCLC,FC,HAD_MATCH)
	IF (.NOT.OK) GOTO 10
	IF (HAD_MATCH) THEN
	   ITYP=-3
	ENDIF	
C
C Finally test whether we are dealing with a ridge or river string. 
C Points from such a string must be flagged in the byte array TPOLGY()
	RIDPTS=.FALSE.
	RIVPTS=.FALSE.
C
C See if the current layer is marked for ridgeline features
	OK=BITMATCH(OVRID,LAYER,HAD_MATCH)
	IF (.NOT.OK) GOTO 10
	IF (HAD_MATCH) RIDPTS=.TRUE.
C
C and repeat for feature code ridgeline assignments
	OK=BITMATCH(FCRID,FC,HAD_MATCH)
	IF (.NOT.OK) GOTO 10
	IF (HAD_MATCH) RIDPTS=.TRUE.
C
C See if the current layer is marked for river features
	OK=BITMATCH(OVRIV,LAYER,HAD_MATCH)
	IF (.NOT.OK) GOTO 10
	IF (HAD_MATCH) RIVPTS=.TRUE.
C
C and repeat for feature code river assignments
	OK=BITMATCH(FCRIV,FC,HAD_MATCH)
	IF (.NOT.OK) GOTO 10
	IF (HAD_MATCH) RIVPTS=.TRUE.
	FEATURE_TYPE=.TRUE.
C
10	RETURN
	END
