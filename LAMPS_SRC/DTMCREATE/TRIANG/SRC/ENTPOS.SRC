	SUBROUTINE ENTPOS(IX,IY,IZ,JZ,TPOL)
C
***	MODULE	ENTPOS
***	IDENT	31JL87
C
C Copyright Laser-Scan Laboratories Limited, Cambridge, England.
C
CMOD	Converted for LSLLIB message formats	TJH	31-Jul-1987
C Author M.J.McCullagh					March-1986
C
C Store IX,IY,IZ as a new node in the data set, by adding it onto
C end of the data presently there, and updating the box structure
C at the same time. The last point stored was NP.
C
	BYTE	TPOL
C
C TPOL is the feature type flag 0 = ordinary point
C				1 = river
C				2 = ridge
C
C Write a warning about data limits if we have crossed the 10% space
C left threshold.
C
	INCLUDE 'LSL$DTMCREATESYS:MESPAR.PAR'
	INCLUDE 'LSL$DTMCREATESYS:MESSAGE.CMN'
	INCLUDE 'LSL$CMNTRIANG:TRIANGMSG.PAR'
	INCLUDE 'LSL$CMNTRIANG:TPOLGY.CMN'
	INCLUDE 'LSL$CMNTRIANG:DATQ.CMN'
C
C ......................................................................
C
	K=NMAX*0.9
	IF (NP.EQ.K) THEN
	   CALL WRITEF('%S 90%% of available stack used !',WARNING)
	ENDIF
C
C Check if we have reached NMAX (the maximum number of points that
C can be held in memory).
	IF (NP+1.GT.NMAX) THEN
	   CALL LSL_GETMSG(TRIANG__STACKOVR,MESSAGE,MESLEN)
	   CALL ALARMS(MESSAGE(:MESLEN))
	ELSE
	   NP=NP+1
	ENDIF
C
C Store the point
	MAC(1,NP)=IX
	MAC(2,NP)=IY
	MAC(3,NP)=IZ
	KLIFFN(NP)=JZ
	TPOLGY(NP)=TPOL
C
C Tack it onto the box structure. Calculate its box number
	K=MAC(2,NP)/MYI*NBJ+MAC(1,NP)/MXI+1
C
C Check through the box for end of box list
	L=IN(K)
	IF(L.NE.0)GOTO 10
C
C This is first point in box, put number in index, and go to
C the section dealing with next point in box, and string marking
	IN(K)=NP
	GOTO 20
C
C Check for a next point in the box chain
10	M=L
	L=IABS(MAC(4,L))
	IF(L.NE.NPMAX)GOTO 10
C
C End of chain has been reached. The last point in box was M. So
C give it the pointer to the new box, but retain its string indicator
C sign (+ve or -ve).
	MAC(4,M)=NP*ISIGN(1,MAC(4,M))
C
C Tack on the new point at end of list including its string marker
20	MAC(4,NP)=NPMAX*LASTSN
C
C Indicate no neighbours have yet been written to file
	MAC(5,NP)=1
30	RETURN
	END
