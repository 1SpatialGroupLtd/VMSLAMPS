	SUBROUTINE FILLIT(IA,IB,JA,JB,IDIR,JDIR,LX,LY,LXINC,LYINC,NP)
C
***	MODULE	FILLIT
**	IDENT	25MY84
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England
C
CMOD	INDB replaced by MD(4,...)		 DM	25-May-1984
C	Standardised				TJH 	06-Apr-1984
C
C Set up the imaginary points from row IA to row IB, and column
C JA to JB, cycling through the rows and columns either forwards
C or backwards depending on the values of IDIR and JDIR (+1 =
C forwards, -1 = backwards). The X,Y location of the first point
C in the sequence is LX,LY, and the increments for the two
C coordinates are LXINC and LYINC. The first point number is to be
C NP+1 and increases by one for every point entered. The first
C point in box (ARRAY IN) is set at the same time.
C
C common blocks...
	INCLUDE 'LSL$CMNLSL:EXPC.CMN'
	INCLUDE 'LSL$CMNTRIANG:FINDIT.CMN'
	INCLUDE 'LSL$CMNTRIANG:STORAG.CMN'
	INCLUDE 'LSL$CMNTRIANG:ZONVAR.CMN'
C
C ......................................................................
C
	DO 20 IC=IA,IB
	   I=IC
	   IF(IDIR.EQ.-1)I=IB-IC+IA
	   K=(I-1)*NBJ
	   DO 10 JC=JA,JB
	      J=JC
	      IF(JDIR.EQ.-1)J=JB-JC+JA
C
C L is the box number of this imaginary point
	      L=K+J
	      NP=NP+1
C
C Put an entry into the first point in box list - IN(L), store the
C Coordinates of the imaginary point, give it a z value of -32767
C to show it is different from all others, set box continuation
C marker from this point to zero.
	      IN(L)=NP
	      MD(1,NP)=LX
	      MD(2,NP)=LY
	      MD(3,NP)=-32767
	      MD(4,NP)=NPMAX
C
C Get next pair of coordinates
	      LX=LX+LXINC*JDIR
10	   CONTINUE
	   LY=LY+LYINC*IDIR
20	CONTINUE
	RETURN
	END
