	SUBROUTINE GETNAY(NOD,NTEM,LAX,NUM)
C
***	MODULE	GETNAY
***	IDENT	31JL87
C
C Copyright Laser-Scan Laboratories Limited, Cambridge, England.
C
CMOD	Converted for LSLLIB message formats	TJH	31-Jul-1987
CMOD    NTEM dimensioned to NAYMAX via DIMS.PAR  DM     05-Jul-1984
CMOD	TEXTOT replaced by WRITEF		 DM	03-Jul-1984
CMOD	More efficient random reads		 DM	11-Jun-1984
CMOD	Inefficient NTEM(LAX) now NTEM(50)	 DM	06-Jun-1984
CMOD    Loops indented, tidied  		TJH     05-Jun-1984
CMOD	NAYB now in MAC				 DM	29-May-1984
C	Standardised				TJH	25-May-1984
C
C......Get the neighbours of a node (NOD) from storage, put them in
C......NTEM. There are NUM of them, and LAX room for them in NTEM.
C
	INCLUDE 'LSL$DTMCREATESYS:MESPAR.PAR'
	INCLUDE 'LSL$DTMCREATESYS:MESSAGE.CMN'
	INCLUDE 'LSL$CMNTRIANG:TRIANGMSG.PAR'
	INCLUDE 'LSL$CMNTRIANG:DATQ.CMN'
	INCLUDE 'LSL$CMNTRIANG:DIMS.PAR'
	INCLUDE 'LSL$DTMCREATESYS:IOLUNS.CMN'
C
	DIMENSION NTEM(NAYMAX)
C
	INTEGER I,J
	INTEGER NTEM2(0:10)		! for efficient reading
C
C ......................................................................
C
	LREC=IABS(MAC(5,NOD))
C
C Unrecognised record number?
	IF (LREC.LE.1.OR.LREC.GT.NMAX) THEN
	   CALL LSL_GETMSG(TRIANG__UNRECREC,MESSAGE,MESLEN)
	   CALL ALARMS(MESSAGE(:MESLEN))
	ENDIF
C
C......Read up to LAX/10 nodes in groups of ten nodes
	NBLK=LAX/10
	J=-9
	DO 30 I=1,NBLK
	   J=J + 10
C......Check whether this record is stored in memory or in the disk file
	   IF (LREC.LE.NFREES) THEN
C......In memory file space, so pick it up
	      M=0
	      DO 10 L=J,J+9
	         M=M + 1
	         NTEM(L)=IFREES(M,LREC)
10	      CONTINUE
	      M=IFREES(11,LREC)
	   ELSE
C......In disk file space, so get offset, and pick it up
	      LOFF=LREC-NFREES
	      READ (LGC1,ERR=40,REC=LOFF) NTEM2
	      DO 20 L=0,9
	         NTEM(J+L)=NTEM2(L)
20	      CONTINUE
	      M=NTEM2(10)
	   ENDIF
C......For both memory and disk space files, check for next record
	   IF (M.GT.0) THEN
C......Get number of neighbours
	      NUM=(I-1)*10 + M
	      GO TO 50
	   ELSE
	      LREC=IABS(M)
	   ENDIF
30	CONTINUE
C
C......Only reach here if more than LAX neighbours in file for node NOD
	CALL LSL_GETMSG(TRIANG__TOMNYNEIB,MESSAGE,MESLEN)
	CALL ALARMS(MESSAGE(:MESLEN)//' - GETNAY')
C
C......Error in random file read
40	CALL ERRSNS(,I,J)
	IF (J.EQ.0) J=I
	CALL WRITEF(' ')
	CALL LSL_GETMSG(TRIANG__RANDRD,MESSAGE,MESLEN)
	CALL ALARMS(MESSAGE(:MESLEN)//' - GETNAY')
	CALL LSL_GETMSG(J,MESSAGE,MESLEN)
	J=INDEX(MESSAGE,'!') - 1		! (ignore any FAO stuff)
	IF (J.LT.0) J=I
	CALL ALARMS(MESSAGE(:J))		! close down & exit
C
C ......................................................................
C
50	RETURN
	END

