	SUBROUTINE GETNAY(NOD,NTEM,LAX,NUM,LUNB)
C
***	MODULE	GETNAY
***	IDENT	23NO87
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England
C
CMOD	DTMCREATE version with LSLLIB			TJH 23-Nov-1987
C	Standardised/incorporated into LSL DTMCREATE	TJH 29-May-1985
C
C Get the neighbours of a node (NOD) from storage, put them in
C NTEM. There are NUM of them, and LAX room for them in NTEM.
C
C Parameters:
	INCLUDE  'LSL$DTMCREATESYS:GRIDPARAMS.PAR'
	INCLUDE  'LSL$CMNTRIDER:TRIDERMSG.PAR'
C
C Common:
	INCLUDE  'LSL$DTMCREATESYS:STACK.CMN'
	INCLUDE  'LSL$DTMCREATESYS:IOLUNS.CMN'
	INCLUDE 'LSL$DTMCREATESYS:MESSAGE.CMN'
C
	DIMENSION NTEM(NTMSIZ)
	DIMENSION NTEM2(0:10)	! for efficient reading
C
C ......................................................................
C
	LREC=IABS(NAYB(NOD))
	IF (LREC.LE.1)THEN
	   CALL LSL_GETMSG(TRIDER__UNRECREC,MESSAGE,MESLEN)
	   CALL ALARMS(MESSAGE(:MESLEN))
	ENDIF
C
C Read upto LAX/10 nodes in groups of ten nodes
	NBLK=LAX/10
	DO 30 I=1,NBLK
	   J=(I-1)*10+1
	   K=J+9
C
C Check whether this record is stored in the memory file, or the
C disk file
	   IF(LREC.LE.NFREES) THEN	! in memory, so pick it up
	      M=0
	      DO 10 L=J,K
	         M=M+1
	         NTEM(L)=IFREES(M,LREC)
10	      CONTINUE
	      M=IFREES(11,LREC)
	   ELSE
C
C In disk file space, so get offset, and pick it up
	      LOFF=LREC-NFREES
	      READ (LUNB,REC=LOFF,ERR=50) NTEM2
	      DO 20 L=0,9
	         NTEM(J+L)=NTEM2(L)
20	      CONTINUE
	      M=NTEM2(10)
	   ENDIF
C
C For both memory and disk space files, check for next record
	   IF(M.GT.0)GOTO 40
	   LREC=IABS(M)
30	CONTINUE
C
C Only reach here if more than LAX neighbours in file for node NOD
	CALL LSL_GETMSG(TRIDER__TOMNYNEIB,MESSAGE,MESLEN)
	CALL ALARMS(MESSAGE(:MESLEN)//' - GETNAY')
C
C Get number of neighbours
40	NUM=(I-1)*10+M
	GOTO 60
C
C Error in random file read
C
C......Error in random file read
50	CALL ERRSNS(,I,J)
	IF (J.EQ.0) J=I
	CALL WRITEF
	CALL LSL_GETMSG(TRIDER__RANDRD,MESSAGE,MESLEN)
	CALL ALARMS(MESSAGE(:MESLEN)//' - GETNAY')
	CALL LSL_GETMSG(J,MESSAGE,MESLEN)
	J=INDEX(MESSAGE,'!') - 1		! (ignore any FAO stuff)
	IF (J.LT.0) J=I
	CALL ALARMS(MESSAGE(:J))		! close down & exit
C
C ......................................................................
C OK termination
60	RETURN
	END
