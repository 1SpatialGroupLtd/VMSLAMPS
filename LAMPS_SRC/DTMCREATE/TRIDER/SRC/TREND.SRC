	SUBROUTINE TREND
C
***	MODULE	TREND
**	IDENT	30MY85
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England
C
C	Standardised/incorporated into LSL DTMCREATE  TJH  29-Jun-1984
C
C
	DIMENSION A(3,4),T(4)
C
	INCLUDE  'LSL$DTMCREATESYS:FINDIT.CMN'
	INCLUDE  'LSL$DTMCREATESYS:STORAG.CMN'
	INCLUDE  'LSL$DTMCREATESYS:TRYVAR.CMN'
	INCLUDE  'LSL$DTMCREATESYS:ZONVAR.CMN'
	INCLUDE  'LSL$DTMCREATESYS:IOLUNS.CMN'
	INCLUDE  'LSL$DTMCREATESYS:GRAFCS.CMN'
C
C ......................................................................
C
C Set graphics colour for imaginary points
C
	IF (GRAPHICS) CALL COLOUR(3,.FALSE.)
C
C Set trend sums of squares to zero
10	DO 30 I=1,3
	   DO 20 J=1,4
	      A(I,J)=0.0
20	   CONTINUE
30	CONTINUE
	T(1)=1.0
C
C Don't include imaginary points in the trend calculation!
	L=LIMPOL+1
	DO 60 I=L,NP
C
C Get sums of squares matrix
	   DO 40 J=1,3
	      T(J+1)=IABS(MD(J,I))
40	   CONTINUE
	   DO 50 J=1,3
	      DO 50 K=1,4
	         A(J,K)=A(J,K)+T(K)*T(J)
50	   CONTINUE
60	CONTINUE
C
C Sums of squares complete. Now find coefficients of the trend
C after putting R.H.S. into T
	DO 70 J=1,3
	   T(J)=A(J,4)
70	CONTINUE
C
	CALL SOLVE(A,T,3,3,IERR)
C
C Trend coefficients are in T - B0, B1(X), B2(Y).
C go through imaginary points (1-LIMPOL), reposition them according
C to the user's wishes.
	DO 90 I=1,LIMPOL
C
C Assign z values calculated from the trend.
	   Z=T(1)+T(2)*MD(1,I)+T(3)*MD(2,I)
	   IF (GRAPHICS) CALL DOT(MD(1,I),MD(2,I))
	   MD(3,I)=AMIN1(AMAX1(Z,BOTTOM),UPPER)
C
C Set derivatives to trend estimate for data set.
	   SLP(1,I)=T(2)
	   SLP(2,I)=T(3)
	   DO 80 J=3,5
80	   SLP(J,I)=0.0
90	CONTINUE
	RETURN
	END
