	SUBROUTINE FACET(IX,IY,IZ,IOP,LIST)
C
***	MODULE	FACET
***	IDENT	04JN85
C
C Copyright Laser-Scan Laboratories Limited, Cambridge, England.
C
C Standardised/incorporated into LSL Panacea  TJH  04-Jun-1985
C
C
C......Interpolates a z value at IX,IY that is in a triangle whose
C......vertices are specified by the elements of LIST. The interpolation
C......is linear across the triangle giving a flat facet. If IOP is
C......0 only an interpolation is made, if 1 triangle coefficients are
C......set up and then an interpolation is made.
C
	DIMENSION LIST(3)
C
	INCLUDE 'LSL$DTMCREATESYS:STORIT.CMN'
	INCLUDE 'LSL$DTMCREATESYS:TRIVIA.CMN'
	INCLUDE 'LSL$DTMCREATESYS:RESOLUTION.CMN'
C
C ......................................................................
C
C......Pick up the number of the last point in the triangle list
	NUMB=LIST(3)
C......Check for option value
	IF(IOP.EQ.0)GOTO 100
C......Calculate coefficients - the hard way!
	TA=0.0
	TB=0.0
	SA=0.0
	SB=0.0
	SC=0.0
	IZVAL=IABS(MD(3,NUMB))
	DO 101 I=1,2
	    P(I)=0.0
	    L=LIST(I)
	    X=MD(1,L)-MD(1,NUMB)
	    Y=MD(2,L)-MD(2,NUMB)
	    Z=IABS(MD(3,L))-IZVAL
	    SA=SA+X*X
	    SB=SB+X*Y
	    SC=SC+Y*Y
	    TA=TA+X*Z
	    TB=TB+Y*Z
  101	CONTINUE
	DET=SA*SC-SB*SB
	IF(DET.LT.1.0E-5)GOTO 100
	P(1)=(TA*SC-TB*SB)/DET
	P(2)=(SA*TB-SB*TA)/DET
C......Slopes for the linear trend passing through the third vertex
C......are now stored in P(1) for F1(X), and in P(2) for F1(Y).
  100	Z=IABS(MD(3,NUMB))+P(1)*(IX-MD(1,NUMB))+P(2)*(IY-MD(2,NUMB))
CCC	IZ=AMAX1(AMIN1(Z+0.5,300000.0),1.0)
	IZ=AMAX1(AMIN1(Z+0.5,MAX_RES_REAL),1.0)
	RETURN
	END
