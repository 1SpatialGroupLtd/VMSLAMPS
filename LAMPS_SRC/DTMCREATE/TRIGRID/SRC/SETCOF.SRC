C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1988-04-05 13:41:46.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL*4 FUNCTION SETCOF(X,Y,Z,ZX,ZY,ZXX,ZXY,ZYY)
C
C
***	MODULE	SETCOF
***	IDENT	03FE86
C
C Copyright Laser-Scan Laboratories Limited, Cambridge, England.
C
CMOD	Zero tests on all floating division added,
C    	made logical function 				TJH  03-Feb-1986
CMOD	IERR added to return 1 if a floating divide by 
C   	zero would occur				TJH  24-Jun-1985
C	Standardised/incorporated into LSL Panacea	TJH  04-Jun-1985
C
C	Returns .FALSE. if a floating divide by zero would have occurred
C
	DIMENSION X(3),Y(3),Z(3),ZX(3),ZY(3),ZXX(3),ZXY(3),ZYY(3)
	DIMENSION ZU(3),ZV(3),ZUU(3),ZUV(3),ZVV(3)
C
	COMMON /COEFFS/ XO,YO,
     1	A11,A12,A21,A22,
     2	P00,P01,P02,P03,P04,P05,
     3	P10,P11,P12,P13,P14,
     4	P20,P21,P22,P23,
     5	P30,P31,P32,
     6	P40,P41,
     7	P50
C
C ......................................................................
C
	SETCOF=.TRUE.			! assume the best
	ZEROCK=1.0E-24
C
CC	XO=X(1)/1000.0
CC	YO=Y(1)/1000.0
CC	X2=X(2)/1000.0-XO
CC	Y2=Y(2)/1000.0-YO
CC	X3=X(3)/1000.0-XO
CC	Y3=Y(3)/1000.0-YO
C
	XO=X(1)
	YO=Y(1)
	X2=X(2)-XO
	Y2=Y(2)-YO
	X3=X(3)-XO
	Y3=Y(3)-YO
	X2Y3=X2*Y3
	X3Y2=X3*Y2
	A=X2Y3-X3Y2
C
	IF (ABS(A).LT.ZEROCK) GOTO 111
C
	A11= Y3/A
	A12=-X3/A
	A21=-Y2/A
	A22= X2/A
	X2X2=X2*X2
	Y2Y2=Y2*Y2
	X3X3=X3*X3
	Y3Y3=Y3*Y3
	X2X3=X2*X3
	Y2Y3=Y2*Y3
	T1=2.0*X2*Y2
	T2=X2Y3+X3Y2
	T3=2.0*X3*Y3
	DO 101 I=1,3
	   ZU(I)=(X2*ZX(I))+(Y2*ZY(I))
	   ZV(I)=(X3*ZX(I))+(Y3*ZY(I))
	   ZUU(I)=(X2X2*ZXX(I))+(T1*ZXY(I))+(Y2Y2*ZYY(I))
	   ZUV(I)=(X2X3*ZXX(I))+(T2*ZXY(I))+(Y2Y3*ZYY(I))
	   ZVV(I)=(X3X3*ZXX(I))+(T3*ZXY(I))+(Y3Y3*ZYY(I))
101	CONTINUE
	P00=Z(1)
	P10=ZU(1)
	P01=ZV(1)
	P20=0.5*ZUU(1)
	P02=0.5*ZVV(1)
	P11=ZUV(1)
	T1=Z(2)-P00-P10-P20
	T2=ZU(2)-P10-ZUU(1)
	T3=ZUU(2)-ZUU(1)
 	P30= (10.0*T1)-(4.0*T2)+(0.5*T3)
	P40=-(15.0*T1)+(7.0*T2)-     T3
	P50= ( 6.0*T1)-(3.0*T2)+(0.5*T3)
	T1=Z(3)-P00-P01-P02
	T2=ZV(3)-P01-ZVV(1)
	T3=ZVV(3)-ZVV(1)
 	P03= (10.0*T1)-(4.0*T2)+(0.5*T3)
	P04=-(15.0*T1)+(7.0*T2)-     T3
	P05= ( 6.0*T1)-(3.0*T2)+(0.5*T3)
	D2=SQRT(X2X2+Y2Y2)
	IF (ABS(D2).LT.ZEROCK) GOTO 111
	D3=SQRT(X3X3+Y3Y3)
	IF (ABS(D3).LT.ZEROCK) GOTO 111
	ANG1=ATAN2(Y2,X2)
	ANG2=ATAN2(Y3,X3)
	ANG3=ATAN2(Y3-Y2,X3-X2)
	ANG4=ANG2-ANG1
	ANG5=ANG3-ANG1
	ANG6=ANG4-ANG5
	A=COS(ANG4)
	P41=(5.0*D3*A*P50)/D2
	P14=(5.0*D2*A*P05)/D3
	T1=ZV(2)-P01-P11-P41
	T2=ZUV(2)-P11-(4.0*P41)
	P21= (3.0*T1)-T2
	P31=-(2.0*T1)+T2
	T1=ZU(3)-P10-P11-P14
	T2=ZUV(3)-P11-(4.0*P14)
	P12= (3.0*T1)-T2
	P13=-(2.0*T1)+T2
	A= SIN(ANG6)/D2
	B=-COS(ANG6)/D2
	C= SIN(ANG5)/D3
	D= COS(ANG5)/D3
	AC=A*C
	AD=A*D
	BC=B*C
	G1=A*AC*((3.0*BC)+(2.0*AD))
	G2=C*AC*((3.0*AD)+(2.0*BC))
	T1=-(A*A*A*((5.0*A*B*P50)+(((4.0*BC)+AD)*P41)))
     1	   -(C*C*C*((5.0*C*D*P05)+(((4.0*AD)+BC)*P14)))
	T2=(0.5*ZVV(2))-P02-P12
	T3=(0.5*ZUU(3))-P20-P21
	IF (ABS(G1+G2).LT.ZEROCK) GOTO 111
	P22=((G1*T2)+(G2*T3)-T1)/(G1+G2)
	P32=T2-P22
	P23=T3-P22
	RETURN
C
111	SETCOF=.FALSE.
	RETURN
	END
