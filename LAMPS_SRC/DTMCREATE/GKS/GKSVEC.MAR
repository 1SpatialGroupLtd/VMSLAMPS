	.TITLE GKSVEC
	.IDENT /08AP92/
; 
; Copyright Laser Scan Laboratories Ltd., Cambridge, England
; Author Clarke Brunt				CCB	09-Sep-1987
;
; GKSLDLIB transfer vector
; Version for DTM programs
;
; The order of the entries in this transfer vector must remain unchanged
; if the new shareable image is to be useable by programs which were
; linked with the old version. New additions should be made at the end.
; If new additions push the size over a page boundary, then the ASSUME
; will fail and the positions of the common blocks (which follow in the
; image) will change, so another page should be reserved, and the major
; id. of the GSMATCH link option incremented, to prevent use of the new
; image by old programs. 
;
; decide if we want a stub or a real version
	STUB = 0
	.IF	NOT_DEFINED MOTIF
	.IF	NOT_DEFINED UIS
	STUB = 1
	.ENDC
	.ENDC

; define a macro to create transfer veector for a CALL entry point
	.MACRO	TRANSFER,ROUTINE
	.IF	EQ STUB
	.TRANSFER	ROUTINE
	.MASK		ROUTINE
	JMP		L^ROUTINE+2
	.IFF
	.ENTRY		ROUTINE,^M<>
	RET
	.BLKB		5
	.ENDC
	.ENDM	TRANSFER

	.PSECT		TRANSFER,EXE,NOWRT

START:
	TRANSFER	GACWK
	TRANSFER	GCLKS
	TRANSFER	GCLRWK
	TRANSFER	GCLWK
	TRANSFER	GDAWK
	TRANSFER	GESC
	TRANSFER	GFA
	TRANSFER	GOPKS
	TRANSFER	GOPWK
	TRANSFER	GPL
	TRANSFER	GPM
	TRANSFER	GQFACI
	TRANSFER	GQFAIS
	TRANSFER	GQFASI
	TRANSFER	GQLN
	TRANSFER	GQLWSC
	TRANSFER	GQMDS
	TRANSFER	GQMK
	TRANSFER	GQPLCI
	TRANSFER	GQPLF
	TRANSFER	GQPMCI
	TRANSFER	GSASF
	TRANSFER	GSCLIP
	TRANSFER	GSDS
	TRANSFER	GSELNT
	TRANSFER	GSFACI
	TRANSFER	GSFAIS
	TRANSFER	GSFASI
	TRANSFER	GSLN
	TRANSFER	GSLWSC
	TRANSFER	GSMK
	TRANSFER	GSPLCI
	TRANSFER	GSPMCI
	TRANSFER	GSVP
	TRANSFER	GSWKVP
	TRANSFER	GSWKWN
	TRANSFER	GSWN
	TRANSFER	GUWK
	TRANSFER	LSG_BEGIN_AREA
	TRANSFER	LSG_END_AREA
;;;	TRANSFER	LSG_SET_PATTERN
	.BLKB		8
;;;	TRANSFER	GGDP
	.BLKB		8
	TRANSFER	GTX
	TRANSFER	GSTXCI
	TRANSFER	GSCHH
	TRANSFER	GSCHUP
	TRANSFER	GSTXFP
	TRANSFER	LSG_SET_ROTATION
;
; up to here was same as used by FPP etc.
; now add things for mouse interaction and other
; requirements of DTMCREATE
;
	TRANSFER	SET_BUTTON_AST
	TRANSFER	GET_MOUSE_POSITION
	TRANSFER	GUIS_PUSH_VIEWPORT
	TRANSFER	LSG_PLANE_OFFSET
	TRANSFER	LSG_PLANE_SELECT
	TRANSFER	LSG_SET_CURSOR
	TRANSFER	LSG_SET_LUT
; UIS version does not have a SET_TRACK_AST, so vector it
; to SET_MOUSE_AST which will do instead
	.IF	DEFINED	UIS
SET_TRACK_AST::
	.MASK		SET_MOUSE_AST
	JMP		L^SET_MOUSE_AST+2
	.IFF
	TRANSFER	SET_TRACK_AST
	.ENDC
	TRANSFER	GSCHSP
END:

; Allow up to one pages for the transfer vector.
; If another page is needed, then the common blocks will be moved, making
; the new version incompatible with the old.
	NPAG = 1
	NVEC = <NPAG*512 - <END-START> >/8
	.IF GE NVEC
	.PRINT		NVEC	;slots remaining in transfer vector
	.IFF
	.ERROR		;transfer vector too long
	.ENDC

	.END
