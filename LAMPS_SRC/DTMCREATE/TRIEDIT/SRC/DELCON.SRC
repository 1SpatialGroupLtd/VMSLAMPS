	SUBROUTINE DELCON(NPTA,NPTB)
C
***	MODULE	DELCON
***	IDENT	07DE87
C
C Copyright Laser-Scan Laboratories Limited, Cambridge, England.
C
CMOD	Converted for TRIEDIT			TJH	07-Dec-1987
CMOD	The word according to St. Michael	 DM	31-Jan-1985
CMOD    NTEM dimensioned to NTMSIZ via DIMS.PAR TJH     05-Jul-1984
C	Standardised				TJH 	25-May-1984
C
C Delete the connection from node NPTA to node NPTB.
C
	INCLUDE 'LSL$CMNTRIEDIT:DIMS.PAR'
C
	INCLUDE 'LSL$CMNTRIEDIT:DATQ.CMN'
C
	DIMENSION NTEM(NTMSIZ)
C
C ......................................................................
C
C......Get neighbours of NPTA
	CALL GETNAY(NPTA,NTEM,NTMSIZ,NUM)
C......Go through list of neighbours searching for NPTB. When found
C......delete it by closing the list up.
	K=0
	DO 10 J=1,NUM
	   IF (NTEM(J).EQ.NPTB) GOTO 10
	   K=K+1
	   NTEM(K)=NTEM(J)
10	CONTINUE
C......Reset NUM to the new smaller list size
	NUM=K
C......Check for first and last points in the list being the same. If
C......they are not we have deleted the same start/end of rotation node
C......from both ends of the list. The other possibility is that this is
C......a peripheral imaginary point (node number <= LIMPOL) in which
C......case we want to leave it alone without duplication of first
C......point.
	IF (NTEM(1).EQ.NTEM(NUM).OR.NPTA.LE.LIMPOL) GOTO 20
C......Yes, we have. make the first node the start of rotation node.
	NUM=NUM+1
	NTEM(NUM)=NTEM(1)
C......Put the modified (shortened) neighbour list back into storage.
20	CALL PUTNAY(NPTA,NTEM,NTMSIZ,NUM)
	RETURN
	END
