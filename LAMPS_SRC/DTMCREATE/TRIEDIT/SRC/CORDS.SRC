	SUBROUTINE CORDS(IXA,IYA,CA,SA,L,IX,IY,IS1,IS2,IT,IOP)
C
***	MODULE  CORDS
***	IDENT	07DE87
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England
C
CMOD	Converted for TRIEDIT			TJH	07-Dec-1987
C	Standardised				TJH	25-May-1984
C
C......Calculates l.l. corner position of a character straddling
C......a contour line held in store, with spacings in IPDIS
C
C
	INCLUDE 'LSL$CMNTRIEDIT:ANNOT.CMN'
C
C ......................................................................
C
C......Find distance from last plotting (line or character)
	IS1=IS1+IT
C......Find distance along contour line that corresponds to a
C......character position
	CALL WIND(IPDIS,IS1,IS2,L,NST)
C......New IX,IY position lies between (L-1) and (L)
	J=L-1
	ID=IPDIS(L)
	IS3=IS2-ID
	ISB=IS1-IS3
	S4=FLOAT(ISB)/FLOAT(ID)
	IX=IXYBUF(J,1)+(IXYBUF(L,1)-IXYBUF(J,1))*S4
	IY=IXYBUF(J,2)+(IXYBUF(L,2)-IXYBUF(J,2))*S4
C......Check for central (non character plotting) option
	IF(IOP.EQ.1)RETURN
C......Find right hand edge of character, even if in a later segment,
C......locate L at this point, and calulate the slope from old L to new
C......L to act as average orientation for  the character to be plotted
C......in this position
	ICA=IS1+IWID
	IS2=IS2-ID
	CALL WIND(IPDIS,ICA,IS2,L,NST)
	AX=IXYBUF(L,1)-IX
	AY=IXYBUF(L,2)-IY
	D=SQRT(AX**2+AY**2)
C......Compute slope cos and sin and then L.L. corner
	CA=AX/D
	SA=AY/D
	IXA=IX+IHT*SA
	IYA=IY-IHT*CA
C......Update ix,iy distances for next call
	IT=IWID
	IS2=IS2-IPDIS(L)
	RETURN
	END
