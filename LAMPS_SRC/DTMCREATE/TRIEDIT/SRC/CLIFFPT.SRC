C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1989-01-26 12:23:18.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION CLIFFPT(KPNT,LEFTZ,KITEZ)
C
***	MODULE  CLIFFPT
***	IDENT   01MR88
C
C Copyright Laser-Scan Laboratories Limited, Cambridge, England
C
C Created						TJH 01-Mar-1988
C
C Function to determine whether point KPNT is part of a cliff
C Function returns false if not.
C
C If KPNT is a cliff point, function determines left and right height
C for point, knowing that from TRIANG cliff expansion, the left side
C nodes will have higher node numbers.
C......................................................................
C
	IMPLICIT NONE
C
C Arguments:
	INTEGER		KPNT		! the point
	INTEGER		LEFTZ,KITEZ	! z left, z right
C
C Common:
	INCLUDE 'LSL$CMNTRIEDIT:DATQ.CMN'
	INCLUDE 'LSL$CMNTRIEDIT:RCONVI.CMN'
C
C W/s
	INTEGER		KBOX		! current box
	INTEGER		LBX		! a point in the box
C
C ......................................................................
C
	CLIFFPT=.FALSE.			! assume non-cliff point
C
C If part of a cliff KPNT will have a perfect duplicate in the 
C current box. Calc current box
	KBOX=MAC(2,KPNT)/MYI*NBJ+MAC(1,KPNT)/MXI+1
	LBX=IN(KBOX)			! get first pt in box
C
C Check for this point having been deleted from the data set
10	IF (ABS(MAC(5,LBX)).NE.1) THEN
C
C Don't compare point with itself!
	   IF (LBX.NE.KPNT) THEN
C
C Is point LBX a perfect duplicate of KPNT? If so it is a cliffline 
C point.
	      IF (MAC(1,LBX).EQ.MAC(1,KPNT)
     &			.AND.MAC(2,LBX).EQ.MAC(2,KPNT)) THEN
	         CLIFFPT=.TRUE.
C
C Now determine which is the left and right height
	         LEFTZ=ABS(MAC(3,MAX(LBX,KPNT)))
	         KITEZ=ABS(MAC(3,MIN(LBX,KPNT)))
	         GOTO 20
	      ENDIF
	   ENDIF
	ENDIF
	LBX=IABS(MAC(4,LBX))		! get next point in box
	IF (LBX.NE.NPMAX) GOTO 10	! last point in box?
20	RETURN
	END
