	LOGICAL FUNCTION DRAW_CMD(HADTOP,IZOOM)
C
***	MODULE	DRAW_CMD
***	IDENT	27MR92
C
C Copyright Laser-Scan Laboratories Limited, Cambridge, England.
C
C	Created						TJH 23-Dec-1987
C
C Routine to DRAW up various data
C
	IMPLICIT NONE
C
C Arguments:
	INTEGER		IZOOM	! zoom flag
	LOGICAL		HADTOP	! had feature coding?
C
C W/s
	INTEGER		I	! temp var
	LOGICAL		TRIFF	! .T. if TRIANGLES to output to IFF
	INTEGER		CMD     ! decoded command number
	LOGICAL		OK	! function return
	BYTE		CH	! for use with RDCHS
C
C
C Parameters:
	INCLUDE 	'LSL$DTMCREATESYS:MESPAR.PAR'
	INCLUDE 	'LSL$CMNLSL:LSLLIBMSG.PAR'
C
C Common:
	INCLUDE 	'LSL$CMNTRIEDIT:ANNOT.CMN'
	INCLUDE 	'LSL$CMNTRIEDIT:COMMAND.CMN'
	INCLUDE 	'LSL$DTMCREATESYS:IOLUNS.CMN'
	INCLUDE 	'LSL$DTMCREATESYS:MESSAGE.CMN'
	INCLUDE 	'LSL$CMNTRIEDIT:ICI.CMN'
	INCLUDE 	'LSL$CMNTRIEDIT:TTTYPE.CMN'
	INCLUDE 	'LSL$CMNIFF:IFFHAN.CMN'
	INCLUDE 	'LSL$CMNLSL:EXCEPTION.CMN'
	INCLUDE 	'LSL$CMNLSL:EXPC.CMN'
	INCLUDE 	'LSL$CMNLSL:CMDCOM.CMN'
C
C Functions:
	LOGICAL		GETFILNAM	! get file spec from TXTBUF
	LOGICAL		IFFCREATE	! open new IFF file to write
	LOGICAL		ICISTOR		! check intermediate IFF storage
	LOGICAL  	RDCHS		! read character from TXTBUF
	LOGICAL  	RDCOMM		! read command from TXTBUF
	INTEGER		SIGCHS		! significant chars (LSLLIB)
C
C Command table:
	EXTERNAL	DRAW_CMD_TABLE
	INTEGER		DRAW_CMD_TABLE
C
C ......................................................................
C
	DRAW_CMD=.TRUE.
C
	CMD=RDCOMM(DRAW_CMD_TABLE)	! get command
C
C Error?
	IF (ERRNUM.NE.LSL__NORMAL) THEN
	   IF (ERRNUM.EQ.LSL__UNEXPCMD) THEN
	      CALL EXPAND('%S unexpected %S',WARNING,CMDNST)
	      CALL OUTOPT(EXPDSC)
	   ELSEIF (ERRNUM.EQ.LSL__AMBIG) THEN
	      CALL EXPAND('%S ambiguous command %S',WARNING,CMDNST)
	      CALL OUTOPT(EXPDSC)
	   ELSEIF (ERRNUM.EQ.LSL__UNEXPEOL) THEN
	      GOTO 10
	   ENDIF
	   GOTO 20
	ENDIF
	CMD=CMD+1
	GOTO (
     &		10,	! error/nothing
     &		30,	! breaklines
     &		40,	! contours
     &		50,	! labels
     &		60,	! nodes
     &		70,	! ridgelines
     &		80,	! rivers
     &		90,	! strings
     &		100	! triangles
     &			), CMD	
C
C Command number out of range
	CALL OUTOPT('Command not yet implemented')
	GOTO 110
C
C ......................................................................
C Error
10	CALL OUTOPT(ERROR//'Specifying command DRAW')
20	CALL OUTOPT(' ')
	CALL OUTOPT('Available DRAW command qualifiers are:')
	CALL OUTOPT(' ')
	CALL OUTOPT(
     &	'BREAKLINES     CONTOURS     LABELS     NODES    '//
     &		'RIDGELINES   RIVERS')
	CALL OUTOPT(' ')
	CALL OUTOPT('STRINGS        TRIANGLES')
	CALL OUTOPT(' ')
	DRAW_CMD=.FALSE.
	GOTO 110
C
C ......................................................................
C DRAW BREAKLINES
30	CALL PDISPL(3,0,9)
	GOTO 110
C
C ......................................................................
C DRAW CONTOURS
C Additionally, by providing a filename, the user can produce 
C an IFF file of the contour data, which can be used to generate
C a hardcopy at a later date.
C
40	IF (KONTBL.EQ.1) THEN		! only ask about IFF if 
C					! auto-redraw option off
	   IF (RDCHS(CH)) THEN		! any (preferably 'IFF') arg?
	      CONIFF=.FALSE.
	   ELSE				! something anyway
	      CALL BSCH
	      OK=GETFILNAM(IFFCON,I,'LSL$IF:IFF.IFF',.TRUE.,' ')
	      IF (.NOT.OK.OR.I.LE.0) THEN
	         CALL OUTOPT(WARNING//'error reading IFF file-spec')
	         GOTO 110			! give up -> nxt cmnd
	      ENDIF
	      CONIFF=.TRUE.
	   ENDIF	  
C
C  Check enough storage is available for an IFF file
	   IF (CONIFF) THEN			! IFF name in buffer
	      OK=ICISTOR()
	      IF (.NOT.OK) THEN 
	         CALL OUTOPT(WARNING//
     &		'not enough memory allocated for IFF generation')
	         CONIFF = .FALSE.
	         GOTO 110
	      ENDIF
	   ENDIF				! coniff = .true.
	ENDIF					! kontbl.eq.1 
C
C Now do the contour generation to the screen, and to an IFF
C file if this option was flagged above.
C
	IF (CONIFF) THEN
	   FIRSTCALL = .TRUE.	! so can init. storage
C
C start the beast up
	   CALL IFFERM(.FALSE.) ! turn IFFLIB error reporting off
	   CALL IFFOPN(LGC8,IFFCON)
	   IF (IERCD.EQ.0) THEN
	      CALL IFFRWD
	      CALL SCNCLR	! clear the screen
	      CALL OUTOPT(' ')
	      CALL EXPAND('IFF file %S opened for write', 
     &		IFFCON(1:SIGCHS(IFFCON)))
	      CALL OUTOPT(EXPDSC)
	   ELSE
	      CALL OUTOPT(ERROR//'opening IFF file')
	      CALL LSL_GETMSG(LSL__IFFCREATE,MESSAGE,MESLEN)
	      CALL EXPAND(
     &		'%'//MESSAGE(1:MESLEN),IFFCON(1:SIGCHS(IFFCON)))
	      CALL OUTOPT(EXPDSC)
	      CALL IFFERM(.TRUE.) ! turn IFFLIB error reporting back on
	      CALL LSL_WAIT(2,2)	! wait for 2 secs.
	      GOTO 110
	   ENDIF
C
C Do the recontour
	   CALL	CONTUR	
	   CALL	IFCLOS(LGC8)			! close the IFF
	ELSE
	   CALL	CONTUR
	ENDIF
	CONIFF = .FALSE.			! reset for other users
C						! of CONTUR
	GOTO 110
C
C ......................................................................
C DRAW LABELS
50	IF (LAB_HT) CALL POINTS(-1)
	IF (LAB_SEQ) CALL PDISPL(2,0,6)
	IF (LAB_STR) CALL PDISPL(4,0,6)
	GOTO 110
C
C ......................................................................
C DRAW NODES
C Display the location of all visible points immediately without
C waiting for a possible display during update sequence. A '+' is
C used as the marker
60	CALL PDISPL(1,1,6)
	GOTO 110
C
C ......................................................................
C DRAW RIDGELINES
70	IF (.NOT.HADTOP) CALL OUTOPT
     &	(WARNING//'No feature codes were read in from the .DTA file')
	CALL PDISPL(6,1,6)
	GOTO 110
C
C ......................................................................
C DRAW RIVERS
80	IF (.NOT.HADTOP) CALL OUTOPT
     &	(WARNING//'No feature codes were read in from the .DTA file')
	CALL PDISPL(7,1,6)
	GOTO 110
C
C ......................................................................
C DRAW STRINGS
C Link all strings together so that they can be seen on the screen.
C NB: this is not the triangle linkage - it may or may not
C correspond with that linkage.
90	CALL PDISPL(5,0,6)
	GOTO 110
C
C ......................................................................
C DRAW TRIANGLES
C Generate a neighbour/link display whether update drawing of
C neighbours is switched on or not
100	IF (RDCHS(CH)) THEN	! any (preferably 'IFF') argument?
	   TRIFF=.FALSE.
	ELSE				! something anyway
	   CALL BSCH
	   OK=GETFILNAM(IFFCON,I,'LSL$IF:IFF.IFF',.TRUE.,' ')
	   IF (.NOT.OK.OR.I.LE.0) THEN
	      CALL OUTOPT(WARNING//'error reading IFF file-spec')
	      GOTO 110			! give up -> nxt cmnd
	   ENDIF
	   TRIFF=.TRUE.
	ENDIF	  
	CALL NAYCON(IFFCON,TRIFF)
	IF (TRIFF) CALL UPDATE(2,IZOOM)	! restore screen
110	RETURN
	END
