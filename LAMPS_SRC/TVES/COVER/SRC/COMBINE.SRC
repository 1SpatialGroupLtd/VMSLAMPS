	SUBROUTINE COMBINE(ICHAN1,ICHAN2)
C
***	IDENT	06SE91
***	MODULE	COMBINE
C
C	Copyright : Laser-Scan Laboratories, Cambridge, England
C	Author    : D R Catlow
C	
C	Routine for COVER to add the temporary intervisibility matrix
C	to the cover matrix. C	This is done in a bit-wise manner. 
C	The cover matrix is opened on Channel ICHAN1
C	The temp. matrix is opened on Channel ICHAN2
C	Special case is when ICHAN2 = 0.  Simply set the
C	appropriate bit in the cover matrix as indicated by
C	the present cell value.
C
C	Modified 20JL87 to use LSLLIB and DTILIB	DRC
C
	IMPLICIT NONE
C
C	DTILIB commons
***	COMMON/LSL$CMNDTI:DTIHDR/	!file header details
C
C arguments:
	INTEGER*4		ICHAN1	!Cover matrix
	INTEGER*4		ICHAN2	!Temp. matrix
C
C locals:
	INTEGER*4		COL	!Column counter
	INTEGER*4		ROW	!Row counter
	INTEGER*4		RETVAL	!function return call
	INTEGER*4		ERROR	!system error code
	INTEGER*2		CELL1	!cell value of file on ICHAN1
	INTEGER*2		CELL2	!cell value of file on ICHAN2
C
C DTILIB functions:
	INTEGER*4	        DTI_CLOSE	! to unmap temp file
	INTEGER*4		DTI_READ_WORD_DATA  !to read a cell value
	INTEGER*4		DTI_WRITE_WORD_DATA !to write a cell value
C
C Systen functions:
	INTEGER*4		LIB$DELETE_FILE	! to delete named file
C
C***********************************************************************
C
C	Loop through the two files, retrieving cell values
C
	DO 200 COL=1,X_EXTENT(ICHAN1)
C
	   DO 100 ROW=1,Y_EXTENT(ICHAN1)
C
	      IF(ICHAN2.EQ.0)THEN
		RETVAL=DTI_READ_WORD_DATA(
     &		       ICHAN1,
     &		       CELL1,
     &		       COL,
     &                 ROW)
C
	   	IF(CELL1.EQ.-1.OR.CELL1.EQ.0)GOTO 100
C	Set to 0 before encoding
		CELL2 = 0
C	Set appropriate bit, and write back	        
	        CELL2 = IBSET(CELL2,CELL1-1)
C	Write back the modified data value
		RETVAL=DTI_WRITE_WORD_DATA(
     &		       ICHAN1,
     &		       CELL2,
     &		       COL,
     &                 ROW)
	        GOTO 100
	      ENDIF
C
C-----------------------------------------------------------------------
C ICHAN2 other than 0
C
	      RETVAL=DTI_READ_WORD_DATA(
     &		       ICHAN2,
     &		       CELL2,
     &		       COL,
     &                 ROW)
C
	      IF(CELL2.EQ.-1)GOTO 100	!no action required
C
	      RETVAL=DTI_READ_WORD_DATA(ICHAN1,CELL1,COL,ROW)
C
	      IF(CELL2.EQ.0)THEN
C
	        IF (CELL1.EQ.-1) RETVAL=DTI_WRITE_WORD_DATA(ICHAN1,
     &		       					0,COL,ROW)
	        GOTO 100
	      ENDIF
C
C---------------------------------------------------------------------
C
	      IF(CELL1.EQ.-1)CELL1=0	!initialise before encoding
C
C Set the appropriate bit
	      CELL1 = IBSET(CELL1,CELL2-1)
C Write the modified value back to the data area
	      RETVAL=DTI_WRITE_WORD_DATA(
     &		     ICHAN1,
     &		     CELL1,
     &		     COL,
     &               ROW)
C
100	    CONTINUE
C
200	  CONTINUE
C
C-------------------------------------------------------------------------
C Close and delete temp. visibility matrix
C
	IF (ICHAN2.NE.0)THEN
	  RETVAL=DTI_CLOSE (ICHAN2,.FALSE.,.TRUE.,ERROR)
	  RETVAL=LIB$DELETE_FILE('LSL$DTI:VISBLE.TMP;0')
	ENDIF
C
C-------------------------------------------------------------------------
C
	RETURN
	END
C
