	LOGICAL FUNCTION DISPLAY_INSIDE(X_TARGET,Y_TARGET,
     &                                  OBSERVER,
     &                                  LEFT_CONE,RIGHT_CONE,DTILUN)
C
***	MODULE	DISPLAY_INSIDE
***	IDENT	11JA89
C
C	Copyright : Laser-Scan Laboratories, Cambridge, England
C	Author    : DRC 
C	Created   : October 1985
C
C	Routine for COVER to test if DTI cell is inside
C	an observer's cone of vision.
C	Function returns true if the target point is inside
C	the cone of vision.
C
	IMPLICIT NONE
C
C	commons
***	COMMON/LSL$CMNDTI:DTIHDR/
C
***	COMMON/HERE:TARGET/
***	COMMON/HERE:DISPLY/
C
C 	arguments:
	INTEGER*4	X_TARGET		! x and y position
	INTEGER*4	Y_TARGET		! of target point
	INTEGER*4	OBSERVER		! observer number
	REAL		LEFT_CONE		! orientation fron north
C						! of cone of vision lefthand
C						! extremity
	REAL		RIGHT_CONE		! right extremity
	INTEGER*4	DTILUN			! LUN on which matrix is
C						! opened
C
C	locals:
	REAL		CONE_DISTANCE		! distance from apex to
C						! base of cone
	REAL		CONE_ANGLE		! angle of cone of vision
	REAL		DIFFX			! x and y components
	REAL		DIFFY			! of observer / target vector
	REAL		TEST_DISTANCE		! distance from apext to
C						! base of cone along line
C						! of bearing between observer
C						! and target
	REAL		DTARGET_ANGLE		! elevation angle of target
C						! to observer in degrees
	REAL		TEST_ANGLE		! 
	REAL		TARGET_ORIENTATION	! orientation of target
C						! point in degrees measured
C						! from north
C	parameter:
	REAL		PI			! for radian to degree
C						! conversion
	PARAMETER	(PI=3.141592654)
C
C
C---------------------------------------------------------------------
C
C	Assume initially that the point is outide the observer's
C	cone of vision
	DISPLAY_INSIDE=.FALSE.
C
C---------------------------------------------------------------------
C	If we are actually on the observer position return immediately
	IF(X_TARGET.EQ.DTI_D_OBSERVERS(OBSERVER,1).AND.
     &	   Y_TARGET.EQ.DTI_D_OBSERVERS(OBSERVER,2))THEN
	   DISPLAY_INSIDE=.TRUE.
	  GOTO 999
	ENDIF
C
C---------------------------------------------------------------------
C	Transfer cone and cone distance values for this observer
	CONE_ANGLE=CON_D_OBSERVERS(OBSERVER)
	CONE_DISTANCE=DIS_D_OBSERVERS(OBSERVER,2)
C
C---------------------------------------------------------------------
C	Calculate distance of target point from observer in metres
	DIFFX=(X_TARGET*X_GRID(DTILUN))-POS_D_OBSERVERS(OBSERVER,1)
	DIFFY=(Y_TARGET*Y_GRID(DTILUN))-POS_D_OBSERVERS(OBSERVER,2)
C
C--------------------------------------------------------------------------
C	Test distance of target point from observer against cone
C	distance
C
	IF(CONE_DISTANCE.EQ.0.0)GOTO 100
C
	TARGET_DISTANCE=SQRT((DIFFX**2)+(DIFFY**2))
	IF(TARGET_DISTANCE.GT.CONE_DISTANCE)GOTO 999
C------------------------------------------------------------------------
C	Does the angle from north of the target point with respect
C	to the observer, lie between the angles define the planimetric
C	extremeties of the cone of vision.
C	This eliminates points that lie behind the observer, or points
C	outside the planimetric extremes of the cone.
C	This test is not carried out if a 360 degree sweep around the
C	observer is required.
C
100	IF(CONE_ANGLE.EQ.360)GOTO 900			!inside
C
C	Caculate angle in degrees
	TARGET_ORIENTATION=ATAN2(DIFFX,DIFFY)*180.0/PI
	IF(TARGET_ORIENTATION.LT.0.0)
     &     TARGET_ORIENTATION=TARGET_ORIENTATION+360.0
C
C	Test the target orientation against the extremeties of the cone
	IF(RIGHT_CONE.GT.LEFT_CONE)THEN
	  IF(TARGET_ORIENTATION.GE.LEFT_CONE.AND.
     &       TARGET_ORIENTATION.LE.RIGHT_CONE)GOTO 900		!inside
	  GOTO 999						!outside
C
	ELSE
	  IF(TARGET_ORIENTATION.GE.LEFT_CONE)THEN
	    IF(TARGET_ORIENTATION.GT.RIGHT_CONE)GOTO 900	!inside
	  ELSE
	    IF(TARGET_ORIENTATION.LT.RIGHT_CONE)GOTO 900	!inside
	  ENDIF
	ENDIF
C
	GOTO 999						!outide
C
C-------------------------------------------------------------------------
900	DISPLAY_INSIDE=.TRUE.
C	
999	RETURN
C
	END

