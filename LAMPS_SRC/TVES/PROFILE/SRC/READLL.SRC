C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1988-04-29 10:23:24.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION READLL(RESULT,NUMVAL)
C
***	IDENT	29AP88
***	MODULE	READLL
C
C	Copyright LaserScan Laboratories Ltd., Cambridge, England
C	Author 	 : D R Catlow
C	Created  : April 1988
C	Modified :
C
C	Function :
C	To read free format latitude and longitude values input on
C	the command line by the user.
C	The values are specified in the format DDD MM SS H
C	The Degrees field and Hemisphere must be present;
C	the minutes and seconds field are optional.
C	The values must be supplied in the order latitude followed
C	by longitude, and the different numerical fields must be
C	separated by a spaces.
C	Function returns true on sucesss, and false if an error
C	is detected.
C	Up to 4 values may be read by the function.
C
C----------------------------------------------------------------------
C
	IMPLICIT NONE
C
C	Arguments:
	INTEGER*4	RESULT(4)	! lat and long in seconds
	INTEGER*4	NUMVAL		! number of values to read

C
***	COMMON/LSL$CMNLSL:TXTC/
C
C	Local
	INTEGER*4	I	! counter
	INTEGER*4	ACCUM	! accumulated seconds
	INTEGER*4	NUMBER	! integer number read using RDLONG
	BYTE		CH	! single character
C
C 	Functions:
	LOGICAL		RDCHS	! read next character ignoring spaces
	LOGICAL		RDLONG	! read an integer 
C
C----------------------------------------------------------------------
C
C	Set to failure initially
	READLL = .FALSE.
C
C----------------------------------------------------------------------
C	Read NUMVAL values
	DO 500 I=1,NUMVAL
C
C	First the degree value (this is compulsory)
	   IF(RDCHS(CH))GOTO 999			!missing value
C
C	Check that the character is a numeral
	   IF(CH.LT.48.OR.CH.GT.57)GOTO 999
C
C	Backspace over the character just read, and try
C	reading the number
	   CALL BSCH
C
C	Initialise seconds total
	   ACCUM = 0
C
C	Read a number
	   IF(RDLONG(NUMBER))GOTO 999
C
C 	Convert to seconds, and transfer to ACCUM
	   ACCUM = NUMBER*3600
C
C	Test if minutes is present (optional)
	   IF(RDCHS(CH))GOTO 999	
C
C	Test for numeral or alpha-character	 
	   IF(CH.GT.64)GOTO 300		! alpha character ?
	   IF(CH.LT.48.OR.CH.GT.57)GOTO 999
C
C	Backspace over character
	   CALL BSCH
C	Read the minutes value
	   IF(RDLONG(NUMBER))GOTO 999
C
C	Convert to seconds, and transfer to ACCUM
	   ACCUM = ACCUM+NUMBER*60
C
C	Test if seconds field is present (optional)
	   IF(RDCHS(CH))GOTO 999	
C
C	Test for numeral or alpha-character	 
	   IF(CH.GT.64)GOTO 300		! alpha character ?
	   IF(CH.LT.48.OR.CH.GT.57)GOTO 999
C
C	Backspace over character
	   CALL BSCH
C
C	Read the seconds value
	   IF(RDLONG(NUMBER))GOTO 999
C
C	Add to ACCUM
	   ACCUM = ACCUM+NUMBER
C
C	Read the hemisphere value and check its validity
	   IF(RDCHS(CH))GOTO 999	! missing
	   IF(CH.LT.65)GOTO 999		! missing
C
C 	Is it a negative type hemisphere
300	   IF(I.EQ.1.OR.I.EQ.3)THEN
	     IF(CH.EQ.'W'.OR.CH.EQ.'w'.OR.CH.EQ.'E'.OR.CH.EQ.'e')THEN
	       CALL WRITEF
     &         ('Latitude and longitude values supplied in wrong order')
	       GOTO 999
	     ENDIF
	   ELSE
	     IF(CH.EQ.'N'.OR.CH.EQ.'n'.OR.CH.EQ.'S'.OR.CH.EQ.'s')THEN
	       CALL WRITEF
     &         ('Latitude and longitude values supplied in wrong order')
	       GOTO 999
	     ENDIF
	   ENDIF
C
C	Check for a correct hemisphere character
	   IF (CH.NE.'S'.AND.CH.NE.'s'.AND.
     &         CH.NE.'N'.AND.CH.NE.'n'.AND.
     &         CH.NE.'W'.AND.CH.NE.'w'.AND.
     &         CH.NE.'E'.AND.CH.NE.'e')GOTO 999
C
    	   IF (CH.EQ.'S'.OR.CH.EQ.'s'.OR.CH.EQ.'W'.OR.CH.EQ.'w')
     &        ACCUM=-ACCUM
C
C	Transfer to result
           RESULT(I)=ACCUM
C
500	CONTINUE
C
C	Signal sucess
	READLL = .TRUE.
C
C-----------------------------------------------------------------------
C
999     RETURN
C
	END
