        SUBROUTINE POINT_VALUE(XPOS,YPOS)
C 
***	MODULE	POINT_VALUE
***	IDENT	03MY88
C
C 	Copyright Laser-Scan Laboratories Ltd.,Cambridge,England
C 	Author  : D R Catlow
C	Created : 21 April 1988
C	Modified:
C
C	Function: 
C	To determine a height value and a clutter index value for
C	the point XPOS,YPOS.
C	The height value consists of a terrain surface height
C	interpolated from the DTM, and a clutter height (eg. the
C	height of a wood) derived from a clutter file.
C	The clutter index indicates from which file
C	the clutter value has been derived.
C	The routine writes this information to
C	the output data buffer.
C
C----------------------------------------------------------------------
C
	IMPLICIT NONE
C
***	COMMON/HERE:PROFILE/
C
C Arguments:
	REAL		XPOS		  !X coordinate position
	REAL		YPOS		  !Y coordinate position
C
C Locals:
	REAL		ZVALUE	  	  !Height value
	REAL		CLUTTER_VALUE	  !Clutter height value
	INTEGER*4	CLUTTER_INDEX	  !Clutter identifier
C
C Functions:
	INTEGER*4	SAMPLE		  !to derive the sample value
	INTEGER*4	GET_CLUTTER	  !to derive a clutter height
C					  !and index.
C
C----------------------------------------------------------------------
C
C	Determine the terrain elevation from the DTM.
	RETVAL = SAMPLE(4,2,XPOS,YPOS,ZVALUE)
	IF(.NOT.RETVAL)GOTO 999
C
C	If the terrain height is 0 or below, then set 
C	the clutter index to indicate water.
	IF(ZVALUE.LE.0)THEN
	  CLUTTER_INDEX=3
	ELSE	  
C
C	Otherwise determine the clutter attribute from
C	the specified clutter files
	  RETVAL = GET_CLUTTER
     &             (XPOS,YPOS,CLUTTER_VALUE,CLUTTER_INDEX)
	  IF(.NOT.RETVAL)GOTO 999
C
C	Add the clutter height on to the terrain height
	  ZVALUE=ZVALUE+CLUTTER_VALUE
	ENDIF
C
C	Add to data buffer
	CALL ADD_POINT(XPOS,YPOS,ZVALUE,CLUTTER_INDEX)
C
C-----------------------------------------------------------------------
999	RETURN
C
	END
