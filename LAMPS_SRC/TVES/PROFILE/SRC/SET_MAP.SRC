	SUBROUTINE SET_MAP
C
***	IDENT	03MY88
***	MODULE	SET_MAP
C
C	Copyright : Laser-Scan Laboratories, Cambridge, England
C
C	Author	  : D R Catlow
C	Created   : April 1988
C	Modified  : 
C
C	Function  :
C 	To prompts for map NW,SW,SE and NE corners to set up TAB_DTI
C 	NE corner is not used -it is accepted only for 
C	compatability purposes.
C 	Map set up fails if the digitised rectangular is more
C	more than 2 degrees off rectangular
C
C-----------------------------------------------------------------------
C
	IMPLICIT NONE
C
***	COMMON/LSL$CMNDTI:DTIHDR/
C
***	COMMON/HERE:PROFILE/
***	COMMON/HERE:TAB/
C
C 	Local
	INTEGER		SW(2),NW(2),SE(2),NE(2)! map corner table coords
	REAL		X_EW,Y_EW	! east to west vector
	REAL		X_NS,Y_NS	! north to south vector
	REAL		L_EW 		! east to west length
	REAL		L_NS 		! north to south length
	REAL		MYCOS,MYSIN	! a cosine and a sine
	REAL		FX,FY		! X and Y scale factors
C
	REAL		PI
	PARAMETER	(PI=3.141592654)
	CHARACTER*6	BELL
	PARAMETER	(BELL 		= CHAR(7)//CHAR(7)//' ')
C
C 	Function
	LOGICAL		WAIT_INPUT	! wait for button press
	LOGICAL		READ_PUCK	! to read puck data
C
C----------------------------------------------------------------------
	MAP_SET = .FALSE.
C
C----------------------------------------------------------------------
C	Prompt the user to digitise each corner in turn
10	CALL WRITEF('%S',BELL)
	IF(.NOT.WAIT_INPUT('Map NW corner',.TRUE.,.FALSE.)) GOTO 999
	IF(READ_PUCK(BUTTON,NW(1),NW(2),ERROR))THEN
	  CALL SHOW_ERROR(ERROR)
	  GOTO 999
	ENDIF
	IF(BUTTON.EQ.15)GOTO 999	!abort set up
C
	CALL WRITEF('%S',BELL)
	IF(.NOT.WAIT_INPUT('Map SW corner',.TRUE.,.FALSE.)) GOTO 999
	IF(READ_PUCK(BUTTON,SW(1),SW(2),ERROR))THEN
	  CALL SHOW_ERROR(ERROR)
	  GOTO 999
	ENDIF
	IF(BUTTON.EQ.15)GOTO 999	!abort set up
C
	CALL WRITEF('%S',BELL)
	IF(.NOT.WAIT_INPUT('Map SE corner',.TRUE.,.FALSE.)) GOTO 999
	IF(READ_PUCK(BUTTON,SE(1),SE(2),ERROR))THEN
	  CALL SHOW_ERROR(ERROR)
	  GOTO 999
	ENDIF
	IF(BUTTON.EQ.15)GOTO 999	!abort set up
C
	CALL WRITEF('%S',BELL)
	IF(.NOT.WAIT_INPUT('Map NE corner',.TRUE.,.FALSE.)) GOTO 999
	IF(READ_PUCK(BUTTON,NE(1),NE(2),ERROR))THEN
	  CALL SHOW_ERROR(ERROR)
	  GOTO 999
	ENDIF
	IF(BUTTON.EQ.15)GOTO 999	!abort set up
	CALL WRITEF(' ')
C
C----------------------------------------------------------------------
C	Check values
	X_EW = SE(1)-SW(1)
	Y_EW = SE(2)-SW(2)
	X_NS = NW(1)-SW(1)
	Y_NS = NW(2)-SW(2)
	L_EW = SQRT(X_EW**2+Y_EW**2)
	L_NS = SQRT(X_NS**2+Y_NS**2)
C
	IF(ABS(X_EW*Y_NS-Y_EW*X_NS).LT.
     +	  L_EW*L_NS*SIN(88*2*PI/360)) THEN
	  CALL WRITEF('%S Setting up MAP',ERROR_BELL)
	  CALL WRITEF('Badly digitised corner points; try again')
	  GOTO 10
	END IF
C
C-----------------------------------------------------------------------
C 	Set up TAB_DTI
	MYCOS = X_EW/L_EW
	MYSIN = -Y_EW/L_EW
	FX = X_EXTENT(4)/L_EW
	FY = Y_EXTENT(4)/L_NS
C
	TAB_DTI(1,1) = FX*MYCOS
	TAB_DTI(1,2) = -FX*MYSIN
	TAB_DTI(1,3) = FX*(-SW(1)*MYCOS+SW(2)*MYSIN)+1
	TAB_DTI(2,1) = FY*MYSIN
	TAB_DTI(2,2) = FY*MYCOS
	TAB_DTI(2,3) = FY*(-SW(1)*MYSIN-SW(2)*MYCOS)+1
	TAB_DTI(3,1) = 0
	TAB_DTI(3,2) = 0
	TAB_DTI(3,3) = 1
C
C-----------------------------------------------------------------------
C	Signal that a map has been correctly set up on the table
	MAP_SET = .TRUE.
C
C-----------------------------------------------------------------------
999	RETURN
C
	END
