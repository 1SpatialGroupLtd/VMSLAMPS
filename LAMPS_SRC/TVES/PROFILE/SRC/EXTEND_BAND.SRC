C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1988-05-09 18:39:20.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
        SUBROUTINE EXTEND_BAND
C 
***	MODULE	EXTEND_BAND
***	IDENT	09MY88
C
C 	Copyright Laser-Scan Laboratories Ltd.,Cambridge,England
C 	Author  : D R Catlow
C	Created : April 1988
C	Modified:
C
C	Function: 
C	Called if a band or series of sampling bands have been defined.
C	Tests whether the highest band is of sufficient width to extend
C	either to the sector arc (if a sector distance has been
C	specified), or to the corners of the rectangular area of
C	interest.
C	The width of the final band is modified if necessary, and the
C	user is told of this action.
C
C----------------------------------------------------------------------
C
	IMPLICIT NONE
C
***	COMMON/HERE:PROFILE/
***	COMMON/HERE:PROFILE_IFF/
C
	REAL		DIFFX		!distance in x from base stat.
C					!to most extreme rect. corner
	REAL		DIFFY		!distance in y from base stat.
C					!to most extreme rect. corner
	REAL		DISTANCE	!distance from base station
C					!to most extreme rect. corner
	INTEGER*4	LAST_BAND	!highest band defined
C----------------------------------------------------------------------
	LAST_BAND=NEXT_UNDEFINED_BAND-1
C
C	Test if the last band extends to the sector arc.
	IF(HAD_SECTOR)THEN
          IF(BAND_DISTANCE(LAST_BAND).LT.SECTOR_DISTANCE)THEN
	    BAND_DISTANCE(LAST_BAND)=SECTOR_DISTANCE
C	Calculate new with of band
	    IF(LAST_BAND.EQ.1)THEN
	      BAND_WIDTH(LAST_BAND)=BAND_DISTANCE(LAST_BAND)
	    ELSE
	      BAND_WIDTH(LAST_BAND)=BAND_DISTANCE(LAST_BAND)-
     &        BAND_DISTANCE(LAST_BAND-1)
	    ENDIF
C	Tell the user what we've just done
	    CALL WRITEF(' ')
	    CALL WRITEF
     &  ('Total distance of BANDS less than sector distance')
	    CALL WRITEF
     &  ('Width of BAND %N extended to %F9.2',
     &  LAST_BAND,BAND_WIDTH(LAST_BAND))
	  ENDIF
	  GOTO 999
	ENDIF
C
C-----------------------------------------------------------------------
C	Test if the total band distance is extends to the corner of
C	the rectangular area of interest which is furthest away from
C	the base station
C
C	Calculate distance from base station to most extreme corner
	DIFFX=MAX(ABS(X_POSITION-SWX_METRES(4)),
     &            ABS(X_POSITION-NEX_METRES(4)))
	DIFFY=MAX(ABS(Y_POSITION-SWY_METRES(4)),
     &            ABS(Y_POSITION-NEY_METRES(4)))
	DISTANCE=SQRT((DIFFX*DIFFX)+(DIFFY*DIFFY))
C
C	Compare distance with total widths of all bands, and adjust
C	final band width if necessary
	IF(BAND_DISTANCE(LAST_BAND).LT.DISTANCE)THEN
	   BAND_DISTANCE(LAST_BAND)=DISTANCE
C	Calculate new with of band
	   IF(LAST_BAND.EQ.1)THEN
	     BAND_WIDTH(LAST_BAND)=BAND_DISTANCE(LAST_BAND)
	   ELSE
	     BAND_WIDTH(LAST_BAND)=BAND_DISTANCE(LAST_BAND)-
     &       BAND_DISTANCE(LAST_BAND-1)
	   ENDIF
C	Tell the user what we've just done
	   CALL WRITEF(' ')
	   CALL WRITEF
     &  ('Total distance of BANDS too small '//
     &  'to extend to edge of area of interest')
	    CALL WRITEF
     &  ('Width of BAND %N extended to %F9.2',
     &  LAST_BAND,BAND_WIDTH(LAST_BAND))
	    GOTO 999
	ENDIF
C
C-----------------------------------------------------------------------
999	RETURN
C
	END
