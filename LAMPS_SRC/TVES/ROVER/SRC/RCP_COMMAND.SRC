	SUBROUTINE RCP_COMMAND(BUTNO)
C
***	IDENT	13FE89
***	MODULE	RCP_COMMAND
C
C	Copyright : Laser-Scan Laboratories, Cambridge, England
C	Created   : D R Catlow                   14-November-1988
C
C	Routine to deal with the RCP command
C	Possible qualifiers are:
C	RCP ADD			add a new RCP to the file
C	RCP DELETE		delete an existing RCP
C	RCP EDIT		edit an existing RCP
C	RCP MOVE                move to a RCP location
C
C---------------------------------------------------------------------------
C
	IMPLICIT NONE
C
***	COMMON/LSL$CMNLSL:CMDCOM/
C
***	COMMON/LSL$CMNROVER:RCP/		! has HAD_FIL_RCP
***	COMMON/LSL$CMNROVER:ROVCMN/
C
C	Argument
	INTEGER*4	BUTNO			! mouse button number
C						! or 3 if from table
C	
	EXTERNAL	RCP_CMD_TABLE

C	Local
	INTEGER*4	CMD			! command dict. index
	INTEGER*4	BUTTON_NUMBER
	INTEGER*4	ADD_ACTION		! action number for
C						! RCP add
C
C	Functions:
	INTEGER*4	RDCOMM			! get command qualifier
C
C-----------------------------------------------------------------------
C	Check we are in registration mode
	IF(.NOT.REGISTERING)THEN
	   CALL WRITEF('%S Specifying command RCP',EBELL)
	   CALL WRITEF('Command only valid in REGISTRATION mode')
	   GOTO 999
	ENDIF
C
C----------------------------------------------------------------------
C	Check that an RCP file has been defined
	IF(TOTAL_RCP_FILES.LT.1)THEN
	   CALL WRITEF('%S Specifying command RCP',EBELL)
	   CALL WRITEF('No RCP files are currently open')
	   GOTO 999
	ENDIF
C
C----------------------------------------------------------------------
C	Check that the currently selected RCP file is open
	IF(.NOT.RCP_LUN_INUSE(CURRENT_RCP_FILE))THEN
	   CALL WRITEF('%S Specifying command RCP',EBELL)
	   CALL WRITEF('Currently selected RCP file is not open')
	   GOTO 999
	ENDIF
C
C----------------------------------------------------------------------
C	Deal with command from mouse  or table button
	IF (BUTNO .NE. 0) THEN
	   IF(BUTNO .EQ. 3) THEN
	      IF (HAD_TABLE_RCP) THEN
	         ADD_ACTION = 4
	      ELSE
	         ADD_ACTION = 3
	      ENDIF
	   ELSEIF(BUTNO .EQ. 2) THEN
	      ADD_ACTION = 2
	   ENDIF	   
	   GOTO 100
	ENDIF
	ADD_ACTION = 1
C
C	Check for a valid qualifier
        CMD = RDCOMM(RCP_CMD_TABLE)
	IF (CMD .LE. 0) THEN
	   CALL WRITEF('%S Specifying command RCP',EBELL)
	   CALL WRITEF
     &     ('Command qualifiers are ADD, DELETE, EDIT or MOVE')
	   GOTO 999
	ENDIF
C
C	Branch according to qualifier
	GOTO (100,200,300,400)CMD
C
C----------------------------------------------------------------------
C	RCP ADD
C	option to add a new RCP entry to the current RCP_FILE
C
100	CALL RCP_ADD(ADD_ACTION)
	GOTO 999
C
C----------------------------------------------------------------------
C	RCP DELETE
C	option to delete a RCP entry in the current RCP_FILE
C	
200	CALL RCP_DELETE
	GOTO 999
C
C----------------------------------------------------------------------
C	RCP EDIT
C	option to edit a RCP entry in the current RCP_FILE
C
300	CALL RCP_EDIT
	GOTO 999
C
C----------------------------------------------------------------------
C	RCP MOVE
C	move to a RCP location
C
400	CALL RCP_MOVE(0)
	GOTO 999
C
C----------------------------------------------------------------------
999	RETURN
C
	END
