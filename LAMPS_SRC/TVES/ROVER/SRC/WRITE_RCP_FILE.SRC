	LOGICAL	FUNCTION WRITE_RCP_FILE(RCP_LUN)
C
***	IDENT	18NO88
***	MODULE	WRITE_RCP_FILE
C
C	Copyright : Laser-Scan Laboratories, Cambridge, England
C	Created   : D R Catlow                   14-November-1988
C
C	Function to write the contents from common RCP
C	to a RCP file opened on lun RCP_LUN.
C
C	RCP file has format as follows:
C
C	* '!' denotes a comment line 
C	* Line 2 may have a date of creation
C	* Line 3 may have a data of modification
C	* Uncommented lines each hold data for a single RCP.
C
C	  The format of a RCP is:
C	  FORMAT(A10,2X,A2,4X,F18.8,2X,F18.8,2X,F18.8,5X,F7.3,3X,F7.3)
C	  The order of data is:
C	  ID, TYPE, X, Y, Z, XY_WEIGHT, Z_WEIGHT
C
C---------------------------------------------------------------------------
C
	IMPLICIT NONE
C
C	Argument
	INTEGER*4		RCP_LUN		! which RCP file
C
***	PARAMETER/LSL$CMNLSL:LSLLIBMSG/
C
***	COMMON/LSL$CMNROVER:RCP/		! has HAD_FIL_RCP
***	COMMON/LSL$CMNROVER:ROVCMN/
C
C	Local
	INTEGER*4		RETVAL		! function return
	INTEGER*4		I		! counter
C
C	Functions
	INTEGER*4		FLWLIN		! write a line
	INTEGER*4		FLWRTF
C
C-----------------------------------------------------------------------
C
C	Check that the RCP LUN is valid
	IF(RCP_LUN.LT.1.OR.RCP_LUN.GT.MAX_RCP_FILES)THEN
	  WRITE_RCP_FILE=.FALSE.
	  GOTO 999
	ENDIF
C
C	Check a file is opened on this RCP LUN
	IF(.NOT.RCP_LUN_INUSE(RCP_LUN))THEN
	  WRITE_RCP_FILE=.FALSE.
	  GOTO 999
	ENDIF
C
C----------------------------------------------------------------------
C
C	Output comment lines
	CALL FLWRTF
     &  ('! This is an LSL Registration Control Points file')
	CALL FLWRTF
     &  ('! -----------------------------------------------')
	CALL FLWRTF('!')
	CALL FLWRTF
     &  ('! Fixed format RCP data follows, one RCP per line')
	CALL FLWRTF
     &  ('! ID       TYPE            X                   Y'
     &	  //'                   Z            XY-WEIGHT  Z-WEIGHT')
	CALL FLWRTF('!')
C
C-----------------------------------------------------------------------
C
C	Write each RCP record
	DO 100 I=1,TOTAL_RCPS(RCP_LUN)
C
C	Skip any RCP that is marked as deleted
	  IF(RCP_ID(RCP_LUN,I).EQ.'!         ')GOTO 100
C
	  CALL EXPAND
     &	  ('%S10  %S2    %F18.8  %F18.8  %F18.8     %F7.3   %F7.3',
     &	  RCP_ID(RCP_LUN,I),
     &    RCP_TYPE(RCP_LUN,I),
     &    RCP_X(RCP_LUN,I),
     &    RCP_Y(RCP_LUN,I),
     &    RCP_Z(RCP_LUN,I),
     &    RCP_WXY(RCP_LUN,I),
     &    RCP_WZ(RCP_LUN,I))
C
	  RETVAL = FLWLIN()
	  IF(.NOT.RETVAL)THEN
	    CALL LSL_PUTMSG(RETVAL)
	    GOTO 999
	  ENDIF
C
100	CONTINUE
C
C-----------------------------------------------------------------------
C	Signal success
C
	WRITE_RCP_FILE=.TRUE.
C
999	RETURN
C
	END
