C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1986-07-17 10:26:36.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION INTSCT(LINE1,LINE2,POINT,IN1,IN2)
C
***	MODULE	INTSCT
***	IDENT	17JL86
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author  Clarke Brunt
C
C To find the intersection point of two line segments
C
C LINE1 is the first line segment (first subscript x or y, second
C subscript point 1 or 2).
C LINE2 is the second line segment.
C POINT returns the intersecion point, if any.
C IN1 and IN2 return 0 if the intersection is outside the respective
C line segment, -1 if inside, 1 if on first end, 3 if on second.
C (This strange choice would allow testing as a logical if wanted)
C
C The function returns TRUE iff the lines are parallel
C
	IMPLICIT NONE
C
C arguments
	REAL	LINE1(2,2)
	REAL	LINE2(2,2)
	REAL	POINT(2)
	INTEGER	IN1
	INTEGER	IN2
C
C w/s
	REAL*8	DX1,DY1,DX2,DY2		! differences
	REAL*8	DX12,DY12
	REAL*8	DET
	REAL*8	S1,S2
	REAL*8	LTD			! a small number
	PARAMETER (LTD=1E-6)
C
C assume lines parallel
	INTSCT = .TRUE.
C
C calculate the differences
	DX1 = LINE1(1,2)-LINE1(1,1)
	DY1 = LINE1(2,2)-LINE1(2,1)
	DX2 = LINE2(1,2)-LINE2(1,1)
	DY2 = LINE2(2,2)-LINE2(2,1)
C
C and an approximation to the line lengths (save doing SQRT)
	S1 = MAX(ABS(DX1),ABS(DY1))
	S2 = MAX(ABS(DX2),ABS(DY2))
C
C calculate cross product (determinant)
	DET = DX1*DY2-DY1*DX2
C
C if too small, then the lines are parallel
	IF (ABS(DET).EQ.0.0) GOTO 999	! prevent zero-divide
	IF (ABS(DET)/(S1*S2).LT.1E-5) GOTO 999
C
C calculate fractional distances along vectors of the intersection point
	DX12 = LINE2(1,1)-LINE1(1,1)
	DY12 = LINE2(2,1)-LINE1(2,1)
	S1 = (DY2*DX12-DX2*DY12)/DET
	S2 = (DY1*DX12-DX1*DY12)/DET
C
C set our flags according to whether within line segments or not
C (S1 and S2 are now approx 0.00 - 1.00)
	IF (S1.LT.-LTD.OR.S1.GT.(1.0+LTD)) THEN
	   IN1 = 0
	ELSEIF (ABS(S1).LT.LTD) THEN
	   IN1 = 1
	ELSEIF (ABS(S1-1.0).LT.LTD) THEN
	   IN1 = 3
	ELSE
	   IN1 = -1
	ENDIF
C
	IF (S2.LT.-LTD.OR.S2.GT.(1.0+LTD)) THEN
	   IN2 = 0
	ELSEIF (ABS(S2).LT.LTD) THEN
	   IN2 = 1
	ELSEIF (ABS(S2-1.0).LT.LTD) THEN
	   IN2 = 3
	ELSE
	   IN2 = -1
	ENDIF
C
C and calculate actual intersection using line 1
	DX1=DX1*S1
	DY1=DY1*S1
	POINT(1) = LINE1(1,1)+DX1
	POINT(2) = LINE1(2,1)+DY1
	INTSCT = .FALSE.			! valid intersection
C
999	RETURN
	END
