	SUBROUTINE OFSEG(DIST,SEG)
C
***	IDENT	06JN86
***	MODULE	OFSEG
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RWR	06-Jun-1986
C	(from LITES2)
C
C Description
C
C	To offset a vector by distance DIST.  If vector is a point,
C	it is moved in the Y direction
C
	IMPLICIT NONE
C
C Arguments
C
	REAL		DIST		! offset distance
	REAL		SEG(2,2)	! vector to offset
C
C Local Variables
C
	REAL		DX1		! diff. in X for orig. segment
	REAL		DY1		! diff. in Y for orig. segment
	REAL		DX2		! diff. in X for offset
	REAL		DY2		! diff. in Y for offset
	REAL		L		! length of segment
	REAL		F		! factor for offset calculation
C
C Calculate differences in X and Y for original segment
C
	DX1 = SEG(1,2) - SEG(1,1)
	DY1 = SEG(2,2) - SEG(2,1)
C
C Calculate segment length
C
	L = SQRT(DX1**2 + DY1**2)
C
C If zero length, move in Y direction
C
	IF (L .EQ. 0.0) THEN
	   SEG(2,1) = SEG(2,1)+DIST
	   SEG(2,2) = SEG(2,1)
	ELSE
	   F = DIST/L
	   DX2 = F*DY1
	   DY2 = -F*DX1
	   SEG(1,1) = SEG(1,1) + DX2
	   SEG(2,1) = SEG(2,1) + DY2
	   SEG(1,2) = SEG(1,2) + DX2
	   SEG(2,2) = SEG(2,2) + DY2
	ENDIF
C
	RETURN
	END
