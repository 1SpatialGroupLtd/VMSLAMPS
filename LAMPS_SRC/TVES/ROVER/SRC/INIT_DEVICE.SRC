	SUBROUTINE INIT_DEVICE
C
***	IDENT	15MR90
***	MODULE	INIT_DEVICE
C
C	Copyright : Laser-Scan Laboratories, Cambridge, England
C	Author    : D R Catlow
C	Created   : 09 November 1988
C	
C	Subroutine to initialise the display device.
C	This is called automatically on program startup,
C	and contains the initialisation calls that were originally
C	made in the main program.
C
C------------------------------------------------------------------------
C
	IMPLICIT NONE
C
C	ROVER common blocks
***	PARAMETER/LSL$CMNROVER:GKSPAR/
***	COMMON/LSL$CMNROVER:FPPDAT/
***	COMMON/LSL$CMNROVER:GKS/
***	COMMON/LSL$CMNROVER:SCLDAT/
C
	REAL		RLWMIN,RLWMAX		!unused args. for GQPLF
	INTEGER*4	GASF(13)		!aspect source flags
	INTEGER*4	ERRCOD			!error for GKS routines
	INTEGER*4	DCUNIT			!units for GQMDS
	INTEGER*4	NLT, LT, NLW, NPPLI  	!unused args. for GQPLF
C
	DATA GASF /13*GINDIV/		! set flags individual
C
C-------------------------------------------------------------------------
C-------------------------------------------------------------------------
C
C	Open GKS
	CALL GOPKS(6)
C
C	Workstation identifier
	WKID=1
C	Connection identifier
	CONID=1001
C	Workstation type
	WTYPE=7000
C
C	Open workstation
	CALL GOPWK(WKID,CONID,WTYPE)	!open workstation
	CALL GACWK(WKID)		!and activate it
C
C 	Set aspect source flags
	CALL GSASF(GASF)
C
C	Set deferal state to output ASTI
C	and implicit regeneration suppressed
	CALL GSDS(WTYPE,GASTI,GSUPPD)
C
C 	Obtain screen range
	CALL GQMDS(WTYPE,ERRCOD,DCUNIT,
     &	           SCRNSZ(1),SCRNSZ(2),RASTSZ(1),RASTSZ(2))
C
C 	Inquire polyline facilities (just to get nominal linewidth)
	CALL GQPLF(WTYPE,1,ERRCOD,NLT,LT,NLW,NOMLW,RLWMIN,RLWMAX,NPPLI)
C
C 	Calculate a screen shaped portion of NDC
	NDCX = 1.0
	NDCY = SCRNSZ(2)/SCRNSZ(1)
	IF (NDCY.GT.1.0) THEN
	   NDCX = 1.0/NDCY
	   NDCY = 1.0
	END IF
C
C 	Set up GKS transformation
	CALL RESGKS
C
C 	Select normalisation transformation
	CALL GSELNT(1)
C
	OPWK = .TRUE.			!now opened
C
C------------------------------------------------------------------
	RETURN
C
	END
