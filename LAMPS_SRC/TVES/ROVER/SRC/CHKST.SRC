	LOGICAL FUNCTION CHKST()
C
***	MODULE	CHKST
***	IDENT	26AU86
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author  R. Daly.
C
C Created.					RD	13-Oct-1983
C Modified for ROVER (LSLLIB)			MWSR	   Jul-1986
C
C Function to check that the number of points in the
C current feature is sensible for the current feature type.
C Function returns TRUE if feature is to be continued
C		   FALSE if feature is to be aborted
C
	IMPLICIT NONE
C
	INCLUDE	'LSL$CMNROVER:CURFT.CMN'
	INCLUDE	'LSL$CMNROVER:DRAWIN.CMN'
	INCLUDE	'LSL$CMNFRT:FRTCOM.CMN'
C
C
C Initialise
	CHKST=.TRUE.			! assume ft is alright
C
C Empty features are to be ignored whatever the feature type
C Empty features with a feature number of 0 are legal so no error
C message is produced
	IF (TOTPTS.LE.0) THEN		! an empty ft
	   CHKST=.FALSE.
	   IF (FTNUM.EQ.0) GOTO 1000	! no error message
	   CALL WRITEF('Empty feature found.')
	   GOTO 998			! abort feature
	ENDIF
C
C Check the number of points in the feature is not more or less
C than is expected for that particular feature type
	GOTO (10,20,20,20,20,60,70,80,90,100,1000,120), FRTGT
	CALL WRITEF('Unknown feature type %^L%N found',FRTGT)
	GOTO 998			! abort feature
C
C Check line string
10	IF (TOTPTS.NE.1) GOTO 1000
	CALL WRITEF('Line string feature with only 1 point found')
	GOTO 998			! abort feature
C
C Check circle arc
20	IF (TOTPTS.EQ.3) GOTO 1000
	CALL WRITEF
     &	('Circle or arc feature with %^W%N points found',TOTPTS)
	IF (TOTPTS.LT.3) GOTO 998	! abort feature
	TOTPTS=3
	GOTO 999			! continue feature
C
C Check interpolated curves
60	IF (TOTPTS.NE.1) GOTO 1000
	CALL WRITEF('Curved feature with only 1 point found')
	GOTO 998			! abort feature
C
C Check unorientated symbol
70	IF (TOTPTS.EQ.1) GOTO 1000
	CALL WRITEF
     &	('Unoriented symbol ft with %^W%N points found',TOTPTS)
	TOTPTS=1
	GOTO 999			! continue feature
C
C Check oriented symbol
80	IF (TOTPTS.LE.2) GOTO 1000
	CALL WRITEF('Oriented symbol ft with %^W%N points found',TOTPTS)
	TOTPTS=2
	GOTO 999			! continue feature
C
C Check scaled symbol
90	IF (TOTPTS.EQ.2) GOTO 1000
	CALL WRITEF('Scaled symbol ft with %^W%N points found',TOTPTS)
	IF (TOTPTS.GT.2) TOTPTS=2	! preserve TOTPTS of 2
	GOTO 999			! continue feature
C
C Check text
100	IF (TOTPTS.LE.2) GOTO 1000
	CALL WRITEF('Text feature with %^W%N points found',TOTPTS)
	TOTPTS=2
	GOTO 999			! continue feature
C
C Check areas
120	IF (TOTPTS.GT.2) GOTO 1000
	CALL WRITEF('Area feature with only %^W%N points found',TOTPTS)
	IF (TOTPTS.EQ.2) GOTO 999	! continue with 2
	GOTO 998			! abort feature
C
C Function fails if the feature is to be aborted
998	CHKST=.FALSE.
999	CALL WRITEF('Layer %^W%I6 - Ft No %^L%I6 - ISQ %I6',
     &	 LAYNUM,FTNUM,ISQ)
C
1000	RETURN
	END
