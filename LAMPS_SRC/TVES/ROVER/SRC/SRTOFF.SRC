	SUBROUTINE SRTOFF(X1,Y1,X,Y)
C
***	IDENT	17JL86
***	MODULE	SRTOFF
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RWR	06-Jun-1986
C
C Description
C
C	To update the point X1, Y1, which has been offset by a call
C 	of OFSET, to take into account the point X, Y, using the data
C	stored in the common block DRAWIN
C
	IMPLICIT NONE
C
C Arguments
C
	REAL		X1,Y1		! offset point to be updated
	REAL		X,Y		! point to be used to indicate
					! direction of next vector
C
C common blocks
C
	INCLUDE	'LSL$CMNROVER:DRAWIN.CMN'
C
C
C Local Variables
C
	REAL		SEG(2,2)	! the next vector
	REAL		PT(2)		! intersection point
	INTEGER		IN1,IN2		! dummy
C
C functions
C
	LOGICAL		INTSCT
C
C set up this vector
C
	SEG(1,1) = LSTPT(1)
	SEG(2,1) = LSTPT(2)
	SEG(1,2) = X
	SEG(2,2) = Y
C
C offset it
C
	CALL OFSEG(OFFDIST,SEG)
C
C update X1, Y1
C
	IF (.NOT.STD) THEN		! first vector
	   X1 = SEG(1,1)
	   Y1 = SEG(2,1)
	   STD = .TRUE.
	ELSE
	   IF (INTSCT(SEG,LSTOFF,PT,IN1,IN2)) THEN	! collinear?
	      X1 = SEG(1,1)
	      Y1 = SEG(2,1)
	   ELSE
	      X1 = PT(1)
	      Y1 = PT(2)
	   ENDIF
	ENDIF
C
	RETURN
	END
