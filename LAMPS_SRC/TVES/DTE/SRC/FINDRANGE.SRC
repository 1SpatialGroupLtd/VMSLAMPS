
	LOGICAL FUNCTION FIND_HGT_RANGE(DTI,SPEAK)
C
***	MODULE FIND_HGT_RANGE
***	IDENT  04OC85
C
C	Copyright : Laser-Scan Laboratories Ltd., Cambridge, England
C	Created   : Oct 1984
C	Author    : ACM  - taken from [mce.dti]range.src, by AJW
C
CMOD	ACM	21-Jan-1986
C	Corrected logic so as not to mess up global height ranges
CMOD	ACM	 4-Oct-1985
C	Made to distinguish between range over whole DTI
C	and DTI window
C
	IMPLICIT NONE
C
***	PARAMETER/HERE:DTEPAR/
C
***	COMMON/HERE:DTECMN/
***	COMMON/LSL$CMNVIO:EXPC/		! for message output
C
C arguments
	INTEGER*2 	DTI(NY_DTI,NX_DTI)	! mapped DTI file
	LOGICAL		SPEAK		! messages?
C
C workspace
	INTEGER   	IX,IY		! DTI coords
	INTEGER		IX_LO,IX_HI	! IX range
	INTEGER		IY_LO,IY_HI	! IY range
	INTEGER		XHMIN,YHMIN	! DTI pos of min hgt
	INTEGER		XHMAX,YHMAX	! DTI pos of max hgt
	INTEGER*2 	HMIN,HMAX	! local copies in INT*2 format
	INTEGER*2	HGT		! DTI height
	LOGICAL		GLOBAL_CHECK	! checking whole DTI or just wdw
C
C:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
C	Finds the height range over DTI window or whole DTI file
C	as necessary
C:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
C
	FIND_HGT_RANGE = .FALSE.
	IF (.NOT.(VALID_MIN_HGT.AND.VALID_MAX_HGT)) THEN
C
C check is required to cover whole DTI
	  GLOBAL_CHECK = .TRUE.
	  IX_LO = 1
	  IX_HI = NX_DTI
	  IY_LO = 1
	  IY_HI = NY_DTI
	ELSE IF (.NOT.(VALID_MIN_HGT_WIN.AND.VALID_MAX_HGT_WIN)) THEN
C
C check is needed only over current DTI window
	  GLOBAL_CHECK = .FALSE.
	  IX_LO = XMIN_DTI
	  IX_HI = XMAX_DTI
	  IY_LO = YMIN_DTI
	  IY_HI = YMAX_DTI
	ELSE
C
C no check is need
	  IF (SPEAK) THEN
	    CALL EXPAND('%SHeight range displayed is still valid',BELL)
	    CALL DTEMSG(EXPDSC)
	  END IF
	  RETURN
	END IF
	FIND_HGT_RANGE = .TRUE.
C
	CALL LIB$UP_SCROLL()
	CALL LIB$SET_CURSOR(BOT_LIN,1)
	CALL WRITEF('Checking possible new range - please wait')
	HMIN = 32767
	HMAX = -32767
	DO 20 IX = IX_LO,IX_HI
	  DO 10 IY = IY_LO,IY_HI
	    HGT = DTI(IY,IX)
	    IF (HGT.LE.-32767) GOTO 10
	    IF (HGT.LT.HMIN) THEN
	      HMIN = HGT
	      XHMIN = IX
	      YHMIN = IY
	    END IF
	    IF (HGT.GT.HMAX) THEN
	      HMAX = HGT
	      XHMAX = IX
	      YHMAX = IY
	    END IF
10	  CONTINUE
20	CONTINUE
C
	IF (GLOBAL_CHECK) THEN
	  MIN_HGT = HMIN
	  MAX_HGT = HMAX
	  VALID_MIN_HGT = .TRUE.
	  VALID_MAX_HGT = .TRUE.
	  CALL WRTINT(MIN_HGT,6,COL1)
	  CALL WRTINT(MAX_HGT,6,COL2)
C
	  VALID_MIN_HGT_WIN = .FALSE.
	  VALID_MAX_HGT_WIN = .FALSE.
	  CALL LIB$PUT_SCREEN('?     ',6,COL4,3)
	  CALL LIB$PUT_SCREEN('?     ',6,COL5,3)
C
	  IF (.NOT.SPEAK) GOTO 30
	  CALL LIB$UP_SCROLL()
	  CALL EXPAND('*** a maximum height is at (%N,%N)',XHMAX,YHMAX)
	  CALL DTEMSG(EXPDSC)
C
	  CALL EXPAND('*** a minimum height is at (%N,%N)',XHMIN,YHMIN)
	  CALL DTEMSG(EXPDSC)
	ELSE
	  MIN_HGT_WIN = HMIN
	  MAX_HGT_WIN = HMAX
	  VALID_MIN_HGT_WIN = .TRUE.
	  VALID_MAX_HGT_WIN = .TRUE.
	  CALL WRTINT(MIN_HGT_WIN,6,COL4)
	  CALL WRTINT(MAX_HGT_WIN,6,COL5)
	END IF
C
30	END
