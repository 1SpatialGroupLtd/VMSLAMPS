	OPTIONS /NOCHECK
C
	SUBROUTINE SETPIX(DTI)
C
***	IDENT	17FE87
***	MODULE	SETPIX
C
C	Copyright : Laser-Scan Laboratories, Cambridge, England
C
C	Author	  : Andrew Morris
C	Created   : Nov 1984
C
CMOD	Change col tab logical to LSL$LOOKUP   MWSR 17-Feb-1987
C
	IMPLICIT NONE
C
***	PARAMETER/HERE:DTEPAR/
C
***	COMMON/HERE:TAB/
***	COMMON/HERE:DTECMN/
***	COMMON/HERE:TRANS/
C
C workspace
	INTEGER*2	DTI(NY_DTI,NX_DTI) ! the DTI matrix
	INTEGER		X_DTI,Y_DTI	! DTI coords
	INTEGER		I		! integer
	INTEGER		HGT		! a height
C
C functions
	INTEGER		ICOL		! height colour
C
C:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
C Redraws the colour-coded DTI file window on the SIGMA display with one
C pixel to one DTI matrix element at the current magnification.
C
C Also updates height info for height range over current DTI edit window
C:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
C
	IF (GOT_MODTEN) THEN
	  CALL VSRDSC('LSL$LOOKUP:DTECOL.DAT')
	  GOT_MODTEN = .FALSE.
	END IF

	IF (BAR_CHART) THEN
	  CALL VSCLR(3)			! delete bar chart
	  CALL VSDEF(2,2,6)
	  CALL VSSEL(3)			! draw pics 1 and 2
	  CALL VSPIC(1)			! write to pic 1
	  CALL MAGNIFY(.TRUE.,.TRUE.)	! restore magnification
	  BAR_CHART = .FALSE.
	  CALL SIGCSWT(XCUR,YCUR,.FALSE.)! redisplay cursor
	END IF
C
	PXARR(3) = XMIN_DTI+DTI_TO_PIX(1,3)	! start in X
	PXARR(4) = YMIN_DTI+DTI_TO_PIX(2,3)	! start in Y
	PXARR(5) = 1			! No. of columns
	PXARR(6) = YMAX_DTI-YMIN_DTI+1	! No. of rows
	PXARR(7) = (PXARR(6)+1)/2	! No. of data words to follow
C
	MIN_HGT_WIN = 32768
	MAX_HGT_WIN = -32766
C
C for each DTI matrix column do...
	DO 20 X_DTI = XMIN_DTI,XMAX_DTI
	PXARR(1) = 0		! MAG-1 (must be reset each time)
	PXARR(2) = 3		!			( " )
	COLOUR(PXARR(6)+1) = 0	! data terminator	( " )
	I = 0
C
C set up PXARR for the whole column
	DO 10 Y_DTI = YMIN_DTI,YMAX_DTI	! do for each DTI row
	I = I+1
	HGT = DTI(Y_DTI,X_DTI)
	IF (HGT.LE.-32767) THEN
	  COLOUR(I) = ICOL(HGT)
	ELSE
	  IF (HGT.GE.BOT_VIZ.AND.HGT.LE.TOP_VIZ) THEN
	    COLOUR(I) = ICOL(HGT)
	  ELSE
	    COLOUR(I) = 0		! black if out of range
	  END IF
	  IF (HGT.LT.MIN_HGT_WIN) MIN_HGT_WIN = HGT
	  IF (HGT.GT.MAX_HGT_WIN) MAX_HGT_WIN = HGT
	END IF
10	CONTINUE
C
C and write it to the display
	CALL VSPXWT(PXARR)
20	PXARR(3) = PXARR(3)+1	! increment start in X
C
	CALL VSGEN		! flush SIGMA drawing command biffer
	CALL WRTINT(MIN_HGT_WIN,6,COL4,3)
	CALL WRTINT(MAX_HGT_WIN,6,COL5,3)
	NEW_MIN_HGT_WIN = .FALSE.
	NEW_MAX_HGT_WIN = .FALSE.
	VALID_MIN_HGT_WIN = .TRUE.
	VALID_MAX_HGT_WIN = .TRUE.
	END
