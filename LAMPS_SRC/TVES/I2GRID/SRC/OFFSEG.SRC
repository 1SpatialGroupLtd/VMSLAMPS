	LOGICAL FUNCTION OFFSEG(DIST,SEG1,SEG2)
C
***	IDENT	02JN86
***	MODULE	OFFSEG
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RJH	03-MAY-1985
C	Modified for use by GRID		DRC     02-JUN-1986
C
C Description
C
C    This function is part of LITES2, and its purpose is to work out
C an offset line segment.
C    The first argument is the offset distance, while the second is a
C line segment on the original feature; both of these arguments are 
C received by the function. The third argument is returned, and it is
C the offset line segment.
C    The function returns true if the points defining the line segment
C received are equal.
C
	IMPLICIT NONE
C
C Arguments
C
	REAL		DIST		! offset distance
	REAL		SEG1(2,2)	! original feature line segment
	REAL		SEG2(2,2)	! offset line segment
C
C Local Variables
C
	REAL		DX1		! diff. in X for orig. segment
	REAL		DY1		! diff. in Y for orig. segment
	REAL		DX2		! diff. in X for offset
	REAL		DY2		! diff. in Y for offset
	REAL		L		! length of segment
	REAL		F		! factor for offset calculation
	LOGICAL*1	FAIL		! failed ?
C
C Assume success
C
	FAIL = .FALSE.
C
C Calculate differences in X and Y for original segment
C
	DX1 = SEG1(1,2) - SEG1(1,1)
	DY1 = SEG1(2,2) - SEG1(2,1)
C
C Calculate segment length
C
	L = SQRT(DX1**2 + DY1**2)
C
C Moan if zero length, otherwise calculate the offset segment
C
	IF (L .EQ. 0.0) THEN
	   FAIL = .TRUE.
	ELSE
	   F = DIST/L
	   DX2 = F*DY1
	   DY2 = -F*DX1
	   SEG2(1,1) = SEG1(1,1) + DX2
	   SEG2(2,1) = SEG1(2,1) + DY2
	   SEG2(1,2) = SEG1(1,2) + DX2
	   SEG2(2,2) = SEG1(2,2) + DY2
	END IF
C
C Assign function value
C
	OFFSEG = FAIL
C
C Return
C
	RETURN
	END
