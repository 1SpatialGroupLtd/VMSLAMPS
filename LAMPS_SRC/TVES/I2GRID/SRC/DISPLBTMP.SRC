	SUBROUTINE DISPLBTMP(ITEMNAM,BITMPSIZ,BITMAP)
C
***	IDENT	19OC87
***	MODULE	DISPLBTMP
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created	for LITES2			RWR	 5-Nov-1984
C	adapted for FPP				RWR	 3-Jun-1986
C
C Description
C
C	This subroutine displays which bits of a bitmap are set.
C	It outputs message "Folling ''ITEMNAM' selected" followed by a
C	list of integers or ranges of integers indicating the bits that
C	are set.  If no bits are set then message "No ''ITEMNAM'
C	selected" is output. 
C
	IMPLICIT NONE
C
C Arguments
C
C ----- input
C
	CHARACTER*(*)	ITEMNAM		! name of item(s) to be output 
	INTEGER*4	BITMPSIZ	! top of bitmap (in bits)
					! ie, map is 0-bitmpsiz
C
	INTEGER*4	BITMAP(BITMPSIZ/32+1)
					! bitmap
C
C Common Blocks
C
	INCLUDE 'LSL$CMNI2GRID:BTMAPDIS.CMN'		! global variables for this
					! subroutine
C
C Functions called
C
	INTEGER*4	EXPAND
C
C Local Variables
C
	INTEGER*4	WORD		! counter
C
C Initialise common BTMAPDIS
C
	OUTADD = %LOC(OUTBUF)		! set up fake descriptor
	OUTLIM = 80
C
	STARTD = .FALSE.
	INRANGE = .FALSE.
	FIRST = -1
	SECOND = -2
	TOPBITMP = BITMPSIZ
	TOPWORD = BITMPSIZ/32+1
C
C Examine words of bitmap in turn. If state changes look more closely
C at the word involved  
C
	DO 10, WORD = 1,TOPWORD
	   IF ((INRANGE.AND.BITMAP(WORD).NE.-1).OR.
     &	       (.NOT.INRANGE.AND.BITMAP(WORD).NE.0)) THEN
	      CALL INWORD(BITMAP(WORD),WORD,ITEMNAM)
	   END IF
   10	CONTINUE
C
C Flush out buffer, if there has been any output
C
	IF (STARTD) THEN
	   IF (INRANGE.AND.SECOND.LT.FIRST) SECOND = TOPBITMP
	   IF (FIRST.EQ.SECOND) THEN	! output number
	      OUTPTR = OUTPTR+EXPAND('%W%N',OUTBUF(OUTPTR+1),FIRST)
	   ELSE				! output range
	      OUTPTR = OUTPTR+EXPAND('%W%N-%N',OUTBUF(OUTPTR+1),
     &				FIRST,SECOND)
	   END IF
	   CALL WRITEF('%S',OUTDSC)
	ELSE
	   CALL WRITEF('No %S selected',ITEMNAM)	! no output
	END IF
C
C Return
C
	RETURN
	END
