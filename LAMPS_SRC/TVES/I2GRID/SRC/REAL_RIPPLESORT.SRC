	SUBROUTINE REAL_RIPPLESORT(A,INDEX,N,INCR,MINMAX)
C
***	IDENT	22JL93
***	MODULE	REAL_RIPPLESORT
C
C	Copyright : Laser-Scan Ltd., Cambridge, England
C	Created   : ACM					      Feb 1986
C
CMOD1253 Altered to sort the MINMAX array as well	JB 21-Jul-1993
C
	IMPLICIT NONE
C
C Parameters in
	INTEGER		N		! size of array A
	LOGICAL		INCR		! increasing order?
C
C Parameters in/out
	REAL*4		A(N)		! real array to be sorted
	INTEGER*4	INDEX(N)	! array of sorted A indices
	LOGICAL		MINMAX(N)	! turning point?
C
C Workspace
	INTEGER*4	I,J
	INTEGER*4	K
	LOGICAL		L
	REAL*4		R
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C	Uses ripple-sort to sort array A into increasing or decreasing
C	order.
C
C	Also returns array of A indices sorted accordingly so that
C	other arrays can subsequently be sorted to correspond
C	with A if required.
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
	DO 10 I = 1,N
10	INDEX(I) = I
C
	IF (INCR) THEN
	  DO 30 I = 2,N
	    DO 20 J = I,2,-1
	      IF (A(J).GE.A(J-1)) GOTO 30
C
	      R = A(J)
	      A(J) = A(J-1)
	      A(J-1) = R
C
	      L = MINMAX(J)
	      MINMAX(J) = MINMAX(J-1)
	      MINMAX(J-1) = L
C
	      K = INDEX(J)
	      INDEX(J) = INDEX(J-1)
	      INDEX(J-1) = K
20	    CONTINUE
30	  CONTINUE
C
	ELSE
	  DO 50 I = 2,N
	    DO 40 J = I,2,-1
	      IF (A(J).LE.A(J-1)) GOTO 50
C
	      R = A(J)
	      A(J) = A(J-1)
	      A(J-1) = R
C
	      L = MINMAX(J)
	      MINMAX(J) = MINMAX(J-1)
	      MINMAX(J-1) = L
C
	      K = INDEX(J)
	      INDEX(J) = INDEX(J-1)
	      INDEX(J-1) = K
40	    CONTINUE
50	  CONTINUE
	ENDIF
C
	RETURN
	END
C
