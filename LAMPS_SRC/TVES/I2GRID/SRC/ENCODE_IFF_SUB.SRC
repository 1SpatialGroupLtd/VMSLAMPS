	SUBROUTINE ENCODE_IFF_SUB(	DTI_XFROM,DTI_YFROM,DTI_ZFROM,
     &					DTI_XTO,DTI_YTO,DTI_ZTO)

***	MODULE	ENCODE_IFF_SUB
***	IDENT	22DE88

C	Copyright : Laser-Scan Laboratories, Cambridge, England
C	Created   : 21MY86
C	Author    : A C Morris (from TRANSFORM_ST by D R Catlow)

CMOD	Changed so that position of DTI grid points
C	output issame as for original I2GRID	ACM	03-May-1988
CMOD	To treat byte value as unsigned         DRC     28-Nov-1988

	IMPLICIT NONE

	INCLUDE		'LSL$CMNDTI:DTILIBMSG.PAR'

	INCLUDE		'HERE:CULTRE.CMN'

C Data in
	REAL*4		DTI_XFROM	! start DTI x coordinate
	REAL*4		DTI_YFROM	! start DTI y coordinate
	REAL*4		DTI_ZFROM	! start DTI z coordinate
	REAL*4		DTI_XTO		! end   DTI x coordinate
	REAL*4		DTI_YTO		! end   DTI y coordinate
	REAL*4		DTI_ZTO		! end   DTI z coordinate

C Workspace
	REAL*4		DTI_X		! DTI X coord
	REAL*4		DTI_Y		! DTI Y coord
	REAL*4		DTI_Z		! DTI Z coord
C
	INTEGER*4	IDTI_XFROM,IDTI_YFROM	! NINT(DTI_FROM)
	INTEGER*4	IDTI_XTO,IDTI_YTO	! NINT(DTI_TO)
	INTEGER*4	IDTI_X		! NINT(DTI_X)
	INTEGER*4	IDTI_Y		! NINT(DTI_Y)
C
	REAL*4		DIFFX		! vec coords from FROM to TO
	REAL*4		DIFFY
	REAL*4		DIFFZ
C
	REAL*4		XSTEP		! step in X
	REAL*4		YSTEP		! step in Y
	REAL*4		ZSTEP		! step in Z
C
	INTEGER*4	NSTEPS,I	! number of steps to take
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C	Routine to plot DTI matrix points between real DTI points
C	(DTI_XFROM,DTI_YFROM,DTI_ZFROM) and (DTI_XTO,DTI_YTO,DTI_ZTO).
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C
C Move IFF end points onto DTI grid so as to conform to old I2GRID
C
60	IDTI_XFROM = NINT(DTI_XFROM)
	IDTI_YFROM = NINT(DTI_YFROM)
	IDTI_XTO = NINT(DTI_XTO)
	IDTI_YTO = NINT(DTI_YTO)
C
C 	Find vector from FROM to TO
	DIFFX = IDTI_XTO-IDTI_XFROM
	DIFFY = IDTI_YTO-IDTI_YFROM
	DIFFZ = DTI_ZTO-DTI_ZFROM
C
	IF (ABS(DIFFY).GT.ABS(DIFFX)) THEN
	  NSTEPS = ABS(DIFFY)+1
	ELSE
	  NSTEPS = ABS(DIFFX)+1
	END IF
C
	IF (NSTEPS.GT.1) THEN
	  XSTEP = DIFFX/(NSTEPS-1)
	  YSTEP = DIFFY/(NSTEPS-1)
	  ZSTEP = DIFFZ/(NSTEPS-1)
	ENDIF
C
C	Set up start matrix location
	DTI_X = IDTI_XFROM
	DTI_Y = IDTI_YFROM
	DTI_Z = DTI_ZFROM
C
C--------------------------------------------------------------------
	DO 100 I = 1,NSTEPS
	  IDTI_X = NINT(DTI_X)
	  IDTI_Y = NINT(DTI_Y)
C
	  IF(PARAM_NUMBER.EQ.-3)THEN
	    VAL=DTI_Z
	    CALL TEST_VALUE(.TRUE.)
	  ENDIF
C
C	Encode this value
	  CALL ENCODE_VALUE(IDTI_X,IDTI_Y)
C
C	Increment current matrix location
	  DTI_X = DTI_X+XSTEP
	  DTI_Y = DTI_Y+YSTEP
	  DTI_Z = DTI_Z+ZSTEP
C
100	CONTINUE
C
C--------------------------------------------------------------------
220	RETURN				
	END
