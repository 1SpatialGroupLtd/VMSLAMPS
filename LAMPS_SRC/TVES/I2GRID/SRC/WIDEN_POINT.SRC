C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1992-01-17 16:20:40.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
 	SUBROUTINE WIDEN_POINT(NEWPTS)
C
***	IDENT	17JA92
***	MODULE	WIDEN_POINT
C
C	Copyright : Laser-Scan Ltd., Cambridge, England
C	Created   : 09 June 1986
C	Author    : D R Catlow

CMOD	Increase TEMPX etc. from 4096 to 32767		JB 17-Jan-1992

C	Routine for GRID to convert a DFAD point feature into an area.
C	This is done using the information contained in the DFAD
C	Orientation, Length and Width entries.
C	Orientation is in the range 0-31 (11.25 degree intervals). A
C	value of 63 indicates the feature is omni-directional
C	(circular), with a radius value contained in the length entry.
C	For square features the length and width entries define the
C	shape.
C
	IMPLICIT NONE
C
	INCLUDE 'LSL$CMNI2GRID:CULTRE.CMN'
	INCLUDE 'LSL$CMNDTI:DTIHDR.CMN'
C
C	Argument:
	INTEGER*4	NEWPTS		!number of pts. for area feature
C					!This is 1 on entry.
C
	REAL		ANGLE		!orientation of feature
	REAL		COSTHETA	!cosine of theta
	REAL		SINTHETA	!sine of theta
	REAL		SINANG,COSANG
	REAL		RUX,RUY,RVX,RVY
	REAL		TEMPX(32767)	!temporary arrays
	REAL		TEMPY(32767)	!for new IFF coordinates
	INTEGER*4	NUMSEG		!number of segments in gen circ
	INTEGER*4	I		!counter
C
C	parameter:
	PARAMETER	PI    = 3.141592654
	PARAMETER	TWOPI = 6.283185308
C
C********************************************************************
C	Test orientation to find what shape the feature is...
	IF (ORIENT.EQ.63) THEN		!circular
C
	  IF (LENGTH.EQ.WIDTH) THEN	!square
	    ANGLE = TWOPI
	    GOTO 20
	  END IF
C
	  NUMSEG = (LENGTH/MIN(X_GRID(1),Y_GRID(1)))*4
	  IF (NUMSEG.LT.5) GOTO 999	!too small - represent by
C					!just one grid matrix point
	  ANGLE = TWOPI/FLOAT(NUMSEG)
C
	  SINTHETA = SIN(ANGLE)
	  COSTHETA = COS(ANGLE)
	  SINANG  = SIN(0.0)
	  COSANG  = COS(0.0)
C
	  DO 10 I = 1,NUMSEG+1
	     TEMPX(I) = XPTS(1)+LENGTH*COSANG
	     TEMPY(I) = YPTS(1)+LENGTH*SINANG
	     ANGLE = SINANG
	     SINANG = SINANG*COSTHETA+COSANG*SINTHETA
	     COSANG = COSANG*COSTHETA-ANGLE *SINTHETA
10	  CONTINUE
C
	  NEWPTS = NUMSEG+1
	  GOTO 850
	END IF	  	  
C
	ANGLE = ORIENT*11.25
	ANGLE = 90.0-ANGLE
C	Convert angle to radians
	ANGLE = (ANGLE*PI)/180
C
20	COSTHETA = COS(ANGLE)
	SINTHETA = SIN(ANGLE)
C
	RUX = (LENGTH*0.5)*COSTHETA
	RUY = (LENGTH*0.5)*SINTHETA
	RVX = (-WIDTH*0.5)*SINTHETA
	RVY =  (WIDTH*0.5)*COSTHETA		
C
	TEMPX(1) = XPTS(1)+RUX+RVX
	TEMPY(1) = YPTS(1)+RUY+RVY
	TEMPX(2) = XPTS(1)+RUX-RVX
	TEMPY(2) = YPTS(1)+RUY-RVY
	TEMPX(3) = XPTS(1)-RUX-RVX
	TEMPY(3) = YPTS(1)-RUY-RVY
	TEMPX(4) = XPTS(1)-RUX+RVX
	TEMPY(4) = YPTS(1)-RUY+RVY		
	NEWPTS = 4
C
C	Transfer from temporary arrays to main arrays
850	DO 900 I = 1,NEWPTS
	   XPTS(I) = TEMPX(I)
	   YPTS(I) = TEMPY(I)
900	CONTINUE
C
C	Close feature
	NEWPTS = NEWPTS+1
	XPTS(NEWPTS) = XPTS(1)
	YPTS(NEWPTS) = YPTS(1)
C
999	RETURN
	END	

