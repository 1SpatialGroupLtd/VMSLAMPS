C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1993-09-28 17:14:30.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE FOLLOW
C
***	IDENT	28SE93
***	MODULE	FOLLOW
C
C	Copyright : Laser-Scan Ltd., Cambridge, England
C	Created   : ACM					Feb 1985
C
	IMPLICIT NONE
C
***	COMMON/HERE:VEC/
***	PARAMETER/HERE:VECTORISEMSG/
C
C workspace
	BYTE		EDG(4)		! right,up,left,down edges?
	INTEGER*2	IEDGE		! EDG index
	INTEGER*2	NEDGES		! No of edges from a point
	INTEGER*2	IDIR,LASTDIR	! 1 for right ... 4 for down
	INTEGER*2	PX,PY		! sector coords for raster scan
	INTEGER*2	IX,IY		! sector coords for following
	INTEGER*2	DIR(2,4)	! right, left, up & down vecs
	INTEGER*2	OPP(4)		! opposite direction mapping
	INTEGER*2	DX,DY
	INTEGER*2	LASTDX,LASTDY
	INTEGER*2	XNODE,YNODE	! node position
	INTEGER*4	INODE,STANOD	! a node index
	INTEGER		STARTPT
	INTEGER		N_CELLS
	INTEGER		IXLEFT,IYLEFT,IXRIGHT,IYRIGHT
	INTEGER		LEN
	INTEGER		I,J,K
	LOGICAL*1	NEWPOINT
	INTEGER		PTR
	REAL		PCENT,LPCENT,PCF! % process completion
C
C functions
	INTEGER*4	NODEINDEX
C			returns node index of given node point, adding
C			node to node list if it is not already there
C
	INTEGER*2	WHICHWAY
C			which way to go at the crossroads?
C
	DATA DIR/1,0,0,1,-1,0,0,-1/
	DATA OPP/3,4,1,2/
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C Finds all links and nodes in current sector
C
C Each link is noted by a pointer to the start & end of a list of points
C held in PT(2,MAXPTS). Each link points to the nodes at its start and
C end with LINKENDS(2,MAXLINKS). The link points in PT include the link
C end points, though no point in the PT array is shared by more than
C one link, so that where several links meet at a single node, the
C corresponding point in PT is duplicated. 
C
C Each node also points to all the links entering or leaving it (up to
C four possible), with NDAT(AM_NDAT), with the link index +'ve for links
C leaving the node and -'ve for links entering. 
C
C Links are exhaustively traversed by raster scanning the EDGE matrix
C over the current sector until a pixel top or right edge is found to be
C an area boundary element. The right edge is followed first until a
C junction node is found or the line goes off the sector and a break
C node is introduced, then the top edge is followed similarly, and then
C the raster scan continues until it is finnished. 
C
C exits on fatal error
C
C Only link corner points and end points are included -ie. only the
C minimum number of pts necessary to describe the shape of the link
C exactly. The reasons for this is are
C	1) There are often so many points that to include all of the
C	   points distance 1 apart along straight sections of link
C	   would result in exceeding the maximum number of pt records
C	   available (there are on avarage about 5 times fewer points
C	   if only end points and corner points are included.
C	2) Non-smoothed links written to .IFJ file always have the
C	   minimum number of pts necessary to describe the shape of the
C	   link, so this is the form required in any case.
C
C NOTE : When links are to be smoothed they must be reinterpolated so
C that all link vectors are of length 1 before smoothing can be applied.
C The reason for initially including only end-points and corner-points
C in this case is that the kinds of image to which line-smoothing is
C generally applied include images with very high point density.
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
	N_CELLS = (PXUR-1)*(PYUR-1)
C
	IF(CRT.AND.DIAG)THEN
	  PCF = 100.0/(PYUR-1)
	  PCENT = 0
	  LPCENT = 0
	  WRITE(*,'(1X,F6.2,''%'')')PCENT
	ENDIF
C
	DO 110 PY = 1,PYUR-1
	  DO 100 PX = 1,PXUR-1
	    IF (EDGE(1,PX,PY).NE.-1) GOTO 95
	    IDIR = 2
	    EDGE(1,PX,PY) = 1			! edge now a was-edge
20	    IX = PX
	    IY = PY
	    N_LINKS = N_LINKS+1			! start new link
C
	    IF (N_LINKS.GT.MAXLINKS) THEN
	      CALL LSL_PUTMSG(VEC__TOOMNYLNKS,MAXLINKS)
	      CALL LSL_EXIT
	    ENDIF
C
	    STARTPT = N_PTS			! note PT index
	    DX = DIR(1,IDIR)
	    DY = DIR(2,IDIR)
	    INODE = NODEINDEX(IX+DXPW-DX,IY+DYPW-DY)
	    LINKENDS(1,N_LINKS) = INODE		! point link to node
C
C place start node into PT array
	    N_PTS = N_PTS+1
C
	    IF (N_PTS.GT.MAXPTS) THEN
	      CALL LSL_PUTMSG(VEC__TOOMNYPTS,MAXPTS)
	      CALL LSL_EXIT
	    ENDIF
C
	    PT(1,N_PTS) = NPOS(1,INODE)
	    PT(2,N_PTS) = NPOS(2,INODE)
C
C add arm to a node INODE (arms are not sorted at this stage, but they
C can be sorted later in a separate pass)
C
C set up new ARM record
	    N_ARMS = N_ARMS+1
C
	    IF (N_ARMS.GT.MAXARMS) THEN
	      CALL LSL_PUTMSG(VEC__TOOMNYARMS,MAXARMS)
	      CALL LSL_EXIT
	    ENDIF
C
	    ARM(LI_ARM,N_ARMS) = N_LINKS
	    ARM(AM_ARM,N_ARMS) = NIL
C
C append ARM record to ARM list
	    PTR = NDAT(AM_NDAT,INODE)
	    IF (PTR.EQ.NIL) THEN
	      NDAT(AM_NDAT,INODE) = N_ARMS
	    ELSE
25	      IF (ARM(AM_ARM,PTR).NE.NIL) THEN
	        PTR = ARM(AM_ARM,PTR)
	        GOTO 25
	      ENDIF
	      ARM(AM_ARM,PTR) = N_ARMS
	    END IF
C
	    LINKPOS(1,N_LINKS) = N_PTS		! first PT in link
	    NEWPOINT = .FALSE.
	    GOTO 36
C
C look around to see which edge to follow next
30	    LASTDIR = IDIR			! this IS needed!
	    LASTDX = DX
	    LASTDY = DY
C
	    IF (IX.EQ.0.OR.IY.EQ.0) THEN	! left or bottom edge?
	      GOTO 220
C
	    ELSEIF (IX.EQ.PXUR) THEN		! right edge?
	      IX = IX-1
	      IF (NEWPOINT) N_PTS = N_PTS-1
	      GOTO 220
C
	    ELSEIF (IY.EQ.PYUR) THEN		! top edge?
	      IY = IY-1
	      IF (NEWPOINT) N_PTS = N_PTS-1
	      GOTO 220
	    ENDIF
C
C count No. of edges connected to this point
36	    EDG(1) = EDGE(2,IX+1,IY)
	    EDG(2) = EDGE(1,IX,IY+1)
	    EDG(3) = EDGE(2,IX,IY)
	    EDG(4) = EDGE(1,IX,IY)
	    LASTDIR = IDIR
	    LASTDX = DX
	    LASTDY = DY
	    NEDGES = 0
	    IDIR = 0
	    DO 40 IEDGE = 1,4
	      IF (EDG(IEDGE)) 38,40,39
38	      IDIR = IEDGE		! direction of edge to follow
39	      NEDGES = NEDGES+1
40	    CONTINUE
C
	    IF (IDIR.EQ.0) GOTO 220	! must be a node
	    DX = DIR(1,IDIR)
	    DY = DIR(2,IDIR)
C
C what happens next depends on how many edges connect to current point
	    GOTO(200,210,220,220) NEDGES
C
C should never get to here - all pixel boundaries are  closed
200	    CALL LSL_PUTMSG(VEC__FOLLOWERR)
	    CALL LSL_EXIT
C
C note new point in link if it is at a corner
210	    NEWPOINT = DX.NE.LASTDX.OR.DY.NE.LASTDY
	    IF (NEWPOINT) THEN
	      N_PTS = N_PTS+1
	      IF (N_PTS.GT.MAXPTS) THEN
	        CALL LSL_PUTMSG(VEC__TOOMNYPTS,MAXPTS)
	        CALL LSL_EXIT
	      ENDIF
C
	      PT(1,N_PTS) = IX+DXPW
	      PT(2,N_PTS) = IY+DYPW
	    ENDIF
C
C and mark edge traversed
	    GOTO (50,60,70,80) IDIR
50	    EDGE(2,IX+1,IY) = 1
	    GOTO 90
60	    EDGE(1,IX,IY+1) = 1
	    GOTO 90
70	    EDGE(2,IX,IY) = 1
	    GOTO 90
80	    EDGE(1,IX,IY) = 1
90	    CONTINUE
C
C and move to end of edge found
	    IX = IX+DX
	    IY = IY+DY
	    GOTO 30
C
C end of current pixel edge is a node
220	    INODE = NODEINDEX(IX+DXPW,IY+DYPW)	! link end node index
	    LINKENDS(2,N_LINKS) = INODE
C
C place end node into PT array
	    N_PTS = N_PTS+1
	    IF (N_PTS.GT.MAXPTS) THEN
	      CALL LSL_PUTMSG(VEC__TOOMNYPTS,MAXPTS)
	      CALL LSL_EXIT
	    ENDIF
C
	    PT(1,N_PTS) = NPOS(1,INODE)
	    PT(2,N_PTS) = NPOS(2,INODE)
	    LINKPOS(2,N_LINKS) = N_PTS		! last PT in link
C
C find left/right codes for this link
C
C form a vector (DX,DY) which lies along this link (all links have
C .GE. two points including node-points)
	    XNODE = PT(1,N_PTS)			! position of end node
	    YNODE = PT(2,N_PTS)
	    DX = XNODE-PT(1,N_PTS-1)
	    DY = YNODE-PT(2,N_PTS-1)
C
	    LEN = ABS(DX)+ABS(DY)
	    DX = DX/LEN				! unit vec for last seg
	    DY = DY/LEN
C
	    IF (DX.GT.0) THEN
	      IXLEFT = XNODE
	      IYLEFT = YNODE+1
	      IXRIGHT = XNODE
	      IYRIGHT = YNODE
C
	    ELSEIF (DX.LT.0) THEN
	      IXLEFT = XNODE+1
	      IYLEFT = YNODE
	      IXRIGHT = XNODE+1
	      IYRIGHT = YNODE+1
C
	    ELSEIF (DY.GT.0) THEN
	      IXLEFT = XNODE
	      IYLEFT = YNODE
	      IXRIGHT = XNODE+1
	      IYRIGHT = YNODE
C
	    ELSE
	      IXLEFT = XNODE+1
	      IYLEFT = YNODE+1
	      IXRIGHT = XNODE
	      IYRIGHT = YNODE+1
	    ENDIF
C
	    LR(LEFT,N_LINKS) = AREAINDEX(IXLEFT-DXPW,IYLEFT-DYPW)
	    LR(RIGHT,N_LINKS) = AREAINDEX(IXRIGHT-DXPW,IYRIGHT-DYPW)
C
C add arm to node INODE
	    N_ARMS = N_ARMS+1
	    IF (N_ARMS.GT.MAXARMS) THEN
	      CALL LSL_PUTMSG(VEC__TOOMNYARMS,MAXARMS)
	      CALL LSL_EXIT
	    ENDIF
C
	    ARM(LI_ARM,N_ARMS) = -N_LINKS	! set up new ARM record
	    ARM(AM_ARM,N_ARMS) = NIL
	    PTR = NDAT(AM_NDAT,INODE)		! append record to list
	    IF (PTR.EQ.NIL) THEN
	      NDAT(AM_NDAT,INODE) = N_ARMS
	    ELSE
666	      IF (ARM(AM_ARM,PTR).NE.NIL) THEN
	        PTR = ARM(AM_ARM,PTR)
	        GOTO 666
	      END IF
	      ARM(AM_ARM,PTR) = N_ARMS
	    ENDIF
C
95	    IF (EDGE(2,PX,PY).NE.-1) GOTO 100
	    IDIR = 1
	    EDGE(2,PX,PY) = 1			! edge now a was-edge
	    GOTO 20
100	  CONTINUE
C
C show progress
	  IF(CRT.AND.DIAG)THEN
	    PCENT = PY*PCF
	    IF (PCENT-LPCENT.GT.1) THEN
	      WRITE(*,'(1H+,F6.2)')PCENT	!don't WRITE too often
	      LPCENT = PCENT
	    ENDIF
	  ENDIF
C
110	CONTINUE
C
C check for the only node in this sector possibly not yet be detected
	IF (EDGE(1,PXUR-1,PYUR).EQ.-1)
     &	  INODE = NODEINDEX(PXUR-1+DXPW,PYUR-1+DYPW)
C
	IF(CRT.AND.DIAG)THEN
	  PCENT = 100
	  WRITE(*,'(1H+,F6.2)')PCENT
	  WRITE(*,'(1H+,''        '')')
	ENDIF
C
	RETURN
	END
C
