	LOGICAL FUNCTION ROVER_CMD()
C
***	MODULE	ROVER_CMD
**	IDENT	03MR88
C
C	Copyright (C), Laser-Scan Laboratories Ltd., Cambridge, England
C
C       Modified for DTICHECK				DJG 03-Mar-1988
C	Created						TJH 13-Oct-1987
C
C Function to get output file-spec from ROVER command. If argument is
C missing the user is prompted. When argument read, output file
C spec is constructed and the files opened.
C
C Function returns .TRUE. if all is well, otherwise .FALSE.
C
	IMPLICIT NONE
C
C Parameters:
C
	INCLUDE	'LSL$DTMCREATESYS:MESPAR.PAR'
	INCLUDE 'LSL$CMNDTICHECK:PARAMS.PAR'
C
C Common:
C
	INCLUDE	'LSL$CMNLSL:FILENAME.CMN'
	INCLUDE	'LSL$CMNLSL:TXTC.CMN'
	INCLUDE 'LSL$CMNDTICHECK:COMMAND.CMN'
	INCLUDE 'LSL$CMNDTICHECK:FILES.CMN'
	INCLUDE 'LSL$CMNDTI:DTIHDR.CMN'
C
C W/s
C
	BYTE		CH		! a character
	LOGICAL		OK,IERR		! for error returns
	REAL		RANGE(4)
	CHARACTER*128	ROVERDEF
C
C Functions
C
	LOGICAL		FLWOPN
	LOGICAL		RCM_OPEN
	LOGICAL		GETFILNAM	! get file-spec
	LOGICAL		RDCHS		! read char. skipping spaces
	LOGICAL		TTRSTR		! read from TT:
	INTEGER		TRNALL
	INTEGER 	SIGCHS
C
C ......................................................................
C
	ROVER_CMD = .TRUE.		! assume success
C
C ......................................................................
C
C If the user hasn't given a file-spec prompt for one
C
	IF (RDCHS(CH)) THEN			! empty line
	   IF (TTIN) THEN
	      CALL WRITEF('%S You must specify a file-spec argument'//
     &		' to the ROVER command',WARNING)
	   ELSE
	      CALL WRITEF('%S You must specify a file-spec argument'//
     &		'to the ROVER command',ERROR)
	   ENDIF	   
	   CALL WRITEF('For example ROVER DUA3:[MYDIR.DAT]ROVER.RCM')
	   IF (TTIN) THEN
	      OK=TTRSTR(,,'Output ROVER file? ',IERR)
	      IF (OK) THEN
	         CALL BSLN
	      ELSE
	         CALL LSL_PUTMSG(OK)
	         CALL LSL_ADDMSG(IERR)
	         GOTO 10
	      ENDIF
	      IF (TXTPTR.EQ.0) GOTO 10		! empty line
	   ELSE
	      GOTO 10  ! someone got their indirect file wrong !!
	   ENDIF
	ENDIF
C
	CALL BSCH
C
C Check that the required logical name has been set up
C
	OK = TRNALL ('LSL$ROVERCMD', ROVERDEF)
	IF (.NOT.OK) THEN
	   CALL WRITEF('%S Logical name not set up',ERROR)
	   CALL LSL_PUTMSG ( OK )
	   GOTO 20
	ENDIF
C
C Parse the input filename against the default
C
	OK = GETFILNAM ( ROVERFILE, ROVERLEN,
     &                'LSL$ROVERCMD:DTICHECK.RCM',.TRUE.,%REF('!') )
	IF (.NOT.OK) THEN
	   CALL WRITEF('%S reading output file-spec',ERROR)
	   CALL LSL_PUTMSG(OK)
	   GOTO 10
	ENDIF
C
C After all that try to open the file!
C
	RANGE(1) = X_OFFSET(1)
	RANGE(2) = Y_OFFSET(1)
	RANGE(3) = X_OFFSET(1) + ( X_GRID(1) * REAL(X_EXTENT(1)) )
	RANGE(4) = Y_OFFSET(1) + ( Y_GRID(1) * REAL(Y_EXTENT(1)) )
C
	OK = RCM_OPEN ( ROVERLUN, ROVERFILE(1:SIGCHS(ROVERFILE)),
     & 			RANGE, .TRUE. )
C	OK = FLWOPN ( ROVERLUN, ROVERFILE(1:SIGCHS(ROVERFILE)) )
	IF (OK) THEN
	   ROVEROPN=.TRUE.
	   CALL FLWSEL ( ROVERLUN )
	   GOTO 20
	ENDIF	
C
C Return with error message
C
10	ROVER_CMD = .FALSE.
C
C ......................................................................
C
20	RETURN
	END
