	SUBROUTINE CORDS(IXA,IYA,CA,SA,L,IX,IY,IS1,IS2,IT,IOP)
C
***	MODULE	CORDS
***	IDENT	02MY89
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England
C
C	DTICONTOUR version				TJH  03-Nov-1987
C
C Calculates L.L. corner position of a character straddling
C a contour line held in store, with spacings in IPDIS
C
	IMPLICIT NONE
C
	INTEGER		IXA,IYA
	INTEGER		IX,IY
	INTEGER		IS1,IS2
	INTEGER		L
	INTEGER		IT
	INTEGER		IOP

	REAL		CA,SA

	INCLUDE 'LSL$CMNDTICONTOUR:PARAMS.PAR'
	INCLUDE 'LSL$CMNDTICONTOUR:ANNOT.CMN'
C
	INTEGER		J
	INTEGER		IS3,ISB
	INTEGER		ID,ICA
C
	REAL 		S4,D
	REAL		AX,AY
C
C ......................................................................
C Find distance from last plotting (line or character)
C
	IS1 = IS1+IT
C
C Find distance along contour line that corresponds to a character
C position
C
	CALL WIND(IPDIS,IS1,IS2,L,NST)
C
C New IX,IY position lies between (L-1) and (L)
C
	J = L-1
	ID = IPDIS(L)
	IS3 = IS2-ID
	ISB = IS1-IS3
	S4 = FLOAT(ISB)/FLOAT(ID)
	IX = IXYBUF(J,1)+(IXYBUF(L,1)-IXYBUF(J,1))*S4+0.5
	IY = IXYBUF(J,2)+(IXYBUF(L,2)-IXYBUF(J,2))*S4+0.5
C
C Check for central (non character plotting) option
C
	IF (IOP.NE.1) THEN
C
C Find right hand edge of character, even if in a later segment,
C locate L at this point, and calulate the slope from old L to new
C L to act as average orientation for the character to be plotted
C in this position
C
	   ICA = IS1+IWID
	   IS2=IS2-ID
	   CALL WIND(IPDIS,ICA,IS2,L,NST)
	   AX = IXYBUF(L,1)-IX
	   AY = IXYBUF(L,2)-IY
	   D = SQRT(AX**2+AY**2)
C
C Compute slope cos and sin and then L.L. corner
C
	   CA = AX/D
	   SA = AY/D
	   IXA = IX+HALF_LABSIZ*SA+0.5
	   IYA = IY-HALF_LABSIZ*CA+0.5
C
C Update IX,IY distances for next call
C
	   IT = IWID
	   IS2 = IS2-IPDIS(L)
	ENDIF
C
	RETURN
	END
C
