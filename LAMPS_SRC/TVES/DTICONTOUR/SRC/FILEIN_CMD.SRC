	LOGICAL FUNCTION FILEIN_CMD()
C
***	MODULE	FILEIN_CMD
***	IDENT	14OC87
C
C	Copyright (C), Laser-Scan Laboratories Ltd., Cambridge, England
C
C	Created						TJH 14-Oct-1987
C
C Function to get input file-spec from FILEIN command. If argument is
C missing the user is prompted.  Checks that a FILEIN command is
C appropriate at this stage of interaction.
C
C Function returns .TRUE. if all is well, otherwise .FALSE.
C
	IMPLICIT NONE
C
C Parameters:
	INCLUDE	'LSL$DTMCREATESYS:MESPAR.PAR'
	INCLUDE	'LSL$CMNDTICONTOUR:PARAMS.PAR'
C
C Common:
	INCLUDE	'LSL$CMNLSL:TXTC.CMN'
	INCLUDE	'LSL$CMNDTICONTOUR:COMMAND.CMN'
	INCLUDE	'LSL$CMNDTICONTOUR:FILES.CMN'
	INCLUDE	'LSL$CMNDTI:DTIHDR.CMN'
C
C W/s
	BYTE		CH	! a character
	LOGICAL		BLANK	! blank line?
	LOGICAL		OK,IERR	! for error returns
C
C Functions
	LOGICAL		GETFILNAM ! get file-spec
	LOGICAL		OPNDTI	! open specified DTI file
	LOGICAL		RDCHS	! read char. skipping spaces
	LOGICAL		TTRSTR	! read from TT:
C
C ......................................................................
C
	FILEIN_CMD=.TRUE.		! assume success
C
C ......................................................................
C
	IF (DTILUN_INUSE(DTILUN)) THEN 	! already successful FILEIN?
	   CALL WRITEF(
     &	   '%S You have already got a DTI file open for input',WARNING)
	   GOTO 10
	ENDIF
C
	BLANK = RDCHS(CH)			! empty line ...
	IF (BLANK.OR.CH.EQ.'!') THEN		! or comment?
	   IF (TTIN) THEN
	      CALL WRITEF ('%S You must specify a file-spec argument'//
     &		' to the FILEIN command',WARNING)
	   ELSE
	      CALL WRITEF ('%S You must specify a file-spec argument'//
     &		'to the FILEIN command',ERROR)
	   ENDIF
	   IF (TTIN) THEN
	      CALL WRITEF ('For example FILEIN SW301.DTI')
	      OK = TTRSTR (,,'Read from which DTI file? ',IERR)
	      IF (OK) THEN
	         CALL BSLN
	      ELSE
	         CALL LSL_PUTMSG(OK)
	         CALL LSL_ADDMSG(IERR)
	         GOTO 10
	      ENDIF
	      IF (TXTPTR.EQ.0) GOTO 10		! empty line
	   ELSE
	      GOTO 10  ! someone got their indirect file wrong !!
	   ENDIF
	ENDIF
C
C ......................................................................
C
C Try interpreting the file-spec
	CALL BSCH
	OK = GETFILNAM (DTI_NAME(DTILUN), DTI_NAME_LENGTH(DTILUN),
     &			'LSL$DTI:DTI.DTI;0',.TRUE.,%REF('!'))
	IF (.NOT.OK) THEN
	   CALL WRITEF ('%S Unable to interpret input file-spec',ERROR)
	   CALL LSL_PUTMSG(OK)
	ELSE
C
C ......................................................................
C
C Attempt to open the DTI file (readonly)
	   CALL STR$UPCASE (DTI_NAME(DTILUN),DTI_NAME(DTILUN))
	   FILEIN_CMD = OPNDTI()
	ENDIF
	GOTO 20
C
10	FILEIN_CMD = .FALSE.
C
20	RETURN
	END
