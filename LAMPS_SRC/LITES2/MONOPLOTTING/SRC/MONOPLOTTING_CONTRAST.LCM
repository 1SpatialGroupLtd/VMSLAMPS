! MONOPLOTTING_CONTRAST.LCM
!
! Created 					CCB	15-Oct-1994
! Layout standardised 				PGH	17-Oct-1994
!
! Together with MONOPLOTTING_CONTRAST.UIM, this implements scale widgets for
! setting contrast and brightness of an image for OSGB Monoplotting.
!
! Note that this is an example template file, for the user to adapt to fit the 
! local environment. As such, variable names and macro names may need to be 
! changed to avoid conflicts.
!
!
! start up the UILMENUS program to display our menus
!
%CREATE MAILBOX 1	! LSL$LITES2AUX, for input from UILMENUS
%CREATE MAILBOX UILM	! for commands output to UILMENUS
%CREATE ABORT_MAILBOX	! so we can have abort functions in the menu
%SPAWN/NOWAIT UILMENUS/INPUT=UILM/ABORT MONOPLOTTING_CONTRAST
%FILE APPEND 1 UILM:	! open mailbox for commands to UILMENUS
!
! define internal variables
!
%DECL INT _C		! colour number
%DECL REAL _S		! grey step size
%DECL REAL _G		! grey value
%DECL REAL _V		! visible?
!
! adjust these as required
!
%DECL INT _BASE		! first colour to set
%DECL INT _TOP		! last colour to set
%DECL INT _OVER		! which overlay to set
!
! the actual brightness and contrast values
!
%DECL REAL _BRIGHTNESS
%DECL REAL _CONTRAST
!
! initial values
!
%LET _BRIGHTNESS=0.0
%LET _CONTRAST=1.0
%LET _BASE=0		! start at colour 0
%LET _TOP=31		! end at colour 31 (5 plane overlay)
%LET _OVER=1		! overlay 1
%LET _V=0		! not initially visible
!
! set colours according to the values in the variables
!
%MACRO _UPDATE_CONTRAST
 %LET _C='_BASE
 %LET _G='_BRIGHTNESS
 %LET _S=(1-'_G)*'_CONTRAST/('_TOP-'_BASE)-1E-6
 %OVER NUM '_OVER
 %OVER DEFER
.next:
 %OVER COL '_C HLS 0 '_G 0
 %LET _G = '_G+'_S
 %LET _C='_C+1
 %TEST _C<='_TOP
 %JTRUE .next
 %OVER UPDATE
%ENDM
!
! toggle visibility of the box
!
%MACRO _TOGGLE_CONTRAST
 %LET _V=1-'_V
 %FILE SELECT 1
 %TEST _V
 %THEN FILE WRITE ADD Contrast
 %ELSE FILE WRITE REMOVE Contrast
%ENDM

!
! macro called by the Contrast slider
!
%MACRO _CONTRAST
 %LET _CONTRAST = '$P1
 _UPDATE_CONTRAST
%ENDM

!
! macro called by the Brightness slider
!
%MACRO _BRIGHTNESS
 %LET _BRIGHTNESS = '$P1
 _UPDATE_CONTRAST
%ENDM

!
! macro to update the sliders if values changed elsewhere
!
%ENABLE SUBS
%MACRO _UPDATE_SLIDERS
 %FILE SELECT 1
 %FILE WRITE SCALE VALUE '_CONTRAST CONTRAST
 %FILE WRITE SCALE VALUE '_BRIGHTNESS BRIGHTNESS
%ENDM

!
! set the initial position of the sliders
!
%FILE SELECT 1
%FILE WRITE FETCH Contrast	! fetch, so that we can set it
_UPDATE_SLIDERS

!
! define QUIT as a macro to kill the menu system
!
%MACRO QUIT
 %FILE SELECT 1
 %FILE WRITE EXIT
 %QUIT
%ENDM

!
! end of MONOPLOTTING_CONTRAST.LCM
!
