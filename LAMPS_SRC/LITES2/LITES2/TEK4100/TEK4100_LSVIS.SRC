C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1987-06-01 15:46:18.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE LSVIS(PACK,IVIS)
C
***	MODULE	LSVIS
***	IDENT	29MY87
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C
C LITES2 GKS level routine
C Set visibility of segment
C
C Expect to be called:
C (a) to set the currently open segment invisible (before putting
C     any primitives in it) when constructing a limbo item, or
C (b) to set a non-open segment invisible (deleting),
C     or visible (recovering).
C
C Version for single workstation with redraw in black to make invisible
C
	IMPLICIT NONE
C
***	PARAMETER/CMN:GKSPAR/
***	PARAMETER/CMN:PACK/
C
***	COMMON/CMN:DRAWIN/
***	COMMON/CMN:SEGMNT/
***	COMMON/CMN:WORKS/
C
C arguments
	INTEGER*4	PACK(PAKLEN)
	INTEGER		IVIS
C
C do nothing if no displays
	IF (NODISP) GOTO 999
C
C for true GKS environment, would be just...
	IF (USESEG.AND.(.NOT.VIEWNG.OR..NOT.UPDATE_SEG)) THEN
C
C if feature is not in segment store, do nothing
	IF ((PACK(PAKFLG).AND.SEGFLG).EQ.0) GOTO 999
C
C are we setting the visibility of the currently open segment?
C In fact just test that a segment is open - we never set the visibility
C of one segment while another is open.
	   IF (SEGCUR.NE.0) THEN	! remember invisible for LCLSG
	      IF (IVIS.EQ.GINVIS) SEGVIS = .FALSE.
	   ELSE				! set segment class
	      IF (IVIS.EQ.GVISI) THEN
C
C remove limbo class (1)
	         IF (.NOT.VIEWNG) CALL WDSGCM(PACK(PAKSEG),1,0)
	      ELSE
	         CALL WDSGCM(PACK(PAKSEG),0,1)	! add limbo class (1)
	      ENDIF
	      IF (.NOT.VIEWNG) CALL STDINI	! remove refresh
	      CALL WSVIS(PACK(PAKSEG),IVIS)
	   ENDIF
	   GOTO 999
	ENDIF
C
C are we setting the visibility of the currently open segment?
C In fact just test that a segment is open - we never set the visibility
C of one segment while another is open.
	IF (SEGCUR.NE.0) THEN	! yes
	   IF (IVIS.EQ.GVISI) GOTO 999		! visible - do nothing
C
C mark current segment invisible
	   SEGVIS = .FALSE.
C
C temporarily deactivate display
	   IF (WKAC(1)) THEN
	      CALL GDAWK(1)
	      WKACT(1) = .FALSE.		! temporarily inactive
	   ENDIF
C
	ELSE
C
C setting visibility of an existing (not open) segment
	   IF (IVIS.EQ.GVISI) THEN		! visible
C
C just re-draw normally
	      CALL DRAFTR(PACK)
C
	   ELSE					! invisible
C
	      OVCOL = 0			! set overriding colour
	      CALL DRAFTR(PACK)		! redraw
	      OVCOL = -1		! unset overriding colour
	   ENDIF
	ENDIF
C
999	RETURN
	END
