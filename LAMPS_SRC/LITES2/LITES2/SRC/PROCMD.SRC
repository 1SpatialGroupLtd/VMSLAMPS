	SUBROUTINE PROCMD	
C
***	IDENT	27JN88
***	MODULE	PROCMD
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RJH	08-AUG-1984
C
C Description
C
C    This subroutine is part of the LITES2 Command Interface, and its
C purpose is to read and process the various parts of the current
C command.
C    There are three possible command arguments additional to those
C handled by CMDLIB. These are filename, rest of command and optional
C rest of command. Primary or secondary commands can have only
C one of the three arguments. Note that apart from secondary commands,
C the only CMDLIB arguments used are integers and reals.
C    On entry to this routine, it is expected that there will be a
C command on the line to process. On successful exit, the appropriate
C information will appear in the command decoding output variables.
C Commands are read up to and including the command terminator or end
C of line.
C    The argument returned is a longword containing bits set
C according to the states in which the command is valid.
C
	IMPLICIT NONE
C
C Included Parameter Files
C
***     PARAMETER/CMN:ARGPAR/		! argument constants
***     PARAMETER/CMN:MOANMESS/		! moan constants
C
C Common Blocks
C
***     COMMON/CMN:GLOBAL/		! global variables
C
C External Routines
C
	EXTERNAL	NASTY		! nasty message
C
C Local Variables
C
	INTEGER*4	ARGFLG		! argument flags
	INTEGER*4	ARGTYP		! type of extra argument
C
C Initialisation
C
	CALL INIOUT			! clear all output variables
C
C Get command
C
	CALL FINCMD(ARGFLG)
C
C Is command on list of privileged commands?
C
	IF (PRIV_LIST.GT.0) THEN
	   CALL CHK_SET_PRIV
	ENDIF
C
C Find the first extra argument type
C
	CALL EXTTYP(ARGFLG,ARGTYP)
C
C Get arguments (an argument type of less than 0 indicates no further
C arguments)
C
100	IF ((.NOT. MOANED) .AND. (ARGTYP .GE. ARGMIN)) THEN
C
C Filename argument
C
	   IF ((ARGTYP .EQ. ARGFIL) .OR. (ARGTYP .EQ. ARGOPF)) THEN
	      CALL FINFIL(ARGTYP)	! find a filename
C
C Rest of command argument
C
	   ELSE IF ((ARGTYP .EQ. ARGRST) .OR. (ARGTYP .EQ. ARGOPT)) THEN
	      CALL FINRST(ARGTYP)	! find rest of command
C
C Command is privilaged
C
	   ELSE IF (ARGTYP .EQ. ARGPRV) THEN
	      CALL CHKPRV
C
C Command required to be typed in full
C
	   ELSE IF (ARGTYP .EQ. ARGFUL) THEN
	      CALL CHKFUL		! check full command was given
C
C Unknown argument type
C
	   ELSE
	      CALL MSGCMD(NASTY,MSG_UNKARGTYPE)
	   ENDIF
C
C Find next extra argument type
C
	   CALL EXTTYP(ARGFLG,ARGTYP)
C
C Branch back for test
C
	   GO TO 100
	ENDIF
C
C Check for illegal terminators
C
	IF (.NOT. MOANED) CALL CHKTRM
C
C Clear output variables if unsuccessful
C
	IF (MOANED) CALL INIOUT
C
C Return
C
	RETURN
	END
