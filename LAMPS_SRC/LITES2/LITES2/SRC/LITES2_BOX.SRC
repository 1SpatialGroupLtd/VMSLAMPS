	SUBROUTINE LITES2_BOX(CRNRS)
C
***	IDENT	09MY96
***	MODULE	LITES2_BOX
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RWR	18-May-1987
C
C Description
C
C to compute the box enclosing a feature
C
	IMPLICIT NONE
C
	INCLUDE	'LSL$CMNIFF:CB.STR'
***	PARAMETER/CMN:AUXDEF/
***	PARAMETER/CMN:CONSTS/
***	PARAMETER/CMN:PACK/
C
***	COMMON/CMN:DRAWIN/
***	COMMON/LSL$CMNFRT:FRTCOM/
***	COMMON/CMN:MASTER/
***	COMMON/CMN:STRIKE/
C
C argument
	REAL		CRNRS(4)	! xmin, xmax, ymin, ymax
C
	INTEGER		I,V1
	REAL		X(4),Y(4)
	REAL		HEIGHT,LENGTH
C
C initialise
	CRNRS(1) = LIMITS(2)
	CRNRS(2) = LIMITS(1)
	CRNRS(3) = LIMITS(4)
	CRNRS(4) = LIMITS(3)
C
C branch on graphical type
	GOTO (100,200,200,200,200,100,300,300,300,300,100,100),
     &	                                       FOPAK(PAKGT)
C 
C linear feature
  100	CONTINUE
	V1=1					! start at first vertex
C
C get some vertices
  110	CALL GETVRT(FOPAK,V1,FOLST)
C
C check them against current corners
	DO 120, I=1,RITOP
	   CRNRS(1) = MIN(CRNRS(1),RIXY(1,I))
	   CRNRS(2) = MAX(CRNRS(2),RIXY(1,I))
	   CRNRS(3) = MIN(CRNRS(3),RIXY(2,I))
	   CRNRS(4) = MAX(CRNRS(4),RIXY(2,I))
  120	CONTINUE
C
	V1=V1+RITOP
	IF (V1.LT.FOLST) GOTO 110
	GOTO 9000
C
C Box around a circle arc
  200	CONTINUE
	DO 210,I = 1,4
	   CRNRS(I) = FOARCBX(I)
  210	CONTINUE
	GOTO 9000
C
C Box around a text and symbol
  300	CONTINUE
	LENGTH=FOAUX(MAXXI)-FOAUX(MINXI)
	HEIGHT=FOAUX(MAXYI)-FOAUX(MINYI)
	X(1)=FOXY(1,1)+FOAUX(MINXI)*FOAUX(COSI)-FOAUX(MINYI)*FOAUX(SINI)
	Y(1)=FOXY(2,1)+FOAUX(MINXI)*FOAUX(SINI)+FOAUX(MINYI)*FOAUX(COSI)
	X(2)=X(1)+FOAUX(COSI)*LENGTH
	Y(2)=Y(1)+FOAUX(SINI)*LENGTH
	X(3)=X(2)-FOAUX(SINI)*HEIGHT
	Y(3)=Y(2)+FOAUX(COSI)*HEIGHT
	X(4)=X(1)-FOAUX(SINI)*HEIGHT
	Y(4)=Y(1)+FOAUX(COSI)*HEIGHT
C
	DO 310, I=1,4
	   CRNRS(1) = MIN(CRNRS(1),X(I))
	   CRNRS(2) = MAX(CRNRS(2),X(I))
	   CRNRS(3) = MIN(CRNRS(3),Y(I))
	   CRNRS(4) = MAX(CRNRS(4),Y(I))
  310	CONTINUE
	GOTO 9000
C
9000	RETURN
	END
