C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1987-06-10 12:43:42.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE PRPDST(PT,LINE,DIST,PROP)
C
***	IDENT	10JN87
***	MODULE	PRPDST
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RWR	 7-May-1985
C
C Description
C
C	Subroutine to return the perpendicular distance from a point 
C	to a line as well as the proportion into which this 
C	perpendicular cuts the origonal line.
C	
C	If the proportion is less than 0.0 then the perpendicular
C	falls outside the line before the first point, and if
C	greater than 1.0 then it falls outside the line after the
C	second point.
C
C	For a line K,L and a point J:
C
C	t=-[(xK-xJ)*(xL-xK)+(yK-yJ)*(yL-yK)]/((xL-xK)**2+(yL-yK )**2)
C	
C	If t is in the range 0 - 1, the perpendicular from the point
C	to the line, cuts the line between K and L.  If t < 0, then it 
C	passes the line at the K end, and if t > 1 at the L end.
C
C	R=SQRT([(xK-xJ)+t*(xL-xK)]**2+[yK-yJ)+t*(yL-yK)]**2)
C
C	gives the perpendicular distance.
C
	IMPLICIT NONE
C
C Arguments
C
	REAL	PT(2)		! coords of point
	REAL	LINE(2,2)	! coords of end of vector
	REAL	DIST		! perpendicular distance
	REAL	PROP		! where perp cuts line
C
C Local Variables
C
	REAL*8	PROP8		! real*8 version
	REAL*8	DLINEX,DLINEY	! x & y difference in line
	REAL*8	DPTLNX,DPTLNY	! x & y differecne between point
				! and start of line
	REAL*8	DENOM		! temporary
C
C Start of subroutine
C
	DLINEX = DBLE(LINE(1,2)) - DBLE(LINE(1,1))
	DLINEY = DBLE(LINE(2,2)) - DBLE(LINE(2,1))
	DPTLNX = DBLE(LINE(1,1)) - DBLE(PT(1))
	DPTLNY = DBLE(LINE(2,1)) - DBLE(PT(2))
C
C case when two ends of line are coincident
C
	IF (DLINEX.EQ.0.0.AND.DLINEY.EQ.0.0) THEN
	   PROP=0.0
	   DIST=SQRT(DPTLNX**2+DPTLNY**2)
	ELSE
	   DENOM=DLINEX**2+DLINEY**2
	   PROP8=-(DPTLNX*DLINEX+DPTLNY*DLINEY)/DENOM
	   DIST=SQRT((DPTLNX+PROP8*DLINEX)**2+(DPTLNY+PROP8*DLINEY)**2)
	   PROP=PROP8
	ENDIF
C
	RETURN
	END
