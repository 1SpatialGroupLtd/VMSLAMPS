	SUBROUTINE SCNSEC(SEC,FUN,FLG)
C
***	MODULE	SCNSEC
***	IDENT	05JN90
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author  Clarke Brunt based on that by Robin Fairbairns for LITES1
C
C Coded 13.iii.84
C
C To scan a sector chain, applying a function to each
C  relevant data pack on the chain
C
	IMPLICIT NONE
C
***	PARAMETER/CMN:PACK/
***	PARAMETER/CMN:REGPAR/
C
***	COMMON/CMN:CNSTRN/
***	COMMON/CMN:OPTION/
***	COMMON/CMN:REGCMN/
C
C functions called
C
	LOGICAL	SETPAK, NXTPAK, RELVNT, VLDSEC
C
C arguments
C
	INTEGER SEC		! sector to scan
	LOGICAL	FUN		! NB function to apply
	INTEGER	FLG		! flag to ignore (for NXTPAK,RELVNT)
C
C SEC is the sector to be scanned
C
C FUN (EXTERNAL in the calling routine) is the function
C	to be applied to each data pack. It is called as
C	FUN(PACK).
C	FUN produces a TRUE result if scanning is to continue.
C
C w/s
C
	INTEGER*4	PACK(PAKLEN)	! a data pack
C
C Set up pointers for the sector to be scanned
	IF (SETPAK(SEC)) GOTO 999	! it's empty
C
C see if sector is in a selected region (if SELECT WINDOW)
	IF (.NOT.SAREGS.AND.SELWIN) THEN
	   IF (.NOT.VLDSEC(SEC)) THEN
	      IF (DIAOPT) CALL WRITEF('Sector %^L%N not examined',SEC)
	      GOTO 999
	   ENDIF
	ENDIF
C
C Get the next pack
10	IF (NXTPAK(PACK,FLG)) GOTO 999	! there isn't one
C
C Is pack relevant to the operation being performed?
	IF (.NOT.RELVNT(PACK,FLG)) GOTO 10	! no
C
C Apply our function to it - .TRUE. if to continue
	IF (FUN(PACK)) GOTO 10
C
999	RETURN
	END
