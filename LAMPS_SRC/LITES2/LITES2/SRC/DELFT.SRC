	SUBROUTINE DELFT(PACK,LPOSEF)
C
***	MODULE	DELFT
***	IDENT	09DE88
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author Clarke Brunt	24-Sep-1984
C
C To completely delete the feature pointed at by PACK (only PAKLNK,
C PAKPNF, PAKPNF relevant), with end pointer LPOSEF (entry after the EF)
C
	IMPLICIT NONE
C
	INCLUDE	'LSL$CMNIFF:CB.STR'
***	PARAMETER/CMN:AUXDEF/
***	PARAMETER/CMN:EFNLUN/
***	PARAMETER/CMN:PACK/
C
***	COMMON/LSL$CMNIFF:IFFHAN/
***	COMMON/CMN:MAPS/
***	COMMON/CMN:STRIKE/
C
C arguments
	INTEGER*4 PACK(PAKLEN)		! pack for feature
	INTEGER*4 LPOSEF		! end of feature pointer
C
C function for SCNFTR
	EXTERNAL DELPAK
C
C workspace
	INTEGER*2	IECODE,IELEN
	INTEGER*4	POSH,PNF
C
C invalidate GETVRT index, if deleting its feature
	IF (PACK(PAKMAP).EQ.RIMAP.AND.PACK(PAKPNF).EQ.RIPNF) RIPNF = -1
C
C select map
	CALL SELIF(PACK(PAKMAP))
C
C If end of feature is at end of file, then move the EOF back,
C otherwise use underhand method of setting mark to start of feature
C and entry in hand to end of feature, so we can void feature.
C This saves us from two IFFPKEs and an EIHMK, but the two
C pointers had better be correct!
	PNF = PACK(PAKPNF)		! take copy of NF pointer
	IF (LPOSEF.EQ.LPOSE) THEN	! at end of file
	   CALL IFFUPP(LPOSF,LPOSM,LPOSH,PNF)
	ELSE
C
C Check that the void will not be too long for IFF (65535 words).
C If it is, then create several voids as long as possible.
10	   IF (LPOSEF-PNF.LE.65535) THEN
	      CALL IFFUPP(LPOSF,PNF,LPOSEF)
	      CALL IFFVOM		! void feature
	      CALL EIHMK(0)		! remove mark
	   ELSE
	      CALL IFFPKE(IECODE,IELEN,PNF)
20	      POSH = LPOSH
	      CALL IFFNXT(IECODE,IELEN)
	      IF (LPOSH-PNF.GT.65535) THEN
	         CALL IFFUPP(LPOSF,PNF,POSH)
	         CALL IFFVOM		! void as much as possible
	         PNF = POSH
	         GOTO 10
	      ENDIF
	      GOTO 20
	   ENDIF
	ENDIF
	CALL SCNFTR(PACK(PAKLNK),DELPAK)	! remove packs
	NOFEAT = NOFEAT-1		! decrement feature count
	RETURN
	END
