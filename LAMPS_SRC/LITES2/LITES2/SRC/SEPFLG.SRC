	SUBROUTINE SEPFLG(ALLFLG,SEPARG,SEPSTA,MSGSTR)
C
***	IDENT	09MY96
***	MODULE	SEPFLG
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RJH	14-AUG-1984
C
C Description
C
C    This subroutine is part of the LITES2 Command Interface, and its
C purpose is to separate the argument flags from the state permission
C flags for the current command.
C    The first argument passed to the subroutine is a longword
C containing user flags returned by CMDLIB. This longword contains state
C permission flags as well as argument flags. Bits 0-7 are reserved for 
C actual arguments to the command and only one of these should be set. 
C The  subroutine checks this, taking only the first one found to be
C set, and moaning if others have been set. Bits 8 and 9 are reserved 
C for use as conditions for teh command and can be set together
C with one of the others. Longwords containing the argument flags and 
C the state permission flags are returned.
C    The last argument passed to the subroutine is a string containing 
C the current command and it is used for error messages.
C
	IMPLICIT NONE
C
C Included Parameter Files
C
***     PARAMETER/CMN:ARGPAR/		! argument constants
***     PARAMETER/CMN:MOANMESS/		! moan constants
C
C Arguments
C
	INTEGER*4	ALLFLG		! user flags from command table
	INTEGER*4	SEPARG		! argument types
	INTEGER*4	SEPSTA		! valid states for command
	CHARACTER*(*)	MSGSTR		! command string for moans
C
C Function called
C
	LOGICAL		LIB$FFS		! find first set bit
C
C Local Variables
C
	INTEGER*4	I		! counter
	LOGICAL		OTHER		! this flag arg. to command ?
	LOGICAL		GOTARG		! had 1 argument to command ?
	LOGICAL		MORE		! more than 1 arg. to command ?
C
C Initialisation
C
	SEPARG = 0			! no flags set
	SEPSTA = ALLFLG			! state flags same as user flags
	GOTARG = .FALSE.
	MORE = .FALSE.
C
C Find argument flags
C
10	IF (.NOT.LIB$FFS(0,ARGMAX+1,SEPSTA,I)) GOTO 20	! no bits set
	SEPSTA = IBCLR(SEPSTA,I)		! clear bit
	OTHER = ((I .NE. ARGFUL) .AND. (I .NE. ARGPRV))
	IF (GOTARG .AND. OTHER) THEN		! had flag & got another
	   MORE = .TRUE.			! too many flags
	ELSE					! flags OK so far
	   SEPARG = IBSET(SEPARG,I)		! set arg. type bit
	   GOTARG = OTHER			! got valid arg. flag
	ENDIF
	GOTO 10					! look for another bit
C
C Output message if more than one argument type flag (other than the
C type in full and priviledge flags) was specified
C
20	IF (MORE) CALL NASTY(MSG_TOOMANYARGS,MSGSTR)
C
	RETURN
	END
