C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1989-05-25 12:11:02.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE VINWIN(NOPTS,XY,Z,INSIDE,INVEC,OUTVEC,
     &	                  GOT_POSIN,GOT_POSOUT,WHERE)
C
***	IDENT	25MY89
***	MODULE	VINWIN
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England
C
C Author Ron Russell, 16 July 1986
C
C To determine if any of a list of vectors should be used for drawing
C the labels for a linear feature
C
	IMPLICIT NONE
C
***	PARAMETER/CMN:ACS/
C
***	COMMON/LSL$CMNFRT:FRTACD/
***	COMMON/CMN:LABELS/
C
C arguments
C (input)
C
	INTEGER*4	NOPTS		! number of points in array
	REAL		XY(2,NOPTS)	! the points
	REAL		Z(NOPTS)
C
C (input/output)
C
	LOGICAL*1	INSIDE		! feature currently in window?
	LOGICAL*1	GOT_POSIN	! got first vector?
	LOGICAL*1	GOT_POSOUT	! got last vector?
C
C (output)
C
	REAL		INVEC(3,2)	! first vector in window
	REAL		OUTVEC(3,2)	! last vector in window
	INTEGER		WHERE		! how far along array when
					! got enough info to draw
					! label
C
C functions
C
	LOGICAL		CLIP
C
C w/s
C
	INTEGER		I		! loop counter
	REAL		OURXY(3,2)	! copy of vector
	INTEGER		M1,M2		! whether end points altered
	LOGICAL		IN		! if (part of) vector in window
C
C look at each vector in turn
C
	DO 20, I = 1,NOPTS-1
	   OURXY(1,1) = XY(1,I)
	   OURXY(2,1) = XY(2,I)
	   OURXY(3,1) = Z(I)
	   OURXY(1,2) = XY(1,I+1)
	   OURXY(2,2) = XY(2,I+1)	   
	   OURXY(3,2) = Z(I+1)
C
C is this vector suitable?
C
	   IN = .NOT.CLIP(OURXY(1,1),M1,OURXY(1,2),M2)
	   IF (IN.AND.				! and not now a point
     &	         (OURXY(1,1).NE.OURXY(1,2).OR.OURXY(2,1).NE.OURXY(2,2))
     &	         ) THEN
C
C is this first vector?
C
	      IF (.NOT.GOT_POSIN.AND..NOT.INSIDE) THEN
	         INVEC(1,1) = OURXY(1,1)
	         INVEC(2,1) = OURXY(2,1)
	         INVEC(3,1) = OURXY(3,1)
	         INVEC(1,2) = OURXY(1,2)
	         INVEC(2,2) = OURXY(2,2)
	         INVEC(3,2) = OURXY(3,2)
	         GOT_POSIN  = .TRUE.		! got first vector
	      ENDIF
C
C now inside window, unless clipped at far end
C
	      IF (M2.NE.1) THEN
	         INSIDE = .TRUE.	! now in window
	      ELSE
	         INSIDE =.FALSE.
	      ENDIF
C
C if we have got the first vector, go and plot it
C
	      IF (ANNOSTART.AND.GOT_POSIN) GOTO 40
C
C always save this vector as last vector - it may be the last
C
	      IF (.NOT.ANNOSTART) THEN
	         OUTVEC(1,1) = OURXY(1,1)
	         OUTVEC(2,1) = OURXY(2,1)
	         OUTVEC(3,1) = OURXY(3,1)
	         OUTVEC(1,2) = OURXY(1,2)
	         OUTVEC(2,2) = OURXY(2,2)
	         OUTVEC(3,2) = OURXY(3,2)
C
C if vector goes out window, we have last vector
C
	         IF (M2.EQ.1) THEN
	            GOT_POSOUT = .TRUE.
	            GOTO 40	! now got first and last
	         ENDIF	            
	      ENDIF
	   ENDIF
   20	CONTINUE
C
C no suitable vector found
C
	GOTO 50
C
C remember where we were in array
C
   40	CONTINUE
	WHERE = I
C
   50	CONTINUE
	RETURN
	END
