C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1986-02-04 11:05:58.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE TRANLN(BASE,INPT1,INPT2,OUTPT1,OUTPT2)
C
***	IDENT	04FE86
***	MODULE	TRANLN
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RWR	28-Jan-1986
C
C Description
C
C	Subroutine to rotate and translate a line onto a base
C
	IMPLICIT	NONE
C
C arguments (input)
C
	REAL	BASE(2,2)	! base to transform onto
	REAL	INPT1(2)	! input line
	REAL	INPT2(2)
C
C (output)
C
	REAL	OUTPT1(2)
	REAL	OUTPT2(2)
C
C Local Variables
C
	REAL	DIST1,DIST2
	REAL	DDIST,DX,DY
	REAL	PROP
C
C project input line onto base
C
	CALL PRPDST(INPT1,BASE,DIST1,PROP)
	OUTPT1(1)=BASE(1,1)+PROP*(BASE(1,2)-BASE(1,1))
	OUTPT1(2)=BASE(2,1)+PROP*(BASE(2,2)-BASE(2,1))
C
	CALL PRPDST(INPT2,BASE,DIST1,PROP)
	OUTPT2(1)=BASE(1,1)+PROP*(BASE(1,2)-BASE(1,1))
	OUTPT2(2)=BASE(2,1)+PROP*(BASE(2,2)-BASE(2,1))
C
C compute length of original and new line
C
	DIST1=SQRT((INPT2(1)-INPT1(1))**2+(INPT2(2)-INPT1(2))**2)
	DIST2=SQRT((OUTPT2(1)-OUTPT1(1))**2+(OUTPT2(2)-OUTPT1(2))**2)
C
C if input line was parallel to base, dist1=dist2, and we are finished.
C (assuming input line had a distance! That is calling routines problem)
C
	IF (DIST1.NE.DIST2) THEN
	   IF (DIST2.GT.0.0) THEN
C
C normal case
C
	      DDIST=(DIST1-DIST2)/2/DIST2
	      DX=OUTPT2(1)-OUTPT1(1)
	      DY=OUTPT2(2)-OUTPT1(2)
	   ELSE
C
C Perpendicular case
C
	      DIST2=(BASE(1,2)-BASE(1,1))**2+(BASE(2,2)-BASE(2,1))**2
	      DDIST=DIST1/2/SQRT(DIST2)
	      DX=BASE(1,2)-BASE(1,1)
	      DY=BASE(2,2)-BASE(2,1)
	   ENDIF
C
	   OUTPT2(1)=OUTPT2(1)+DX*DDIST
	   OUTPT2(2)=OUTPT2(2)+DY*DDIST
	   OUTPT1(1)=OUTPT1(1)-DX*DDIST
	   OUTPT1(2)=OUTPT1(2)-DY*DDIST
C
	ENDIF

	RETURN
	END
