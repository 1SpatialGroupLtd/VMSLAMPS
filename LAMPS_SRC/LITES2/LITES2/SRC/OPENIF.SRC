	LOGICAL FUNCTION OPENIF( MAP )
C
***	IDENT	10SE90
***	MODULE	OPENIF
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England
C
C Author Clarke Brunt, 5 September 1984
C
C To open a map for editing (or just reading)
C Always called from SELIF, which has checked that the map number
C is valid, and that it isn't open already.
C
	IMPLICIT NONE
C
***	PARAMETER/CMN:EFNLUN/
***	PARAMETER/CMN:MOANMESS/
C
***	COMMON/LSL$CMNIFF:IFFHAN/
***	COMMON/CMN:MAPS/
C
C argument
C
	INTEGER		MAP		! map number to open
C
C function called
	LOGICAL CLOSIF
C
C w/s
	INTEGER		LUN		! IFF LUN to use
	INTEGER		MINCNT,MINLUN
C
	OPENIF = .TRUE.			! assume failure
C
C try to find a free LUN
C search for one not in use, or failing that, choose the
C least recently used one
	MINCNT = LUNCNT(1)		! initialise count
	MINLUN = 1			! and LUN with smallest count
	DO 10 LUN=1,LUNLST
	   IF (MAPNO(LUN).LT.0) GOTO 20	! LUN is free
	   IF (LUNCNT(LUN).LT.MINCNT) THEN
	      MINCNT = LUNCNT(LUN)
	      MINLUN = LUN
	   ENDIF
10	CONTINUE
C
C none free - use least recently used one, close map on it
	LUN = MINLUN
	IF (CLOSIF(MAPNO(LUN))) GOTO 999 ! CLOSIF will moan if needed
C
C try to open the file, memory mapped if required
20	IF (MAPMAP(MAP)) CALL IFFMAP(.TRUE.)
	IF (MAPRO(MAP)) THEN			! read-only
	   CALL IFFOPI(LUN,MAPNAM(1,MAP),FILEID(1,MAP),0)
	ELSE					! read/write
	   CALL IFFOPI(LUN,MAPNAM(1,MAP),FILEID(1,MAP))
	ENDIF
	IF (MAPMAP(MAP)) CALL IFFMAP(.FALSE.)
	IF (IERCD.NE.0) THEN
	   CALL NASTY(MSG_IFFOPEN,MAP+1,MAPNAM(1,MAP))
	   GOTO 999
	ENDIF
C
	CALL IFFIRV(1)		! input revision level 1, for CB's
C
C Position the EOF to writing position (remove EO,EM,EJ)
CCCC	IF (.NOT.MAPRO(MAP)) CALL IFFUPP(0,LPOSM,LPOSH,LPOSFT(MAP))
CCCC
CCCC This change made by RWR, 10-Sep-1990. IFFLIB documentation says
CCCC IFFUPP should only be used by people who know what is happening.
CCCC I think this is probably alright, but CCB may like to check it
CCCC
	IF (.NOT.MAPRO(MAP)) 
     &	         CALL IFFUPP(LPOSF_SAV(MAP),LPOSM,LPOSH,LPOSFT(MAP))
C
C Allocate the LUN
	MAPNO(LUN) = MAP
	MAPLUN(MAP) = LUN
C
C and this map is now selected (current)
	MAPCUR = MAP	! not necessary if called from SELIF, but safer
	OPENIF = .FALSE.		! succeeded
C
999	RETURN
	END
