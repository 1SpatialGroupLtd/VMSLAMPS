	SUBROUTINE SELEFC(SA,SLCT,MAP)
C
***	IDENT	07MR88
***	MODULE	SELEFC
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RWR	31-Oct-1984
C
C Description
C
C	LITES2 cartographic editor: SELECT FC command
C
	IMPLICIT NONE
C
***     PARAMETER/CMN:MOANMESS/         ! moan constants
C
***     COMMON/CMN:DOUT/		! command decoding output
***     COMMON/CMN:FCS/			! data for FC selection
***	COMMON/CMN:FILES/		! filename details
***     COMMON/CMN:MASTER/
C
C Arguments
	LOGICAL*1	SA		! .TRUE. when all FCs selected
	LOGICAL*1	SLCT		! .TRUE. when in FC select mode
	INTEGER*4	MAP(FCMAX)	! FC selection bitmap
C
	EXTERNAL	LEARN
C
C Local Variables
	INTEGER*4	IERR		! for testing returns
	INTEGER*4	REMAIN		! to work out size of unused
					! end of bit map
	INTEGER*4	I		! counter
C
C Check had FRT file
	IF (.NOT. HADFRT) THEN
	   CALL MOAN(MSG_NOFILENAME,'FRT')
	   CALL MSGCMD(LEARN,MSG_REQFRTNAME)
	   GOTO 900
	ENDIF
C
C clear temporary bit map
C
	CALL VIOCLR(SELBUF,FCMAX*4,0)
C
C set unused bits at top of bit map
C
	REMAIN=MOD(MAXFC+1,32)
	IF (REMAIN.GT.0)
     &		 CALL LIB$INSV(-1,REMAIN,32-REMAIN,MAP(FCMAX))
C
C convert string of integers, integer ranges and groups to a bit map
C
	CALL RDSEL(MAXFC,DSTR(1:DSTRL),.TRUE.,FCMAX,SELBUF,IERR)
	IF (IERR.LT.0) GOTO 900
C
C selections changed
	CHASEL = .TRUE.
C
C if this is the first selection, since a SELECT ALL, clear all bits
C of bit map
C
	IF (.NOT.SLCT) THEN
	   CALL VIOCLR(MAP,FCMAX*4,0)		! set all bits to 0
	   REMAIN=MOD(MAXFC+1,32)
	   IF (REMAIN.GT.0)			! set unused bits at top
     &		 CALL LIB$INSV(-1,REMAIN,32-REMAIN,SELBUF(FCMAX))
	   SLCT=.TRUE.			! now in selecting mode
	ENDIF
C
C now set MAP
C
	DO 30,	I=1,FCMAX
	   MAP(I)=MAP(I).OR.SELBUF(I)
30	CONTINUE
C
C finally, check if all bits are set
C
	SA=.FALSE.
	DO 40, I=1,FCMAX
	   IF (MAP(I).NE.-1) GOTO 1000
40	CONTINUE
	SA=.TRUE.
	GOTO 1000
C
900	CALL IGNCMD
1000	RETURN
	END
