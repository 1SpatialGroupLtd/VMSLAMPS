C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1994-05-25 14:09:04.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE JOUCMD
C
***	IDENT	25MY94
***	MODULE	JOUCMD
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RJH	23-NOV-1984
C
C Description
C
C    This subroutine is part of the LITES2 Command Interface, and its
C purpose is to journal the current command.
C    Macros, command file directives and jump commands are logged as 
C comments, except during macro definition; commands to be ignored are
C always logged as comments.
C
	IMPLICIT NONE
C
***     PARAMETER/CMN:EFNLUN/		! LITES2 logical units
***     PARAMETER/CMN:MOANMESS/		! moan constants
*** 	PARAMETER/LSL$CMNLSL:LSLLIBMSG/
C
***	COMMON/CMN:DFILRD/		! command filename details
***     COMMON/CMN:DTYPED/		! command as typed
***     COMMON/CMN:DVAR/		! command decoding variables
***     COMMON/CMN:GLOBAL/		! global variables
***     COMMON/CMN:OPTION/		! options
***	COMMON/CMN:SAVFLE/		! filenames related to terminal
C
C Functions called
C
	INTEGER*4	FLWSTR		! writes a string to a file
	INTEGER*4	FLWSEL		! selects file for writing
	INTEGER*4	FLWCLO		! closes file
	LOGICAL		STORE		! defining a macro ?
	LOGICAL		CMTCMD		! command not to be journalled ?
C
C Local Variables
C
	INTEGER*4	RETCOD		! file I/O return code
	INTEGER*4	IERR		! system error code
	INTEGER*4	I		! counter
C
C Check that the current line is not blank, and that journalling
C has not been suppressed, or turned off to hide encrypted stuff
C
	IF ((DTNUM .NE. 0) .AND. (.NOT. (JOUSUP.OR.DENCF.GT.0))) THEN
C
C Select file for writing
C
	   RETCOD = FLWSEL(JOULUN)
	   JOUSUP = (.NOT.RETCOD)
C
C Output message if error
C
	   IF (JOUSUP) THEN
	      CALL NASTY(MSG_FLWSELERR,JOUFLE(1:JL),JOULUN)
C
C Write to file
C
	   ELSE
	      I = 0
100	      I = I + 1
	      IF ((I .NE. DTNUM) .OR.	! macro to be executed
     &		  ((NEWFIL .OR.		! command file to be executed
     &		    (CMTCMD() .AND. .NOT. STORE()) ) .AND.
     &		   .NOT.JOUFCE) .OR.	! jump command to be executed
     &		  DIGN) THEN		! command to be ignored
C
C Command is to be commented out, only write if not ENABLE BRIEF
	         IF (.NOT.BRIOPT)
     &			RETCOD = FLWSTR('!'//DTYP(I)(1:DTYPL(I)),IERR)
					! comment
	      ELSE
	         RETCOD = FLWSTR(DTYP(I)(1:DTYPL(I)),IERR)
					! executable command or part of
					! macro definition
	      ENDIF
	      JOUSUP = (.NOT.RETCOD)
	      IF ((I .NE. DTNUM) .AND. (.NOT. JOUSUP)) GOTO 100
C
C Output message if error
C
	      IF (JOUSUP) CALL NASTY(MSG_FLWSTRERR,JOUFLE(1:JL),JOULUN)
	   ENDIF
C
C Output messages if error occurred, and quietly try to close the file
C
	   IF (JOUSUP) THEN
	      CALL LITES2_SIGNAL(RETCOD)
	      IF (RETCOD .EQ. LSL__SYSWRITE) CALL LITES2_SIGNAL(IERR)
	      CALL LEARN(MSG_JOUSUPPRESS)
	      RETCOD = FLWCLO(JOULUN,IERR)
	   ENDIF
	ENDIF
C
	RETURN
	END
