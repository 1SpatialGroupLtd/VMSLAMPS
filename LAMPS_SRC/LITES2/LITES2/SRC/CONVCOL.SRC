C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1991-03-16 17:21:20.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
C
C Routines to convert between colour schemes
C
        LOGICAL FUNCTION CONV_HLS_RGB(A1,A2,A3)
C
***     IDENT   16MR91
***     MODULE  CONV_HLS_RGB
C
C       Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C       Written                                 CCB	25-Jan-1991
C
        IMPLICIT NONE
C
C Arguments
	REAL		A1,A2,A3
C
C Functions
C
C Local Variables
	REAL		H,L,S
	REAL		RGB(3)
	REAL		M1,M2
	INTEGER		I
C
C Statement function
	REAL	R01
	R01(H) = MAX(0.0,MIN(1.0,H))
C
	H = A1
	L = A2
	S = A3
C
	IF (H.LT.0.OR.H.GT.360) GOTO 9990
	IF (L.LT.0.OR.L.GT.1) GOTO 9990
	IF (S.LT.0.OR.S.GT.1) GOTO 9990
C
	IF (L.LE.0.5) THEN
	   M2 = L * (1 + S)
	ELSE
	   M2 = L + S - L*S
	ENDIF
	M1 = 2*L - M2
C
	H = H + 120
	IF (H.GT.360) H = H - 360
	DO 10 I=1,3
	   IF (H.LT.0) H = H + 360
C
	   IF (H.LT.60) THEN
	      RGB(I) = M1 + (M2-M1)*H/60
	   ELSEIF (H.LT.180) THEN
	      RGB(I) = M2
	   ELSEIF (H.LT.240) THEN
	      RGB(I) = M1 + (M2-M1)*(240-H)/60
	   ELSE
	      RGB(I) = M1
	   ENDIF
C	
	   H = H - 120
10	CONTINUE
C
	A1 = R01(RGB(1))
	A2 = R01(RGB(2))
	A3 = R01(RGB(3))
	CONV_HLS_RGB = .TRUE.		! success
	GOTO 9999
C
9990	CONV_HLS_RGB = .FALSE.		! failure
C
9999	RETURN
	END
C
C
        LOGICAL FUNCTION CONV_RGB_HLS(A1,A2,A3)
C
***     IDENT   24JA91
***     MODULE  CONV_RGB_HLS
C
C       Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C       Written                                 CCB	25-Jan-1991
C
        IMPLICIT NONE
C
C Arguments
	REAL		A1,A2,A3
C
C Functions
C
C Local Variables
	REAL		R,G,B
	REAL		H,L,S
	REAL		RR,GG,BB
	REAL		M1,M2
C
C Statement function
	REAL	R01
	R01(H) = MAX(0.0,MIN(1.0,H))
C
	R = A1
	G = A2
	B = A3
C
	IF (R.LT.0.OR.R.GT.1) GOTO 9990
	IF (G.LT.0.OR.G.GT.1) GOTO 9990
	IF (B.LT.0.OR.B.GT.1) GOTO 9990
C
	M1 = MAX(R,G,B)
	M2 = MIN(R,G,B)
C
	IF (M1.NE.M2) THEN
	   RR = (M1-R)/(M1-M2)
	   GG = (M1-G)/(M1-M2)
	   BB = (M1-B)/(M1-M2)
	ENDIF
C
	L = (M1+M2)/2
	IF (M1.EQ.M2) THEN
	   S = 0
	ELSE
	   IF (L.LE.0.5) THEN
	      S = (M1-M2)/(M1+M2)
	   ELSE
	      S = (M1-M2)/(2-M1-M2)
	   ENDIF
	ENDIF
C
	IF (S.EQ.0) THEN
	   H = 0
	ELSEIF (R.EQ.M1) THEN
	   H = BB-GG
	ELSEIF (G.EQ.M1) THEN
	   H = 2+RR-BB
	ELSE
	   H = 4+GG-RR
	ENDIF
	H = H*60
	IF (H.LT.0) H = H+360
C
	A1 = H
	A2 = R01(L)
	A3 = R01(S)
C	
	CONV_RGB_HLS = .TRUE.		! success
	GOTO 9999
C
9990	CONV_RGB_HLS = .FALSE.		! failure
C
9999	RETURN
	END
C
C
        LOGICAL FUNCTION CONV_HSV_RGB(A1,A2,A3)
C
***     IDENT   06JL90
***     MODULE  CONV_HSV_RGB
C
C       Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C       Written                                 CCB	25-Jan-1991
C
        IMPLICIT NONE
C
C Arguments
	REAL		A1,A2,A3
C
C Functions
C
C Local Variables
	REAL		H,S,V
	REAL		T(0:5)
	INTEGER		I
C
C Statement function
	REAL	R01
	R01(H) = MAX(0.0,MIN(1.0,H))
C
	H = A1
	S = A2
	V = A3
C
	IF (H.LT.0.OR.H.GT.360) GOTO 9990
	IF (S.LT.0.OR.S.GT.1) GOTO 9990
	IF (V.LT.0.OR.V.GT.1) GOTO 9990
C
	H = H/60
	I = H
	H = H-I
	IF (I.EQ.6) I=0
	T(0) = V
	T(5) = V
	T(1) = V*(1-S*H)
	T(2) = V*(1-S)
	T(3) = T(2)
	T(4) = V*(1-S*(1-H))
C
	A1 = R01(T(I))		! R
	A2 = R01(T(MOD(I+4,6)))	! G
	A3 = R01(T(MOD(I+2,6)))	! B
C
	CONV_HSV_RGB = .TRUE.		! success
	GOTO 9999
C
9990	CONV_HSV_RGB = .FALSE.		! failure
C
9999	RETURN
	END
C
C
        LOGICAL FUNCTION CONV_RGB_HSV(A1,A2,A3)
C
***     IDENT   06JL90
***     MODULE  CONV_RGB_HSV
C
C       Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C       Written                                 CCB	25-Jan-1991
C
        IMPLICIT NONE
C
C Arguments
	REAL		A1,A2,A3
C
C Functions
C
C Local Variables
	REAL		R,G,B
	REAL		H,S,V
	REAL		RR,GG,BB
	REAL		M1,M2
C
C Statement function
	REAL	R01
	R01(H) = MAX(0.0,MIN(1.0,H))
C
	R = A1
	G = A2
	B = A3
C
	IF (R.LT.0.OR.R.GT.1) GOTO 9990
	IF (G.LT.0.OR.G.GT.1) GOTO 9990
	IF (B.LT.0.OR.B.GT.1) GOTO 9990
C
	M1 = MAX(R,G,B)
	M2 = MIN(R,G,B)
C
	V = M1
C
	IF (V.NE.0) THEN
	   S = (M1-M2)/M1
	ELSE
	   S = 0
	ENDIF
C
	IF (S.EQ.0) THEN
	   H = 0
	ELSE
	   RR = (M1-R)/(M1-M2)
	   GG = (M1-G)/(M1-M2)
	   BB = (M1-B)/(M1-M2)
	   IF (R.EQ.M1) THEN
	      H = BB-GG
	   ELSEIF (G.EQ.M1) THEN
	      H = 2+RR-BB
	   ELSE
	      H = 4+GG-RR
	   ENDIF
	   H = H*60
	   IF (H.LT.0) H = H+360
	ENDIF
C
	A1 = H
	A2 = R01(S)
	A3 = R01(V)
C	
	CONV_RGB_HSV = .TRUE.		! success
	GOTO 9999
C
9990	CONV_RGB_HSV = .FALSE.		! failure
C
9999	RETURN
	END
