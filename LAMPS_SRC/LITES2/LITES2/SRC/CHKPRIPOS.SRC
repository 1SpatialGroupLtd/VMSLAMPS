	SUBROUTINE CHKPRIPOS(DEV,BUTTON,POS)
C
***	IDENT	07FE92
***	MODULE	CHKPRIPOS
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RWR	 7-Mar-1989
C
C to set POS for devices that TRNTIP does not know about
C
	IMPLICIT NONE
C
C Arguments
C
	INTEGER*4	DEV	! (input) number of device that input
					! was on (BITLIN or DIGLIN)
	INTEGER*4	BUTTON	! (input) number of button that was
					! pressed (first is 0)
	LOGICAL*1	POS	! (output) .TRUE. if positioning command
C
***	PARAMETER/CMN:EFNLUN/
***     PARAMETER/CMN:MACMEN/		! parameter file for
					! macros, menus and puck buttons
C
***	COMMON/CMN:MACRO/
***	COMMON/CMN:MENUS/
C
C Functions called
	INTEGER*4	LIB$EXTV
C
C Local Variables
	INTEGER		I
	INTEGER		PUCK
C
C only deal with devices 2,4 and 6 (bitpad, ball/mouse and DSR)
	IF (DEV.NE.2.AND.DEV.NE.4.AND.DEV.NE.6) GOTO 999
C
C find which puck has been pressed
	PUCK = 0
	DO 10, I=1,PUKTOP
	   IF (PUKDEV(I).EQ.DEV) THEN
	      PUCK=I
	      GOTO 20
	   ENDIF
   10	CONTINUE
C
   20	CONTINUE
C
C set default values of POS
	IF (DEV.EQ.2.OR.DEV.EQ.4) THEN		! bitpad and mouse
	   POS = .FALSE.
	ELSEIF (DEV.EQ.6) THEN			! DSR
	   POS = .TRUE.
	ENDIF
C
C if puck has not been set up, then don't check further
	IF (PUCK.EQ.0) GOTO 999
C
C has default action been altered?
	IF (.NOT.PUKALT(PUCK)) GOTO 999
C
C set POS according to this button number
	POS = LIB$EXTV(PUKBAS(PUCK)-MAXUSR+BUTTON,1,%VAL(PUKPOS_ADDR))
C
999	RETURN
	END
