	SUBROUTINE PDIST(VERTEX,DELTA,POINT,DIST,LENGTH)
C
***	IDENT	01AP85
***	MODULE	PDIST
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RJH	01-APR-1985
C
C Description
C
C    This subroutine is part of LITES2, and its purpose is to calculate
C the perpendicular distance from a point to the current segment on the 
C found feature.
C    The arguments received are the current vertex, the differences in
C X and Y to the next vertex, and the point. Those returned are the 
C perpendicular distance and the length of the current line segment.
C   Note that the subscript for the arrays containing points denotes X 
C or Y.
C
	IMPLICIT NONE
C
C Arguments
C
	REAL		VERTEX(2)	! current vertex
	REAL		DELTA(2)	! differences to next vertex
	REAL		POINT(2)	! point
	REAL		DIST		! perpendicular distance
	REAL		LENGTH		! length of segment
C
C Local Variables
C
	REAL		DX1		! diff. in X along segment
	REAL		DY1		! diff. in Y along segment
	REAL		DX2		! diff. in X vertex to point
	REAL		DY2		! diff. in Y vertex to point
C
C Find the differences in X and Y from the current vertex to the
C next vertex, and to the point given
C
	DX1 = DELTA(1)
	DY1 = DELTA(2)
	DX2 = POINT(1) - VERTEX(1)
	DY2 = POINT(2) - VERTEX(2)
C
C Calculate length of segment
C
	LENGTH = SQRT(DX1**2 + DY1**2)
C
C If the two vertices have the same coordinates, the distance to the
C point is used. Otherwise, the perpendicular distance is calculated
C using the cross product. Note that the sign is reversed as our
C convention is to have positive distances to the right of a line 
C segment, with negative distances to the left.
C
	IF ((DX1 .EQ. 0.0) .AND. (DY1 .EQ. 0.0)) THEN
	   DIST = SQRT(DX2**2 + DY2**2)
	ELSE
	   DIST = ((-DX1*DY2) + (DY1*DX2)) / LENGTH
	ENDIF
C
C Return
C
	RETURN
	END
