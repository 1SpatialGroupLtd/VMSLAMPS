        LOGICAL FUNCTION ADDDEF(LIST,TYPE,VALUE,ADDED)
C
***     IDENT   04JA88
***     MODULE  ADDDEF
C
C       Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C       Written                                 RWR	4-Dec-1987
C
C	function to add a value to a list of definitions. If there
C	is already a definition with the specified type on the list,
C	then the new value overwrites the old one
C	Returns TRUE for success, and FALSE when collapsed
C
        IMPLICIT NONE
C
***	COMMON/CMN:DEFINE/
C
C arguments
	INTEGER		LIST		! the header of the list
	INTEGER		TYPE		! the attribute type
	INTEGER		VALUE		! the value to add
	LOGICAL		ADDED		! a new definition has been added
					! false when old one overwritten
C
C functions
	LOGICAL		GETDEF
C
C w/s
C
	INTEGER		ADDR		! address of definition	
	INTEGER		NXTADDR		! address of next definition
	INTEGER		CURRTYPE	! type of current definition
	INTEGER		TEMP		! address of new definition (PPT)
C
	ADDDEF = .TRUE.
	ADDED  = .FALSE.
C
C get start of relevant linked list
	ADDR    = %LOC(LIST)
	NXTADDR = ADDR
C
10	CONTINUE
	IF (NXTADDR.LE.0) THEN			! at end of list
	   IF (GETDEF(TEMP)) THEN		! collapsed
	      ADDDEF = .FALSE.
	      GOTO 999
	   ENDIF
C
C fill up this definition
	   CALL PUTLNG(TYPE,%VAL(TEMP+DEF_TYP))
	   CALL PUTLNG(VALUE,%VAL(TEMP+DEF_VALUE))
C
C and put at end of list
	   CALL PUTLNG(0,%VAL(TEMP))
	   CALL PUTLNG(TEMP,%VAL(ADDR))
	   ADDED = .TRUE.
	   GOTO 999
	ENDIF
C
C get type of this definition
	CALL PUTLNG(%VAL(NXTADDR+DEF_TYP),CURRTYPE)
C
C if this is the one that we are looking for, then overwrite it
	IF (CURRTYPE.EQ.TYPE) THEN
	   CALL PUTLNG(VALUE,%VAL(NXTADDR+DEF_VALUE))
	   GOTO 999
	ENDIF
C
C try next definition
	ADDR = NXTADDR
	CALL PUTLNG(%VAL(NXTADDR),NXTADDR)
	GOTO 10
C
999	CONTINUE
        RETURN
        END
