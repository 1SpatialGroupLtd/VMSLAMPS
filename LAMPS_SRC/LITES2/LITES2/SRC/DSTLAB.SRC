C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1989-09-12 11:11:02.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE DSTLAB(HADLABEL,INVEC,OUTVEC,PACK)
C
***	MODULE	DSTLAB
***	IDENT	12SE89
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author Ron Russell		21-July-1986
C
C To compute the position and to display a label for linear features
C
	IMPLICIT NONE
C
***	PARAMETER/CMN:ACS/
***	PARAMETER/CMN:CONSTS/
***	PARAMETER/CMN:PACK/
C
***	COMMON/CMN:CLIP/
***	COMMON/LSL$CMNFRT:FRTACD/
***	COMMON/CMN:LABELS/
C
C arguments
C
	LOGICAL*1	HADLABEL	! has label been built yet
	REAL		INVEC(3,2)	! 1st vector of feature in window
	REAL		OUTVEC(3,2)	! last vector of feature
	INTEGER*4 	PACK(PAKLEN)	! a pack for the feature
C
C
C w/s
C
	REAL		XYPOS(3)	! where to start drawing
	REAL		ZPOS
	REAL		ANG		! the angle to draw label at
	REAL		SINA,COSA	! sine and cosine of angle
	REAL		X1,X2,Y1,Y2	! ends of vector to be used
	REAL		DIST2IN		! distance (squared) from LL
					! corner of window to INVEC
	REAL		DIST2OUT	! distance (squared) from LL
					! corner of window to OUTVEC
C
	EQUIVALENCE	(ZPOS,XYPOS(3))
C
C select vector to use
C 1) if using start vector this is easy
C
	IF (ANNOSTART) THEN
	   X1 = INVEC(1,1)
	   Y1 = INVEC(2,1)
	   ZPOS = INVEC(3,1)
	   X2 = INVEC(1,2)
	   Y2 = INVEC(2,2)
C
C 2) if drawing labels at left and bottom, select the vector nearest
C    the LL of the window (???? if it works)
C
	ELSE
	   DIST2IN  = (INVEC(1,1) - CLIPLO(1))**2 + 
     &	              (INVEC(2,1) - CLIPLO(2))**2
	   DIST2OUT = (OUTVEC(1,2) - CLIPLO(1))**2 + 
     &	              (OUTVEC(2,2) - CLIPLO(2))**2
C
	   IF (DIST2IN.LE.DIST2OUT) THEN
	      X1 = INVEC(1,1)
	      Y1 = INVEC(2,1)
	      ZPOS = INVEC(3,1)
	      X2 = INVEC(1,2)
	      Y2 = INVEC(2,2)
	   ELSE
	      X1 = OUTVEC(1,2)
	      Y1 = OUTVEC(2,2)
	      ZPOS = OUTVEC(3,2)
	      X2 = OUTVEC(1,1)
	      Y2 = OUTVEC(2,1)
	   ENDIF
	ENDIF
C
C now work out the start position of the label and its angle
C (avoid invalid arguments to ATAN2)
C
	IF (((Y2-Y1).NE.0.0).OR.((X2-X1).NE.0.0)) THEN
	   ANG = ATAN2(Y2-Y1,X2-X1)
	ELSE
	   ANG = 0.0
	ENDIF
	IF (ANG.LT.0.0) ANG = TWOPI + ANG
C
	SINA = SIN(ANG)
	COSA = COS(ANG)
C
	XYPOS(1) = X1 + (ANNOOFFX*COSA - ANNOOFFY*SINA)
	XYPOS(2) = Y1 + (ANNOOFFX*SINA + ANNOOFFY*COSA)
C
C draw label in this position
C
	CALL SRI_SET_Z(ZPOS)
	CALL DISPLAB(PACK,HADLABEL,XYPOS,ANG)
C
	RETURN
	END
