	LOGICAL FUNCTION GTYPAC(CHAN,COUNT,BYTE1)
C
***	IDENT	05OC89
***	MODULE	GTYPAC
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RWR	20-Aug-1985
C
C Description
C
C	Function to get the number of characters in the type ahead buffer
C	Returns .TRUE. if successful, .FALSE. otherwise (error in QIOW)
C
	IMPLICIT NONE
C
***	PARAMETER/CMN:MOANMESS/
C
C Arguments
	INTEGER*2	CHAN		! channel number (input)
	INTEGER*4	COUNT		! number of characters in buffer
	BYTE		BYTE1		! first character
C
C system parameters
	INCLUDE		'($IODEF)'
C
C functions called
	INTEGER*4	SYS$QIOW
C
C workspace
	INTEGER*4	IERR
	INTEGER*2	BUFFW(4)
	BYTE		BUFFB(8)
	EQUIVALENCE	(BUFFW,BUFFB)
C
	GTYPAC=.FALSE.				! assume failure
C
C get data from typeahead buffer
	IERR=SYS$QIOW(,				! efn
     &	              %VAL(CHAN),		! channel
     &	              %VAL(IO$_SENSEMODE.OR.IO$M_TYPEAHDCNT),! function
     &	              ,				! iosb
     &	              ,				! astadr
     &	              ,				! astprm
     &	              BUFFW,			! terminal char'istics
     &	              %VAL(8),			! length of BUFF
     &	              ,				! p3
     &	              ,				! p4
     &	              ,				! p5
     &	               )			! p6
C
	IF (.NOT.IERR) THEN
	   CALL MOAN(MSG_DUFFQIO)
	   CALL LITES2_SIGNAL(IERR)
	ELSE	   
	   COUNT=BUFFW(1)
	   BYTE1=BUFFB(3)
	   GTYPAC=.TRUE.
	ENDIF
C
	RETURN
	END
