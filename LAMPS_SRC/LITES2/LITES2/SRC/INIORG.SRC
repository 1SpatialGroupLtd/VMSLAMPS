	SUBROUTINE INIORG
C
***	MODULE	INIORG
***	IDENT	10MY96
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author  Clarke Brunt
C
C To calculate LITES2 origin
C
	IMPLICIT NONE
C
***	COMMON/CMN:IMAGE/
***	COMMON/CMN:MAPS/
***	COMMON/CMN:MASTER/
C
C functions
	LOGICAL	GEOG_TRANS
C
C w/s
	INTEGER	MAP
	INTEGER	MAPS_SO_FAR
	REAL*8	TMPORGXY(2)
C
C if not using RANGE command, then reset absolute limits, so that
C returns to INITIAL state will compute a new working area. However, once
C a range command has been given, keep using the (expanded) range, as
C this is probably what people would expect)
	IF (.NOT.HAD_RANGE_CMD) THEN
	   ABS_LIMITS(1) = 1.0E38
	   ABS_LIMITS(2) = -1.0E38
	   ABS_LIMITS(3) = 1.0E38
	   ABS_LIMITS(4) = -1.0E38
	ENDIF
C
C work out origin offsets for each map
	LITES2ORG(1)=ABS_LIMITS(1)
	LITES2ORG(2)=ABS_LIMITS(3)
	MAPS_SO_FAR = 0
	DO 30, MAP=0,MAPTOP		! workout SW corner
	   IF (.NOT.MAP_EXIST(MAP)) GOTO 30
	   IF (DO_FLY_TRANS) THEN
	      IF (GEOG_TRANS(MAPORG(1,MAP),MAP_SYS(MAP),MAP_ZONE(MAP),
     &	                     MAP_PARAMS(1,MAP),MAP_UNIT(MAP), 
     &	                     TMPORGXY,LITES2SYS,LITES2ZONE,LITES2PAR,
     &	                     LITES2UNIT)) THEN
	         LITES2ORG(1)=MIN(LITES2ORG(1),TMPORGXY(1))
	         LITES2ORG(2)=MIN(LITES2ORG(2),TMPORGXY(2))
	      ENDIF
	   ELSE
	      LITES2ORG(1)=MIN(LITES2ORG(1),MAPORG(1,MAP))
	      LITES2ORG(2)=MIN(LITES2ORG(2),MAPORG(2,MAP))
	   ENDIF
	   MAPS_SO_FAR = MAPS_SO_FAR+1
30	CONTINUE
C
C update all the map origins, if all on the same plane already
	IF (.NOT.DO_FLY_TRANS) THEN
	   DO 40, MAP=0,MAPTOP		! subtract it from all the rest
	      IF (.NOT.MAP_EXIST(MAP)) GOTO 40
	      MAPORG(1,MAP)=MAPORG(1,MAP)-LITES2ORG(1)
	      MAPORG(2,MAP)=MAPORG(2,MAP)-LITES2ORG(2)
40	   CONTINUE
	ENDIF
C
C if no maps, then set the origin to be minimum image position
	IF (MAPS_SO_FAR.EQ.0) THEN
	   DO 45 MAP=1,MAXIMAGE
	      IF (IMAGE_TYPE(MAP).NE.0) THEN
	         LITES2ORG(1)=MIN(LITES2ORG(1),IMAGE_UORIGIN(1,MAP))
	         LITES2ORG(2)=MIN(LITES2ORG(2),IMAGE_UORIGIN(2,MAP))
	      ENDIF
45	   CONTINUE
	ENDIF
C
999	RETURN
	END
