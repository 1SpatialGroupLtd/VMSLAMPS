	SUBROUTINE DIG4PT(SET,CPTS,ABORT,EXSTSU)
C
***	IDENT	28SE94
***	MODULE	DIG4PT
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RWR	 8-Feb-1985
C
C Description
C
C	Subroutine to digitise corner points of map for 4 point setup.
C
	IMPLICIT NONE
C
C Arguments
C
	INTEGER*4	SET
	REAL		CPTS(4,4)	! corner point values as follows
					! x1,y1,x2,y2 NW
					! x1,y1,x2,y2 SW
					! x1,y1,x2,y2 SE
					! x1,y1,x2,y2 NE  where
					! x1,y1 are digitiser coords
					! x2,y2 are coords from IFF file
	LOGICAL		ABORT		! flag for abort
	LOGICAL		EXSTSU		! flag to use existing set up
C
C Included Parameter Files
C
***     PARAMETER/CMN:EFNLUN/		! event flags and luns
***	PARAMETER/CMN:MACMEN/
***     PARAMETER/CMN:MOANMESS/		! moan constants
***     PARAMETER/CMN:TRNTYP/		! transformation types
C
C Common Blocks
C
***     COMMON/CMN:MAPS/		! all about the maps
***	COMMON/CMN:OTRANS/
***     COMMON/CMN:WORKS/		! info about worksatation
C
C Local Variables
C
	CHARACTER*2	CPTN(4)		! corner point names (all 4)
	INTEGER*4	DEVICE		! device where point digitised
					! = BITLIN for bitpad
					! = DIGLIN for digitiser table
	INTEGER*4	BUTTON		! number of puck button
	INTEGER*4	I		! counter
C
C initialisation
C
	DATA	CPTN/'NW','SW','SE','NE'/
C
200	CONTINUE
	CALL INFORM(MSG_BLANK)
	CALL INFORM(MSG_FRMAPSETUP,IFFNAM(1,MAP_SETUP(SET)))
C
C read coordinates from digitising table
C
	DO 210, I=1,4
  205	   CONTINUE
	   CALL PING				! attract attention
	   CALL INFORM(MSG_DIG,CPTN(I))
	   CALL GTDGXY(DEVICE,BUTTON,CPTS(1,I),EXSTSU,ABORT) 
	   IF (ABORT) THEN
	      GOTO 900
	   ELSEIF (EXSTSU) THEN
	      IF (OMPDEF(SET)) THEN
	         IF (TRNTYP.NE.MAPTRNTYP(SET)) CALL INFORM(MSG_TRANWARN,
     &	          TR_TYPE(MAPTRNTYP(SET))(1:TR_TYPE_LEN(MAPTRNTYP(SET))),
     &	          TR_TYPE(TRNTYP)(1:TR_TYPE_LEN(TRNTYP)))
	         GOTO 900
	      ELSE
	         CALL MOAN(MSG_NOEXSTVAL)
	         CALL LEARN(MSG_DGNEWVALS)
	         GOTO 200
	      ENDIF
	   ELSEIF (DEVICE.EQ.BITLIN) THEN
	      CALL INFORM(MSG_MAPONTABLE)
	      GOTO 205
	   ENDIF
  210	CONTINUE
C
  900	CONTINUE
	RETURN
	END
