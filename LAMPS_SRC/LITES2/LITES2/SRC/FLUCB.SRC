	SUBROUTINE FLUCB
C
***	MODULE	FLUCB
***	IDENT	22MR93
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C
C Construct and flush out CB entry (called from FLUSH)
C
	IMPLICIT NONE
C
	INCLUDE	'LSL$CMNIFF:CB.STR'
***	PARAMETER/CMN:AUXDEF/
***	PARAMETER/CMN:EFNLUN/
***	PARAMETER/CMN:MOANMESS/
***	PARAMETER/CMN:PACK/
***	COMMON/CMN:CB/
***	COMMON/CMN:MAPS/
***	COMMON/CMN:OIIH/
C
C function
	LOGICAL	GEOG_TRANS_ARRAY
C
C workspace
	INTEGER	I,ROW,COL,IND
C
C use a local copy of CB header, as this routine is called between
C the header, and using it in IFFBDY. (There may be a case for a local
C CB data as well, but I don't see any problems at present)
C
C the data is in LITES2 coordinate space, so convert to coordinate system
C for this map (unless being called from IFFBDY, when we already have
C a copy of the coordinates
	IF (DO_FLY_TRANS) THEN
	   IF (FROM_IFFBDY) THEN
	      DO 10, I=2,COIIND
	         COIXY(1,I)=COPY_COIXY(1,I)
	         COIXY(2,I)=COPY_COIXY(2,I)
10	      CONTINUE
	   ELSE
	      IF (.NOT.GEOG_TRANS_ARRAY(LITES2SYS,LITES2ZONE,LITES2PAR,
     &	                            LITES2UNIT,LITES2ORG,
     &	                            CURR_SYS,CURR_ZONE,CURR_PARAMS,
     &	                            CURR_UNIT,CURR_ORG,
     &	                            COIIND-1,COIXY(1,2))) THEN
	         GOTO 999
	      ENDIF
	   ENDIF
	ELSE
	   DO 20, I=2,COIIND		! convert to this map
	      COIXY(1,I)=COIXY(1,I)-ORGXY(1)
	      COIXY(2,I)=COIXY(2,I)-ORGXY(2)
20	   CONTINUE
	ENDIF
C
C construct CB header
	CBHL.FLAGS = OIENDS
	CBHL.GTYPE = COIPAK(PAKGT)
	CBHL.NROW = COIIND-1
	CBHL.NCOL = 2+COINATT
	CBHL.COLH(1) = IFF_C_X
	CBHL.COLH(2) = IFF_C_Y
	DO 30 I=1,COINATT
	   CBHL.COLH(2+I) = COIATTC(I)
30	CONTINUE
	CBHL.NATT = 0
C
C if no attributes, use XY array directly
	IF (COINATT.EQ.0) THEN
	   CALL IFFCB( CBHL, COIXY(1,2) )
	ELSE
C
C otherwise construct real CB
	   IND = 0
	   DO 50 ROW=1,CBHL.NROW
	      DO 40 COL=1,CBHL.NCOL
	         IND = IND+1
	         IF (COL.LE.2) THEN
	            CBD.R(IND) = COIXY(COL,ROW+1)
	         ELSE
	            CBD.I(IND) = COIATTV(COL-2,ROW+1)
	         ENDIF
40	      CONTINUE
50	   CONTINUE
	   CALL IFS_CB_COMPRESS(CBHL,CBD)
	   CALL IFFCB( CBHL, CBD )
	ENDIF
C
999	RETURN
	END
