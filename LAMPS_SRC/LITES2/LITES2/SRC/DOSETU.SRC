        SUBROUTINE DOSETU
C
***     IDENT   17OC94
***     MODULE  DOSETU
C
C       Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C       Written                                 RWR	21-Nov-1984
C
C Description
C
C	LITES2 cartographic editor SETUP command.
C
C
C  SETUP NONE	   -  no setup i.e. no document on table
C  SETUP TWO	   -  2-point setup
C  SETUP FOUR	   -  4-point setup (default)
C  SETUP AGAIN     -  setup maps and menus on table again
C  SETUP OSMULTI   -  OS multiple point setup
C  SETUP EDGE      -  setup by digitising points on sheet edges
C  SETUP TRANSFORM -  select transformation to use
C
C                     PROJECTIVE    X = (a0*x + a1*y + a2)/
C                                       (a6*x + a7*y +1)
C                                   Y = (a3*x + a4*y + a5)/
C                                       (a6*x + a7*y +1)
C                     ORTHOGONAL    X = a0 + a1*x - a2*y
C                                   Y = a3 + a2*y + a1*y
C
C                     AFFINE        X = a0 + a1*x + a2*y
C                                   Y = b0 + b1*x + b2*y
C
C                     EXTENDED      X = a0 + a1*x + a2*y + a3*x*y
C                                   Y = b0 + b1*x + b2*y + b3*x*y
C
C SETUP TABLE__COUNTS - specify the units the table returns
C SETUP CANCEL - cancel all maps that are on the list for being set up
C SETUP MAP - add the specified map to the list of maps being set up
C SETUP INITIAL - maps specified in INITIAL state will be set up on table
C
C
        IMPLICIT NONE
C
***	PARAMETER/CMN:EFNLUN/		! needed for common maps
***	PARAMETER/CMN:MACMEN/		! needed for common menus
***     PARAMETER/CMN:MOANMESS/         ! moan constants
***	PARAMETER/CMN:STAPAR/
***	PARAMETER/CMN:TRNTYP/
C
***     COMMON/CMN:DOUT/                ! cmd dec. output
***	COMMON/CMN:GLOBAL/
***	COMMON/CMN:MAPS/		! data about maps
***	COMMON/CMN:MENUS/		! data about menus
***	COMMON/CMN:OPTION/
***	COMMON/CMN:STATE/
***	COMMON/CMN:TRACKS/		! data about tracking areas
***	COMMON/CMN:WORKS/		! workstation control
C
C External variables
	EXTERNAL	NASTY,MOAN
	EXTERNAL 	SETUP_TRANSFORMATION_CMD_TABLE
C
C Local Variables
	INTEGER		I
	INTEGER		TOTAL
C
C Despatch according to secondary command
C
	GOTO (100,200,300,400,500,600,700,800,900,1000,1100), DSECN
C
C Secondary command number out of range
C
	CALL MSGCMD(NASTY,MSG_ERRSECIND)
	GOTO 9999
C
C  SETUP NONE	 -  no setup i.e. no document on table
C
100	CONTINUE
	TABSUP=0
C
C if in READY state, still need SETUP AGAIN command
C
	IF (SINDEX.EQ.STATE_READY) CALL INFORM(MSG_NXTSETUP)
C
	GOTO 10000
C
C  SETUP TWO	 -  2-point setup
C
200	CONTINUE
	TABSUP=TABSUP_2PT
C
C if in READY state, still need SETUP AGAIN command
C
	IF (SINDEX.EQ.STATE_READY) CALL INFORM(MSG_NXTSETUP)
C
	GOTO 10000
C
C  SETUP FOUR	 -  4-point setup (default)
C
300	CONTINUE
	TABSUP=TABSUP_4PT
C
C if in READY state, still need SETUP AGAIN command
C
	IF (SINDEX.EQ.STATE_READY) CALL INFORM(MSG_NXTSETUP)
C
	GOTO 10000
C
C  SETUP AGAIN	 -  set up maps and menus on table again
C
400	CONTINUE
	IF (SINDEX.EQ.STATE_INITIAL) THEN
	   CALL MSGCMD(MOAN,MSG_CMDNOTVALID)
	   CALL IGNCMD
	ELSE
C
C get old transformations from file
C
	   CALL GTOLDT
C
C set up menus on digitising table/bitpad
C
	   DO 410, I=1,MENTOP
	      CALL INIMNU(I)
410	   CONTINUE
C
C set up maps on table
C
	   MAP_SETUP_TOTAL = 0		! no maps setup yet
	   DO 420, I=0,MAPTOP
	      CALL INIMAP(I)
420	   CONTINUE
C
C set up tracking areas
C
	   DO 430, I=1,TRATOP
	      CALL INITRK(I)
430	   CONTINUE
C
C put any new transformations onto file
C
	   CALL PTOLDT
C
C give a message to confirm completion
C
	   IF (NOWOPT) CALL INFORM(MSG_NEWSTATE,STASTR(1:STALEN))
	ENDIF
	GOTO 10000
C
C  SETUP OS  -  use OS multiple point setup
C
500	CONTINUE
	TABSUP=TABSUP_OS
C
C if in READY state, still need SETUP AGAIN command
C
	IF (SINDEX.EQ.STATE_READY) CALL INFORM(MSG_NXTSETUP)
C
	GOTO 10000
C
C  SETUP EDGE  -  digitise points around the edge of map
C
600	CONTINUE
	TABSUP=TABSUP_EDGE
C
C if in READY state, still need SETUP AGAIN command
C
	IF (SINDEX.EQ.STATE_READY) CALL INFORM(MSG_NXTSETUP)
C
	GOTO 10000
C
C  SETUP TRANSFORM -  set transformation to use
C
700	CONTINUE
	CALL GET_COMMAND(DSTR(:DSTRL),SETUP_TRANSFORMATION_CMD_TABLE)
	IF (MOANED) GOTO 9999
C
	GOTO (711,712,713,714), DPRIN
C
C---Secondary command number out of range
C
	CALL MSGCMD(NASTY,MSG_ERRSECIND)
	GOTO 9999
C
711	TRNTYP = TRNTYP_PROJECTIVE
	GOTO 750
712	TRNTYP = TRNTYP_ORTHOG
	GOTO 750
713	TRNTYP = TRNTYP_AFFINE
	GOTO 750
714	TRNTYP = TRNTYP_LASERAID
	GOTO 750
C
750	IF (SINDEX.EQ.STATE_READY) CALL INFORM(MSG_NXTSETUP)
C
	GOTO 10000
C
C SETUP TABLE_COUNTS - specify the units the table counts are in
C
800	CONTINUE
	TABLE_COUNTS = ABS(DREALS(1))
	GOTO 10000
C
C SETUP CANCEL - cancel all maps that are on the list for being set up
900	CONTINUE
	DO 910, I=0,MAPTOP
	   MAP_TABLE(I) = .FALSE.
910	CONTINUE
	HAD_SETUP_CANCEL = .TRUE.
C
C if in READY state, still need SETUP AGAIN command
C
	IF (SINDEX.EQ.STATE_READY) CALL INFORM(MSG_NXTSETUP)
	GOTO 10000
C
C SETUP MAP - add the specified map to the list of maps being set up
1000	CONTINUE
	IF (DINTS(1).LT.1.OR.DINTS(1).GT.MAXMAP) THEN
	   CALL MOAN(MSG_ILLEGLMAP,MAXMAP)
	   GOTO 9999
	ELSEIF (DINTS(1).GT.MAPTOP+1.OR..NOT.MAP_EXIST(DINTS(1)-1)) THEN
	   CALL MOAN(MSG_MAPNOTEXIST,DINTS(1))
	   GOTO 9999
	ELSE
C
C check that there are not already enough maps specified
	   TOTAL = 0
	   DO 1010, I = 0,MAPTOP
	      IF (MAP_EXIST(I).AND.MAP_TABLE(I)) TOTAL = TOTAL + 1
1010	   CONTINUE
	   IF (TOTAL.GE.MAX_SETUP) THEN
	      CALL MOAN(MSG_MAXSETUP,TOTAL)
	      CALL LEARN(MSG_USESETUPCANCEL)
	      GOTO 9999
	   ELSE
C
C add this map to list
	      MAP_TABLE(DINTS(1)-1) = .TRUE.
	   ENDIF
	ENDIF
C
C if in READY state, still need SETUP AGAIN command
	IF (SINDEX.EQ.STATE_READY) CALL INFORM(MSG_NXTSETUP)
	GOTO 10000
C
C SETUP INITIAL - maps read in INITIAL state will be set up
1100	CONTINUE
	HAD_SETUP_CANCEL = .FALSE.
	GOTO 10000
C
9999	CALL IGNCMD
10000	CONTINUE
        RETURN
        END
