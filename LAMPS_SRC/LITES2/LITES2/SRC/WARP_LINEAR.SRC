C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1994-09-21 10:23:56.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL*4 FUNCTION WARP_LINEAR(NPTS,PTS,COEFFS,RESID,XMAX,YMAX,
     &				  XRMS,YRMS)
C
***	MODULE	WARP_LINEAR
***	IDENT	21SE94
C
C Copyright Laser-Scan Laboratories Limited, Cambridge, England.
C
C Author: R. Russell 
C
C	Created 				 RWR	20-Sep-1994
C
C Performs a one or two point transformation.
C
C This is not a LEAST SQUARES solution, it only uses (at most) two
C control points. If there is only one point, then a shift is calculated
C 
C This transformation is sufficient to correct for shifting 
C and rotation.
C
C WARP_LINEAR takes an array of NPTS sets of coordinates, with each set
C arranged in the order:
C
C		 Xmeasured Ymeasured Xtarget Ytarget
C
C A set of coefficients (COEFFS) is calculated which will transform the
C 'measured' coordinate system into the 'target' one. X and Y residuals
C are returned in the user array RESID, and the maximum and RMS errors
C are also calculated.
C
	IMPLICIT NONE
C
	INTEGER*4 NPTS			! number of points
	REAL	  PTS(4,NPTS)		! measured & target coordinates
	REAL*8	  COEFFS(4)		! returned transform coeffs
	REAL	  RESID(2,NPTS)		! X,Y residuals (target space)
	REAL	  XMAX,YMAX		! max residuals    "      "
	REAL	  XRMS,YRMS		! RMS residuals    "      "
C
	INTEGER	I
	REAL*8	X,Y,XX,YY
	REAL*8	DX1,DY1,DX2,DY2
	REAL*8	DIST,SINANG,COSANG
	REAL*8	A0,A1,A2,A3
C
	WARP_LINEAR = .TRUE.
C
	IF (NPTS .LT. 1) GOTO 999
C
	X  = PTS(1,1)
	Y  = PTS(2,1)
	XX = PTS(3,1)
	YY = PTS(4,1)
C
C angles - use cross product for SIN and dot product for COS
	IF (NPTS .LT. 2) THEN
	   SINANG = 0.0
	   COSANG = 1.0
	ELSE
	   DX1 = PTS(1,2) - X
	   DY1 = PTS(2,2) - Y
	   DX2 = PTS(3,2) - XX
	   DY2 = PTS(4,2) - YY
C
C normalising factor, to ensure SIN and COS are in abs range 0.0 -> 1.0
C
	   DIST = SQRT(DX1*DX1 + DY1*DY1) * SQRT(DX2*DX2 + DY2*DY2)
C
	   IF (DIST .LE. 1.0D-15) THEN
 	      SINANG = 0.0
	      COSANG = 1.0
	   ELSE
	      SINANG = (DX1*DY2 - DX2*DY1)/DIST
	      COSANG = (DX1*DX2 + DY1*DY2)/DIST
	   ENDIF
	ENDIF
C
	A1 = COSANG
	A2 = SINANG
	A0 = XX - A1*X + A2*Y
	A3 = YY - A2*X - A1*Y
C
	COEFFS(1) = A0
	COEFFS(2) = A1
	COEFFS(3) = A2
	COEFFS(4) = A3
C
C return these for consistancy with routines in TRANLIB
	XRMS = 0.0
	YRMS = 0.0
	XMAX = 0.0
	YMAX = 0.0
	DO 20, I = 1,NPTS
	   RESID(1,I) = 0.0
	   RESID(2,I) = 0.0
20	CONTINUE
C
	WARP_LINEAR = .FALSE.
999	CONTINUE
	RETURN
	END
