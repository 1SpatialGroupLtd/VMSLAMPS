C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1988-07-11 13:55:56.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE D1465
C
***	MODULE	D1465
***	IDENT	11JL88
C
	IMPLICIT NONE
C
C Created by Alan Wylie 5-Feb-1980
C From the Ordnance Survey CORAL66 squaring program
C
C Determine line segments within specified alignment tolerence
C with datum line.
C 1) If a datum has been found on this pass (i.e. at least one
C line (the datum) remains unadjusted):-
C	For each entry in the line array not yet dealt with
C	   determine the alignment,with respect to the datum, and if 
C	   grossly out,redetermine as a perpendicular to the datum.
C	   Calculate a misalignment factor. If this is less than
C	   the specified squaring tolerance,or no tolerance has been
C	   specified;
C	   set the status word in the line array to show "to be
C	   processed on this pass"
C	   Add the misalignment factor to the total of such factors for
C	   the current pass.
C	   Add the line length to the total of line lengths for
C	   the current pass.
C
***	COMMON/CMN:D14IFC/
***	COMMON/CMN:D14CMN/
C
	PARAMETER HALF=0.7071
	REAL*8 M,LAMDA
	INTEGER TWAPTR
	LOGICAL ISPERP
C
	LAMDA=HALF
C
C do we have a datum ?
C
	IF (.NOT.((DATUM.GE.0).OR.
     &            USING_BASE)) GOTO 50
C
C we have a datum
C
	   DO 40 TWAPTR=0,D14NPT-2
	      IF ((STATUS(TWAPTR).AND.1).EQ.0) THEN
		 LAMDA=DATSIN*ORCOS(TWAPTR)-ORSINE(TWAPTR)*DATCOS
	         ISPERP = (ABS(LAMDA).GT.HALF)
		 IF (ISPERP) THEN
		    LAMDA=DATCOS*ORCOS(TWAPTR)+DATSIN*ORSINE(TWAPTR)
		    STATUS(TWAPTR)=STATUS(TWAPTR).OR.2
		 ELSE
		    STATUS(TWAPTR)=STATUS(TWAPTR).AND."177775
		 ENDIF
		 M=LILEN(TWAPTR)*LAMDA
		 IF ((TOL.EQ.0.0).OR.(ABS(M).LT.TOL)) THEN
		    STATUS(TWAPTR)=STATUS(TWAPTR).OR.4
		    IF (USING_BASE.OR.
     &                 (DATUM.EQ.TWAPTR).OR.
     &                 (LILEN(TWAPTR).GE.DCL)) THEN
C
C feature has no privileged points
	               IF (.NOT.HAD_PRIV) THEN
		          SUMM=SUMM+M
		          SUMLA=SUMLA+LILEN(TWAPTR)
C
C line has privileged points at both ends
	               ELSEIF(((STATUS(TWAPTR).AND.16).NE.0).AND.
     &	                      ((STATUS(TWAPTR+1).AND.16).NE.0)) THEN
		          SUMM=SUMM+M
		          SUMLA=SUMLA+LILEN(TWAPTR)
C
C line has privileged points, but not two on this line
	               ELSE
	                  SUMM = SUMM
	                  SUMLA = SUMLA
	               ENDIF
		    ENDIF
C
	            IF(ISPERP) THEN
	               D14PER = D14PER + 1
	            ELSE
	               D14PAR = D14PAR + 1
	            ENDIF
		 ENDIF
	      ENDIF
40	   CONTINUE
50	CONTINUE
C
	RETURN
	END
