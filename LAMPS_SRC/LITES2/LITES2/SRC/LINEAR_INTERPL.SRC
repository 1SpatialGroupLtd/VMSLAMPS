	SUBROUTINE LINEAR_INTERPL(XY1,XY2,XYP,Z1,Z2,ZP)
C
***	MODULE	LINEAR_INTERPL
***	IDENT	17JL89
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author  R.W. Russell					26-Jan-1989
C
C To calculate the Z value of a point, given its XY position
C and the ends of the line it lies on 
C
	IMPLICIT NONE
C
	INCLUDE 'LSL$CMNIFF:CB.STR'
C
C arguments
C in
	REAL		XY1(2)		! xy position of start of line
	REAL		XY2(2)		!                end
	REAL		XYP(2)		! xy position of point
	REAL		Z1,Z2		! z values at ends of lines
C out
	REAL		ZP		! z value of point
C
C w/s
	REAL		DX,DY
	REAL		ZA,ZB
	REAL		R
	INTEGER		ZAI,ZBI
	EQUIVALENCE	(ZA,ZAI),(ZB,ZBI)
C
C check we have valid Z values
	CALL PUTLNG(Z1,ZA)
	CALL PUTLNG(Z2,ZB)
C
	IF (ZAI.EQ.IFF_ABSENT.AND.ZBI.EQ.IFF_ABSENT) THEN
	   CALL PUTLNG(IFF_ABSENT,ZP)			! no value
	ELSEIF (ZAI.EQ.IFF_ABSENT) THEN
	   ZP = ZB					! one value
	ELSEIF (ZBI.EQ.IFF_ABSENT) THEN
	   ZP = ZA					! one value
	ELSE	
	   DX = XY2(1) - XY1(1)				! interpolate
	   DY = XY2(2) - XY1(2)
	   IF (DX.NE.0.0.OR.DY.NE.0.0) THEN
	      IF (ABS(DX).GE.ABS(DY)) THEN
	         R=(XYP(1)-XY1(1))/DX
	      ELSE
	         R=(XYP(2)-XY1(2))/DY
	      ENDIF
	      ZP = Z1 + (Z2 - Z1)*R
	   ELSE
	      ZP = (Z1 + Z2)/2
	   ENDIF
	ENDIF
C
	RETURN
	END

