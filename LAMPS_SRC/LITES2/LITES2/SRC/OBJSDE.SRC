	INTEGER FUNCTION OBJSDE(VERTEX,LSTVRT,TOP,XY,LINE)
C
***	IDENT	30NO87
***	MODULE	OBJSDE
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RWR	13-May-1985
C
C Description
C
C	Integer function to test if the next to an end vertex is 
C	to the left or right of a line.
C	It returns the value +10 if the current vertex is not the
C	first or last of the feature, 0 if the vertex is on the line,
C	+1 if second end vertex is to the right of the line and -1
C	if it is to the left.
C
	IMPLICIT NONE
C
C Arguments
C
	INTEGER		VERTEX		! current vertex
	INTEGER		LSTVRT		! last vertex number of feature
	INTEGER		TOP		! last used index of XY (in case
					! whole feature is not in it)
	REAL		XY(2,TOP)	! coordinates
	REAL		LINE(2,2)	! line that is being used to
					! match
C
C working storage
C
	INTEGER		INDEX		! index of second end point
	REAL		CP		! cross product
C
C Start of subroutine
C
	IF (VERTEX.NE.1.AND.VERTEX.NE.LSTVRT) THEN	! not on an end
	   OBJSDE=+10
	ELSE
C
C get the index of the required point in XY
C
	   IF (VERTEX.EQ.1) THEN
	      INDEX=2
	   ELSE
	      INDEX=TOP-1
	   ENDIF
C
C work out cross product of this point with line
C
10	   CONTINUE
	   CP=(LINE(1,2)-LINE(1,1))*(XY(2,INDEX)-LINE(2,1))-
     &	      (XY(1,INDEX)-LINE(1,1))*(LINE(2,2)-LINE(2,1))
C
C interpret cross product
C
	   IF (CP.EQ.0.0) THEN		! on line
C
C so try next vertex
	      IF (VERTEX.EQ.1) THEN
	         IF (INDEX.NE.TOP) THEN
	            INDEX = INDEX+1
	            GOTO 10
	         ENDIF
	      ELSE
	         IF (INDEX.NE.1) THEN
	            INDEX = INDEX-1
	            GOTO 10
	         ENDIF
	      ENDIF
	      OBJSDE=0			! can't resolve it
	   ELSEIF (CP.LT.0.0) THEN	! to right of line
	      OBJSDE=+1
	   ELSE				! must be on left
	      OBJSDE=-1
	   ENDIF
	ENDIF
C
	RETURN
	END
