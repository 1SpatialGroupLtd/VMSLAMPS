	LOGICAL FUNCTION SYMREG(N,XY)
C
***	IDENT   09MY96
***	MODULE  SYMREG
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RWR	 26-Jun-1987
C
C	LITES2 cartographic editor - get region around a symbol
C
	IMPLICIT NONE
C
C Parameters
C
	INCLUDE	'LSL$CMNIFF:CB.STR'
***	PARAMETER/CMN:AUXDEF/
***	PARAMETER/CMN:CTXS/
***	PARAMETER/CMN:MOANMESS/
***	PARAMETER/CMN:PACK/
C
C Common Blocks
C
***	COMMON/CMN:CTXS/
***	COMMON/CMN:DRAWIN/
***	COMMON/CMN:OPTION/
***	COMMON/CMN:OIIH/
***	COMMON/CMN:STRIKE/
***	COMMON/LSL$CMNFRT:FRTCOM/
C
C Arguments
C
	INTEGER*4	N		! no. of points defining region
	REAL		XY(2,N)		! points defining the region
C
C Local Variables
C
	REAL		COSANG		! cosine of rotation angle
	REAL		SINANG		! sine of rotation angle
	REAL		LENGTH,HEIGHT	! size of box around symbol
	REAL		ORIGX		! X coord of locating point
	REAL		ORIGY		! Y coord
	REAL		MINX,MINY
C
C Assume success
C
	SYMREG = .FALSE.
C
C get data about symbol, either from FO or COI variables
C
	IF (FO) THEN
	   COSANG = FOAUX(COSI)
	   SINANG = FOAUX(SINI)
	   LENGTH = FOAUX(MAXXI) - FOAUX(MINXI)
	   HEIGHT = FOAUX(MAXYI) - FOAUX(MINYI)
	   MINX   = FOAUX(MINXI)
           MINY   = FOAUX(MINYI)
C
	   ORIGX = FOXY(1,1)
	   ORIGY = FOXY(2,1)
	ELSE
	   COSANG = COIAUX(COSI)
	   SINANG = COIAUX(SINI)
	   LENGTH = COIAUX(MAXXI) - COIAUX(MINXI)
	   HEIGHT = COIAUX(MAXYI) - COIAUX(MINYI)
	   MINX   = COIAUX(MINXI)
           MINY   = COIAUX(MINYI)
C
	   ORIGX = COIXY(1,2)
	   ORIGY = COIXY(2,2)
	ENDIF
C
C add expansion factor to box
C
	MINX = MINX - (EXPROP*HEIGHT)
	MINY = MINY - (EXPROP*HEIGHT)
	LENGTH = LENGTH + 2.0*(EXPROP*HEIGHT)
	HEIGHT = HEIGHT + 2.0*(EXPROP*HEIGHT)
C
C calculate the 4 corners of the box
C
	XY(1,1)=ORIGX + MINX*COSANG - MINY*SINANG
	XY(2,1)=ORIGY + MINX*SINANG + MINY*COSANG
	XY(1,2)=XY(1,1) + COSANG*LENGTH
	XY(2,2)=XY(2,1) + SINANG*LENGTH
	XY(1,3)=XY(1,2) - SINANG*HEIGHT
	XY(2,3)=XY(2,2) + COSANG*HEIGHT
	XY(1,4)=XY(1,1) - SINANG*HEIGHT
	XY(2,4)=XY(2,1) + COSANG*HEIGHT
C
	N = 4
C
	RETURN
	END
