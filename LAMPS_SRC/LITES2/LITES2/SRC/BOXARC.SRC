C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1987-02-19 11:16:18.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE BOXARC(AUX,MINX,MAXX,MINY,MAXY)
C
***	MODULE	BOXARC
***	IDENT	19FE87
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author  Ron Russell
C
C To find box that surrounds a circle arc
C If the arc goes into more than 1 quadrant, then that extreme of the
C corressponding CIRCLE must be included.
C
C AUX is the auxiliary array containing info about the arc.
C MINX,MAXX,MINY,MAXY - limits of surrounding box
C
	IMPLICIT NONE
C
***	PARAMETER/CMN:AUXDEF/
***	PARAMETER/CMN:CONSTS/
C
C arguments
	REAL	AUX(AUXLEN)
	REAL	MINX,MAXX,MINY,MAXY
C
C w/s
	REAL	RADIUS,TOTAL,START
	REAL	MINARCX,MAXARCX,MINARCY,MAXARCY
	REAL	DIFF1,DIFF2
C
C
	RADIUS = AUX(RADI)
	TOTAL  = AUX(TOTI)
	START  = AUX(STAI)
C
C limits of circle
C
	MINARCX = AUX(XCENI) - RADIUS
	MAXARCX = AUX(XCENI) + RADIUS
	MINARCY = AUX(YCENI) - RADIUS
	MAXARCY = AUX(YCENI) + RADIUS
C
C arcs over 270 degrees, are boxed by the full circle
C
	IF (ABS(TOTAL).GE.THREEPIBI2) THEN
	   MINX = MINARCX
	   MAXX = MAXARCX
	   MINY = MINARCY
	   MAXY = MAXARCY
	   GOTO 900
	ENDIF
C
C convert to anticlockwise, if necessary
C
	IF (TOTAL.LT.0.0) THEN
	   START = START + TOTAL
   10	   IF (START.LT.0.0.OR.START.GE.TWOPI) THEN
	      IF (START.LT.0.0) START = START + TWOPI
	      IF (START.GE.TWOPI) START = START - TWOPI
	      GOTO 10
	   ENDIF
	   TOTAL = -TOTAL
	ENDIF
C
C work out max and min of end points of arc
C
	DIFF1 = RADIUS*COS(START)
	DIFF2 = RADIUS*COS(START+TOTAL)
	MINX  = AUX(XCENI) + MIN(DIFF1,DIFF2)
	MAXX  = AUX(XCENI) + MAX(DIFF1,DIFF2)
C
	DIFF1 = RADIUS*SIN(START)
	DIFF2 = RADIUS*SIN(START+TOTAL)
	MINY  = AUX(YCENI) + MIN(DIFF1,DIFF2)
	MAXY  = AUX(YCENI) + MAX(DIFF1,DIFF2)
C
C jump to quadrant that start of arc starts in
C
	IF (START.LT.PIBI2) THEN
	   GOTO 100
	ELSEIF (START.LT.PI) THEN
	   GOTO 200
	ELSEIF (START.LT.THREEPIBI2) THEN
	   GOTO 300
	ELSE
	   GOTO 400
	ENDIF
C
C if adding on the remaining angle does not go into the next quadrant
C then finished, otherwise add on PIBI2 or total (whichever is smaller)
C and go into the next quadrant
C
C quadrant 1
C 
  100	CONTINUE
	IF ((START+TOTAL).LT.PIBI2) GOTO 900		! finished
	IF (TOTAL.LE.PIBI2) THEN
	   START = START + TOTAL
	   TOTAL = 0
	ELSE
	   START = START + PIBI2
	   TOTAL = TOTAL - PIBI2
	ENDIF
C
	MAXY = MAXARCY
C
C quadrant 2
C
  200	CONTINUE					! quadrant2
	IF ((START+TOTAL).LT.PIBI2) GOTO 900		! finished
	IF (TOTAL.LE.PI) THEN
	   START = START + TOTAL
	   TOTAL = 0
	ELSE
	   START = START + PIBI2
	   TOTAL = TOTAL - PIBI2
	ENDIF
C
	MINX = MINARCX
C
C quadrant 3
C
  300	CONTINUE
	IF ((START+TOTAL).LT.THREEPIBI2) GOTO 900	! finished
	IF (TOTAL.LE.PIBI2) THEN
	   START = START + TOTAL
	   TOTAL = 0
	ELSE
	   START = START + PIBI2
	   TOTAL = TOTAL - PIBI2
	ENDIF
C
	MINY = MINARCY
C
C quadrant 4
C
  400	CONTINUE
	IF ((START+TOTAL).LT.TWOPI) GOTO 900		! finished
	IF (TOTAL.LE.PIBI2) THEN
	   START = START + TOTAL
	   TOTAL = 0
	ELSE
	   START = START + PIBI2
	   TOTAL = TOTAL - PIBI2
	ENDIF
C
	MAXX = MAXARCX
	START = START - TWOPI
	GOTO 100				! goto quadrant 1

900	RETURN
	END
