	LOGICAL FUNCTION GEOM_BOX(ISTAT,INPTS,XY,TEXT,TXSIZE,ANG,OUT_XY)
C
***     IDENT	19OC93
***     MODULE  GEOM_BOX
C
C       Copyright Laser-Scan Ltd., Cambridge, England.
C
C       Written                                 RWR 	8-Oct-1993
C
C to generate a box (for a geometry) around a text or symbol
C
C	returns FALSE for success
C
        IMPLICIT NONE
C
***	PARAMETER/CMN:AUXDEF/
***     PARAMETER/CMN:MOANMESS/         ! moan constants
***	PARAMETER/CMN:PACK/
C
***     COMMON/LSL$CMNFRT:FRTCOM/
***	COMMON/CMN:DRAWIN/
***	COMMON/LSL$CMNLSL:TXTC/
C
C arguments
	INTEGER*2	ISTAT(4) 	! feature status words
	INTEGER		INPTS		! number of input pts
	REAL		XY(2,INPTS)	! the input points
	CHARACTER*(*)	TEXT		! any text
	INTEGER		TXSIZE		! text size from TH entry
	REAL		ANG		! rotation angle
	REAL		OUT_XY(2,*)	! output points (5)
C
C functions
	LOGICAL		FRTFND
	LOGICAL		GENAUX
C
C w/s
	INTEGER		FC
	REAL		AUX(AUXLEN)
	INTEGER		PACK(PAKLEN)
	REAL		COSANG		! cosine of rotation angle
	REAL		SINANG		! sine of rotation angle
	REAL		LENGTH,HEIGHT	! size of box around symbol
	REAL		MINX,MINY
C
C assume failure
	GEOM_BOX = .TRUE.
C
C intitialise
	FC = ISTAT(1)
C	
	IF (FRTFND(FC)) THEN
	   CALL NASTY(MSG_BADCODE,FC)
	   CALL COLAPS(MSG_BADCODE)
	   GOTO 999
	ENDIF
C
C fill in required elements of pack
	PACK(PAKFC) = FC
	PACK(PAKGT) = FRTGT
C
	IF (GENAUX(INPTS,XY,AUX,TXTDSC,ANG,TXSIZE,PACK,ISTAT)) GOTO 999
C
	COSANG = AUX(COSI)
	SINANG = AUX(SINI)
	LENGTH = AUX(MAXXI) - AUX(MINXI)
	HEIGHT = AUX(MAXYI) - AUX(MINYI)
	MINX   = AUX(MINXI)
	MINY   = AUX(MINYI)
C       
C       add expansion factor
	MINX = MINX - (EXPROP*HEIGHT)
	MINY = MINY - (EXPROP*HEIGHT)
	LENGTH = LENGTH + 2.0*(EXPROP*HEIGHT)
	HEIGHT = HEIGHT + 2.0*(EXPROP*HEIGHT)
C       
C calculate the 4 corners of the box
C
	OUT_XY(1,1)=XY(1,1) + MINX*COSANG - MINY*SINANG
	OUT_XY(2,1)=XY(2,1) + MINX*SINANG + MINY*COSANG
	OUT_XY(1,2)=OUT_XY(1,1) + COSANG*LENGTH
	OUT_XY(2,2)=OUT_XY(2,1) + SINANG*LENGTH
	OUT_XY(1,3)=OUT_XY(1,2) - SINANG*HEIGHT
	OUT_XY(2,3)=OUT_XY(2,2) + COSANG*HEIGHT
	OUT_XY(1,4)=OUT_XY(1,1) - SINANG*HEIGHT
	OUT_XY(2,4)=OUT_XY(2,1) + COSANG*HEIGHT
	OUT_XY(1,5)=OUT_XY(1,1)
	OUT_XY(2,5)=OUT_XY(2,1)
C
	GEOM_BOX = .FALSE.
C
 999	CONTINUE
	RETURN
	END
	
