	LOGICAL FUNCTION STANXT(NXTADR,NXTCHS,NXTPTR,NXTFIL,NXTARGL,NXTARG)
C
***	IDENT	17MY94
***	MODULE	STANXT
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RJH	09-NOV-1984
C
C Description
C
C    This function is part of the LITES2 Command Interface, and its
C purpose is to get to the start of the next command or macro.
C    The arguments returned are the address of the current buffer, the
C length of the buffer, a pointer to the current decoding position
C on the line, a flag indicating whether or not the buffer came from
C a command file, the argument string length and the string itself.
C    The function returns true if the end of line is reached.
C
	IMPLICIT NONE
C
***     COMMON/CMN:DVAR/		! command decoding variables
***     COMMON/CMN:GLOBAL/		! global variables
C
C Arguments
	INTEGER*4	NXTADR		! buffer address
	INTEGER*4	NXTCHS		! buffer length
	INTEGER*2	NXTPTR		! buffer decode pointer
	INTEGER*4	NXTFIL		! buffer from command file ?
	INTEGER*4	NXTARGL		! argument string length
	CHARACTER*(*)	NXTARG		! argument string
C
C Functions
	LOGICAL		SKIP		! skips command terminators
	LOGICAL		EMPSTK		! empty stack ?
C
C Local Variables
	LOGICAL		EOLN		! end of line ?
C
C Skip trailing terminators on current line
C
	EOLN = SKIP()
C
C Skip trailing terminators at lower levels, until either another
C command is found or the end of the terminal or command file input line
C is reached
C
100	IF ((.NOT. EOLN) .OR. EMPSTK() .OR. NXTFIL .OR. MOANED)
     &	   GO TO 200
	CALL POPSTK(NXTADR,NXTCHS,NXTPTR,NXTFIL,NXTARGL,NXTARG)	! pop details
	IF (.NOT. MOANED) THEN
	   CALL SETAUX(%VAL(NXTADR),NXTCHS)	! set new buffer
	   CALL DCPSET(NXTPTR)			! set decode pointer
	   EOLN = SKIP()			! skip trailing term's
	ENDIF
	GO TO 100				! back for test
C
C If a command has been found, save the current line pointer
C
200	IF (.NOT. (EOLN .OR. MOANED)) CALL DCPSAV(NXTPTR)
C
C Assign function value
C
	STANXT = EOLN
C
	RETURN
	END
