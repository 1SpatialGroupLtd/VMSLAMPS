        SUBROUTINE DOFRT
C
***     IDENT   08NO93
***     MODULE  DOFRT
C
C       Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C       Written                                 RJH	25-SEP-1984
C
C	LITES2 cartographic editor FRT command.
C
        IMPLICIT NONE
C
***	PARAMETER/CMN:EFNLUN/
***     PARAMETER/CMN:MOANMESS/
***	PARAMETER/CMN:STAPAR/
C
***     COMMON/LSL$CMNLSL:CMDCOM/
***     COMMON/CMN:DOUT/
***	COMMON/CMN:FILES/
***	COMMON/LSL$CMNFRT:FRTCOM/
***	COMMON/CMN:MAPS/
***	COMMON/CMN:OPTION/
***	COMMON/LSL$CMNFRT:SRISCL/
***	COMMON/LSL$CMNLSL:TXTC/
***	COMMON/CMN:STATE/
C
C Functions called
	LOGICAL		FRTINI		! read FRT file
	LOGICAL		PARFILN		! parses a filename
	INTEGER		LIB$FIND_FILE
C
C Local Variables
	INTEGER		I
	INTEGER*4	RETCOD		! file parse return code
	INTEGER*2	NAMLEN		! length of filename
C
	REAL		S_SRIWID,S_PLTSCL,S_SRISCL,S_SRIROT,S_ENLSCL
C
	LOGICAL		VERNUM		! allow version numbers
	PARAMETER	(VERNUM=.TRUE.)
C
C save old FRT scales if in READY state
	IF (SINDEX.NE.STATE_INITIAL) THEN
	   S_SRIWID = SRIWID
	   S_PLTSCL = PLTSCL
	   S_SRISCL = SRISCL
	   S_SRIROT = SRIROT
	   S_ENLSCL = ENLSCL
	ENDIF
C
C Parse FRT filename
	RETCOD = PARFILN(
     &	  FRTTIT,NAMLEN,DSTR(1:DSTRL),FRTTEM,VERNUM)
	IF (.NOT.RETCOD) THEN
	   CALL MOAN(MSG_ERRFILENAME,'FRT')
	   CALL LITES2_SIGNAL(RETCOD)
	   GOTO 998
	ENDIF
C
C check file exists if in READY state
	IF (SINDEX.NE.STATE_INITIAL) THEN
	   I = 0
	   TXTPTR = 255
	   RETCOD = LIB$FIND_FILE(FRTTIT(:NAMLEN),TXTDSC,I)
	   CALL LIB$FIND_FILE_END(I)
	   IF (.NOT.RETCOD) THEN
	      CALL MOAN(MSG_ERRINFILE,'FRT')
	      CALL LITES2_SIGNAL(RETCOD)
	      GOTO 998
	   ENDIF
	ENDIF
C
C flag not had FRT in case FRTINI fails
	HADFRT = .FALSE.
C
C open FRT file
	CALL INFORM(MSG_RDNGFILE,'FRT',FRTTIT(:NAMLEN))
	IF (FRTINI(FRTTIT(:NAMLEN))) THEN
	   CALL MOAN(MSG_ERRINFILE,'FRT')
	   GOTO 998
	ENDIF
C
C FRTINI turns on CMDLIB messages, so turn off again
	NOMESS = .TRUE.
C
C Set FRT thick lines to agree with LITES2 (set by FRTINI)
	FRTHKS = .NOT.THKOPT
C
	HADFRT = .TRUE.			! now had FRT file
C
C If in INITIAL state, then cause SRI/TRI to be (re)-read
C In READY state, leave them as they are
	IF (SINDEX.EQ.STATE_INITIAL) THEN
	   NEWSRI = .TRUE.			! need new SRI
	   NEWTRI = .TRUE.			! need new TRI
	ENDIF
C
C have new groups now, so OS_MAP_HEADER operations are invalid
	IF (OS_MH_EDIT) THEN
	   DO 20, I=0,OS_MH_FLAGS-1
	      OS_MH_GRP(I) = 0
	      IF (OS_MH_CAT_LIST(I) .GT. 0)
     &	               CALL DELDEF(OS_MH_CAT_LIST(I))
20	   CONTINUE
	   CALL INFORM(MSG_NOOSMAPOPER)
	   OS_MH_EDIT = .FALSE.
	ENDIF
	GOTO 999
C
C error exit
998	CALL IGNCMD
C
C restore old FRT scales if in READY state
999	IF (SINDEX.NE.STATE_INITIAL) THEN
	   CALL SRISLW(S_SRIWID)
	   CALL SRIUNI(S_PLTSCL,S_SRISCL,S_SRIROT,S_ENLSCL)
	ENDIF
	RETURN
	END
