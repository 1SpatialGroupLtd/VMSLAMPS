C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1989-10-12 10:59:52.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	INTEGER FUNCTION CISECS(AUX,SECS)
C
***	MODULE	CISECS
***	IDENT	15AP86
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author  R. Fairbairns
C
C Coded Feb'76
C Stolen for LITES2, July 1984		Clarke Brunt
C
C To find the sectors intersected by a circle arc
C
C Returns the number of sectors intersected. Their numbers
C are put into SECS(1:CISECS)
C
	IMPLICIT NONE
C
***	PARAMETER/CMN:AUXDEF/
***	PARAMETER/CMN:CONSTS/
***	COMMON/CMN:MASTER/
C
C arguments
	REAL	AUX(AUXLEN)	! auxiliary array for circle
C
	INTEGER	SECS(*)		! array to put sector numbers in
C
C w/s
	REAL STAANG, TOTANG, R, RR, XC, YC, X, Y, D, D1
	INTEGER	I
	REAL PHI,PHI1
	INTEGER S
	EQUIVALENCE (X, Y)
C
C function called
	INTEGER SECTOR
C
C extract simple variables from the argument arrays
	R = AUX(RADI)
	RR = R*R
	STAANG = AUX(STAI)
	TOTANG = AUX(TOTI)
C ensure clockwise, internally
	IF (TOTANG.GT.0.0) THEN
	   STAANG = STAANG+TOTANG
	   IF (STAANG.GT.TWOPI) STAANG = STAANG-TWOPI
	   TOTANG = -TOTANG
	ENDIF
C get centre coordinates
	XC = AUX(XCENI)
	YC = AUX(YCENI)
C
	CISECS = 0			! empty list of sectors
C
C add in sector of start point
	CALL ADDSN(SECS,CISECS,SECTOR(XC+R*COS(STAANG),
     &	                  YC+R*SIN(STAANG)))
C
C look for intersections with Y-axes
	X = XOFF
	DO 10 I = 1,YRANGE
	   D = X-XC
	   D1 = RR-D*D
	   IF (D1.GT.0.0) THEN
	      D1 = SQRT(D1)
	      PHI = ATAN2(D1, D)
	      IF (PHI.LT.0.0) PHI = PHI+TWOPI
	      PHI1 = PHI-STAANG
	      IF (PHI1.GT.0.0) PHI1 = PHI1-TWOPI
	      IF (PHI1.GE.TOTANG) THEN
	         S = IFIX((YC+D1-YOFF)/YSECTW)*YRANGE + I
	         CALL ADDSN(SECS, CISECS, S)
	         CALL ADDSN(SECS, CISECS, S-1)
	      ENDIF
C take the other sign for the square root
	      PHI = TWOPI-PHI-STAANG
	      IF (PHI.GT.0.0) PHI = PHI-TWOPI
	      IF (PHI.GE.TOTANG) THEN
	         S = IFIX((YC-D1-YOFF)/YSECTW)*YRANGE + I
	         CALL ADDSN(SECS, CISECS, S)
	         CALL ADDSN(SECS, CISECS, S-1)
	      ENDIF
	   ENDIF
	   X = X+XSECTW
10	CONTINUE
C
C now look for intersections with X-axes
	Y = YOFF
	DO 30 I = 1, XRANGE*YRANGE, YRANGE
	   D = Y-YC
	   D1 = RR-D*D
	   IF (D1.GT.0.0) THEN
	      D1 = SQRT(D1)
	      PHI = ATAN2(D, D1)
	      PHI1 = PHI-STAANG
	      IF (PHI.LT.0.0) PHI1 = PHI1+TWOPI
	      IF (PHI1.GT.0.0) PHI1 = PHI1-TWOPI
	      IF (PHI1.GE.TOTANG) THEN
	         S = IFIX((XC+D1-XOFF)/XSECTW) + I
	         CALL ADDSN(SECS, CISECS, S)
	         CALL ADDSN(SECS, CISECS, S-YRANGE)
	      ENDIF
C take the other sign for the square root -
C  not so easy when dealing with coss!
	      PHI = PI-PHI-STAANG
	      IF (PHI.GT.0.0) PHI = PHI-TWOPI
	      IF (PHI.GE.TOTANG) THEN
	         S = IFIX((XC-D1-XOFF)/XSECTW) + I
	         CALL ADDSN(SECS, CISECS, S)
	         CALL ADDSN(SECS, CISECS, S-YRANGE)
	      ENDIF
	   ENDIF
	   Y = Y+YSECTW
30	CONTINUE
C
	RETURN
	END
