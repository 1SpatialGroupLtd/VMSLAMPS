	INTEGER FUNCTION CREATE_ACSEL(
     &			NAC,ACS,ACTYPE,SEL,TEXT,TXT,TXTL,HADVAL)
C
***	IDENT	09SE89
***	MODULE	CREATE_ACSEL
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					CCB	6-Jun-1988
C
C	Find or create entry in list of AC selections
C	Returns index of entry modified/created
C
	IMPLICIT NONE
C
***     PARAMETER/CMN:MOANMESS/
C
***     COMMON/CMN:ACSEL/
C
C Arguments
	INTEGER		NAC		! selections in group
	RECORD/ACSEL/	ACS(MAX_ACSEL)	! selection record
	INTEGER		ACTYPE		! type of AC
	LOGICAL		SEL		! select or deselect
	LOGICAL		TEXT		! text substring present?
	CHARACTER*(MAX_ACTXTL)	TXT	! the text
	INTEGER		TXTL		! text length
	LOGICAL		HADVAL		! had values?
C
C Functions
	INTEGER		FNDAC		! find ACTYPE in list
C
C workspace
	INTEGER		AC
	INTEGER		I,N
C
	AC = FNDAC(NAC,ACS,ACTYPE)	! search for ACTYPE
C
	IF (AC.EQ.0) THEN		! no selections for this AC
	   IF (NAC.GE.MAX_ACSEL) THEN
	      CALL MOAN(MSG_TOOMANYACSEL,MAX_ACSEL)
	      GOTO 1000
	   ENDIF
C
C create new entry for this ACTYPE
	   NAC = NAC+1
	   AC = NAC
	   ACS(AC).TYPE = ACTYPE
	   ACS(AC).ALL = .TRUE.	! all values selected
	   ACS(AC).SLCT = .FALSE.	! not yet in select mode
	   ACS(AC).NTOG = 0		! no toggle points
	ENDIF
	ACS(AC).PRES = (SEL.AND..NOT.HADVAL).OR.TEXT	! present?
C
C in case existing entry being switched to absent
	IF (.NOT.(SEL.OR.TEXT.OR.HADVAL)) THEN	! required absent
	   ACS(AC).ALL = .TRUE.	! all
	   ACS(AC).SLCT = .FALSE.
	   ACS(AC).NTOG = 0
C
C remove any text entries for this AC
	   N = AC
100	   N = N+1
	   IF (N.LE.NAC) THEN
	      IF (ACS(AC+N).TYPE.EQ.ACTYPE) GOTO 100
	   ENDIF
	   IF (N.GT.AC+1) THEN
	      DO 105 I=N,NAC
	         ACS(I-N+AC+1) = ACS(I)
105	      CONTINUE
	   ENDIF
	ENDIF
C
	IF (.NOT.TEXT) GOTO 1000	! that's all if not TEXT
C
C does required text selection exist already
110	AC = AC+1				! next entry
	IF (AC.GT.NAC) GOTO 120		! doesn't exist
	IF (ACS(AC).TYPE.NE.ACTYPE) GOTO 120	! wrong type
	IF (ACS(AC).TXTL.NE.TXTL) GOTO 110	! wrong length
	IF (TXTL.EQ.0) GOTO 140			! null text
	IF (ACS(AC).TXT(:ACS(AC).TXTL).NE.TXT(:TXTL)) GOTO 110
	GOTO 140				! identical
C
C not already present
120	IF (NAC.GE.MAX_ACSEL) THEN
	   CALL MOAN(MSG_TOOMANYACSEL,MAX_ACSEL)
	   AC = 0
	   GOTO 1000
	ENDIF
	DO 130 I=NAC,AC,-1		! create space for new entry
	   ACS(I+1) = ACS(I)
130	CONTINUE
	NAC = NAC+1
	ACS(AC).TYPE = ACTYPE		! AC type
	ACS(AC).ALL = .TRUE.		! all values selected
	ACS(AC).SLCT = .FALSE.		! no select commands yet
	ACS(AC).NTOG = 0		! no toggle points
	ACS(AC).TXTL = TXTL		! text length
	IF (TXTL.GT.0) ACS(AC).TXT = TXT(:TXTL) ! and text
C
C already present
140	ACS(AC).PRES = SEL.AND..NOT.HADVAL	! selected?
	IF (.NOT.(SEL.OR.HADVAL)) THEN		! if deselected then
	   ACS(AC).ALL = .TRUE.		! remove all value selections
	   ACS(AC).SLCT = .FALSE.
	   ACS(AC).NTOG = 0
	ENDIF
C	
1000	CREATE_ACSEL = AC		! return index of entry
	RETURN
	END
