        SUBROUTINE DOLABE
C
***     IDENT   24NO93
***     MODULE  DOLABE
C
C       Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C       Written                                 RWR	18-Jul-1986
C
C Description
C
C	LITES2 cartographic editor LABEL command.
C
C LABEL NONE	  - cancels all labelling 
C LABEL FSN	  - label with FSN
C LABEL FC	  - label with FC
C LABEL HEIGHT	  - label with AC 3 value (as real)
C LABEL CONTOUR	  - label with AC 2 (integer) values
C LABEL LH	  - label with AC 4 text
C LABEL RH	  - label with AC 5 text
C LABEL ACINT	  - label with AC  (integer) values
C LABEL ACREAL	  - label with AC  (real) values
C LABEL ACTEXT	  - label with AC  text
C LABEL AC	  - label with AC value in correct format
C LABEL FEATURE	  - label features (default)
C LABEL POINT	  - label points in features
C LABEL ATTRIBUTE - label points that have specified attribute
C
        IMPLICIT NONE
C
	INCLUDE	'LSL$CMNIFF:CB.STR'
***	PARAMETER/CMN:ACS/
***     PARAMETER/CMN:MOANMESS/
C
***	COMMON/CMN:DOUT/		! command decoding output
***	COMMON/LSL$CMNFRT:FRTACD/
***	COMMON/CMN:LABELS/
***	COMMON/CMN:PLOT/
C
	EXTERNAL MOAN,NASTY
C
C w/s
C
	INTEGER		I		! loop counter
	INTEGER		ACTYP		! type of AC
	INTEGER		ACKIND		! part of AC to display
					! 1 = display text
					! 2 = display value as integer
					! 3 = display value as real
					! 4 = display value as specified
	INTEGER		INDEX		! index to AC table
	INTEGER		OUTSTR_L	! the length of the string
	CHARACTER*1	OUTSTR		! the output string
	INTEGER		ACVALUE		! the value (as an integer)
	LOGICAL		GOT_VALUE
C
C functions
	LOGICAL		ATTDECOD

C
C Dispatch according to secondary command
C
	GOTO (100,200,300,400,500,600,700,800,900,1000,1010,
     &	      1020,1030,1040),DSECN
C
C Secondary command number out of range
C
	CALL MSGCMD(NASTY,MSG_ERRSECIND)
	GO TO 8000
C
C LABEL NONE	  - cancels all labelling 
C
  100	LABFSN = .FALSE.
	LABFC  = .FALSE.
	NUM_LAB_AC = 0
	NUM_LAB_ATTR = 0
	NOLAB = .TRUE.
	GOTO 9000
C
C LABEL FSN	  - label with FSN
C
200	LABFSN = .TRUE.
	NOLAB = .FALSE.
	GOTO 9000
C
C LABEL FC	  - label with FC
C
300	LABFC = .TRUE.
	NOLAB = .FALSE.
	GOTO 9000
C
C LABEL HEIGHT	  - label with AC 3 value (as real)
C
400	ACTYP = 3
	ACKIND = 3
	GOTO 2000
C
C LABEL CONTOUR	  - label with AC 2 (integer) values
C
500	ACTYP = 2
	ACKIND = 2
	GOTO 2000
C
C LABEL LH	  - label with AC 4 text
C
600	ACTYP = 4
	ACKIND = 1		! text
	GOTO 2000
C
C LABEL RH	  - label with AC 5 text
C
700	ACTYP = 5
	ACKIND = 1		! text
	GOTO 2000
C
C LABEL ACINT	  - label with AC n (integer) values
C
800	CONTINUE
C
C decode the ACD type; don't want value
	IF (.NOT.ATTDECOD(DSTR(1:DSTRL),ACTYP,OUTSTR_L,OUTSTR,
     &	                 ACVALUE,.FALSE.,GOT_VALUE)) GOTO 8000
C
C don't want value
	IF (GOT_VALUE) THEN
	   CALL MOAN(MSG_VALNOTREQD)
	   GOTO 8000
	ENDIF
C
	ACKIND = 2		! integer
	GOTO 2000
C
C LABEL ACREAL	  - label with AC n (real) values
C
900	CONTINUE
C
C decode the ACD type; don't want value
	IF (.NOT.ATTDECOD(DSTR(1:DSTRL),ACTYP,OUTSTR_L,OUTSTR,
     &	                 ACVALUE,.FALSE.,GOT_VALUE)) GOTO 8000
C
C don't want value
	IF (GOT_VALUE) THEN
	   CALL MOAN(MSG_VALNOTREQD)
	   GOTO 8000
	ENDIF
C
	ACKIND = 3		! real
	GOTO 2000
C
C LABEL ACTEXT	  - label with AC n text
C
1000	CONTINUE
C
C decode the ACD type; don't want value
	IF (.NOT.ATTDECOD(DSTR(1:DSTRL),ACTYP,OUTSTR_L,OUTSTR,
     &	                 ACVALUE,.FALSE.,GOT_VALUE)) GOTO 8000
C
C don't want value
	IF (GOT_VALUE) THEN
	   CALL MOAN(MSG_VALNOTREQD)
	   GOTO 8000
	ENDIF
C
	ACKIND = 1		! text
	GOTO 2000
C
C LABEL AC - label with AC as general
C
1010	CONTINUE
C
C decode the ACD type; don't want value
	IF (.NOT.ATTDECOD(DSTR(1:DSTRL),ACTYP,OUTSTR_L,OUTSTR,
     &	                 ACVALUE,.FALSE.,GOT_VALUE)) GOTO 8000
C
C don't want value
	IF (GOT_VALUE) THEN
	   CALL MOAN(MSG_VALNOTREQD)
	   GOTO 8000
	ENDIF
C
	ACKIND = 4
	GOTO 2000
C
C LABEL FEATURE	  - label features (default)
C
1020	CONTINUE
	LABFEAT = .TRUE.
	GOTO 9000
C
C LABEL POINT	  - label points in features
C
1030	CONTINUE
	LABFEAT = .FALSE.
	GOTO 9000
C
C LABEL ATTRIBUTE - label points that have specified attribute
C
1040	CONTINUE
C
C decode the ACD type; don't insist on value
	IF (.NOT.ATTDECOD(DSTR(1:DSTRL),ACTYP,OUTSTR_L,OUTSTR,
     &	                 ACVALUE,.FALSE.,GOT_VALUE)) GOTO 8000
C
C was there something at end of line?
	IF (OUTSTR_L.GT.0) THEN
	   CALL MOAN(MSG_ILLEGALTERM,%REF(OUTSTR))
	   GOTO 8000
	ENDIF
C
C is this Attribute type in table?

	DO 1042, I=1,NUM_LAB_ATTR
	   IF (ACTYP.NE.LABATTR(I,1)) GOTO 1042
	   INDEX = I
	   GOTO 1044
 1042	CONTINUE
C
C not in table - so add it (if possible)
C
	IF (NUM_LAB_ATTR.EQ.TOT_LAB_ATTR) THEN
	   CALL MOAN(MSG_TOOMANYACLS,'attribute',TOT_LAB_ATTR)
	   GOTO 8000
	ELSE
	   NUM_LAB_ATTR= NUM_LAB_ATTR + 1
	   INDEX = NUM_LAB_ATTR
	   LABATTR(INDEX,1) = ACTYP
	ENDIF
C
C add appropriate value to table 
 1044	CONTINUE
	IF (GOT_VALUE) THEN
	   LABATTR(INDEX,2) = ACVALUE
	ELSE
	   LABATTR(INDEX,2) = IFF_ABSENT
	ENDIF
C
C set formats in table
C
	CALL ACDFND(ACTYP,.FALSE.)	! cannot fail!!!
	LABATTRFORMAT(INDEX) = ACD_FORMAT
	LABATTRF_LEN(INDEX)  = ACD_FORMAT_LEN

	GOTO 9000
C
C fill in AC table
C
2000	CONTINUE
C
C is this AC type in table?
C
	DO 2010, I=1,NUM_LAB_AC
	   IF (ACTYP.NE.LABAC(I)) GOTO 2010
	   INDEX = I
	   GOTO 2020
 2010	CONTINUE
C
C not in table - so add it (if possible)
C
	IF (NUM_LAB_AC.EQ.TOT_LAB_AC) THEN
	   CALL MOAN(MSG_TOOMANYACLS,'AC',TOT_LAB_AC)
	   GOTO 8000
	ELSE
	   NUM_LAB_AC= NUM_LAB_AC + 1
	   INDEX = NUM_LAB_AC
	   LABAC(INDEX) = ACTYP
	   LABACV(INDEX) = .FALSE.
	   LABACTXT(INDEX) = .FALSE.
	ENDIF
C
 2020	CONTINUE
	NOLAB = .FALSE.
C
C set flags in table
C
	IF (ACKIND.EQ.1) THEN
	   LABACTXT(INDEX) = .TRUE.
	ELSE
	   LABACV(INDEX) = .TRUE.
	   IF (ACKIND.EQ.2) THEN
	      LABACFORMAT(INDEX) = '%N'
	      LABACF_LEN(INDEX) = 2
	   ELSEIF (ACKIND.EQ.3) THEN
	      LABACFORMAT(INDEX) = '%F0.3'
	      LABACF_LEN(INDEX) = 5
	   ELSE
	      CALL ACDFND(ACTYP,.FALSE.)	! cannot fail!!!
	      LABACFORMAT(INDEX) = ACD_FORMAT
	      LABACF_LEN(INDEX)  = ACD_FORMAT_LEN
	   ENDIF
	ENDIF
C
	GOTO 9000
C
8000	CALL IGNCMD
	GOTO 10000
C
C success, maybe write ANNO JOURNAL
9000	IF (ANNOMAC.GT.0.AND..NOT.PLOT_ON) CALL STORE_ANNO
C
10000	RETURN
        END
