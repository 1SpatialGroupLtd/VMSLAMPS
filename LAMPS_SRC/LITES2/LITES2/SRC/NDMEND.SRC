	LOGICAL FUNCTION NDMEND(AUTO,CTRL)
C
***	IDENT	11SE92
***	MODULE	NDMEND
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Written					CCB	10-Sep-1986
C
C	LITES2 cartographic editor END MEND routine
C	Returns FALSE if successful
C
	IMPLICIT NONE
C
	INCLUDE	'LSL$CMNIFF:CB.STR'
***	PARAMETER/CMN:AUXDEF/
***     PARAMETER/CMN:MOANMESS/
***	PARAMETER/CMN:PACK/
C
***	COMMON/CMN:DEFINE/
***	COMMON/CMN:CSTRCT/
***	COMMON/CMN:MASTER/
***	COMMON/CMN:OIIH/
***	COMMON/CMN:OPTION/
***	COMMON/CMN:RFCTRL/
***	COMMON/CMN:STRIKE/
C
C argument
	LOGICAL*1	AUTO		! MEND AUTO, or MANUAL?
	LOGICAL*1	CTRL		! had control C
C
C functions called
	LOGICAL	STRTFT
	LOGICAL*1 HADBREAK
C
C workspace
	INTEGER	I
	INTEGER OPER,OPERFM
	REAL	DX,DY
C
	NDMEND = .TRUE.			! assume failure
C
	IF (.NOT.FO) THEN		! nothing to join to
	   CALL MOAN(MSG_NOFEATURE)
	   GOTO 999
	ENDIF
C
C set up attributes of joined points
	OPER = DEFINITION(P_JOIN)
	IF (OPER.EQ.0) OPER = DEFINITION(P_OTHER) 
	OPERFM = DEFINITION(P_FAR_MOVE)
C
C Start off the new feature
	IF (CONFST) THEN
	   CALL CPYPAK(PAKLEN,IHPAK,COIPAK)
	   COIPAK(PAKFLG) = 0		! not deleted
	   FLG_GEOCHANGE = .TRUE.
	   IF (STRTFT(IHPAK,IHTC,IHFS,IHAC,IHTH)) THEN
	      CALL COLAPS(121)
	   ENDIF
	   OIDRAW = .FALSE.		! don't draw output (yet)
	   CONFST = .FALSE.		! feature started
C
C add first vertex
	   IF (IHVRT.EQ.IHLST) THEN
	      CALL ADDVRT(IHPAK,1,1,.FALSE.)
	   ELSE
	      CALL ADDVRT(IHPAK,IHLST,IHLST,.FALSE.)
	   ENDIF
	ENDIF
C
C Delete old segment (remove from screen)
50	IH = .FALSE.			! pretend to drop in hand
	CALL RFDRAW			! and refresh without it
	CALL LDSG(IHPAK,.TRUE.)
C
C maintain visibility of last segment of IH
	IF (IHVRT.NE.IHLST) THEN
	   CALL GETVRT(IHPAK,IHLST,IHLST)
	   IF ((RIFLG(1).AND.1).EQ.0) THEN
	      CALL FLUSH(1)
	      OIENDS = 0		! invisible
	   ENDIF
	ENDIF
C
C Output points of object in hand (excluding ends)
	IF (IHLST.GT.2) THEN
	   IF (IHVRT.EQ.IHLST) THEN	! join last point of IH
	      CALL ADDVRT(IHPAK,2,IHLST-1,.FALSE.)
	   ELSE				! join first point of IH
	      CALL ADDVRT(IHPAK,IHLST-1,2,.FALSE.)
	   ENDIF
	ENDIF
C
C maintain visibility of end segment of IH
	IF (IHVRT.EQ.IHLST) THEN
	   CALL GETVRT(IHPAK,IHLST,IHLST)
	ELSE
	   CALL GETVRT(IHPAK,2,2)
	ENDIF
	IF ((RIFLG(1).AND.1).EQ.0) THEN
	   CALL FLUSH(1)
	   OIENDS = 0		! invisible
	ENDIF
C
C Remove the old object in hand completely
	CALL DELFT(IHPAK,IHEF)
C
C no attributes for this point yet
	CONATT = 0
C
C apply Z position of cursor, if appropriate
	IF (ZOPT.AND.CSZI.NE.IFF_ABSENT) THEN
	   CALL ADDZ(CSZI,MXATTR,CONATT,COATTC,COATTV)
	ENDIF
C
C use attributes from operation list
	CALL LSTARR(OPER,CONATT,COATTC,COATTV,.TRUE.)
	IF (OPERFM .GT. 0) THEN
	   DX = RIXY(1,1) - CSXY(1)
	   DY = RIXY(2,1) - CSXY(2)
	   IF ((DX**2 + DY**2) .GT. FAR_MOVE_IFF**2)
     &	      CALL LSTARR(OPERFM,CONATT,COATTC,COATTV,.TRUE.)
	ENDIF
C
C add cursor position
	CALL ADCORD(CSXY,CONATT,COATTC,COATTV)
C
C move to opposite end of the found object from the place we are
C joining to
	IF (JOIFST) THEN
	   CALL DOLAST
	ELSE
	   CALL DOFIRS
	ENDIF
C
C set up rubber banding
	RUBCHG = .TRUE.
	RUBLIN(1) = .FALSE.
	RUBLIN(2) = .FALSE.
	CALL RFDRAW				! update refresh picture
C
	CALL HANDLE				! take object into hand
C
C set up rubber banding
	RUBCHG = .TRUE.
	RUBLIN(1) = .TRUE.
	RUBLIN(2) = .FALSE.
	I = 2
	IF (IHVRT.GT.1) I = IHVRT-IHVO-1
	RUBARR(1,1) = IHXY(1,I)
	RUBARR(2,1) = IHXY(2,I)
C
	CALL RFDRAW				! update refresh
	MENDNG = .TRUE.				! no 'nothing found'
	CALL DOFIND				! attempt to FIND
	MENDNG = .FALSE.
C
	CTRL = HADBREAK()			! check for CTRL/C
C
C If MEND AUTOMATIC, and nothing found, then terminate operation
	IF (AUTO.AND.HITITC.EQ.0) THEN
	   CALL RFDRAW
	   CALL ABANDN
	   GOTO 998
	ENDIF
C
C If CTRL/C, or MEND MANUAL, or multiple objects found, then
C return for commands
	IF (CTRL.OR.HITITC.GT.1.OR..NOT.AUTO) THEN
	   IF (HITITC.EQ.0) CALL INFORM(MSG_NOMEND)
	   CALL LEARN(MSG_AMBIGMEND)
	   GOTO 998
	ENDIF
C
C Special treatment for MEND AUTOMATIC
	IF (HITITC.EQ.1) THEN			! single item
	   CALL RFDRAW
	   GOTO 50
	ENDIF
C
998	NDMEND = .FALSE.			! success
C
999	RETURN
        END
