	SUBROUTINE SHCOMM(CMDTAB,UFLAGM)
C
***	IDENT	19DE88
***	MODULE	SHCOMM
C
C Copyright LaserScan Laboratories Ltd., Cambridge, England
C Author Paul Hardy from CMDLIB CMDPRT		PGH	22-Jan-1985
C
C	LITES2 cartographic editor SHOW COMMANDS command.
C
	IMPLICIT NONE
C
	INTEGER*4	CMDTAB(*)	! the command table
	INTEGER*4	UFLAGM		! user flags mask
C
C
***	PARAMETER/LSL$CMNLSL:TBLFLG/
C
C workspace
	INTEGER*4	I		! loop variable
	INTEGER*4	LOOPOF		! loop offset
C					! =1 => User flags in this table
	INTEGER*4	UFLPTR		! pointer to user flags array
	INTEGER*4	UFLVAL		! user flags for this command
	INTEGER*4	NCMD		! number of commands
	INTEGER*4	CMDNO		! command number in table
	INTEGER*4	CMDCNT		! count of how many printed
	INTEGER*4	TABIND		! index into table
	INTEGER*4	TABHDR		! to hold table header
	INTEGER*2	TABHD2(2)	! the same
	EQUIVALENCE	(TABHDR, TABHD2(1))
	INTEGER		L		! length of line
	INTEGER		N		! number of spaces
	INTEGER		CLEN		! length of name
C
C functions
	INTEGER*4	LIB$EXTZV	! to get user flags from array
C
C ensure the table is in order
	CALL LSL_SORTAB(CMDTAB)
C
C have we user flags to look at ?
	TABHDR = CMDTAB(1)
	IF ((TABHD2(2).AND.CMT$M_NOUFLG) .EQ. 0) THEN
	  LOOPOF = 1			! user flags for this table
	  UFLPTR = CMDTAB(3)		! pointer to flags
	ELSE
	  LOOPOF = 0			! no user flags
	  UFLPTR = 0			! no pointer to flags
	ENDIF
C
C are there secondary commands in the table?
	IF ((TABHD2(2).AND.CMT$M_MNEMS) .NE. 0) LOOPOF = LOOPOF+1
C
C how many to print ?
	NCMD = (CMDTAB(1).AND.'7FFF'X)
C
C and get them
C
	L = 0
	N = 0
	DO 10 I=1,NCMD
	   TABIND=I*2+LOOPOF+1
	   CMDNO = LIB$EXTZV(16,16,CMDTAB(TABIND))
	   CLEN = CMDTAB(TABIND).AND.'7FFF'X
	   IF (UFLPTR.NE.0) THEN
	      UFLVAL = LIB$EXTZV((CMDNO-1)*32,32,%VAL(UFLPTR))
	      IF ((UFLAGM.AND.UFLVAL).EQ.0) GOTO 10
	   ENDIF
	   IF (80-L-N.LT.CLEN) THEN
	      CALL TTWLIN
	      L = 0
	      N = 0
	   ENDIF
	   IF (L.EQ.0) THEN
	      CALL EXPAND
	   ELSE
	      CALL APPEND('%[ %]',N)
	   ENDIF
	   CALL APPEND('%S',CMDTAB(TABIND))
	   L = L+CLEN+N
	   N = 16-CLEN
	   IF (N.LE.0) N = N+16
	   CMDCNT = CMDCNT + 1
10	CONTINUE
	IF (L.GT.0) CALL TTWLIN
C
C all done
C
999	RETURN
	END
