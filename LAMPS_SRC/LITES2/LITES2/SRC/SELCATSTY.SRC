C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1988-08-09 09:40:28.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE SELCATSTY(MAP,THING_MAX,MAX_THING,SAV,SLCT)
C
***	IDENT	09AU88
***	MODULE	SELCATSTY
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RWR	9-August-1988
C
C Description
C
C	LITES2 cartographic editor: SELECT FSN command
C
	IMPLICIT NONE
C
***     COMMON/CMN:DOUT/		! command decoding output
***	COMMON/CMN:FCS/
***	COMMON/CMN:MASTER/		! for CHASEL
C
C arguments
	INTEGER*4	THING_MAX	! size of bit map
	INTEGER*4	MAP(THING_MAX)	! Bit map
	INTEGER*4	MAX_THING	! maximium value allowed
	LOGICAL*1	SAV		! all things selected
	LOGICAL*1	SLCT		! in selecting mode
C
C Local Variables
	INTEGER*4	IERR		! for testing return
	INTEGER*4	REMAIN		! to work out size of unused
					! end of bit map
	INTEGER*4	I		! counter
C
C clear temporary bit map
C
	DO 10,I = 1,THING_MAX
	   SELBUF(I) = 0
10	CONTINUE
C
C set unused bits at top of bit map
C
	REMAIN=MOD(MAX_THING+1,32)
	IF (REMAIN.GT.0)
     &		 CALL LIB$INSV(-1,REMAIN,32-REMAIN,MAP(THING_MAX))
C
C convert string of integers, integer ranges and groups to a bit map
C
	CALL RDSEL(MAX_THING,DSTR(1:DSTRL),.FALSE.,THING_MAX,SELBUF,IERR)
	IF (IERR.LT.0) GOTO 900
C
C selections changed
	CHASEL = .TRUE.
C
C if this is the first selection, since a SELECT ALL, clear all bits
C of bit map
C
	IF (.NOT.SLCT) THEN
	   DO 20, I = 1,THING_MAX
	      MAP(I) = 0
20	   CONTINUE
	   REMAIN=MOD(MAX_THING+1,32)
	   IF (REMAIN.GT.0)			! set unused bits at top
     &		 CALL LIB$INSV(-1,REMAIN,32-REMAIN,SELBUF(THING_MAX))
	   SLCT=.TRUE.			! now in selecting mode
	ENDIF
C
C now set MAP
C
	DO 30,	I=1,THING_MAX
	   MAP(I)=MAP(I).OR.SELBUF(I)
30	CONTINUE
C
C finally, check if all bits are set
C
	SAV=.FALSE.
	DO 40, I=1,THING_MAX
	   IF (MAP(I).NE.-1) GOTO 1000
40	CONTINUE
	SAV=.TRUE.
	GOTO 1000
C
900	CALL IGNCMD
C
1000	RETURN
	END
