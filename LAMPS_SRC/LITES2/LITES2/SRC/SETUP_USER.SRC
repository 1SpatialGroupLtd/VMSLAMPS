C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1989-07-03 12:36:18.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION SETUP_USER(LOG_NAME,NROUTINES,ROUTINE_NAMES,
     &	                              ROUTINE_ADDRESSES,TYPE,
     &	                              WHAT,HELP,MAPPED)
C
***	IDENT	03JL89
***	MODULE	SETUP_USER
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RWR	 18-Nov-1988
C
C	Function to initialise calls to user_routines
C	This function returns .TRUE. if the image file is found.
C	If any entry point is not found, an address of 0 is returned
C
	IMPLICIT NONE
C
***	PARAMETER/CMN:MOANMESS/
	INCLUDE '($LIBDEF)'
	INCLUDE '($SHRDEF)'
	INCLUDE '($SSDEF)'
C
***	COMMON/CMN:DOUT/
C
C arguments
	CHARACTER*(*)	LOG_NAME		! name of image
	INTEGER		NROUTINES
	CHARACTER*(*)	ROUTINE_NAMES(NROUTINES)
	INTEGER		ROUTINE_ADDRESSES(NROUTINES)
	CHARACTER*(*)	TYPE		! what is being initialised
	LOGICAL		MAPPED		! set .TRUE. if file is mapped,
					! .FALSE. otherwise
	CHARACTER*(*)	WHAT		! what is being used (eg KERN DSR
					! or geographicals)
	CHARACTER*(*)	HELP		! under which command help on the
					! logical name is to be found
C
C functions called
C
	INTEGER		SIGCHS
	INTEGER*4	LIB$MATCH_COND
	INTEGER*4	LIB$FIND_IMAGE_SYMBOL
	INTEGER*4	TRNALL
	CHARACTER*(1)	TEMP		! file specification
C
C Local Variables
C
	INTEGER*4	IERR		! error code
	INTEGER		I
C
	EXTERNAL	EXP_HAND_FIMS_USER
C
C assume failure
	SETUP_USER = .FALSE.
	MAPPED = .TRUE.
C
C check if logical name has been set up.
	IF (TRNALL(LOG_NAME,TEMP).EQ.SS$_NOLOGNAM) THEN
	   CALL MOAN(MSG_TRNLOGERR,LOG_NAME)
	   GOTO 9100
	ENDIF
C
C get entry points of routine in shared image
	CALL LIB$ESTABLISH(EXP_HAND_FIMS_USER)	! set  condition handler
	DO 10, I= 1,NROUTINES
	   IERR = LIB$FIND_IMAGE_SYMBOL(
     &	            LOG_NAME,
     &	            ROUTINE_NAMES(I)(1:SIGCHS(ROUTINE_NAMES(I))),
     &	            ROUTINE_ADDRESSES(I))
	   IF (.NOT.IERR) THEN
C
C use LIB$MATCH_COND, as LIB$FIND_IMAGE_SYMBOL seems to signal error
C as a warning, despite LIB$_KEYNOTFOU being fatal
	      IF (LIB$MATCH_COND(IERR,LIB$_KEYNOTFOU).EQ.1) THEN
	         ROUTINE_ADDRESSES(I) = 0
	      ELSE
C
C if error is LIB$_ACTIMAGE, then file has not been mapped
C note: $_ACTIMAGE is in fact a shared message (from $SHRDEF)
C LIB$MATCH_COND checks against this value, as the error code in
C IERR has the facility specific bit (^X8000) clear
	         IF (LIB$MATCH_COND(IERR,SHR$_ACTIMAGE).NE.0) THEN
	            MAPPED = .FALSE.
	         ENDIF
	         GOTO 9000
	      ENDIF
	   ENDIF
10	CONTINUE
C
C success
	SETUP_USER = .TRUE.
	GOTO 9500
C
9000	CALL MOAN(MSG_NOUSERRTS,TYPE,LOG_NAME)
9100	CONTINUE
	CALL LEARN(MSG_USESHARED1,WHAT,LOG_NAME)
	CALL LEARN(MSG_USESHARED2,HELP)
9500	CONTINUE
	RETURN
	END
