	SUBROUTINE DELMAP(MAP)
C
***	MODULE	DELMAP
***	IDENT	01SE92
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author  Clarke Brunt
C
C Coded 26-Aug-1992
C
C To scan all sectors deleting packs for MAP
C
	IMPLICIT NONE
C
***	PARAMETER/CMN:PACK/
***	COMMON/CMN:MASTER/
***	COMMON/CMN:SECMAP/
***	COMMON/CMN:WORKS/
C
C functions called
	LOGICAL	SETPAK, NXTPAK
C
C arguments
	INTEGER MAP		! map number to delete
C
C worksapce
	INTEGER		SEC
	INTEGER*4	PACK(PAKLEN)	! a data pack
	LOGICAL		EMPTY
C
C if using segments, they will need to be deleted
	IF (USESEG) THEN
	   DO 10 SEC=1,TOPSEC
	      CALL CLRSEC(DRAFLG,SEC)
10	   CONTINUE
	ENDIF
C
	DO 100 SEC=1,TOPSEC
C
C Set up pointers for the sector to be scanned
	   IF (SETPAK(SEC)) GOTO 100	! it's empty
C
C Scan the packs - delete ones for our map and note if there are any others
C
	   EMPTY = .TRUE.
20	   IF (NXTPAK(PACK,0)) GOTO 50	! end of the sector
	   IF (PACK(PAKMAP).EQ.MAP) THEN
C
C if using segments, and the segment has not been deleted (DRAFLG), then
C delete it and set the flag
	      IF (USESEG) THEN
	         IF ((PACK(PAKFLG).AND.DRAFLG).EQ.0) THEN
	            CALL LDSG(PACK,.TRUE.)
	            CALL SETFTR( DRAFLG, PAKBLK+PAKPTR )
	         ENDIF
	      ENDIF
	      CALL DELPAK( PACK, PAKBLK+PAKPTR )
	   ELSE
	      EMPTY = .FALSE.
	   ENDIF
	   GOTO 20
C
C If the sector is now completely empty, then free its memory rather
C than leaving a chain of deleted packs
50	   IF (EMPTY) CALL FRESEC(SEC)
C
100	CONTINUE
C
	RETURN
	END
