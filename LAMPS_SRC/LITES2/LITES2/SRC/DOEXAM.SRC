	SUBROUTINE DOEXAM
C
***     IDENT   09MY96
***     MODULE  DOEXAM
C
C       Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C       Written                                 RWR	23-Oct-1984
C
C	LITES2 cartographic editor EXAMINE command.
C
C EXAMINE MAP		  -  displays map that current featre is in
C EXAMINE <OVERLAY,LAYER> -  displays layer of current feature
C EXAMINE <FSN,NF>	  -  displays serial number of current feature
C EXAMINE <FC,IT>	  -  displays feature code of current feature
C EXAMINE <GT,TYPE>	  -  displays graphical type of current feature
C EXAMINE WIDTH		  -  displays line width of current feature (mm)
C EXAMINE PATTERN	  -  displays line pattern of current feature
C EXAMINE SECONDARY	  -  displays SC from FRT
C EXAMINE SIZE		  -  displays size of current symbol (mm)
C EXAMINE HEIGHT	  -  displays height of current 
C			     text feature (mm)
C EXAMINE POINTSIZE	  -  displays text height in points
C EXAMINE CATEGORY	  -  displays category field for OS text
C EXAMINE LOCATION	  -  displays text location field for OS text
C EXAMINE STYLE		  -  displays typeface field for OS text
C EXAMINE CROSSREF	  -  displays any cross reference FSN's
C EXAMINE <AC,TC,CH>	  -  displays AC, TC, CH entries
C EXAMINE VERTEX	  -  displays vertex number(s) for 
C			  -  cursor position
C EXAMINE PC		  -  displays any process code
C EXAMINE SUMMARY	  -  displays current feature layer, FSN, 
C			  -  FC, GT (default)
C EXAMINE ALL		  -  displays all information 
C			     about current feature
C EXAMINE LINE		  -  displays length and angle of a line
C EXAMINE LENGTH	  -  displays the length of a linear feature
C EXAMINE AREA		  -  displays the area enclosed by a linear
C			  -  feature
C EXAMINE BOX		  -  displays the corner of the bounding box
C			  -  around a feature
C EXAMINE ANGLE		  -  displays angle of non arc features
C EXAMINE POSITION	  -  displays X and Y of current vertex
C EXAMINE ATTRIBUTE	  -  displays attributes of current vertex
C EXAMINE DISTANCE	  -  displays distance along feature
C EXAMINE BEARING	  -  displays bearing of non arc features
C
	  IMPLICIT NONE
C
	INCLUDE	'LSL$CMNIFF:CB.STR'
***	PARAMETER/CMN:ACS/
***	PARAMETER/CMN:AUXDEF/
***	PARAMETER/CMN:EFNLUN/
***     PARAMETER/CMN:MOANMESS/
***	PARAMETER/CMN:PACK/
C
***	COMMON/CMN:ACS/
***	COMMON/CMN:DOUT/
***	COMMON/CMN:DRAWIN/
***	COMMON/LSL$CMNFRT:FRTCOM/
***	COMMON/CMN:GLOBAL/
***	COMMON/CMN:LAYERS/
***	COMMON/CMN:MAPS/
***	COMMON/CMN:OPTION/
***	COMMON/CMN:STRIKE/
C
C Functions called
	INTEGER*2	LIB$EXTZV
	REAL*8		AREA
	REAL		EXLENG
	REAL		EXDIST
	REAL		ANGLE
	LOGICAL		FRTFND
	REAL		ANGLE_BEARING
C
C External variables
	EXTERNAL	MOAN
C
C Local Variables
	INTEGER*2	LOCAT
	REAL		R
	REAL*8		RR
	REAL		CRNRS(4)
	CHARACTER*80	STRNG
C
	IF (.NOT.FO) THEN
	   CALL MOAN(MSG_NOFEATURE)
	   CALL IGNCMD
	   GOTO 9000
	ENDIF
C
C Dispatch according to secondary command
C
	GOTO (100,200,300,400,500,600,700,800,900,1000,1100,1200,
     &		1300,1400,1500,1600,1700,1800,1900,2000,
     &	        2100,2200,2300,2400,2500,2600,2700,2800,
     &	        2900), DSECN
C
C---Secondary command number out of range
C
	IF (DSECN.EQ.0) GOTO 1900	! EXAMINE is EXA SUM
	CALL NASTY(MSG_ERRSECIND,DPRI(1:DPRIL))
	CALL IGNCMD
	GOTO 9000
C
C EXAMINE MAP	  - displays map that current feature is in
C
100	CALL INFORM(MSG_MAP,FOPAK(PAKMAP)+1,IFFNAM(1,FOPAK(PAKMAP)))
	GOTO 9000
 
C
C EXAMINE <OVERLAY,LAYER> -  displays layer of current feature
C
200	CALL INFORM(MSG_LAYER,OVNOS(FOPAK(PAKOVL)))
	GOTO 9000
C
C EXAMINE <FSN,NF>	  -  displays serial number of current feature
C
300	CALL INFORM(MSG_FSN,FOPAK(PAKFSN))
	GOTO 9000
C
C EXAMINE <FC,IT>	  -  displays feature code of current feature
C
400	CALL EXFC
	GOTO 9000
C
C EXAMINE <GT,TYPE>	  -  displays graphical type of current feature
C
500	CALL INFORM(MSG_GRAPHTYP,FOPAK(PAKGT))
	GOTO 9000
C
C EXAMINE WIDTH		  -  displays line width of current feature (mm)
C
600	CALL EXWDTH(FOPAK(PAKFC),.TRUE.)
	GOTO 9000
C
C EXAMINE PATTERN	  -  displays line pattern of current feature
C
700	CALL EXPATT(FOPAK(PAKFC),.TRUE.)
	GOTO 9000
C
C EXAMINE SECONDARY	  -  displays secondary code for current feature
C
800	CALL EXSEC(FOPAK(PAKFC),.TRUE.)
	GOTO 9000
C
C EXAMINE SIZE		  -  displays size of current symbol (mm)
C
900	CALL EXSIZE(FOPAK(PAKFC),.TRUE.)
	GOTO 9000
C
C EXAMINE HEIGHT  -  displays height of current text feature (mm)
C
1000	IF (FOPAK(PAKGT).NE.TEXTYP) THEN
	   CALL MOAN(MSG_NOTTEXT)
	ELSEIF (HEIOPT.AND.POIOPT) THEN
	   CALL MOAN(MSG_ILLEGLMMS)
	ELSE
	   IF (.NOT.HEIOPT.AND..NOT.FRTFND(FOPAK(PAKFC))) THEN
	      R = FRTSIZ
	   ELSE
	      R=FOTH/100.0
	   ENDIF
	   CALL INFORM(MSG_TXTHEIGHT,R,R*SCLFRT)
	ENDIF
	GOTO 9000
C
C EXAMINE POINTSIZE	  -  displays text height in points
C
1100	IF (FOPAK(PAKGT).NE.TEXTYP) THEN
	   CALL MOAN(MSG_NOTTEXT)
	ELSEIF (.NOT.HEIOPT) THEN
	   CALL MOAN(MSG_ILLEGLTXT)
	ELSEIF (.NOT.POIOPT) THEN
	   CALL MOAN(MSG_ILLEGLPOI)
	ELSE
	   CALL INFORM(MSG_TXTPOINT,FOTH)
	ENDIF
	GOTO 9000
C
C EXAMINE CATEGORY	  -  displays category field for OS text
C
1200	IF (FOPAK(PAKGT).NE.TEXTYP) THEN
	   CALL MOAN(MSG_NOTTEXT)
	ELSE
	   CALL INFORM(MSG_TXTCLASS,LIB$EXTZV(6,6,FOFS(3)))
	ENDIF
	GOTO 9000
C
C EXAMINE LOCATION	  -  displays text location field for OS text
C
1300	IF (FOPAK(PAKGT).NE.TEXTYP) THEN
	   CALL MOAN(MSG_NOTTEXT)
	ELSEIF (.NOT.POSOPT) THEN		! positioning switched
	   CALL MOAN(MSG_NOPOSITIONING)		! off
	ELSE
	   LOCAT=FOFS(3).AND.'F'X	! get bottom 4 bits of flag word
	   IF (LOCAT.GT.8) LOCAT=0	! there are only 8 possible
	   CALL INFORM(MSG_TXTLOC,LOCAT)
	ENDIF
	GOTO 9000
C
C EXAMINE STYLE		  -  displays typeface field for OS text
C
1400	IF (FOPAK(PAKGT).NE.TEXTYP) THEN
	   CALL MOAN(MSG_NOTTEXT)
	ELSE
	   CALL INFORM(MSG_TXTFONT,LIB$EXTZV(4,2,FOFS(3)))
	ENDIF
	GOTO 9000
C
C EXAMINE CROSSREF	  -  displays any cross reference FSN's
C
1500	CALL MSGCMD(MOAN,MSG_CMDNOTIMPLE)
	GOTO 9000
C
C EXAMINE <AC,TC,CH>	  -  displays AC, TC, CH entries
C
1600	CALL GETAC(FOPAK,FOTC,FOAC)	! get ACs
	CALL VERFAC(FTRACD)		! verify them
	GOTO 9000
C
C EXAMINE VERTEX	-  displays vertex number(s) for cursor position
C
1700	CALL EXVERT
	GOTO 9000
C
C EXAMINE PC		-  displays PC for non-text features
C
1800	IF (FOPAK(PAKGT).EQ.TEXTYP) THEN
	   CALL MOAN(MSG_NOPCINTEXT)
	ELSE
	   CALL INFORM(MSG_PC,LIB$EXTZV(0,14,FOFS(3)))
	ENDIF 
	GOTO 9000
C
C EXAMINE SUMMARY	  -  displays current feature's map,
C			  -  layer, FSN, FC, GT (default)
1900	CALL INFORM(MSG_MAP,FOPAK(PAKMAP)+1,IFFNAM(1,FOPAK(PAKMAP)))
	CALL INFORM(MSG_LAYER,OVNOS(FOPAK(PAKOVL)))
	CALL INFORM(MSG_FSN,FOPAK(PAKFSN))
	CALL EXFC
	CALL INFORM(MSG_GRAPHTYP,FOPAK(PAKGT))
	GOTO 9000
C
C EXAMINE ALL	  -  displays all information about current feature
C
2000	CALL EXALL
	GOTO 9000
C
C EXAMINE LINE 	  -  displays length and angle of the selected line
C
2100	CALL EXLINE(.TRUE.)
	GOTO 9000
C
C EXAMINE LENGTH  -  displays length of the found feature 
C
2200	R = EXLENG()
	CALL INFORM(MSG_FEATURELENGTH,R/SCLFRT,R)
	IF (UNIT.EQ.3) CALL INFORM(MSG_UNIFAC,R*UNIT_FACTOR,UNIT_DESC)
	GOTO 9000
C
C EXAMINE AREA 	  -  displays area enclosed by the found feature
C
2300	RR = AREA()
	CALL INFORM(MSG_FEATUREAREA,
     &			REAL(RR)/(SCLFRT**2),REAL(RR))

	GOTO 9000
C
C EXAMINE BOX	  -  displays corners of box surrounding found feature
C
2400	CALL LITES2_BOX(CRNRS)
	CALL INFORM(MSG_BOXROUNDFT)
C
C note: have to build up string by myself, to get numbers columnated
	CALL EXPAND('%WSIFF Units',STRNG)
	CALL EXPAND('%WS%G0.8',STRNG(20:35),CRNRS(1))
	CALL EXPAND('%WS%G0.8',STRNG(36:51),CRNRS(2))
	CALL EXPAND('%WS%G0.8',STRNG(52:67),CRNRS(3))
	CALL EXPAND('%WS%G0.8',STRNG(68:80),CRNRS(4))
	CALL INFORM(MSG_STRING,STRNG)
C
	CALL EXPAND('%WSSheet mm',STRNG)
	CALL EXPAND('%WS%G0.8',STRNG(20:35),CRNRS(1)/SCLFRT)
	CALL EXPAND('%WS%G0.8',STRNG(36:51),CRNRS(2)/SCLFRT)
	CALL EXPAND('%WS%G0.8',STRNG(52:67),CRNRS(3)/SCLFRT)
	CALL EXPAND('%WS%G0.8',STRNG(68:80),CRNRS(4)/SCLFRT)
	CALL INFORM(MSG_STRING,STRNG)
	GOTO 9000
C
C EXAMINE ANGLE	  -  displays angle of feature (or vector of feature)
C
2500	IF (FOPAK(PAKGT).GE.CLOTYP.AND.FOPAK(PAKGT).LE.FULTYP) THEN
	   CALL MOAN(MSG_NOANGLEARC)
	ELSE
	   R = ANGLE()
	   IF (FOPAK(PAKGT).GE.UNOTYP.AND.FOPAK(PAKGT).LE.TEXTYP) THEN
	      CALL INFORM(MSG_TXTSYMANGLE,R)
	   ELSE
	      CALL INFORM(MSG_LINEANGLE,R)
	   ENDIF
	ENDIF
	GOTO 9000
C
C EXAMINE POSITION -  displays X and Y of vertex
C
2600	IF (.NOT.(FONLIN.AND.FONVRT)) THEN
	   CALL MOAN(MSG_NOTONVRT)
	   CALL LEARN(MSG_USEVERT)
	   CALL IGNCMD
	ELSE
	   CALL INFORM(MSG_VERTPOS,FOCXY(1),FOCXY(2),
     &				FOCXY(1)/SCLFRT,FOCXY(2)/SCLFRT)

	ENDIF
	GOTO 9000
C
C EXAMINE ATTRIBUTE -  displays attribute of feature
C
2700	IF (.NOT.(FONLIN.AND.FONVRT)) THEN
	   CALL MOAN(MSG_NOTONVRT)
	   CALL LEARN(MSG_USEVERT)
	   CALL IGNCMD
	ELSE
	   CALL EXATTR
	ENDIF
	GOTO 9000
C
C EXAMINE DISTANCE -  displays distance along feature
C
2800	R = EXDIST()
	IF (MOANED) THEN
	   CALL IGNCMD
	   GOTO 9000
	ENDIF
	CALL INFORM(MSG_DIST,R/SCLFRT,R)
	IF (UNIT.EQ.3) CALL INFORM(MSG_UNIFAC,R*UNIT_FACTOR,UNIT_DESC)
	GOTO 9000
C
C EXAMINE BEARING	  -  displays bearing of feature (or vector of feature)
C
2900	IF (FOPAK(PAKGT).GE.CLOTYP.AND.FOPAK(PAKGT).LE.FULTYP) THEN
	   CALL MOAN(MSG_NOANGLEARC)
	ELSE
	   R = ANGLE_BEARING(ANGLE())
	   IF (FOPAK(PAKGT).GE.UNOTYP.AND.FOPAK(PAKGT).LE.TEXTYP) THEN
	      CALL INFORM(MSG_TXTSYMBEARING,R)
	   ELSE
	      CALL INFORM(MSG_LINEBEARING,R)
	   ENDIF
	ENDIF
	GOTO 9000
C
9000	RETURN
	END
