	SUBROUTINE PRTVAR(CMDTAB,SYS)
C
**	IDENT	19DE88
**	MODULE	PRTVAR
C
C Copyright LaserScan Laboratories Ltd., Cambridge, England
C
C Author Clarke Brunt from PGH's CMDPRT in CMDLIB
C Created				CCB	21-Jan-1986
C
C To print out a CMDLIB command table optioanlly putting $ on
C the start of names (for variables, and system variables)
C
	IMPLICIT NONE
C
	INTEGER*4	CMDTAB(*)	! the command table
	LOGICAL		SYS		! add $
C
***	PARAMETER/LSL$CMNLSL:TBLFLG/
C
C workspace
	INTEGER*4	I		! loop variable
	INTEGER*4	LOOPOF		! loop offset
C					! =0,1 => user flags in table
	INTEGER*4	NCMD		! number of commands
	INTEGER*4	TABIND		! index into table
	INTEGER*4	TABHDR		! to hold table header
	INTEGER*2	TABHD2(2)	! the same
	EQUIVALENCE	(TABHDR, TABHD2(1))
	INTEGER		L		! length of line
	INTEGER		N		! number of spaces
	INTEGER		CLEN		! length of command
C
C ensure the table is in order
	CALL LSL_SORTAB(CMDTAB)
C
C have we user flags to look at ?
	TABHDR = CMDTAB(1)
	LOOPOF = 0
C
C are there user flags?
	IF ((TABHD2(2).AND.CMT$M_NOUFLG) .EQ. 0) LOOPOF = 1
C
C or secondary commands?
	IF ((TABHD2(2).AND.CMT$M_MNEMS) .NE. 0) LOOPOF = LOOPOF+1
C
C how many to print ?
	NCMD = (CMDTAB(1).AND.'7FFF'X)
	IF (NCMD.LE.0) GOTO 999
C
C and get them
C
	L = 0
	N = 0
	DO 10 I=1,NCMD
	   TABIND=I*2+LOOPOF+1
	   CLEN = CMDTAB(TABIND).AND.'7FFF'X
	   IF (80-L-N.LT.CLEN) THEN
	      CALL TTWLIN
	      L = 0
	      N = 0
	   ENDIF
	   IF (L.EQ.0) THEN
	      CALL EXPAND
	   ELSE
	      CALL APPEND('%[ %]',N,)
	   ENDIF
	   IF (SYS) THEN
	      CALL APPEND('$')
	      L = L+1
	   ENDIF
	   CALL APPEND('%S',CMDTAB(TABIND))
	   L = L+CLEN+N
	   N = 16-CLEN
	   IF (SYS) N = N-1
	   IF (N.LE.0) N = N+16
10	CONTINUE
	IF (L.GT.0) CALL TTWLIN
C
999	RETURN
	END
