	LOGICAL	FUNCTION INIVAR
C
***	MODULE	INIVAR
***	IDENT	09MY96
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England
C Author Ron Russell					28-Jan-1992
C
C To initialise VARIABLE operations			
C Returns .FALSE. if initialised, .TRUE. if there was an error
C
	IMPLICIT NONE
C
***	PARAMETER/LSL$CMNLSL:TBLFLG/
C
***	COMMON/CMN:VARIABLE/
C
C functions called
C
	LOGICAL		GET_MEM
	INTEGER		TRNALL		! LSLLIB, translate logical name
	LOGICAL		VIOCLR
	LOGICAL		RDLONG
	LOGICAL		RDCHS
C
C local variable
	INTEGER*4	VARARR_ADDRESS
	INTEGER		VARSIZ
	INTEGER		LENGTH
	BYTE		CH
C
	INIVAR = .TRUE.			! assume failure
C
C get the maximum number of variables to be allowed
C (use TSTCMD as a temporary string)
	MAXVAR = DEFAULT_MAXVAR
	IF (TRNALL(MAXVAR_LOG_NAME,TSTCMD)) THEN
	   LENGTH = LEN(TSTCMD)
	   CALL SETAUX(%REF(TSTCMD),LENGTH)
	   IF (RDLONG(MAXVAR)) MAXVAR = DEFAULT_MAXVAR
	   IF (.NOT.RDCHS(CH)) MAXVAR = DEFAULT_MAXVAR
	ENDIF
C
C don't allow negative number of variables
	MAXVAR = MAX(0,MAXVAR)
C
C get the space for the command table
	VARSIZ = MAXVAR*2 + 16		! as long words
	LENGTH = VARSIZ * 4		! as bytes
C
	IF (GET_MEM(VAR_ZONE,VARTAB_ADDRESS,LENGTH)) GOTO 999	! failed
C
C for the argument array (MAXVAR words)
	IF (MAXVAR .GT. 0) THEN
	   LENGTH = MAXVAR * 2 
	   IF (GET_MEM(VAR_ZONE,VARARR_ADDRESS,LENGTH)) GOTO 999 ! failed
C
C and for the descriptors for the variables (8 bytes each)
	   LENGTH = MAXVAR * 8
	   IF (GET_MEM(VAR_ZONE,VAR_DESC_ADDRESS,LENGTH)) GOTO 999 ! failed
C
C make sure that descriptors are all clear
	   IF (.NOT.VIOCLR(%VAL(VAR_DESC_ADDRESS),LENGTH)) GOTO 999
	ENDIF
C
C Define the variable command table and save the details
	CALL INITAB(%VAL(VARTAB_ADDRESS),
     &              VARSIZ,0,0,%VAL(VARARR_ADDRESS),MAXVAR)
	CALL SAVTAB(VARSAV)
C
C Set the 'exact match' command table flag
C (We actually set the bit in the 'save' array, since the actual table
C is reset from here when we next SELTAB and ADDNAM)
	VARSAV(16) = VARSAV(16).OR.CMT$M_EXACT	! exact match
C
	INIVAR = .FALSE.				! success
C
999	RETURN
	END
