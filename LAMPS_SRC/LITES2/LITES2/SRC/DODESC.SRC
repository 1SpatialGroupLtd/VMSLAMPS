	SUBROUTINE DODESC
C
***	IDENT   09MY96
***	MODULE  DODESC
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Written					RWR	23-Jan-1987
C
C Description
C
C	LITES2 cartographic editor DESCRIBE command.
C
	IMPLICIT NONE
C
***     PARAMETER/CMN:MACMEN/
C
***     COMMON/CMN:MACRO/
C
C This is a jacket routine that calls the real routine with the address
C of an array, so that the real routine can access it as an array
C
	CALL DESCRIBE(%VAL(MANDSL_ADDR))
C
	RETURN
	END


	SUBROUTINE DESCRIBE(MANDSL)
C
***	IDENT   07FE92
***	MODULE  DESCRIBE
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Written					RWR	23-Jan-1987
C
C Description
C
C	LITES2 cartographic editor DESCRIBE command.
C
	IMPLICIT NONE
C
***     PARAMETER/CMN:CHARS/
***     PARAMETER/CMN:MACMEN/
***     PARAMETER/CMN:MERPAR/
***	PARAMETER/CMN:MOANMESS/
C
***     COMMON/CMN:DMACRD/
***     COMMON/CMN:DOUT/
***     COMMON/CMN:MENUS/
***     COMMON/CMN:MACRO/
C
C argument
	INTEGER*4	MANDSL(2,*)
C
C functions called
	INTEGER	STR$CASE_BLIND_COMPARE
	INTEGER	GETMAC
	INTEGER EXPAND
	LOGICAL	ALPHA
C
C workspace
	BYTE	CH
	INTEGER	I
	INTEGER	IER
	INTEGER PTR
	INTEGER TMPLEN
	CHARACTER*(FSM)  TMPSTR		! temporary string
C
	EXTERNAL	MOAN,NASTY
C
C Despatch according to secondary command
	GOTO ( 100,200), DSECN
C
C Secondary command number out of range
	CALL MSGCMD(NASTY,MSG_ERRSECIND)
	GOTO 9990
C
C Secondary command MACRO
C
100	CONTINUE
C
C try for macro name
	IER = GETMAC(DSTR(1:DSTRL),.FALSE.)
C
C macro name found, and nothing else on line - save a space
C
	IF (IER.EQ.0) THEN
	   CALL STR$COPY_DX(MANDSL(1,DMACN),' ')
C
C sort out end of line, and save that
C
	ELSEIF (IER.EQ.MEREXT) THEN
	   IF (DRARG.EQ.0) THEN		! dealing with simple macro
	      PTR = DTMACL+1
	   ELSE
	      TMPLEN = EXPAND('%WS%^W%N',TMPSTR,DRARG)
	      PTR = INDEX(DSTR(1:DSTRL),TMPSTR(1:TMPLEN)) + TMPLEN
	   ENDIF
c
	   DO 110, I = PTR,DSTRL
	      IF (DSTR(I:I).EQ.TABC.OR.DSTR(I:I).EQ.BLANK) THEN
	         PTR = PTR + 1
	      ELSE
	         GOTO 120
	      ENDIF
110	   CONTINUE
C
120	   IF (PTR.GT.DSTRL) THEN
	      CALL STR$COPY_DX(MANDSL(1,DMACN),' ')
	   ELSE
	      CALL STR$COPY_DX(MANDSL(1,DMACN),DSTR(PTR:DSTRL))
	   ENDIF	
	ELSE 					! error in finding macro
	   CALL ERRMAC(IER,'macro')
	   GOTO 9990
	ENDIF
C
	GOTO 9999
C
C************************************************************************
C
C Secondary command SCREENMENU
C
200	CONTINUE
C
C Scan for end of potential puck name in DSTR
	PTR = 0
210	PTR = PTR+1
	IF (PTR.LE.DSTRL) THEN
	   CH = ICHAR(DSTR(PTR:PTR))
	   IF (ALPHA(CH).OR.CH.EQ.UND) GOTO 210
	ENDIF
C
C try for puck name
	IER = GETMAC(DSTR(1:MAX(1,PTR-1)),.FALSE.)
C
	IF (IER.EQ.0) THEN		! found ordinary macro
	   CALL MOAN(MSG_NOSUCHMENU,DMAC(1:DMACL))
	   GOTO 9990
	ELSEIF (IER.NE.MEREOL) THEN	! other error
	   CALL ERRMAC(IER,'puck')
	   GOTO 9990
	ENDIF
C
C Got menu/puck, so check it is a puck on device 1
	DO 220 I=1,PUKTOP
	   IF (STR$CASE_BLIND_COMPARE(
     &		DMAC(1:DMACL),PUKNAM(I)(1:PUKLEN(I))).EQ.0) THEN
	      IF (PUKDEV(I).NE.1) THEN
	         CALL MOAN(MSG_NOTSCREENMENU,DMAC(1:DMACL))
	         GOTO 9990
	      ENDIF
	      SCRMEN_PUKNO = I
	      GOTO 230
	   ENDIF
220	CONTINUE
C	
C must be a menu
	CALL MOAN(MSG_NOTPUCK,DMAC(1:DMACL))
	GOTO 9990
C
C check for correct number of menu squares
230	CONTINUE
	IF ((DINTS(1)*DINTS(2)).NE.PUKBUT(SCRMEN_PUKNO)) THEN
	   CALL MOAN(MSG_WRNGTOTBOXES,
     &	               PUKNAM(SCRMEN_PUKNO)(1:PUKLEN(SCRMEN_PUKNO)),
     &	               PUKBUT(SCRMEN_PUKNO),DINTS(1)*DINTS(2))
	   GOTO 9990
C
C check for valid locating code
	ELSEIF (DINTS(3).LT.0.OR.DINTS(3).GT.8) THEN
	   CALL MOAN(MSG_ILLEGLLOC)
	   GOTO 9990
C
C check for menu size > 0
	ELSEIF (DREALS(1).LE.0.0.OR.DREALS(2).LE.0.0) THEN
	   CALL MOAN(MSG_NEGARGS)
	   GOTO 9990
C
C check for valid location on screen
	ELSEIF (DREALS(3).LT.0.0.OR.DREALS(4).LT.0.0) THEN
	   CALL MOAN(MSG_NEGARGS)
	   GOTO 9990
	ELSEIF (DREALS(3).GT.1.0.OR.DREALS(4).GT.1.0) THEN
	   CALL MOAN(MSG_BADFRAC)
	   GOTO 9990
	ELSE
C
C skip spaces and tabs
C	   
	   PTR = DTMACL+1
	   DO 240, I = DTMACL+1,DSTRL
	      IF (DSTR(I:I).EQ.TABC.OR.DSTR(I:I).EQ.BLANK) THEN
	         PTR = PTR + 1
	      ELSE
	         GOTO 250
	      ENDIF
240	   CONTINUE
C
C store data about screen menu
C
250	   CONTINUE
	   IF (PTR.GT.DSTRL) THEN
	      SCRMEN_LTIT = 1
	      SCRMEN_TITLE = ' '
	   ELSE
	      SCRMEN_LTIT = MIN(DSTRL - PTR + 1,MSCR_TIT)
	      SCRMEN_TITLE = DSTR(PTR:DSTRL)
	   ENDIF
	   SCRMEN_INX = DINTS(1)
	   SCRMEN_INY = DINTS(2)
	   SCRMEN_LOC = DINTS(3)
	   SCRMEN_SIZX = DREALS(1)
	   SCRMEN_SIZY = DREALS(2)
	   SCRMEN_ORGX = DREALS(3)
	   SCRMEN_ORGY = DREALS(4)
	   SCRMEN_BASE = MENINF(PTBASE,DTABN-MAXUSR)
	   SCRMEN_DEF  = .TRUE.		! screen menu defined
	ENDIF
	GOTO 9999
C
C Error exit
9990	CALL IGNCMD
C
9999	RETURN
	END
