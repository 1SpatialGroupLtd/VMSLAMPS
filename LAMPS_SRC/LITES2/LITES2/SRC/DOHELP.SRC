	SUBROUTINE DOHELP
C
***	IDENT	09MY96
***	MODULE	DOHELP
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Written					PGH     05-Oct-1984
C
C	LITES2 cartographic editor HELP command.
C
	IMPLICIT NONE
C
	INCLUDE	'($HLPDEF)'
***     PARAMETER/CMN:MOANMESS/         ! moan constants
***     COMMON/CMN:DOUT/                ! cmd dec. output
C
C Functions called
C
	INTEGER*4	LBR$OUTPUT_HELP
	EXTERNAL	TTWSTR
	EXTERNAL	HLPIN		! see below
	LOGICAL		BOTSTK,RSPINP
C
C Local Variables
C
	INTEGER*4	IERR
	INTEGER		FLAGS
C
	CHARACTER*19	HLPFIL
	PARAMETER	(HLPFIL='LSL$HELP:LITES2.HLB')
C
C prevent bad subscript if null string
	IF (DSTRL.LE.0) THEN
	   DSTRL=1
	   DSTR(1:1)=' '
	ENDIF
C
C help prompting only if input is interactive
	FLAGS = 0
	IF (BOTSTK().OR.RSPINP()) FLAGS = HLP$M_PROMPT
C
	IERR=LBR$OUTPUT_HELP(TTWSTR,		! output_routine
     &                       ,			! output_width
     &                       DSTR(1:DSTRL),	! line_descr
     &                       HLPFIL,		! library_name
     &                       FLAGS,		! flags
     &                       HLPIN)		! input_routine
	IF (.NOT.IERR) THEN
	   CALL NASTY(MSG_HLPERR,HLPFIL)
	   CALL LITES2_SIGNAL(IERR)
	ENDIF
C
        RETURN
        END
C
C
	INTEGER FUNCTION HLPIN(RESULT,PROMPT,RESLEN)
C
***	IDENT	05OC89
***	MODULE	HLPIN
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Written					CCB	16-Mar-1991
C
C	LITES2 cartographic editor HELP input routine
C
	IMPLICIT NONE
C
	INCLUDE	'($RMSDEF)'
	INCLUDE	'($SSDEF)'
***	COMMON/CMN:PROMPT/
***	COMMON/CMN:WORKS/
***	COMMON/LSL$CMNLSL:TXTC/
C
C arguments	(same as for LIB$GET_INPUT)
	CHARACTER*(*)	RESULT		! resultant string
	CHARACTER*(*)	PROMPT		! prompt string
	INTEGER		RESLEN		! resultant string length
C
C Functions called
	INTEGER		CPYSTR
	LOGICAL		HADBREAK
C
C Local Variables
	LOGICAL*1	CTRLZ
C
C Copy the prompt into INTERACT's prompt string
	PRILEN = CPYSTR(PROMPT,PINSTR)
C
C get a line from interactive devices
	PROINQ = .TRUE.			! want INQUIRE prompt
	TTPROM = .TRUE.			! need new prompt
	CALL INTERACT(CTRLZ)		! get line
	PROINQ = .FALSE.		! cancel prompt
	TTPROM = .TRUE.			! need new prompt
C
C copy the line into the resultant string
	RESLEN = CPYSTR(TXTDSC,RESULT)
C
C and return appropriate code
	IF ((CTRLZ.AND.TXTPTR.EQ.0).OR.HADBREAK()) THEN
	   HLPIN = RMS$_EOF
	ELSE
	   HLPIN = SS$_NORMAL
	ENDIF
C
        RETURN
        END
