	SUBROUTINE BOX_AUX(X,Y,Z,AUX,EXP,XOUT,YOUT,ZOUT)
C
***	IDENT   18SE92
***	MODULE  BOX_AUX
C
C	Copyright Laser-Scan Ltd., Cambridge, England.
C
C	Created					RWR	 11-Sep-1989
C
C to calculate a box, given locating points, expansion factor and 
C auxiliary array
C
	IMPLICIT NONE
C
***	PARAMETER/CMN:AUXDEF/
C
C Arguments
	REAL		X,Y		! locating point
	REAL		Z
	REAL		AUX(AUXLEN)	! auxiliary array
	REAL		EXP		! expansion factor
	REAL		XOUT(*),YOUT(*)	! output coordinates
	REAL		ZOUT(*)
C
C Local Variables
	REAL		COSANG		! cosine of rotation angle
	REAL		SINANG		! sine of rotation angle
	REAL		LENGTH,HEIGHT	! size of box around symbol
	REAL		MINX,MINY
	INTEGER		I
C
	COSANG = AUX(COSI)
	SINANG = AUX(SINI)
	LENGTH = AUX(MAXXI) - AUX(MINXI)
	HEIGHT = AUX(MAXYI) - AUX(MINYI)
	MINX   = AUX(MINXI)
	MINY   = AUX(MINYI)
C
C add expansion factor to box
C
	MINX = MINX - (EXP*HEIGHT)
	MINY = MINY - (EXP*HEIGHT)
	LENGTH = LENGTH + 2.0*(EXP*HEIGHT)
	HEIGHT = HEIGHT + 2.0*(EXP*HEIGHT)
C
C calculate the 4 corners of the box
C
	XOUT(1)=X + MINX*COSANG - MINY*SINANG
	YOUT(1)=Y + MINX*SINANG + MINY*COSANG
	XOUT(2)=XOUT(1) + COSANG*LENGTH
	YOUT(2)=YOUT(1) + SINANG*LENGTH
	XOUT(3)=XOUT(2) - SINANG*HEIGHT
	YOUT(3)=YOUT(2) + COSANG*HEIGHT
	XOUT(4)=XOUT(1) - SINANG*HEIGHT
	YOUT(4)=YOUT(1) + COSANG*HEIGHT
	XOUT(5)=XOUT(1)
	YOUT(5)=YOUT(1)
C
	DO 10, I= 1,5
	   ZOUT(I) = Z
10	CONTINUE
C
	RETURN
	END
