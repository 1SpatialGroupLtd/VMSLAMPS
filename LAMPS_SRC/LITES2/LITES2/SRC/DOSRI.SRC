        SUBROUTINE DOSRI
C
***     IDENT   18SE92
***     MODULE  DOSRI
C
C       Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C       Written                                 RJH	25-SEP-1984
C
C	LITES2 cartographic editor SRI command.
C
        IMPLICIT NONE
C
***	PARAMETER/CMN:ACS/
***     PARAMETER/CMN:MOANMESS/         ! moan constants
***	PARAMETER/CMN:STAPAR/
C
***     COMMON/CMN:DOUT/                ! cmd dec. output
***	COMMON/CMN:FILES/		! filenames
***	COMMON/LSL$CMNFRT:FRTACD/
***	COMMON/CMN:LABELS/
***	COMMON/CMN:MAPS/
***	COMMON/CMN:STATE/
***	COMMON/LSL$CMNLSL:TXTC/
C
C Functions called
	LOGICAL		PARFILN		! parses a filename
	LOGICAL		SRIINI
	INTEGER		LIB$FIND_FILE
C
C Local Variables
	INTEGER*4	RETCOD		! file parse return code
	INTEGER*4	NAMLEN		! length of filename
	INTEGER		I
C
	LOGICAL		VERNUM		! allow version numbers
	PARAMETER	(VERNUM=.TRUE.)
C
C Parse SRI filename
	RETCOD = PARFILN(
     &	  SRITIT,NAMLEN,DSTR(1:DSTRL),SRITEM,VERNUM)
	IF (.NOT.RETCOD) THEN
	   CALL MOAN(MSG_ERRFILENAME,'SRI')
	   CALL LITES2_SIGNAL(RETCOD)
	   GOTO 998
	ENDIF
C
C if in READY state, then read the file, otherwise flag to read later
	IF (SINDEX.NE.STATE_INITIAL) THEN
	   HADSRI = .FALSE.
	   NEWSRI = .FALSE.
C
C check file exists
	   I = 0
	   TXTPTR = 255
	   RETCOD = LIB$FIND_FILE(SRITIT(:NAMLEN),TXTDSC,I)
	   CALL LIB$FIND_FILE_END(I)
	   IF (.NOT.RETCOD) THEN
	      CALL MOAN(MSG_ERRINFILE,'SRI')
	      CALL LITES2_SIGNAL(RETCOD)
	      GOTO 998
	   ENDIF
C
C Close any open IFF files
	   DO 10 I=0,MAPTOP
	      IF (MAP_EXIST(I).AND.MAPLUN(I).NE.0) CALL CLOSIF(I)
10	   CONTINUE
C
	   CALL INFORM(MSG_RDNGFILE,'SRI',SRITIT(:NAMLEN))
	   IF (SRIINI(SRITIT(:NAMLEN))) THEN
	      CALL MOAN(MSG_ERRINFILE,'SRI')
	      GOTO 998
	   ENDIF
	   HADSRI = .TRUE.
C
C can now calculate bounding box of ANNOTATION mark, if required
	   IF (HAD_ANNOMARK)
     &		CALL SRISCN(ANNOMARK,ANNOMARK_MINX,ANNOMARK_MAXX,
     &			    ANNOMARK_MINY,ANNOMARK_MAXY)
	ELSE
	   HADSRI = .TRUE.			! had explicit SRI filename
	   NEWSRI = .TRUE.			! and need new file
	ENDIF
	GOTO 999
C
C error exit
998	CALL IGNCMD
C
999	RETURN
	END
