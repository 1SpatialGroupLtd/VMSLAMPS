C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1990-03-02 12:10:04.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
        SUBROUTINE VIEW_LIGHT
C
***     IDENT   02MR90
***     MODULE  VIEW_LIGHT
C
C       Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C       Written                                 RWR	Apr-1989
C
C	LITES2 cartographic editor VIEW LIGHT command.
C
        IMPLICIT NONE
C
***	PARAMETER/CMN:CONSTS/
***	PARAMETER/CMN:MOANMESS/
C
***	COMMON/CMN:DOUT/
***	COMMON/CMN:GLOBAL/
***	COMMON/CMN:VIEW/
C
C Functions
	EXTERNAL	NASTY,MOAN
	EXTERNAL	VIEW_LIGHT_CMD_TABLE
C
C w/s
	REAL		DX,DY,DZ
	REAL		ANGLE,DIST
	INTEGER		VC
	INTEGER		VLC
	EQUIVALENCE	(VC,VIEW_CURRENT)
	EQUIVALENCE	(VLC,VIEW_LIGHT_CURRENT)
C
C get tertiary command
	CALL GET_COMMAND(DSTR(:DSTRL),VIEW_LIGHT_CMD_TABLE)
	IF (MOANED) GOTO 9990
C
C Dispatch according to tertiary command
	GOTO (100,200,300,400,500,600,700,800,900), DPRIN
C
C Tertiary command number out of range
	CALL MSGCMD(NASTY,MSG_ERRTERIND)
	GOTO 9990
C
C VIEW LIGHT NUMBER -  succeeding VIEW LIGHT commands refer to light n
100 	CONTINUE
	IF (DINTS(1).LE.0.OR.DINTS(1).GT.MAXLIGHT) THEN
	   CALL MOAN(MSG_LIGHTRNG,1,MAXLIGHT)
	   GOTO 9990
	ELSE
	   VIEW_LIGHT_CURRENT = DINTS(1)
	   VIEW_LIGHT_IN_USE(VLC,VC) = .TRUE.
	ENDIF
	GOTO 9999
C
C VIEW LIGHT BEARING - bearing of target from light source
200 	CONTINUE
	VIEW_LIGHT_BEARING(VLC,VC) = DREALS(1)
	VIEW_LIGHT_HAD_BEARING(VLC,VC) = .TRUE.	   
	GOTO 450
C
C VIEW LIGHT DISTANCE - distance of light target from source
300 	CONTINUE
	IF (DREALS(1).LE.0.0) THEN
	   CALL MSGCMD(MOAN,MSG_POSARGSRQD)
	   GOTO 9990
	ELSE
	   VIEW_LIGHT_DISTANCE(VLC,VC) = DREALS(1)
	ENDIF
	VIEW_LIGHT_HAD_DISTANCE(VLC,VC) = .TRUE. 
	GOTO 450
C
C VIEW LIGHT ELEVATION - elevation of light beam
400 	CONTINUE
	VIEW_LIGHT_ELEVATION(VLC,VC) = DREALS(1)
	VIEW_LIGHT_HAD_ELEVATION(VLC,VC) = .TRUE.	   
C
C check if new value of target is required, or if From can be calculated
450	CONTINUE
	IF (.NOT.VIEW_LIGHT_FROM_SETUP(VLC,VC).AND.
     &	    .NOT.VIEW_LIGHT_TO_SETUP(VLC,VC)) GOTO 9999
	IF (VIEW_LIGHT_HAD_BEARING(VLC,VC).AND.
     &	    VIEW_LIGHT_HAD_DISTANCE(VLC,VC).AND.
     &	    VIEW_LIGHT_HAD_ELEVATION(VLC,VC)) THEN
	   ANGLE = VIEW_LIGHT_BEARING(VLC,VC)*PI/180.0
	   DX = VIEW_LIGHT_DISTANCE(VLC,VC)*SIN(ANGLE)
	   DY = VIEW_LIGHT_DISTANCE(VLC,VC)*COS(ANGLE)
	   ANGLE = VIEW_LIGHT_ELEVATION(VLC,VC)*PI/180.0
	   DZ = VIEW_LIGHT_DISTANCE(VLC,VC)*TAN(ANGLE)
	   IF (VIEW_LIGHT_FROM_SETUP(VLC,VC)) THEN
	      VIEW_LIGHT_TO(1,VLC,VC) = VIEW_LIGHT_FROM(1,VLC,VC)+DX
	      VIEW_LIGHT_TO(2,VLC,VC) = VIEW_LIGHT_FROM(2,VLC,VC)+DY
	      VIEW_LIGHT_TO(3,VLC,VC) = VIEW_LIGHT_FROM(3,VLC,VC)+DZ
	      VIEW_LIGHT_TO_SETUP(VLC,VC) = .TRUE.
	   ELSEIF (VIEW_LIGHT_TO_SETUP(VLC,VC)) THEN
	      VIEW_LIGHT_FROM(1,VLC,VC) = VIEW_LIGHT_TO(1,VLC,VC)-DX
	      VIEW_LIGHT_FROM(2,VLC,VC) = VIEW_LIGHT_TO(2,VLC,VC)-DY
	      VIEW_LIGHT_FROM(3,VLC,VC) = VIEW_LIGHT_TO(3,VLC,VC)-DZ
	      VIEW_LIGHT_FROM_SETUP(VLC,VC) = .TRUE.
	   ENDIF
	ENDIF
	GOTO 9999
C
C VIEW LIGHT DELETE - remove curently selected light source
500 	CONTINUE
	VIEW_LIGHT_IN_USE(VLC,VC) = .FALSE.
	GOTO 9999
C
C VIEW LIGHT FROM - light source position
600 	CONTINUE
	IF (VIEW_LIGHT_TO_SETUP(VLC,VC).AND.
     &	    DREALS(1).EQ.VIEW_LIGHT_TO(1,VLC,VC).AND.
     &	    DREALS(2).EQ.VIEW_LIGHT_TO(2,VLC,VC)) THEN
	   CALL MOAN(MSG_DUPPOSITION,'target light')
	   GOTO 9990
	ENDIF
	CALL CPYPAK(3,DREALS,VIEW_LIGHT_FROM(1,VLC,VC))
	VIEW_LIGHT_FROM_SETUP(VLC,VC) = .TRUE.
C
C may now be able to calculate target point, or distance bearing 
C and elevation
	IF (.NOT.VIEW_LIGHT_TO_SETUP(VLC,VC)) THEN
	   GOTO 450
	ELSE
	   GOTO 850
	ENDIF
C
C VIEW LIGHT TO - light target psoition
700 	CONTINUE
	IF (VIEW_LIGHT_FROM_SETUP(VLC,VC).AND.
     &	    DREALS(1).EQ.VIEW_LIGHT_FROM(1,VLC,VC).AND.
     &	    DREALS(2).EQ.VIEW_LIGHT_FROM(2,VLC,VC)) THEN
	   CALL MOAN(MSG_DUPPOSITION,'source light')
	   GOTO 9990
	ENDIF
	CALL CPYPAK(3,DREALS,VIEW_LIGHT_TO(1,VLC,VC))
	VIEW_LIGHT_TO_SETUP(VLC,VC) = .TRUE.
	IF (.NOT.VIEW_LIGHT_FROM_SETUP(VLC,VC)) GOTO 450
C
C calculate new vectors?
850	CONTINUE
	DX = VIEW_LIGHT_TO(1,VLC,VC) - VIEW_LIGHT_FROM(1,VLC,VC)
	DY = VIEW_LIGHT_TO(2,VLC,VC) - VIEW_LIGHT_FROM(2,VLC,VC)
	DZ = VIEW_LIGHT_TO(3,VLC,VC) - VIEW_LIGHT_FROM(3,VLC,VC)
C
C bearing
	ANGLE = ATAN2(DX,DY)	       ! bearing (angle clockwise from N)
	IF (ANGLE.LT.0.0) ANGLE = ANGLE + TWOPI
	VIEW_LIGHT_BEARING(VLC,VC) = ANGLE*180.0/PI
C
C distance
	DIST = SQRT(DX*DX+DY*DY)
	VIEW_LIGHT_DISTANCE(VLC,VC) = DIST	
C
C elevation
	VIEW_LIGHT_ELEVATION(VLC,VC) = ATAN2(DZ,DIST)*180.0/PI
C
	VIEW_LIGHT_HAD_BEARING(VLC,VC) = .TRUE.
	VIEW_LIGHT_HAD_DISTANCE(VLC,VC) = .TRUE.
	VIEW_LIGHT_HAD_ELEVATION(VLC,VC) = .TRUE.
	GOTO 9999
C
C VIEW LIGHT CONE - cone of illumination
800 	CONTINUE
	IF (DREALS(1).LT.0.0.OR.DREALS(1).GE.180.0) THEN
	   CALL MOAN(MSG_ANGNOTINRNGE,0.0,180.0,'degrees')
	   GOTO 9990
	ELSE
	   VIEW_LIGHT_CONE(VLC,VC) = DREALS(1)
	ENDIF
	GOTO 9999
C
C VIEW LIGHT COLOUR - colour of illumination
900	CONTINUE
	IF (DREALS(1).LT.0.0.OR.DREALS(1).GT.1.0.OR.
     &	    DREALS(2).LT.0.0.OR.DREALS(2).GT.1.0.OR.
     &	    DREALS(3).LT.0.0.OR.DREALS(3).GT.1.0) THEN
	   CALL MOAN(MSG_BADCOLVAL)
	   GOTO 9990
	ENDIF
	VIEW_LIGHT_COLOUR(1,VLC,VC) = DREALS(1)
	VIEW_LIGHT_COLOUR(2,VLC,VC) = DREALS(2)
	VIEW_LIGHT_COLOUR(3,VLC,VC) = DREALS(3)
	GOTO 9999
C
9990	CALL IGNCMD
9999	RETURN
        END
