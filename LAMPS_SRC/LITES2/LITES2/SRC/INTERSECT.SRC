	LOGICAL*4 FUNCTION INTERSECT(COEFFS1,COEFFS2,XY)
C
***	MODULE	INTERSECT
***	IDENT	17MR88
C
C Copyright Laser-Scan Laboratories Limited, Cambridge, England.
C
C Author: R. Russell
C
C	Created 				 RWR	17-Mar-1988
C
C This routine computes the intersection between two lines that
C are in the form
C
C                     ax + by + c = 0
C
C INTERSECT returns .TRUE. if the lines are parallel
C
	IMPLICIT NONE
C
	REAL*8	  COEFFS1(3)		! coeffs, a, b and c, 1st line
	REAL*8	  COEFFS2(3)		! coeffs, a, b and c, 2nd line
	REAL	  XY(2)			! intersection point
C
	REAL*8	  CRIT			! criterion for parallel lines
	REAL*8	  DET
	REAL*8	  DINV
C
C ......................................................................
C
	CRIT = 1.0D-20			! nice and arbitrary
C
	DET = COEFFS1(1)*COEFFS2(2) - COEFFS2(1)*COEFFS1(2)
	INTERSECT = ABS(DET).LT.CRIT
C
	IF (.NOT.INTERSECT) THEN
	   DINV = 1.0/DET
	   XY(1) = (COEFFS1(2)*COEFFS2(3) - COEFFS2(2)*COEFFS1(3))*DINV
	   XY(2) = (COEFFS2(1)*COEFFS1(3) - COEFFS1(1)*COEFFS2(3))*DINV
	ENDIF
C
	RETURN
	END


