	SUBROUTINE FINCMD(COMARG)
C
***	IDENT	10NO87
***	MODULE	FINCMD
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RJH	31-JUL-1984
C
C Description
C
C    This subroutine is part of the LITES2 Command Interface, and its
C purpose is to read commands and arguments which can be handled by
C CMDLIB.
C    The argument returned by the function is an integer with bits set 
C according to the additional arguments required for the current 
C command. If no errors arise, the relevant command
C decoding output variables are given the appropriate values.
C
	IMPLICIT NONE
C
***     PARAMETER/LSL$CMNLSL:LSLLIBMSG/
***     PARAMETER/CMN:MOANMESS/		! moan constants
C
***     COMMON/LSL$CMNLSL:EXCEPTION/	! ERROR handling
***     COMMON/LSL$CMNLSL:CMDCOM/	! CMDLIB command handling
***     COMMON/CMN:DOUT/		! command decoding output
***     COMMON/CMN:GLOBAL/		! global variables
C
C Arguments
	INTEGER*4	COMARG		! further argument flags
C
C Functions called
	INTEGER*4	CPYSTR		! copies character strings
	INTEGER*4	RDCOMM		! CMDLIB - reads a command
	LOGICAL		RDCHS
C
C Local Variables
	INTEGER*4	COMSTA		! valid states for command
	INTEGER*4	RESULT		! command read result
	INTEGER*4	I		! counter
	BYTE		CH
C
C Primary Command Table
	EXTERNAL	PRIME_CMD_TABLE
C
	COMARG = 0			! assume no further arguments
	COMSTA = 0			! clear state flags
C
C Check for blank line (only after % hopefully)
C
	IF (RDCHS(CH)) THEN
	   CALL MOAN(MSG_CMDNOTFND)
	   GOTO 999
	ELSE
	   CALL BSCH
	ENDIF
C
C Read next command
C
	RESULT = RDCOMM(PRIME_CMD_TABLE)
C
C Check user flags and state
C
	IF (CMDNUM .NE. 0) THEN
	   CALL PRIFLG(COMARG,COMSTA)		! primary flags
	   IF (.NOT. MOANED) CALL CHKSTA(COMSTA)	! check state
	   IF ((.NOT. MOANED) .AND. (SECMDN .GT. 0))
     &	      CALL SECFLG(COMARG)		! sec. flags
	ENDIF
C
C Check for reading errors
C
	IF ((.NOT. MOANED) .AND. (RESULT .LE. 0)) THEN
	   IF (ERRNUM .NE. LSL__NORMAL) CALL ERRCMD('command')
	ENDIF
C
C Assign output variables
C
	IF (.NOT. MOANED) THEN
	   DPRIN = CMDNUM		! primary command number
	   DSECN = SECMDN		! secondary command number
	   DPRIL = CPYSTR(CMDFST,DPRI)	! copy primary command string
	   IF (DSECN.GT.0) DSECL = CPYSTR(CMSFST,DSEC)	! and secondary
	   IF (CMDICT .GT. 0) THEN
	      DNINT = CMDICT		! no. of integer arguments
	      DO 10 I=1,DNINT
		 DINTS(I) = INTARG(I)	! integer arguments
10	      CONTINUE
	   ENDIF
	   IF (CMDRCT .GT. 0) THEN
	      DNREA = CMDRCT		! no. of reals
	      DO 20 I=1,DNREA
		 DREALS(I) = REALAR(I)	! real arguments
20	      CONTINUE
	   ENDIF
	ENDIF
C
999	RETURN
	END
