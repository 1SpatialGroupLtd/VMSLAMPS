	LOGICAL FUNCTION INTAARC(AUX1,AUX2,LIMITS1,LIMITS2,
     &	                        NUM,POINT1,POINT2,TOUCH)
C
***	MODULE	INTAARC
***	IDENT	19MR87
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author  Ron Russell
C
C To find the intersection point(s) of two arcs, that lie within both
C
C AUX1 and AUX2 are the auxiliary arrays containing info about the arcs.
C LIMITS1 and LIMITS2 are the boxes surrounding the arcs,
C     in order MINX,MAXX,MINY,MAXY
C NUM returns the number of intersection points (if any)
C POINT1 and POINT2 return the intersecion points, if any.
C TOUCH is TRUE if the line segment is a tangent to the arc
C
C The function returns TRUE iff the line does not cut arc
C
	IMPLICIT NONE
C
***	PARAMETER/CMN:AUXDEF/
***	PARAMETER/CMN:CONSTS/
C
C arguments
	REAL	AUX1(AUXLEN),AUX2(AUXLEN)
	REAL	LIMITS1(4),LIMITS2(4)
	INTEGER	NUM
	REAL	POINT1(2),POINT2(2)
	LOGICAL TOUCH
C
C w/s
	INTEGER INARC1(2),INARC2(2)
	INTEGER	NUMBER
	REAL	P1(2),P2(2)
C
	INTAARC = .TRUE.
	NUM     = 0
	TOUCH = .FALSE.
C
C test if box round round arcs intersect
C
	IF (LIMITS1(1).GT.LIMITS2(2).OR.
     &	    LIMITS1(2).LT.LIMITS2(1).OR.
     &	    LIMITS1(3).GT.LIMITS2(4).OR.
     &	    LIMITS1(4).LT.LIMITS2(3))    GOTO 999
C
C calculate intersection
C
	CALL ARCARC(AUX1,AUX2,NUMBER,P1,P2,INARC1,INARC2)
C
C perhaps no intersection
C
	IF (NUMBER.EQ.0) GOTO 999
	TOUCH = NUMBER.EQ.1
C
C First point within both arcs
C
	IF (INARC1(1).GE.-1.AND.INARC1(1).LE.1.AND.
     &	    INARC2(1).GE.-1.AND.INARC2(1).LE.1) THEN
	   NUM = 1
	   POINT1(1) = P1(1)
	   POINT1(2) = P1(2)
	ENDIF
C
C second point within both arc and line?
C
	IF (NUMBER.EQ.2.AND.
     &	    INARC1(2).GE.-1.AND.INARC1(2).LE.1.AND.
     &	    INARC2(2).GE.-1.AND.INARC2(2).LE.1) THEN
	   NUM = NUM + 1
	   IF (NUM.EQ.1) THEN
	      POINT1(1) = P2(1)
	      POINT1(2) = P2(2)
	   ELSE
	      POINT2(1) = P2(1)
	      POINT2(2) = P2(2)
	   ENDIF
	ENDIF
C
C had any intersections?
C
	IF (NUM.EQ.0) TOUCH = .FALSE.	! even if TOUCH was previously
					! true!
	INTAARC = NUM.EQ.0
C
999	RETURN
	END
