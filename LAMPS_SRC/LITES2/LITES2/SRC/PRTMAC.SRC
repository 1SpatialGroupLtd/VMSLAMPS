C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1996-05-09 12:55:02.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION PRTMAC(CMDTAB,MACRO)
C
***	IDENT	09MY96
***	MODULE	PRTMAC
C
C Copyright LaserScan Laboratories Ltd., Cambridge, England
C Author Ron Russell from SHCOMM by PGH		 4-Feb-1985
C
C	Function to print out all the macros or menus and pucks that
C	have been defined depending on value of MACRO. It returns
C	.TRUE. if nothing has been output, and .FALSE. when something
C	has been.
C
	IMPLICIT NONE
C
***	PARAMETER/CMN:MACMEN/
***	PARAMETER/LSL$CMNLSL:TBLFLG/
C
***	COMMON/CMN:MACRO/
C
C arguments
	INTEGER*4	CMDTAB(*)	! the command table
	LOGICAL		MACRO		! .TRUE. if to print out macros,
					! .FALSE. for menus and pucks
C
C
C workspace
	INTEGER*4	I		! loop variable
	INTEGER*4	LOOPOF		! loop offset
C					! =1 => User flags in this table
	INTEGER*4	NCMD		! number of commands
	INTEGER*4	CMDNO		! command number in table
	INTEGER*4	CMDCNT		! count of how many printed
	INTEGER*4	TABIND		! index into table
	INTEGER*4	TABHDR		! to hold table header
	INTEGER*2	TABHD2(2)	! the same
	EQUIVALENCE	(TABHDR, TABHD2(1))
	INTEGER		L		! length of line
	INTEGER		N		! number of spaces
	INTEGER		CLEN		! length of name
C
C functions
	INTEGER*4	LIB$EXTZV	! to get user flags from array
C
C initialise
	CMDCNT = 0			! no commands
C
C ensure the table is in order
	CALL LSL_SORTAB(CMDTAB)
C
C have we user flags to look at ?
	TABHDR = CMDTAB(1)
	LOOPOF = 0
C
C are there user flags?
	IF ((TABHD2(2).AND.CMT$M_NOUFLG) .EQ. 0) LOOPOF = 1
C
C or secondary commands?
	IF ((TABHD2(2).AND.CMT$M_MNEMS) .NE. 0) LOOPOF = LOOPOF+1
C
C how many to print ?
	NCMD = CMDTAB(1).AND.'7FFF'X
C
C and get them
C
	L = 0
	N = 0
	DO 20 I=1,NCMD
	   TABIND=I*2+LOOPOF+1
	   CMDNO = LIB$EXTZV(16,16,CMDTAB(TABIND))
	   CLEN = CMDTAB(TABIND).AND.'7FFF'X
	   IF (MACRO.EQV.(CMDNO.LE.MAXUSR)) THEN
	      IF (80-L-N.LT.CLEN) THEN
	         CALL TTWLIN
	         L = 0
	         N = 0
	      ENDIF
	      IF (L.EQ.0) THEN
	         CALL EXPAND
	      ELSE
	         CALL APPEND('%[ %]',N)
	      ENDIF
	      CALL APPEND('%S',CMDTAB(TABIND))
	      L = L+CLEN+N
	      N = 16-CLEN
	      IF (N.LE.0) N = N+16
	      CMDCNT = CMDCNT + 1
	   ENDIF
20	CONTINUE
	IF (L.GT.0) CALL TTWLIN
C
C all done
C
999	PRTMAC = CMDCNT.EQ.0
C
	RETURN
	END
