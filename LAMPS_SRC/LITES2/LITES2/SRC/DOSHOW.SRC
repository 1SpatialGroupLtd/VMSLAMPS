        SUBROUTINE DOSHOW
C
***     IDENT	09MY96
***     MODULE  DOSHOW
C
C       Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C       Written                                 PGH	15-OCT-1984
C
C Description
C
C	LITES2 cartographic editor SHOW command.
C
C SHOW MAPS	  - lists all maps, and their IFF filenames
C SHOW ATTRIBUTE  - lists the current or requested attribute set
C SHOW LAYERS	  - lists all layers in use
C SHOW SELECTIONS - lists layers and gives details of current selections
C SHOW PATTERNS	  - lists details of patterned lines
C SHOW <FC,IT>	  - lists feature codes available
C SHOW GROUP [grp]- with argument shows feature codes in group
C		  - without argument gives list of groups
C SHOW POSITION	  - gives position of cursor
C SHOW INTERPOLATION - gives drawing & construction interpolation method
C SHOW SCALES	  - gives information concerning scales
C SHOW SCROLL	  - gives scroll area of terminal
C SHOW SECTORS	  - gives number of sectors
C SHOW OPTIONS	  - lists options selected by ENABLE/DISABLE
C SHOW STATE	  - gives current state
C SHOW VERSION	  - gives version, date of linking and licence info
C SHOW MACRO [mac]- with argument shows macro expansion
C		  - without argument gives list of macros defined
C SHOW COMMANDS	  - gives list of available commands in current state
C SHOW MENUS [men]- with argument shows expansion
C		  - without argument gives list of menus defined
C SHOW TOLERANCE  - show tolerances that have been set by the TOLERANCE
C		  - command
C SHOW BASES	  - show bases that have been selected for squaring
C SHOW MATCH      - lists matches that set for joining or tieing
C SHOW REGION [n] - lists coordinates of specified (or all) regions
C SHOW SORT       - gives sort options
C SHOW VARIABLES [var] - with argument gives value of variable
C		       - without lists variable names
C SHOW MEMORY     - gives memory usage statistics
C SHOW LABELS	  - lists what will be used to label features
C SHOW ANNOTATION - shows how lables will be drawn
C SHOW FILL	  - lists details of patterned areas
C SHOW ZOOM	  - gives amount picture on screen in zoomed in
C SHOW WINDOW     - gives the corners of the current window
C SHOW LIMITS     - gives the limits of the working area
C SHOW TRANSFORMATION - gives details of current transformation
C SHOW ACD	  - with argument shows characteritics of ACD
C		  - without argument gives list of attributes
C SHOW AFTER	  - gives commands to execute after certain points
C SHOW OPERATIONS - gives operations set up by OPERATION
C SHOW SETUP      - gives details of map setups
C SHOW OVERLAYS [n] - gives details of specified (or all) overlays
C SHOW PTOLERANCE - shows privileged tolerances (set by PTOLERANCE)
C SHOW PRIVILEGE  - shows things that have been set privileged
C SHOW GEOGRAPHICAL - shows cursor position in LAT/LONG
C SHOW IMAGES [n] - gives details of specified (or all) images
C SHOW ABSOLUTE   - shows cursor position PLUS origin offset
C SHOW CP [n]	  - shows current corner points for specified map
C SHOW VIEW [n]	  - shows details of specified (or all) images
C SHOW DISPLAY [n] - shows details of specified (or all) displays
C SHOW LIGHTS [n[n]] - for specified (or all) views shows details
C                      of specified (or all) lights
C SHOW COLOURS	- output colours for specified display and overlay
C SHOW FC_PRIORITIES  - lists details of drawing priorities (set up in FRT)
C SHOW PLOT	  - gives details of hardcopy plot
C SHOW PROJECTION - gives details of projection of LITES2 coordinate space
C SHOW GEOMETRIES - gives details of specified, or defined geometries
C SHOW WARP       - gives details of warping
C
        IMPLICIT NONE
C
	INCLUDE	'LSL$CMNIFF:CB.STR'
***	PARAMETER/CMN:CONSTS/
***	PARAMETER/CMN:EFNLUN/
***     PARAMETER/CMN:MOANMESS/
***     PARAMETER/CMN:STAPAR/
C
***	COMMON/CMN:AFTER/
***     COMMON/CMN:DOUT/
***	COMMON/CMN:DRAWIN/
***	COMMON/LSL$CMNFRT:FRTPRIO/
***	COMMON/CMN:LICENC/
***	COMMON/CMN:MAPS/
***	COMMON/CMN:MASTER/
***	COMMON/LSL$CMNIFF:MD2DES/
***	COMMON/CMN:OPTION/
***     COMMON/CMN:SETUP/
***     COMMON/CMN:STATE/
***	COMMON/CMN:TRANSFORM/
***     COMMON/CMN:VTCTRL/
C
	EXTERNAL 	MOAN,NASTY
	EXTERNAL 	PRIME_CMD_TABLE
	REAL		SCFRT,SCALE
	CHARACTER*80	STRNG
C
	IF (SINDEX.EQ.STATE_INITIAL) THEN	! SCLDIS not set up yet
	   SCALE=1
	   SCFRT=1
	ELSE
	   SCFRT=SCLFRT
	   SCALE=SCLDIS
	ENDIF
C
C Dispatch according to secondary command
C
	GO TO ( 100, 200, 300, 400, 500, 600, 700, 800, 900,1000,
     &	       1100,1200,1300,1400,1500,1600,1700,1800,1900,2000,
     &	       2100,2200,2300,2400,2500,2600,2700,2800,2900,3000,
     &	       3100,3200,3300,3400,3500,3600,3700,3800,3900,4000,
     &	       4100,4200,4300,4400,4500,4600,4700,4800,4900,5000,
     &	       5100,5200), DSECN
C
C Secondary command number out of range
C
	IF (DSECN.EQ.0) GOTO 800	! SHOW is SHOW POSITION
	CALL MSGCMD(NASTY,MSG_ERRSECIND)
	CALL IGNCMD
	GO TO 9000
C
C SHOW MAPS	  - lists all maps, and their IFF filenames
C
100	CALL SHMAPS
	GOTO 9000
C
C SHOW ATTRIBUTE  - lists the current or requested attribute set
C
200	CALL SHATTR
	GOTO 9000
C
C SHOW LAYERS	  - lists all layers in use, with max. FSN's
C
300	CALL SHLAYE
	GOTO 9000
C
C SHOW SELECTIONS - lists layers and gives details of current selections
C
400	CALL SHSELE
	GOTO 9000
C
C SHOW PATTERNS	  - lists details of patterned lines
C
500	CALL SHPAT
	GOTO 9000
C
C SHOW <FC,IT>	  - lists feature codes available
C
600	CALL SHFC
	GOTO 9000
C
C SHOW GROUP grp  - lists feature codes in groups specified
C
700	CALL SHGRP
	GOTO 9000
C
C SHOW POSITION	  - gives position of cursor
C
800	IF (SINDEX.NE.STATE_INITIAL) THEN
	   IF (CSZI.EQ.IFF_ABSENT) THEN
	      IF (.NOT.ZOPT) THEN
	         CALL INFORM(MSG_CURSPOS,CSXY(1),CSXY(2),
     &				CSXY(1)/SCLFRT,CSXY(2)/SCLFRT)
	      ELSE
	         CALL INFORM(MSG_CURS3DPOSNOZ,CSXY(1),CSXY(2),
     &				CSXY(1)/SCLFRT,CSXY(2)/SCLFRT)
	      ENDIF
	   ELSE
	      CALL INFORM(MSG_CURS3DPOS,CSXY(1),CSXY(2),CSXY(3),
     &				CSXY(1)/SCLFRT,CSXY(2)/SCLFRT)
	   ENDIF
	ELSE
	   IF (DSECN .EQ. 0) THEN		! no secondary command
	      DSECN = 1				! so lie
	      DSEC  = 'POSITION'
	      DSECL = 8
	   ENDIF
	   CALL MSGCMD(MOAN,MSG_CMDNOTVALID)
	ENDIF
	GOTO 9000
C
C SHOW INTERPOLATION - gives drawing & construction interpolation method
C
900	IF (MCCON) THEN
	   CALL INFORM(MSG_INTERPL,'McConalogue')
	ELSE
	   CALL INFORM(MSG_INTERPL,'Akima')
	ENDIF
	GOTO 9000
C
C SHOW SCALES	  - gives information concerning scales
C
1000	CALL SHSCAL
	GOTO 9000
C
C SHOW SCROLL	  - gives scroll area of terminal
C
1100	CALL INFORM(MSG_SCROLL,VTSSIZ,VTSSCR)
	GOTO 9000
C
C SHOW SECTORS	  - gives number of sectors
C
1200	CALL INFORM(MSG_SECTORS,TOPSEC,YRANGE,XRANGE)
	IF (SINDEX.NE.STATE_INITIAL) THEN
	   CALL INFORM(MSG_SECSIZE,XSECTW,XSECTW/SCLFRT,
     &	                           YSECTW,YSECTW/SCLFRT)
	ENDIF
	GOTO 9000
C
C SHOW OPTIONS	  - lists options selected by ENABLE/DISABLE
C
1300	CALL SHOPTI
	GOTO 9000
C
C SHOW STATE	  - gives current state
C
1400	CALL INFORM(MSG_NEWSTATE,STASTR(1:STALEN))
	GOTO 9000
C
C SHOW VERSION	  - gives version and date of linking of program
C
1500	CALL WRTVER
	CALL INFORM(MSG_BLANK)
	CALL SHLIC(%VAL(LFUNAD))
	GOTO 9000
C
C SHOW MACRO	  - lists macro names or their expansions
C
1600	CALL SHMACR
	GOTO 9000
C
C SHOW COMMANDS	  - lists command names
C
1700	CALL INFORM(MSG_CMDVALIDSTATE,STASTR(1:STALEN))
	CALL SHCOMM(PRIME_CMD_TABLE,SMASK)
	GOTO 9000
C
C SHOW MENUS	  - 	lists menu/puck names or their expansions
C
1800	CALL SHMENU
	GOTO 9000
C
C SHOW TOLERANCE  -	lists tolerances that have been set up
C
1900	CALL SHTOLE
	GOTO 9000
C
C SHOW BASES	  -	lists bases that have been selected for squaring
C
2000	CALL SHBASE
	GOTO 9000
C
C SHOW MATCH
C
2100	CALL SHMATC
	GOTO 9000
C
C SHOW REGION
C
2200	CALL SHREG
	GOTO 9000
C
C SHOW SORT
C
2300	IF (SORIFF.EQ.SORT_BY_IFF) THEN
	   CALL INFORM(MSG_SORT,'IFF')
	ELSEIF (SORIFF.EQ.SORT_BY_FSN) THEN
	   CALL INFORM(MSG_SORT,'FSN')
	ELSEIF (SORIFF.EQ.SORT_BY_GT) THEN
	   CALL INFORM(MSG_SORT,'GT')
	ELSEIF (SORIFF.EQ.SORT_BY_PRIORITY) THEN
	   CALL INFORM(MSG_SORT,'priority')
	ENDIF
	GOTO 9000
C
C SHOW VARIABLES
C
2400	CALL SHVAR
	GOTO 9000
C
C SHOW MEMORY
C
2500	CALL SHMEM
	GOTO 9000
C
C SHOW LABELS
C
2600	CALL SHLABE
	GOTO 9000
C
C SHOW ANNOTATION
C
2700	CALL SHANNO
	GOTO 9000
C
C SHOW FILL
C
2800	CALL SHFILL
	GOTO 9000
C
C SHOW ZOOM
C
2900	CALL INFORM(MSG_ZOOMFACTOR,FULLSCLDIS/SCALE)
	GOTO 9000
C
C SHOW WINDOW
C
3000	CALL INFORM(MSG_WINDOW)
C
C note: have to build up string by myself, to get numbers columnated
	CALL EXPAND('%WSIFF Units',STRNG)
	CALL EXPAND('%WS%G0.8',STRNG(20:35),WINDOW(1))
	CALL EXPAND('%WS%G0.8',STRNG(36:51),WINDOW(2))
	CALL EXPAND('%WS%G0.8',STRNG(52:67),WINDOW(3))
	CALL EXPAND('%WS%G0.8',STRNG(68:80),WINDOW(4))
	CALL INFORM(MSG_STRING,STRNG)
C
	CALL EXPAND('%WSSheet mm',STRNG)
	CALL EXPAND('%WS%G0.8',STRNG(20:35),WINDOW(1)/SCFRT)
	CALL EXPAND('%WS%G0.8',STRNG(36:51),WINDOW(2)/SCFRT)
	CALL EXPAND('%WS%G0.8',STRNG(52:67),WINDOW(3)/SCFRT)
	CALL EXPAND('%WS%G0.8',STRNG(68:80),WINDOW(4)/SCFRT)
	CALL INFORM(MSG_STRING,STRNG)
	GOTO 9000
C
C SHOW LIMITS
C
3100	CALL INFORM(MSG_LIMITS)
C
C note: have to build up string by myself, to get numbers columnated
	CALL EXPAND('%WSIFF Units',STRNG)
	CALL EXPAND('%WS%G0.8',STRNG(20:35),LIMITS(1))
	CALL EXPAND('%WS%G0.8',STRNG(36:51),LIMITS(2))
	CALL EXPAND('%WS%G0.8',STRNG(52:67),LIMITS(3))
	CALL EXPAND('%WS%G0.8',STRNG(68:80),LIMITS(4))
	CALL INFORM(MSG_STRING,STRNG)
C
	CALL EXPAND('%WSSheet mm',STRNG)
	CALL EXPAND('%WS%G0.8',STRNG(20:35),LIMITS(1)/SCFRT)
	CALL EXPAND('%WS%G0.8',STRNG(36:51),LIMITS(2)/SCFRT)
	CALL EXPAND('%WS%G0.8',STRNG(52:67),LIMITS(3)/SCFRT)
	CALL EXPAND('%WS%G0.8',STRNG(68:80),LIMITS(4)/SCFRT)
	CALL INFORM(MSG_STRING,STRNG)
	GOTO 9000
C
C SHOW TRANSFORMATION
C
3200	CONTINUE
	IF (.NOT.(TRNS_FROM.OR.TRNS_TO).AND..NOT.TRNS_COEFF) THEN
	   CALL INFORM(MSG_NOTRANSFORMDEF)
	ELSE
	   IF (.NOT.TRNS_COEFF) THEN
C
C transform set up by TRANSFORM FROM and TRANSFORM TO commands
	      IF (TRNS_FROM) THEN
	         CALL INFORM(MSG_SPACEDEF,'Object')
C
C build up string, to get some sort of tabulation
	         CALL EXPAND('%WSA)',STRNG)
	         CALL EXPAND('%WS%G0.8',STRNG(6:21),TRNS_FROMA(1))
	         CALL EXPAND('%WS%G0.8',STRNG(22:37),TRNS_FROMA(2))
	         CALL EXPAND('%WSB)',STRNG(38:43))
	         CALL EXPAND('%WS%G0.8',STRNG(44:59),TRNS_FROMB(1))
	         CALL EXPAND('%WS%G0.8',STRNG(60:80),TRNS_FROMB(2))
	         CALL INFORM(MSG_STRING,STRNG)
	      ENDIF
	      IF (TRNS_TO) THEN
	         CALL INFORM(MSG_SPACEDEF,'Target')
C
C build up string, to get some sort of tabulation
	         CALL EXPAND('%WSA)',STRNG)
	         CALL EXPAND('%WS%G0.8',STRNG(6:21),TRNS_TOA(1))
	         CALL EXPAND('%WS%G0.8',STRNG(22:37),TRNS_TOA(2))
	         CALL EXPAND('%WSB)',STRNG(38:43))
	         CALL EXPAND('%WS%G0.8',STRNG(44:59),TRNS_TOB(1))
	         CALL EXPAND('%WS%G0.8',STRNG(60:80),TRNS_TOB(2))
	         CALL INFORM(MSG_STRING,STRNG)
	      ENDIF
	      IF (TRNS_FROM.NE.TRNS_TO)THEN
	         CALL INFORM(MSG_REQOTERLINE)
	         GOTO 9000
	      ELSE
	         CALL INFORM(MSG_BLANK)
	      ENDIF
	   ENDIF
C
	   CALL INFORM(MSG_TRANSFORMDEF)
	   CALL INFORM(MSG_STRING,'X = a*x - b*y + c1')
	   CALL INFORM(MSG_STRING,'Y = b*x + a*y + c2')
	   CALL INFORM(MSG_WHERE)
	   CALL INFORM(MSG_EQUALS,' a',TRNS_A)
	   CALL INFORM(MSG_EQUALS,' b',TRNS_B)
	   CALL INFORM(MSG_EQUALS,'c1',TRNS_C1)
	   CALL INFORM(MSG_EQUALS,'c2',TRNS_C2)
	   CALL INFORM(MSG_BLANK)
	   CALL INFORM(MSG_IMPLIES)
	   CALL INFORM(MSG_IMPLICATION,'Rotation of      ',
     &	               TRNS_ROT/PI*180.0,'degrees')
	   CALL INFORM(MSG_IMPLICATION,'Scale factor of  ',TRNS_SCL,' ')
	   CALL INFORM(MSG_IMPLICATION,'Local X origin of',TRNS_XORG,
     &	               'IFF units')
	   CALL INFORM(MSG_IMPLICATION,'Local Y origin of',TRNS_YORG,
     &                 'IFF units')
	   CALL INFORM(MSG_IMPLICATION,'X shift of       ',TRNS_XOFF,
     &	               'IFF units')
	   CALL INFORM(MSG_IMPLICATION,'Y shift of       ',TRNS_YOFF,
     &                 'IFF units')
	ENDIF
	GOTO 9000
C
C SHOW ACD - lists attributes read in
C
3300	CALL SHACD(STRNG)
	GOTO 9000
C
C SHOW AFTER - lists commands to execute after certain events
C
3400	CONTINUE
C
C after input
	IF (AFTER_INPUT_LEN.LE.0) THEN
	   CALL INFORM(MSG_NOAFTERDEF,'AFTER INPUT')
	ELSE
	   CALL INFORM(MSG_AFTERDEF,AFTER_INPUT(1:AFTER_INPUT_LEN),
     &	         'AFTER INPUT')
	ENDIF
C
C after error
	IF (AFTER_ERROR_LEN.LE.0) THEN
	   CALL INFORM(MSG_NOAFTERDEF,'AFTER ERROR')
	ELSE
	   CALL INFORM(MSG_AFTERDEF,AFTER_ERROR(1:AFTER_ERROR_LEN),
     &	         'AFTER ERROR')
	ENDIF
C
C after abort
	IF (AFTER_ABORT_LEN.LE.0) THEN
	   CALL INFORM(MSG_NOAFTERDEF,'AFTER ABORT')
	ELSE
	   CALL INFORM(MSG_AFTERDEF,AFTER_ABORT(1:AFTER_ABORT_LEN),
     &	         'AFTER ABORT')
	ENDIF
	GOTO 9000
C
C SHOW OPERATIONS - lists operations set up
C
3500	CALL SHOPER
	GOTO 9000
C
C SHOW SETUP - lists setups of maps
C
3600	CALL SHSETU
	GOTO 9000
C
C SHOW OVERLAYS - lists details of display overlays
C
3700	CALL SHOVER
	GOTO 9000
C
C SHOW PTOLERANCE  -	lists priviliged tolerances that have been set up
C
3800	CALL SHPTOL
	GOTO 9000
C
C SHOW PRIVILEGE   -	lists priviliged commands, attributes and points
C
3900	CALL SHPRIV
	GOTO 9000
C
C SHOW GEOGRAPHICAL -	gives cursor position as LAT/LONG
C
4000	CALL SHGEOG
	GOTO 9000
C
C SHOW IMAGES - lists details of raster images
C
4100	CALL SHIMAG
	GOTO 9000
C
C SHOW ABSOLUTE	  - gives position of cursor
C
4200	IF (SINDEX.NE.STATE_INITIAL) THEN
	   IF (CSZI.EQ.IFF_ABSENT) THEN
	      IF (.NOT.ZOPT) THEN
	         CALL INFORM(MSG_ABSPOS,LITES2ORG(1)+CSXY(1),
     &	                          LITES2ORG(2)+CSXY(2))
	      ELSE
	         CALL INFORM(MSG_ABS3DPOSNOZ,LITES2ORG(1)+CSXY(1),
     &	                          LITES2ORG(2)+CSXY(2))
	      ENDIF
	   ELSE
	      CALL INFORM(MSG_ABS3DPOS,LITES2ORG(1)+CSXY(1),
     &	                          LITES2ORG(2)+CSXY(2),CSXY(3))
	   ENDIF
	ELSE
	   CALL MSGCMD(MOAN,MSG_CMDNOTVALID)
	ENDIF
	GOTO 9000
C
C SHOW CP - gives corner points for maps
C
4300	CALL SHCP(STRNG)
	GOTO 9000
C
C SHOW VIEW - gives details of specified (or all) views
C
4400	CALL SHVIEW(STRNG)
	GOTO 9000
C
C SHOW DISPLAYS - gives details of specified (or all) displays
C
4500	CALL SHDISP
	GOTO 9000
C
C SHOW LIGHTS - for specified (or all) views gives details of specified
C               (or all) lights
C
4600	CALL SHLIGHTS(STRNG)
	GOTO 9000
C
C SHOW COLOURS - for specified display and overlay
4700	CALL SHCOLOURS
	GOTO 9000
C
C SHOW FC_PRIORITIES - give details of priority that specified FC will be
C		       drawn at
4800	CALL SHFCPRIO(%VAL(PRIOINT_LOC))
	GOTO 9000
C
C SHOW PLOT - gives details of hardcopy plot
4900	CALL SHPLOT
	GOTO 9000
C
C SHOW PROJECTION - gives details of projection of LITES2 coordinate space
5000	CALL SHPROJ
	GOTO 9000
C
C SHOW <GEOMETRY,GEOMETRIES> [n] - show details of geometries
5100	CALL SHGEOM
	GOTO 9000
C
C SHOW WARP - show details of warping
5200	CALL SHWARP
	GOTO 9000
C
9000	RETURN
        END
