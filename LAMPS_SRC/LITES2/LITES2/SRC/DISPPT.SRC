	SUBROUTINE DISPPT(XY,Z,IND)
C
***	MODULE	DISPPT
***	IDENT	08MY96
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author Ron Russell		15-August-1988
C
C To draw the label/marks at the points XY (attribute data is in CB)
C
	IMPLICIT NONE
C
***	PARAMETER/CMN:ACS/
	INCLUDE	'LSL$CMNIFF:CB.STR'
C
***	COMMON/CMN:CB/
***	COMMON/CMN:CLIP/
***	COMMON/LSL$CMNFRT:FRTACD/
***	COMMON/LSL$CMNIFF:IFFSTR/
***	COMMON/CMN:LABELS/
***	COMMON/CMN:OPTION/
***	COMMON/LSL$CMNLSL:TXTC/
***	COMMON/CMN:WORKS/
C
C arguments
C
	INTEGER		IND		! index on XY
	REAL		XY(2,IND)	! coords
	REAL		Z(IND)
C
C functions
	INTEGER		EXPAND
C
C w/s
	INTEGER		I,J
	INTEGER		ATTR(TOT_LAB_ATTR) ! Attributes for point
	LOGICAL		SOMETHING
	REAL		XX,YY
C
	CHARACTER*(*)	BLANK
	PARAMETER	(BLANK = '-')
C
C for each point, decide if anything to draw, then draw it
	DO 600,I=1,CBH.NROW
	   SOMETHING = .FALSE.
C
C if no attributes set up, then must be marking all points (we won't
C have got here if labelling points)
	   IF (NUM_LAB_ATTR.EQ.0) GOTO 500
C
C get any required attributes for this point
	   CALL IFS_CB_READ(CBH,CBD,ATTR,NUM_LAB_ATTR,LABATTR,
     &	                  I,I)
C
C check if this point has any of required attributes
	   DO 100, J = 1,NUM_LAB_ATTR
C
C mark any attribute in present in list 
	      IF (MARKING) THEN
C
C if any required attribute is present, then draw mark
	         IF ((ATTR(J).NE.IFF_ABSENT).AND.
     &	             ((LABATTR(J,2).EQ.IFF_ABSENT).OR.
     &	                (LABATTR(J,2).EQ.ATTR(J)))) GOTO 500
	      ELSE
C
C if any required attribute is present, then add to list
C of texts to output
	         IF ((ATTR(J).NE.IFF_ABSENT).AND.
     &	             ((LABATTR(J,2).EQ.IFF_ABSENT).OR.
     &	                (LABATTR(J,2).EQ.ATTR(J)))) THEN
	               SOMETHING = .TRUE.
	               LABATTIND(J) = EXPAND(
     &	                  '%W'//LABATTRFORMAT(J)(1:LABATTRF_LEN(J)),
     &	                     %REF(LAB_ATTR(J)),ATTR(J))
	         ELSE
	            LABATTIND(J) = EXPAND('%W%S',%REF(LAB_ATTR(J)),BLANK)
	         ENDIF
	      ENDIF		
  100	   CONTINUE
C
	   IF (MARKING.OR..NOT.SOMETHING) GOTO 600
C
C now draw something
  500	   CONTINUE
	   IF (KRSINI) CALL SRI_SET_Z(Z(I))
	   IF (MARKING) THEN
	      IF (XY(1,I).GE.CLIPLO(1).AND.XY(1,I).LE.CLIPHI(1).AND.
     &		  XY(2,I).GE.CLIPLO(2).AND.XY(2,I).LE.CLIPHI(2)) THEN
	         IF (WITH_SYMBOL) THEN
	            CALL SRIPLT(ANNOMARK,XY(1,I),XY(2,I),ANNOSIZ,0.0)
	         ELSE
	            XX = XY(1,I) + ANNOOFFX
	            YY = XY(2,I) + ANNOOFFY
	            TXTPTR = EXPAND('%W%N',TXTBUF,I+NUMBER_OFFSET)
	            IF (BLAOPT) CALL BLANK_LABEL(XX,YY,Z(I),TXTDSC,
     &				ANNOFONT,0.0,ANNOSIZ,HWTOPT.AND.ANNOHWT)
	            CALL TRITXT(TXTDSC,ANNOFONT,XX,YY,ANNOSIZ,0.0,
     &				HWTOPT.AND.ANNOHWT)
	         ENDIF
	      ENDIF
	   ELSE
C
C get position to draw at
	      XX = XY(1,I) + ANNOOFFX
	      YY = XY(2,I) + ANNOOFFY + NUM_LAB_ATTR*ATTROFFY
C
C and draw the requisite number of labels
	      DO 550, J = 1,NUM_LAB_ATTR
	         YY = YY - ATTROFFY
	         IF (BLAOPT) CALL BLANK_LABEL(XX,YY,Z(I),
     &				LAB_ATTR(J)(1:LABATTIND(J)),
     &				ANNOFONT,0.0,ANNOSIZ,HWTOPT.AND.ANNOHWT)
	         CALL TRITXT(LAB_ATTR(J)(1:LABATTIND(J)),ANNOFONT,
     &	               XX,YY,ANNOSIZ,0.0,HWTOPT.AND.ANNOHWT)
  550	      CONTINUE
	   ENDIF
  600	CONTINUE
C
9999	RETURN
	END

