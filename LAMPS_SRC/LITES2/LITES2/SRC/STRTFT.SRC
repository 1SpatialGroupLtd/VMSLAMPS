C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1992-02-28 12:16:46.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION STRTFT(PACK,TC,FS,AC,TH)
C
***	MODULE	STRTFT
***	IDENT	28FE92
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author: Clarke Brunt
C
C Coded 24.ix.84
C
C To start off an output feature.
C COIPAK must already be loaded with FSN, FC, MAP, LAYER, GT, SEG, FLG
C Returns true if fails
C
	IMPLICIT NONE
C
	INCLUDE	'LSL$CMNIFF:CB.STR'
***	PARAMETER/CMN:AUXDEF/
***	PARAMETER/CMN:EFNLUN/
***	PARAMETER/CMN:MOANMESS/
***	PARAMETER/CMN:PACK/
C
***	COMMON/CMN:DEFINE/
***	COMMON/CMN:DRAWIN/
***	COMMON/LSL$CMNFRT:FRTCOM/
***	COMMON/LSL$CMNIFF:IFFHAN/
***	COMMON/CMN:MAPS/
***	COMMON/CMN:OIIH/
***	COMMON/CMN:OPTION/
C
C arguments
	INTEGER*4 PACK(PAKLEN)		! source feature for TCs and ACs
	INTEGER*4 TC			! TC address
	INTEGER*2 FS(4)			! FS entry to use
	INTEGER*4 AC			! AC address
	INTEGER*4 TH			! TH entry to use
C
C NB FS(1) had better be the same as COIPAK(PAKFC)!
C
C function called
	LOGICAL	FRTFND
C w/s
	INTEGER	I
	INTEGER LSAVE			! another save for LPOSE
C
	STRTFT = .TRUE.			! assume failure
C
C is map read only?
	IF (MAPRO(COIPAK(PAKMAP))) THEN
	   CALL MOAN(MSG_MAPREADONLY,COIPAK(PAKMAP)+1)
	   GOTO 999
	ENDIF
C
C Set up feature code in FRTCOM
	IF (FRTFND(COIPAK(PAKFC))) THEN
	   CALL MOAN(MSG_BADCODE,COIPAK(PAKFC))
	   GOTO 999
	ENDIF
C
C call GENAUX to get size of symbol string
	IF (FRTGT.EQ.STRTYP) CALL GENAUX(
     &	 COIIND,COIXY,COIAUX,COIDSC,COIANG,TH,COIPAK,FS)
	CALL FLUSH(0)			! initialise FLUSH
C
C get any ACs or TCs into AC sector
C do this before selecting map, as GETAC may select a different map
	IF (TC.GT.0.OR.AC.GT.0) CALL GETAC(PACK,TC,AC)
C
C Get output map
	CALL SELIF(COIPAK(PAKMAP))
C
C Debugging check. If IFFLIB thinks we are still in a feature, then
C the previous feature has not got an EF. IFFLIB will add one for us
C if given the chance, which will mess up the pointers to this feature.
C Add the EF explicitly, then COLAPS to indicate the error. Previous
C feature is probably duff, but at least this one will be OK.
	IF (LPOSF.NE.0) THEN		! still in a feature
	   CALL IFFEF			! add the EF
	   CALL NASTY(MSG_PREVNOEF1)
	   CALL NASTY(MSG_PREVNOEF2)
	   CALL COLAPS(MSG_PREVNOEF1)
	ENDIF
C
C Get correct overlay
	CALL STRTOV(COIPAK(PAKOVL))
C
C Now know where the new feature will go, so...
C Don't set COIPAK(PAKPNF) yet in case our argument PACK is actually
C COIPAK (as in call from STRTER), so that COPYAC may still take
C entries from the AC sector.
	LSAVE = LPOSE			! save position
C
C copy over any required TC's
	IF (TC.NE.0) CALL COPYAC(TC,.FALSE.)
C
C Start the feature off
	I = 0
	CALL IFFNF(COIPAK(PAKFSN),I)
C
C set deleted bit in FS approprately
	IF (IAND(COIPAK(PAKFLG),LIMFLG).EQ.0) THEN
	   FS(2) = IAND(FS(2),NOT(16))		! not deleted
C
C set edited bit in the fourth word
	   IF (EDTOPT) THEN
	      FS(4)= IOR(FS(4),1)
	   ENDIF
	ELSE
	   FS(2) = IOR(FS(2),16)		! deleted
	ENDIF
C
C
	CALL IFFFS(FS)
C
C copy this FS entry to OIIH common block, for text drawing routines
C (called in STDRAW by FLUSH)
	CALL CPYPAK(2,FS,COIFS)
C
C copy over any required AC's
	IF (AC.NE.0) CALL COPYAC(AC,.TRUE.)
C
C add any required ACs that have been defined 
	IF (DEFINITION(F_ADD).GT.0.AND.FLG_ADD) CALL FADDAC
C
C output a TH entry - except for text features, as they sometimes need
C                     a TS entry first
	IF (.NOT.FRTTEX) CALL IFFTH(TH)
C
C Set start of feature pointer
	COIPAK(PAKPNF) = LSAVE
C
C don't draw curves until complete (in case closed loop)
C and if drawing with priority draw at end as well
	OIDRAW = .NOT.(FRTCUR.OR.DRAW_PRIORITY)
C
C not a composite text, as far as we know
	COMTXT = .FALSE.
C
	STRTFT = .FALSE.		! succeeded
999	RETURN
	END
