	SUBROUTINE RFDRAW
C
***	MODULE	RFDRAW
***	IDENT	13DE89
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author Clarke Brunt	10-August-1984
C
C To draw the refresh picture
C Version for HRD (cf TEK part of TEK/ARGS version)
C
	IMPLICIT NONE
C
***	PARAMETER/CMN:AUXDEF/
***	PARAMETER/CMN:CTXS/
***	PARAMETER/CMN:END/
***	PARAMETER/CMN:GKSPAR/
***	PARAMETER/CMN:MOANMESS/
***	PARAMETER/CMN:PACK/
***	PARAMETER/CMN:STAPAR/
C
	INCLUDE 'LSL$CMNIFF:CB.STR'
C
***	COMMON/CMN:CSTRCT/
***	COMMON/CMN:CTXS/
***	COMMON/CMN:DRAWIN/
***	COMMON/LSL$CMNFRT:FRTCOM/
***	COMMON/CMN:MASTER/
***	COMMON/CMN:MODIFY/
***	COMMON/CMN:OPTION/
***	COMMON/CMN:OIIH/
***	COMMON/CMN:RFCTRL/
***	COMMON/CMN:STATE/
***	COMMON/CMN:STRIKE/
***	COMMON/CMN:WORKS/
C
C functions called
C
	LOGICAL		FRTFND
C w/s
C
	REAL	X,Y			! temporary coordinates
	INTEGER	WKID
	INTEGER*2 DUMMYE		! dummy I*2 variable
	LOGICAL*1 DUMMYL		! dummy L*1 variable
	LOGICAL*1 TEKFLS		! flush TEK (cursor moved)
C
	DATA	DUMMYE,DUMMYL/0,.TRUE./
C
C Do nothing if no display
	IF (NODISP) GOTO 999
C
	TEKFLS = .FALSE.		! don't need to flush TEK
C
C Do we need a cursor?
	IF (CURS) THEN			! yes
	   IF (.NOT.INTCUR) THEN	! not initialised yet
	      CSXY(1) = (WINDOW(1)+WINDOW(2))/2.0
	      CSXY(2) = (WINDOW(3)+WINDOW(4))/2.0
	      INTCUR = .TRUE.
	      GOTO 10
	   ENDIF
C
	   IF (CSXY(1).EQ.OCSXY(1) .AND. CSXY(2).EQ.OCSXY(2)) GOTO 20
10	   CALL GKSW2D(1,CSXY(1),CSXY(2),X,Y)
	   CALL CSWT(X,Y)
	   TEKFLS = .TRUE.		! need to flush
	   OCSXY(1) = CSXY(1)
	   OCSXY(2) = CSXY(2)
C
20	   IF (.NOT.RFCURS) THEN
	      RFCURS = .TRUE.
	      GOTO 450
	   ENDIF
	ELSE
	   IF (RFCURS) THEN
	      RFCURS = .FALSE.
	      GOTO 450
	   ENDIF
	ENDIF
C
C do we need to clear the old refresh picture?
C
C Has the found object changed?
	IF (FO) THEN			! there is a found object
	   IF (RFFSEG.EQ.0) GOTO 450	! none drawn
	   IF ( RFFSEG.NE.FOPAK(PAKSEG).OR.	! not same item or
     &		RFFVO.NE.FOVO) GOTO 450		! different vertices
	   IF (FOVRT-RFFOFF.LE.RFCRIT) THEN	! want earlier
	      IF (RFFOFF.GT.FOVO) GOTO 450	! more available
	   ELSEIF (FOVRT-RFFOFF.GT.RFFTOP-RFCRIT) THEN	! want later
	      IF (RFFOFF+RFFTOP.LT.FOVO+FOTOP) GOTO 450	! available
	   ENDIF
C
C has cursor moved on composite text?
	   IF (FOPAK(PAKGT).EQ.TEXTYP.AND.
     &	               (RFFCTX.NE.FTRCTXD(CTXD_HOME))) GOTO 450
	ELSE				! no found object
	   IF (RFFSEG.EQ.0) GOTO 100	! and none drawn
	   IF (IH.AND.RFFSEG.EQ.IHPAK(PAKSEG)) THEN	! now in hand
	      RFHSEG = RFFSEG
	      RFHOFF = RFFOFF
	      RFHTOP = RFFTOP
	      RFFSEG = 0
	      GOTO 100		! no change to make, no refresh blink
	   ENDIF
	   GOTO 450
	ENDIF
C
C now deal with the object in hand
100	IF (.NOT.IH) THEN		! no object in hand
	   IF (RFHSEG.NE.0) GOTO 450	! but one drawn
	ENDIF
C
C now deal with any construction
	IF (((SINDEX.EQ.STATE_CONSTRUCT).OR.
     &	     (SINDEX.EQ.STATE_ON.AND.NDMODE.EQ.END_INCLUDE)).AND.
     &	      CONIND.NE.0) THEN
	   IF (RFCIND.EQ.0) GOTO 450		! no construction drawn
	   IF (CONIND.NE.RFCIND) GOTO 450	! construction changed
	ELSE					! not constructing
	   IF (RFCIND.NE.0) GOTO 450		! but something drawn
	ENDIF
C
C now deal with modification
	IF ((SINDEX.EQ.STATE_READY.OR.SINDEX.EQ.STATE_MODIFY).AND.
     &		MODMOD) THEN
	   MODMOD=.FALSE.
	   GOTO 450
	ENDIF
C
C Has rubber banding changed?
	IF (RUBCHG) GOTO 450		! yes
C
	IF (TEKFLS) CALL REFRSH		! set refresh going
C
	GOTO 999			! nothing changed
C
C Clear refresh picture
450	REFNG = .TRUE.			! now refreshing
	CALL DISMOD(1)		! select refresh
	IF (RFCURS) THEN
	   CALL CURSOR		! add cursor
	ENDIF
	RFFSEG = 0
	RFHSEG = 0
	RFCIND = 0
C
C preserve drawing controls
	THKSAV = THKOPT
	AKISAV = CUROPT
	PATSAV = PATOPT
C
C turn off all fancy features
	OVCOL = 1			! overriding colour 1
	THKOPT = .FALSE.
	CUROPT = .FALSE.
	PATOPT = .FALSE.
C
C draw the new refresh picture
	IF (FO) THEN			! there is a found item
C
	   RFFOFF = MIN(FOVRT-RFOFF,	! to place FOVRT at RFOFF
     &			FOLST-RFFULL)	! to place last vertex at RFFULL
	   RFFOFF = MAX(RFFOFF,FOVO)	! force into FOXY
	   RFFTOP = MIN(RFFULL,FOTOP+FOVO-RFFOFF)
C
C is it a line, curve, symbol string, or area
	   IF (	FOPAK(PAKGT).EQ.LINTYP .OR.
     &		FOPAK(PAKGT).EQ.CURTYP .OR.
     &		FOPAK(PAKGT).EQ.STRTYP .OR.
     &		FOPAK(PAKGT).EQ.ARETYP ) THEN
C
C just draw selected vertices we have in memory
	      FRTARE = .FALSE.		! refresh area as line
	      FRTFC = -1		! invalidate FRTCOM
	      CALL STDBFF(0)		! clear buffer
	      CALL STDBFA(RFFTOP,FOXY(1,RFFOFF+1-FOVO),
     &	                  FOZI(RFFOFF+1-FOVO)) ! add points
	      CALL STDBFF(2)		! flush them out
	   ELSE
C
C draw entire feature for circles, symbols, or text
	      IF (FRTFND(FOPAK(PAKFC))) THEN
	         CALL NASTY(MSG_BADCODE,FOPAK(PAKFC))
	         CALL COLAPS(MSG_BADCODE)
	      ENDIF
	      IF (FRTTEX) THEN				! draw text
	         CALL BOXCTX(FTRCTXD,FOPAK,DUMMYL,DUMMYL)
	         RFFCTX = FTRCTXD(CTXD_HOME)
	      ELSE					! draw symbols
	         CALL STDRAW(				! and circles	
     &		FOTOP,FOXY,FONATT,STR_MAX_ATTR,FOATTC,FOATTV,
     &		FOAUX,FOTXT(1:MAX(1,FOPTR)),
     &		DUMMYL,DUMMYL,DUMMYE,DUMMYL,FOPAK,FOFS)
	      ENDIF
	   ENDIF
	   RFFSEG = FOPAK(PAKSEG)	! have a found object refreshed
	   RFFVO = FOVO			! its vertex offset
C
	ENDIF
C
	IF (IH.AND.SINDEX.NE.STATE_MODIFY) THEN ! there is an
						!  object in hand
						!  that needs redrawing
C
C is it a line, curve, symbol string, or area
	   IF (	IHPAK(PAKGT).EQ.LINTYP .OR.
     &		IHPAK(PAKGT).EQ.CURTYP .OR.
     &		IHPAK(PAKGT).EQ.STRTYP .OR.
     &		IHPAK(PAKGT).EQ.ARETYP ) THEN
C
C just draw selected vertices we have in memory
	      FRTARE = .FALSE.		! refresh area as line
	      FRTFC = -1		! invalidate FRTCOM
	      CALL STDBFF(0)		! clear buffer
	      CALL STDBFA(RFHTOP,IHXY(1,RFHOFF+1-IHVO),
     &	                     IHZ(RFHOFF+1-IHVO)) ! add points
	      CALL STDBFF(2)		! flush them out
	   ELSE
C
C draw entire feature for circles, symbols, or text
	      IF (FRTFND(IHPAK(PAKFC))) THEN
	         CALL NASTY(MSG_BADCODE,IHPAK(PAKFC))
	         CALL COLAPS(MSG_BADCODE)
	      ENDIF
	      IF (FRTTEX) THEN				! draw texts
	         CALL BOXCTX(IHCTXD,IHPAK,DUMMYL,DUMMYL)
	      ELSE
	         CALL STDRAW(		! draw symbols and circles
     &		IHTOP,IHXY,0,1,0,0,IHAUX,IHTXT(1:MAX(1,IHPTR)),
     &		DUMMYL,DUMMYL,DUMMYE,DUMMYL,IHPAK,IHFS)
	      ENDIF
	   ENDIF
	   RFHSEG = IHPAK(PAKSEG)	! have object in hand refreshed
	ENDIF
C
C draw construction
	IF (((SINDEX.EQ.STATE_CONSTRUCT).OR.
     &	     (SINDEX.EQ.STATE_ON.AND.NDMODE.EQ.END_INCLUDE)).AND.
     &	      CONIND.GT.0) THEN
	   IF (CONIND.GT.1) THEN	! can't refresh a single point
	      FRTARE = .FALSE.		! refresh area as line
	      FRTFC = -1		! invalidate FRTCOM
	      CALL STDBFF(0)		! clear buffer
	      RFCIND = MIN(CONIND,RFFULL) ! number to refresh
	      CALL STDBFA(RFCIND,CONXY(1,CONIND-RFCIND+1), ! add points
     &	                        CONZ(CONIND-RFCIND+1))
	      CALL STDBFF(2)		! flush them out
	   ENDIF
	   RFCIND = CONIND		! have refreshed construction
C
C set up rubber band
	   RUBLIN(1) = .TRUE.
	   RUBARR(1,1) = CONXY(1,CONIND)
	   RUBARR(2,1) = CONXY(2,CONIND)
	ENDIF
C
C draw modification
	IF (SINDEX.EQ.STATE_MODIFY) THEN
C
C and draw symbol/text
C
C First fill FRTCOM as it tends to get corrupted!
	   IF (FRTFND(COIPAK(PAKFC))) THEN
	      CALL NASTY(MSG_BADCODE,COIPAK(PAKFC))
	      CALL COLAPS(MSG_BADCODE)
	   ENDIF
C
C now display symbol/text
C
	   IF (FRTTEX) THEN
	      CALL BOXCTX(COICTXD,COIPAK,DUMMYL,DUMMYL)
	   ELSE
	      CALL STDRAW(
     &	COIIND-1,COIXY(1,2),COINATT,OIH_MAX_ATTR,COIATTC,COIATTV(1,2),
     &		COIAUX,COIDSC,
     &		DUMMYL,DUMMYL,DUMMYE,DUMMYL,COIPAK,COIFS)
	   ENDIF
	ENDIF
C
C draw rubber band lines
	RUBCHG = .FALSE.
	IF (RUBLIN(1).OR.RUBLIN(2)) THEN
	   CALL RFBAND(1)
	ENDIF
C
C restore everything
	OVCOL = -1
	THKOPT = THKSAV
	CUROPT = AKISAV
	PATOPT = PATSAV
C
C update displays
	CALL REFRSH			! start refresh picture
C
	REFNG = .FALSE.			! no longer refreshing
C
999	RETURN
	END
C
	SUBROUTINE PEN
C
***	MODULE	PEN
***	IDENT	01MY85
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author Clarke Brunt	10-August-1984
C
C Dummy PEN routine for LDLIB
C
	IMPLICIT NONE
C
	RETURN
	END
C
	SUBROUTINE LDWAIT
C
***	MODULE	LDWAIT
***	IDENT	01MY85
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author Clarke Brunt	10-August-1984
C
C Dummy LDWAIT routine for LDLIB
C To zap LDWAIT call from GCLRWK
C
	IMPLICIT NONE
C
	RETURN
	END
C
	SUBROUTINE GCLRWK
C
***	MODULE	GCLRWK
***	IDENT	20DE85
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author Clarke Brunt	20-December-1985
C
C Replacement GCLRWK for GKSLDLIB for HRD (LDLIB)
C Include call to RFSTOP before PCFRAM to avoid hang
C
	IMPLICIT NONE
C
	CALL RFSTOP
	CALL PCFRAM(1.0)
C
	RETURN
	END
