C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1995-04-27 13:30:54.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION LSCR(INDEX,R,G,B)
C
***	MODULE	LSCR
***	IDENT	27AP95
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C
C LITES2 GKS level routine
C Set colour representation
C
	IMPLICIT NONE
C
***	COMMON/CMN:WORKS/
C
C arguments
	INTEGER		INDEX		! colour index
	REAL		R,G,B		! red, green, blue
C
C workspace
	INTEGER		I,J
	REAL		RGB(3)
	INTEGER		NULL
	CHARACTER*1	NULLC
	INTEGER		PLANES
	INTEGER		WKID
C
	LSCR = .TRUE.			! assume failed
C
	IF (.NOT.(WKOP(1).OR.WKOP(2))) GOTO 999	! no workstation
	IF (WKOP(2)) WKID = 2
	IF (WKOP(1)) WKID = 1
C
C get number of planes being used
	CALL TSTWK(WKID,WTYPES(WKID),NULL,NULLC,PLANES)
C
	IF (INDEX.LT.0.OR.INDEX.GE.2**PLANES) GOTO 999
	IF (R.LT.0.0.OR.R.GT.1.0) GOTO 999
	IF (G.LT.0.0.OR.G.GT.1.0) GOTO 999
	IF (B.LT.0.0.OR.B.GT.1.0) GOTO 999
	CALL GSCR(WKID,INDEX,R,G,B)	! set colour
C
C if using 6 planes of an 8 planes system, or
C 10 planes of a 12 plane system, then
C set matching refresh colour to the primary of which
C this colour has least 
	IF (PLANES.EQ.6.OR.PLANES.EQ.10) THEN
	   RGB(1) = R
	   RGB(2) = G
	   RGB(3) = B
	   J = 2
	   IF (RGB(1).LT.RGB(2)) J = 1
	   IF (RGB(3).LT.RGB(J)) J = 3
	   DO 10 I = 1,3
	      IF (I.EQ.J) THEN
	         RGB(I) = 1.0
	      ELSE
	         RGB(I) = 0.0
	      ENDIF
10	   CONTINUE
	   CALL GSCR(WKID,2**(PLANES+1)-1-INDEX,RGB(1),RGB(2),RGB(3))
	ENDIF
	LSCR = .FALSE.			! success
C
999	RETURN
	END
C
	LOGICAL FUNCTION LSCRS(WKID,N,R,G,B)
C
***	MODULE	LSCRS
***	IDENT	09MR88
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C
C LITES2 GKS level routine
C Set colour representation for colours 0:N-1
C
	IMPLICIT NONE
C
***	COMMON/CMN:WORKS/
C
C arguments
	INTEGER		WKID		! workstation identifier
	INTEGER		N		! number of colours
	REAL		R(*),G(*),B(*)	! red, green, blue
C
C workspace
	INTEGER		I,J,NN
	REAL		RGB(3)
	INTEGER		NULL
	CHARACTER*1	NULLC
	INTEGER		PLANES
	INTEGER		COLS
C
	LSCRS = .TRUE.			! assume failed
	NN = N
C
	IF (WKID.LE.2) THEN
C
C primary/secondary workstation needs highlighting colours set
C get number of planes being used
	   CALL TSTWK(WKID,WTYPES(WKID),NULL,NULLC,PLANES)
C
C if using 6 planes of an 8 planes system,
C or 10 planes of a 12 plane system,
C then set matching refresh colours
	   IF (PLANES.EQ.6.OR.PLANES.EQ.10) THEN
	      COLS = 2**PLANES		! e.g. 64 for 6 planes
	      DO 20 NN=1,MIN(COLS,N)
	         RGB(1) = R(NN)
	         RGB(2) = G(NN)
	         RGB(3) = B(NN)
	         J = 2
	         IF (RGB(1).LT.RGB(2)) J = 1
	         IF (RGB(3).LT.RGB(J)) J = 3
	         DO 10 I = 1,3
	            IF (I.EQ.J) THEN
	               RGB(I) = 1.0
	            ELSE
	               RGB(I) = 0.0
	            ENDIF
10	         CONTINUE
	         R(2*COLS+1-NN) = RGB(1)
	         G(2*COLS+1-NN) = RGB(2)
	         B(2*COLS+1-NN) = RGB(3)
20	      CONTINUE
C
C NB we are making grubby use of the input array by writing the
C refresh colours into it, also also relies on N being the same
C a COLS. This is believed always to be the case.
	      NN = 2*COLS
	   ENDIF	   
	ENDIF
	CALL LSG_SET_COLOURS(WKID,NN,R,G,B)	! set colour
	LSCRS = .FALSE.			! success
C
999	RETURN
	END
C
	SUBROUTINE	LQCR(WKID,CI,TYPE,ERRIND,RED,GREEN,BLUE)
C
**	MODULE	LQCR
**	IDENT	13DE89
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C Created					RWR	13-Dec-1989
C
C LITES2 GKS level routine
C Inquire colour representation
C
	IMPLICIT NONE
C
C Arguments:
	INTEGER	WKID			! workstation-identifier
	INTEGER	CI			! colour index
	INTEGER	TYPE			! type of values (unused)
	INTEGER	ERRIND			! error indicator
	REAL	RED,GREEN,BLUE		! colour values
C
C simply call GKS routine
	CALL GQCR(WKID,CI,TYPE,ERRIND,RED,GREEN,BLUE)
C
	RETURN
	END
