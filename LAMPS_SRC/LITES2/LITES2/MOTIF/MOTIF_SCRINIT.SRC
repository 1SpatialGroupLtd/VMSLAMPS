	LOGICAL FUNCTION SCRINIT(ON)
C
***	IDENT	15MR91
***	MODULE	SCRINIT
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RWR	29-Nov-1984
C
C Description
C
C	Hardware dependant version of screen initialisation
C	----   This version for screen menu   ----
C
	IMPLICIT NONE
C
***	PARAMETER/CMN:MOANMESS/
***	PARAMETER/CMN:MACMEN/
***	PARAMETER/CMN:STAPAR/
C
***	COMMON/CMN:MENUS/
***	COMMON/CMN:OPTION/
***	COMMON/CMN:STATE/
***	COMMON/CMN:WORKS/
C
C argument
	LOGICAL	ON			! turn on or off?
C
C w/s
	INTEGER		IER
C
C functions used
	LOGICAL		CREATE_MENU
	LOGICAL		CREATE_KRSMENU
C
	SCRINIT = SCRINI		! assume present state
	IF (SCRINI.NEQV.ON) THEN
	   IF (ON) THEN
C
C need the screen menu defined to create a screen menu
C
	      IF (.NOT.SCRMEN_DEF) GOTO 999
	      IF (WKOP(1).OR.WKOP(2)) THEN
	         IF (CREATE_MENU(IER)) THEN
	            SCRINIT = .TRUE.
	         ELSE
	            CALL MOAN(MSG_ERRDEFSCRMEN,IER)
	         ENDIF
	      ENDIF
	      IF (KRSINI) THEN
	         IF (KRSMENU) THEN	! KRISS menu on already
	            SCRINIT = .TRUE.
	         ELSE
	            IF (CREATE_KRSMENU(IER)) THEN
	               SCRINIT = .TRUE.
	               KRSMENU = .TRUE.
	            ELSE
	               CALL MOAN(MSG_ERRDEFSCRMEN,IER)
	            ENDIF
	         ENDIF
	      ENDIF
	   ELSE
	      IF (WKOP(1).OR.WKOP(2)) CALL MENU_DELETE	! delete the menu
	      IF (KRSINI.AND.KRSMENU) THEN
	         CALL DELETE_KRSMENU
	         KRSMENU = .FALSE.
	         USE_KRSMENU = .FALSE.
	      ENDIF
	      SCRINIT = .FALSE.
	   ENDIF
	ELSE
	   IF (ON.AND.SINDEX.NE.STATE_INITIAL.AND.KRSINI.AND.KRSMENU
     &						) USE_KRSMENU = .TRUE.
	ENDIF
C
999	RETURN
	END
