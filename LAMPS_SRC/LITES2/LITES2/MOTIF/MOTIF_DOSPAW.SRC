	SUBROUTINE DOSPAW
C
***	IDENT	21FE92
***	MODULE	DOSPAW
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RWR	 5-Aug-1985
C
C Description
C
C	LITES2 cartographic editor SPAWN command.
C	This a a special version for MOTIF version.
C	In order to allow ASTs to process X events while spawned, we
C	actually use NOWAIT, but then hibernate (in user mode) until the
C	subprocess completes.
C	This leaves us susceptible to control-y, so disable it.
C	This version could be used for all LITES2 versions.
C
	IMPLICIT NONE
C
	INCLUDE	'($CLIDEF)'
	INCLUDE	'($LIBCLIDEF)'
***	PARAMETER/CMN:LICPAR/
***	PARAMETER/CMN:MOANMESS/
C
***     COMMON/CMN:DOUT/
***     COMMON/CMN:GLOBAL/
***     COMMON/CMN:OPTION/
***     COMMON/CMN:VTCTRL/
***	COMMON/CMN:WORKS/
C
	EXTERNAL	CTRLCAST
	EXTERNAL	SPAWN_CMD_TABLE
	EXTERNAL	DOSPAW_AST
C
C Functions called
	LOGICAL		LIB$SPAWN
	LOGICAL		LICENSED
C
C Local Variables
	LOGICAL		IERR
	LOGICAL		NOWAIT
	INTEGER		OLD_MASK
C
C check that this facility is licenced
	IF (LICENSED(LOCK_SPAWN)) THEN
C
C check for /NOWAIT
	   NOWAIT = .FALSE.
	   IF (DSTR(1:1).EQ.'/') THEN
C
C if '/' followed by nothing, then let GET_COMMAND complain about the
C '/', otherwise change it into a space
	      IF (DSTRL.GT.1) DSTR(1:1) = ' '
	      CALL GET_COMMAND(DSTR(:DSTRL),SPAWN_CMD_TABLE)
	      IF (MOANED) GOTO 8900
	      NOWAIT = .TRUE.
	   ENDIF
C
C take down any outstanding terminal read
	   IF (TTASN.AND..NOT.NOWAIT) THEN	! if channel assigned
	      CALL SYS$CANCEL(%VAL(TTICHN))	! cancel any QIO
	      TTIOSB(1) = 0			! clear cancel status
	      TTREAD=.FALSE.			! request new QIO
	   ENDIF
C
C spawn command line
	   IF (NOWAIT) THEN
	      IERR=LIB$SPAWN(DSTR(1:DSTRL),,,CLI$M_NOWAIT)
	   ELSE
	      IERR=LIB$SPAWN(DSTR(1:DSTRL),,,CLI$M_NOWAIT,,,,,DOSPAW_AST)
	   ENDIF
	   IF (.NOT.IERR) THEN
	      CALL LITES2_SIGNAL(IERR)
	      CALL IGNCMD
	   ELSE
	      IF (.NOT.NOWAIT) THEN
C
C disable CTRL/C outband AST so that CTRL/C will work in spawned process
	         IF (ITERM) THEN
	            CALL LIB$DISABLE_CTRL(LIB$M_CLI_CTRLY,OLD_MASK)
	            CALL SET_OUTBAND_AST
	         ENDIF
	         CALL SYS$HIBER
C
C re-enable CTRL/C recognition
	         IF (ITERM) THEN
	            CALL LIB$ENABLE_CTRL(OLD_MASK)
	            CALL SET_OUTBAND_AST(CTRLCAST,8)
	         ENDIF
	      ENDIF
C
C output two blank lines to ensure that we don't overwrite any of
C the possible output from the spawned command
	      CALL WRITEF	! n.b. not MSG_BLANK because that
	      CALL WRITEF	! is actually a single space
	   ENDIF
C
C get status line back up (in case spawned process ruined it)
	   CALL STATUS(STAOPT,VTSTTS)
	ENDIF
	GOTO 9000
C
8900	CALL IGNCMD
C
9000	RETURN
	END
C
C
C
	SUBROUTINE DOSPAW_AST
C
***	IDENT	16DE89
***	MODULE	DOSPAW_AST
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					CCB	10-Mar-1990
C
C	Awaken process after SPAWN
C
	IMPLICIT NONE
C
	INTEGER*4	DAYTIM(2)/0,0/
C
	CALL SYS$SCHDWK(,,DAYTIM,)
	RETURN
	END
