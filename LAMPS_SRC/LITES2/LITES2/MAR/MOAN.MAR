;
; * This file is part of the LAMPS distribution, released as a software
; * preservation project to archive digital history for future historians.
; * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
; *
; * Timestamp of this file for the 2002 release was: 1988-05-19 12:10:34.000000000 +0100
; *
; * This program is free software: you can redistribute it and/or modify
; * it under the terms of the GNU General Public License as published by
; * the Free Software Foundation, version 3.
; *
; * This program is distributed in the hope that it will be useful, but
; * WITHOUT ANY WARRANTY; without even the implied warranty of
; * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
; * General Public License for more details.
; *
; * You should have received a copy of the GNU General Public License
; * along with this program. If not, see http://www.gnu.org/licenses/.
;
	.TITLE MOAN
	.IDENT /19MY88/
; 
; Copyright Laser Scan Laboratories Ltd., Cambridge, England
; Author Paul Hardy					3-AUG-1984
;
; multiple entry points for severity		PGH	11-Oct-1984
; 
; LITES2 main message lookup routines.
; all call WRITEF after converting message number into format string.
; Variants are allow different severities.
;
;	name	prefix	rasp?	enabled		use
;
;	INFORM	none	no	ENABLE INFORM	general output
;	MOAN	???	yes	always		operator errors
;	LEARN	...	no	ENABLE LEARNER	operator aid
;	NASTY	!!!	yes	always		internal errors
;
; RASP only if ENABLE BELL

	.LIBRARY	/LSL$LIBRARY:LSLMACLIB/
	CODE_PSECT	LITES

; CALL MOAN(messno[, arg, arg, ...])
 
 .ENTRY	MOAN,^M<>
	CALLS	#0,BELLR		; look at BELOPT in /OPTION/
	BLBC	R0,$10			; suppress if not BELOPT
	CALLS	#0,RASP			; blow raspberry
$10:	CALLS	#0,MOANMK		; set MOANED in /GLOBAL/
	PUSHAQ	MOANPREFIX		; prefix is "???"
	CALLS	#1,G^EXPAND		; put prefix in buffer
	BRB	LOOKUP			; join common code

; CALL NASTY(messno[, arg, arg, ...])
 
 .ENTRY	NASTY,^M<>
	CALLS	#0,BELLR		; look at BELOPT in /OPTION/
	BLBC	R0,$11			; suppress if not BELOPT
	CALLS	#0,RASP			; blow raspberry
$11:	CALLS	#0,MOANMK		; set MOANED in /GLOBAL/
	PUSHAQ	NASTYPREFIX		; prefix is "!!!"
	CALLS	#1,G^EXPAND		; put prefix in buffer
	BRB	LOOKUP			; join common code

; CALL INFORM(messno[, arg, arg, ...])
 
 .ENTRY INFORM,^M<>
	CALLS	#0,INFRMR		; look at INFOPT in /OPTION/
	BLBC	R0,DONE			; suppress if not INFOPT
	CALLS	#0,G^EXPAND		; null prefix 
	BRB	LOOKUP			; join common code

; CALL LEARN(messno[, arg, arg, ...])
 
 .ENTRY	LEARN,^M<>
	CALLS	#0,LEARNR		; look at LEAOPT in /OPTION/
	BLBC	R0,DONE			; suppress if not LEAOPT
	CALLS	#0,INFRMR		; look at INFOPT in /OPTION/
	BLBC	R0,DONE			; suppress if not INFOPT
	PUSHAQ	LEARNPREFIX		; prefix is "..."
	CALLS	#1,G^EXPAND		; put prefix in buffer
	BRB	LOOKUP			; join common code

; LOOKUP passes argument list on to WRITEF, having first substituted
; the format string for the message number.

LOOKUP:	CVTWL	@4(AP),R1		; moan number
	DECL	R1			; as offset
	MOVAQ	MOANDSC[R1],R1		; address of message descriptor
	MOVZWL	(AP),R0			; number of args
	DECL	R0			; just the extra ones
	BLEQ	2$			; no extra
1$:	PUSHL	4(AP)[R0]		; stack the args
	SOBGTR	R0,1$			; for all args
2$:	PUSHL	R1			; moan descriptor (first arg)
	CALLS	(AP),G^WRTAPP		; and pass it all on
DONE:	RET
 
	DATA_PSECT	LITES

MOANPREFIX:	.ASCID	/??? /		; prefix for MOAN errors
NASTYPREFIX:	.ASCID	/!!! /		; prefix for NASTY errors
LEARNPREFIX:	.ASCID	/... /		; prefix for LEARN information

	.END
