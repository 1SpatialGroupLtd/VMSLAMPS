C
C Beware changes to the real SETLIN!!!
C
	SUBROUTINE SETLIN
C
***	MODULE	SETLIN
***	IDENT	12NO93
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C
C To set the line style for lines, curves, and circle arcs
C according to the feature code set up in FRTCOM.
C Sets up pattern and line width.
C Or to set interior style for areas.
C
C This special version always uses the initial value of SCLDIS,
C so that when using segments on the Sigmex, line width is correct
C for initial draw scale.
C 
	IMPLICIT NONE
C
***	PARAMETER/CMN:GKSPAR/
***	PARAMETER/CMN:MOANMESS/
C
***	COMMON/CMN:DRAWIN/
***	COMMON/LSL$CMNFRT:FRTCOM/
***	COMMON/LSL$CMNFRT:FRTFIL/
***	COMMON/CMN:OPTION/
C
C w/s
	REAL		CURWID		! linewidth scale factor
	LOGICAL		OK(2)		! had error substituting symbols?
	INTEGER		SYMB(2)		! symbol not substituted
	INTEGER		PATT		! in pattern
C
	REAL		SCLD/0.0/
	LOGICAL*1	FIRST
	SAVE		SCLD,FIRST
C
C Functions
	LOGICAL	  FILFND
	LOGICAL*1 PATSET
C
	IF (SEGOPT) THEN
	   IF (FIRST) THEN
	      SCLD = SCLDIS
	      FIRST = .FALSE.
	   ENDIF
	ELSE
	   SCLD = SCLDIS
	ENDIF
C
	IF (FRTARE .AND. FRTSC .GE. -1) THEN	! it's a normal area
C
	   IF (FILOPT) THEN
	      CALL GSFAIS(FRTAST)	! interior style
	      CALL GSFASI(FRTAIX)	! style index
	   ELSE
	      CALL GSFAIS(GHOLOW)
	   ENDIF
	   CALL LSG_BEGIN_AREA		! begin composite area
C
	ELSE				! it's a line or pattern area
C
C do we need a pattern?
	   IF (PATOPT.AND.(FRTSC.GT.0)) THEN	! set up the pattern
	      IF (PATSET(FRTSC)) THEN
	         CALL MOAN(MSG_PATNOTFND,FRTSC)
	      ELSE
C
C Check to see if any substitution symbols have been suppressed
	         CALL PATERR(OK,SYMB,PATT)
	         IF (.NOT.OK(1)) CALL MOAN(MSG_UNSUBSYM,SYMB(1),PATT)
	         IF (.NOT.OK(2)) CALL MOAN(MSG_UNSUBSYM,SYMB(2),PATT)
	      ENDIF
C
C do we need pattern fill ?
	   ELSEIF (FRTARE) THEN			! patterned fill area
	      IF (FILFND(FRTSC)) THEN
	         CALL MOAN(MSG_PAFNOTFND,FRTSC)
	      ELSE
	         IF (FILOPT.AND.PATOPT) THEN
	            CALL FRT_BEGIN_FILL		! begin composite area
	         ELSE			! do as ordinary area
	            IF (FILOPT) THEN
	               CALL GSFAIS(GHATCH)	! interior style
	               CALL GSFASI(FILPAT)	! style index
	            ELSE
	               CALL GSFAIS(GHOLOW)
	            ENDIF
	            CALL LSG_BEGIN_AREA		! begin composite area
	         ENDIF
	      ENDIF
	   ENDIF
C
C line width
	   CURWID = 1.0
	   IF (THKOPT) CURWID = (FRTWID*SCLFRT)/(NOMLW*SCLD)
C
C width 0 in FRT means 'normal width' (shouldn't be negative)
	   IF (CURWID.LE.0.0) CURWID = 1.0
C
C set the line width
	   CALL GSLWSC(CURWID)
	ENDIF
C
	RETURN
C
C Entry point to be called from device dependent INIWK, so that first
C call to SETLIN above sets the scale
	ENTRY SETLIN_RESET
	FIRST = .TRUE.
	SCLD = 0.0
	RETURN
C
C Entry point to be called from device dependent LRSGWK, to reset
C FRTLIB nominal line thickness to that appropriateto initial scale.
C (FLVIEW varies it as we zoom)
	ENTRY SETLIN_SETFRT
	IF (SCLD.EQ.0.0) SCLD = SCLDIS
	CALL SRISLW(NOMLW*SCLD/SCLFRT)
	RETURN
C
	END
