	SUBROUTINE LSVIS(PACK,IVIS)
C
***	MODULE	LSVIS
***	IDENT	22MY87
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C
C LITES2 GKS level routine
C Sigma 6100 version
C Set visibility of segment
C
C Expect to be called:
C (a) to set the currently open segment invisible (before putting
C     any primitives in it) when constructing a limbo item, or
C (b) to set a non-open segment invisible (deleting),
C     or visible (recovering).
	IMPLICIT NONE
C
***	PARAMETER/CMN:GKSPAR/
***	PARAMETER/CMN:PACK/
C
***	COMMON/CMN:DRAWIN/
***	COMMON/CMN:SEGMNT/
***	COMMON/CMN:WORKS/
C
C arguments
	INTEGER*4	PACK(PAKLEN)
	INTEGER		IVIS
C
C workspace
	INTEGER		CLASS			! segment class
C
C do nothing if no displays
	IF (NODISP) GOTO 999
C
	IF (.NOT.VIEWNG) CALL STDINI		! remove cursor
C
	IF (USESEG.AND.(.NOT.VIEWNG.OR..NOT.UPDATE_SEG)) THEN
C
C if feature is not in segment store, do nothing
	   IF ((PACK(PAKFLG).AND.SEGFLG).EQ.0) GOTO 999
C
C set Sigma 6100 selective erase mode for this operation
	   IF (.NOT.VIEWNG) CALL WSSELM(.TRUE.)
C
C for true GKS environment, would be just...
	   CALL GSVIS(PACK(PAKSEG),IVIS)
C
	   IF (.NOT.VIEWNG) THEN
C
C unset selective erase again
	      CALL WSSELM(.FALSE.)
C
C for Sigma 6100, set segment class to indicate limbo state
	      CLASS = 2				! limbo bit
	      IF (IVIS.EQ.GVISI) CLASS = 1	! normal bit
	      CALL WDSGCM(PACK(PAKSEG),CLASS)
	   ENDIF
	   GOTO 999
	ENDIF
C
C are we setting the visibility of the currently open segment?
C In fact just test that a segment is open - we never set the visibility
C of one segment while another is open.
	IF (SEGCUR.NE.0) THEN	! yes
	   IF (IVIS.EQ.GVISI) GOTO 999		! visible - do nothing
C
C mark current segment invisible
	   SEGVIS = .FALSE.
C
C temporarily deactivate display
	   IF (WKAC(1)) THEN
	      CALL GDAWK(1)
	      WKACT(1) = .FALSE.		! temporarily inactive
	   ENDIF
C
	ELSE
C
C setting visibility of an existing (not open) segment
	   IF (IVIS.EQ.GVISI) THEN		! visible
C
C just re-draw normally
	      CALL DRAFTR(PACK)
C
	   ELSE					! invisible
C
	      OVCOL = 0			! set overriding colour
	      CALL DRAFTR(PACK)		! redraw
	      OVCOL = -1		! unset overriding colour
	   ENDIF
	ENDIF
C
999	RETURN
	END
