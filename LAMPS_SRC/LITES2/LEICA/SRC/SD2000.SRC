C
	SUBROUTINE DSR_INQUIRE(CODE,VALUE)
C
***	IDENT	24MY93
***	MODULE	DSR_INQUIRE
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					CCB	26-Apr-1993
C
C	Inquire facilities of this shareable image
C
	IMPLICIT NONE
C
C arguments
	INTEGER	CODE
	INTEGER VALUE
C
C workspace
	INTEGER	IVAL
	REAL	RVAL
	EQUIVALENCE (IVAL,RVAL)
C
C code 1 is the only one we do (what stereo digitiser type)
	IF (CODE.EQ.1) THEN
	   IVAL = 1	! LEICA type
	ELSEIF (CODE.EQ.2) THEN
	   RVAL = 0.01	! mm size of pixel (for SRIUNI)
	ELSE
	   GOTO 999
	ENDIF
C
	VALUE = IVAL	
C
999	RETURN
	END
C
	LOGICAL FUNCTION DSR_INIT(ON)
C
***	IDENT	26AP93
***	MODULE	DSR_INIT
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					CCB	 2-Mar-1989
C
C	Function to initialise DSR
C	Returns .TRUE. if successful, .FALSE. otherwise
C
	IMPLICIT NONE
C
C argument
	LOGICAL	ON
C
C overlay on KERN P2PLIB Pascal common block
	INTEGER*4	P2_IO
	COMMON/P2_IO/	P2_IO
C
***	COMMON/CMN:SD2000/
C
C workspace
	LOGICAL	OK
	LOGICAL	WAS_READING
C
	DSR_INIT = .TRUE.	! assume OK
C
	IF (.NOT.ON) THEN
C Not obvious whether we should return TRUE or FALSE here.
C LITES2 ignores return anyway.
	   IF (DSR_READING) THEN
	      CALL SYS$CANCEL(%VAL(P2_IO))
C
C Cancel will trigger the AST and set user's event flag
C To prevent signal of abort in INTERACT, change status to success
C (zero in the button character will be ignored).
	      DSR_IOSB(1) = 1
	      CALL DSR_COPY(0,%VAL(BUFF_ADDR),DSR_IOSB,%VAL(IOSB_ADDR))
	   ENDIF
	   IF (DSR_MODE.NE.0) CALL DSR_SETMODE(0) ! back in polled mode
	   GOTO 9999
	ENDIF
C
C initialise, if not done already
	IF (P2_IO.EQ.0) THEN
	   CALL SUP_START_USER_PROGRAM
C
C Get Model/Ground transformations
	   CALL GMTGTM_GET_MODEL_TO_GROUND_TRAN(OK)
	   IF (.NOT.OK) GOTO 9990
	   CALL GGTMTM_GET_GROUND_TO_MODEL_TRAN(OK)
	   IF (.NOT.OK) GOTO 9990
	   CALL DSR_SETMODE(0)
	ENDIF
C
	WAS_READING = DSR_READING
	IF (DSR_READING) THEN
	   DSR_READING = .FALSE.
	   CALL SYS$CANCEL(%VAL(P2_IO))
	ENDIF
	IF (DSR_MODE.NE.0) CALL DSR_SETMODE(0)
C
C clear any button presses stored on the PC
	CALL CP2B_CLEAR_P2_BUFFER
C
C get read going again
	IF (WAS_READING) CALL DSR_START(USER_EFN,
     &		%VAL(IOSB_ADDR),%VAL(AST_ADDR),%VAL(BUFF_ADDR))
C
	GOTO 9999
C
9990	DSR_INIT = .FALSE.
C
9999	RETURN
	END
C
	SUBROUTINE DSR_MOVE(X,Y,Z,WAIT)
C
***	IDENT	02MR89
***	MODULE	DSR_MOVE
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					CCB	 2-Mar-1989
C
C	Move DSR to position
C
	IMPLICIT NONE
C
C overlay on KERN P2PLIB Pascal common block
	INTEGER*4	P2_IO
	COMMON/P2_IO/	P2_IO
C
***	COMMON/CMN:SD2000/
C
C arguments
	REAL*8	X,Y,Z
	LOGICAL	WAIT
C
C workspace
	REAL*8	XM,YM,ZM
	LOGICAL	OK
	LOGICAL	WAS_READING
C
	WAS_READING = DSR_READING
	IF (DSR_READING) THEN
	   DSR_READING = .FALSE.
	   CALL SYS$CANCEL(%VAL(P2_IO))
	ENDIF
	IF (DSR_MODE.NE.0) CALL DSR_SETMODE(0)
C
C transform ground to model
	CALL TGM_TRANSFORM_GROUND_TO_MODEL(X,Y,Z,XM,YM,ZM)
C
C move to position
	IF (WAIT) THEN
	   CALL P2MPFM_P2_MOVE_PLATES_FAST_WITH(OK,XM,YM,ZM)
C
C OK if point in range
C define stage moving possibility to be "move in Model System"
	   CALL P2DSM_P2_DEFINE_STAGE_MOTIONS(4)
	ELSE
	   CALL P2TM_P2_TRACK_WITH_MODEL_COORDI(OK,XM,YM,ZM)
	ENDIF
C
C get read going again
	IF (WAS_READING) CALL DSR_START(USER_EFN,
     &		%VAL(IOSB_ADDR),%VAL(AST_ADDR),%VAL(BUFF_ADDR))
C
	RETURN
	END
C
	SUBROUTINE DSR_READ(X,Y,Z,USE_BUTTON)
C
***	IDENT	02MR89
***	MODULE	DSR_READ
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					CCB	 2-Mar-1989
C
C	Read DSR position
C
	IMPLICIT NONE
C
C overlay on KERN P2PLIB Pascal common block
	INTEGER*4	P2_IO
	COMMON/P2_IO/	P2_IO
C
***	COMMON/CMN:SD2000/
C
C arguments
	REAL*8	X,Y,Z
	LOGICAL	USE_BUTTON	! if .T., stored coordinate will do
C
C workspace
	LOGICAL	WAS_READING
C
C will a coordinate stored when button was pressed do?
	IF (USE_BUTTON) THEN
C
C transform model to ground
	   CALL TMG_TRANSFORM_MODEL_TO_GROUND(
     &		DSR_MODEL(1),DSR_MODEL(2),DSR_MODEL(3),X,Y,Z)
	   GOTO 990
	ENDIF
C
C no, so read a real coordinate
	WAS_READING = DSR_READING
	IF (DSR_READING) THEN
	   DSR_READING = .FALSE.
	   CALL SYS$CANCEL(%VAL(P2_IO))
	ENDIF
	IF (DSR_MODE.NE.0) CALL DSR_SETMODE(0)
C
	CALL RGC_READ_GROUND_COORDINATES(X,Y,Z)
C
C get read going again
	IF (WAS_READING) CALL DSR_START(USER_EFN,
     &		%VAL(IOSB_ADDR),%VAL(AST_ADDR),%VAL(BUFF_ADDR))
C
990	RETURN
	END
C
	LOGICAL FUNCTION DSR_START(EFN,IOSB,AST,BUFF)
C
***	IDENT	02MR89
***	MODULE	DSR_START
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					CCB	 2-Mar-1989
C
C	Function to put up a read to DSR
C	Returns .TRUE. if successful, .FALSE. otherwise (error in QIO)
C
	IMPLICIT NONE
C
***	COMMON/CMN:SD2000/
C
C overlay on KERN P2PLIB Pascal common block
	INTEGER*4	P2_IO
	COMMON/P2_IO/	P2_IO
C
C Arguments
	INTEGER*4	EFN		! event flag to wait for
	INTEGER*2	IOSB(4)
	EXTERNAL	AST
	BYTE		BUFF		! buffer for output	
C
C system parameters
	INCLUDE		'($IODEF)'
C
C functions called
	INTEGER*4	SYS$QIO
	EXTERNAL	DSR_AST
C
C workspace
	INTEGER*4	IERR
	INTEGER*4	TERMSK(2)
	DATA		TERMSK/0,'2000'X/	! end on C/R
	INTEGER		I
C
	DSR_START=.TRUE.			! assume OK
C
C get the DSR into button/key stream mode
	IF (DSR_MODE.NE.1) CALL DSR_SETMODE(1)
C
C remember user's arguments
	USER_EFN = EFN
	IOSB_ADDR = %LOC(IOSB)
	AST_ADDR = %LOC(AST)
	BUFF_ADDR = %LOC(BUFF)
	DO 10 I=1,4
	   IOSB(I) = 0
10	CONTINUE
C
	DSR_READING = .TRUE.
	IERR=SYS$QIO(%VAL(EFN),			! efn
     &	             %VAL(P2_IO),		! channel
     &	             %VAL(IO$_TTYREADALL.OR.IO$M_NOECHO),! function
     &	             DSR_IOSB,			! iosb
     &	             DSR_AST,			! astadr
     &	             AST,			! astprm
     &	             DSR_BUFF,			! buffer for output
     &	             %VAL(19),			! length of buffer
     &	             ,				! p3
     &	             TERMSK,			! p4
     &	             ,				! p5
     &	              )				! p6
C
	IF (IERR) GOTO 9999			! OK
	DSR_START=.FALSE.			! QIO failed??
C
9999	RETURN
	END
C
	INTEGER FUNCTION DSR_TRN(CH)
C
***	IDENT	02MR89
***	MODULE	DSR_TRN
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					CCB	 2-Mar-1989
C
C	Translate a character from DSR P3 to button number
C
	IMPLICIT NONE
C
C Arguments
	BYTE		CH		! the character
C
C just return the character itself (zero extended), but subtract 1
C so that ASCII n will correspond to puck button n
	DSR_TRN = (CH.AND.255) - 1
C
	RETURN
	END
C
 	SUBROUTINE DSR_SETMODE(MODE)
C
***	IDENT	14AP93
***	MODULE	DSR_SETMODE
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					CCB	14-Apr-1993
C
C	Set DSR to stream/command mode (opcode 37)
C	This is not provided in P2PLIB
C
	IMPLICIT NONE
C
***	COMMON/CMN:SD2000/
C
C argument
	INTEGER*4	MODE
C
C workspace
	INTEGER*4	SEND_WORD(3)
	LOGICAL		TIME_OUT
	INTEGER*4	DESC(8)
C
C Try to put into event stream mode
	SEND_WORD(1) = MODE	! button/key stream=1, command=0
	CALL RTI_REAL_TO_INTEGER(0.0D0,SEND_WORD(2),SEND_WORD(3))
C
C Pascal expecting SEND_WORD argument by this horrid descriptor
C - only fill in the fields required
	DESC(5) = %LOC(SEND_WORD) - 4
	DESC(7) = 1
	DESC(8) = 3
	CALL P2SST_SEND_STRING_TO_P2(37,3,DESC,TIME_OUT)
	DSR_MODE = MODE
C
	RETURN
	END
C
 	SUBROUTINE DSR_AST(AST)
C
***	IDENT	14AP93
***	MODULE	DSR_AST
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					CCB	14-Apr-1993
C
C IO completion AST for SD2000 read
C
	IMPLICIT NONE
C
	INCLUDE	'($SSDEF)'
***	COMMON/CMN:SD2000/
C
C argument
	EXTERNAL	AST
C
C workspace
	INTEGER	B
	INTEGER	IB(17)
	INTEGER	I,P
	INTEGER	CHECK,RCHECK
C
	IF (.NOT.DSR_READING) GOTO 999
	IF (DSR_IOSB(1).EQ.0) GOTO 999	! ignore spurious call
	DSR_READING = .FALSE.
	B = 0
	IF (.NOT.DSR_IOSB(1) .OR. DSR_IOSB(2).NE.18) GOTO 990
	IF (DSR_BUFF(1).NE.2 .OR. DSR_BUFF(19).NE.13) GOTO 900
C
C get the characters into integers and form checksum
	CHECK = 0
	DO 10 I=1,16
	   IB(I) = (DSR_BUFF(I+1).AND.'FF'X) - 32
	   CHECK = CHECK + IB(I)
10	CONTINUE
	CHECK = MOD(CHECK,64) + 32
	RCHECK = DSR_BUFF(18).AND.'FF'X
	IF (CHECK.NE.RCHECK) GOTO 900
C
C save the button codes
	DO 20 I=1,4
	   DSR_BUTTON(I) = IB(I)
20	CONTINUE
C
C convert 3 coordinates
	P = 5
	DO 30 I=1,3
	   DSR_MODEL(I) = IB(P)   * '40000'X +
     &			  IB(P+1) *  '1000'X +
     &			  IB(P+2) *    '40'X +
     &			  IB(P+3)
	   IF (DSR_MODEL(I).GE.8388608) DSR_MODEL(I) = 8388608-DSR_MODEL(I)
	   P = P+4
30	CONTINUE
C
C try to make something of the button codes
	I = DSR_BUTTON(2)*4096 + DSR_BUTTON(3)*64 + DSR_BUTTON(4)
	B = MOD(I,256)			! ASCII code
	IF (B.EQ.0) B = I/256 + 128	! no ASCII, so scan code
	IF (DSR_BUTTON(1).EQ.1) B = MOD(B,32)	! button, or pedal
C
	GOTO 990
C
900	DSR_IOSB(1) = SS$_ABORT
C
C copy over the IOSB and returned character
990	CALL DSR_COPY(B,%VAL(BUFF_ADDR),DSR_IOSB,%VAL(IOSB_ADDR))
C
C call the user's AST routine (passed as our AST parameter)
	IF (%LOC(AST).NE.0) CALL AST
C
999	RETURN
	END
C
 	SUBROUTINE DSR_COPY(B1,B2,IOSB1,IOSB2)
C
***	IDENT	14AP93
***	MODULE	DSR_COPY
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					CCB	14-Apr-1993
C
C Just to copy things into variables we have the address of
C
	IMPLICIT NONE
C
C arguments
	BYTE		B1,B2
	INTEGER*2	IOSB1(4),IOSB2(4)
C
C workspace
	INTEGER		I
C
	B2 = B1
	DO 10 I=1,4
	   IOSB2(I) = IOSB1(I)
10	CONTINUE
	RETURN
	END
