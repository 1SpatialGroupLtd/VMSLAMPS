!
! define _HA_ANNOTATION macro for drawing annotation specified in a file
! ensure lcm only invoked once, so multiple applications can use it
!
%ASK STATUS VARIABLE HA_MINX
%TEST $ASK_INT1>0
   %THEN %ABORT FILE
!
%DECLARE REAL HA_MINX
%DECLARE REAL HA_MAXX
%DECLARE REAL HA_MINY
%DECLARE REAL HA_MAXY
%DECLARE INTEGER HA_I
%DECLARE REAL HA_X
%DECLARE REAL HA_Y
%DECLARE REAL HA_H
%DECLARE INTEGER HA_LFC
%DECLARE REAL HA_L
%DECLARE INTEGER HA_J
%DECLARE REAL HA_JMINX
%DECLARE REAL HA_JMINY
%DECLARE REAL HA_FX
%DECLARE REAL HA_FY
%DECLARE REAL HA_FH
%DECLARE REAL HA_TX
%DECLARE REAL HA_TY
!
%MACRO _HA_ANNOTATION
!
! set up defaults
   %LET HA_H=1.0
   %LET HA_L=1.5
   %LET HA_LFC=0
   %LET HA_J=0
   %LET HA_JMINX=0.0
   %LET HA_JMINY=0.0
!
   %FILE OPEN 2 '$P1
!
! set up coefficients for user co-ords to IFF co-ord transformation
!
!          X = f  * (x + t )
!               x         x
!
!          Y = f  * (y + t )
!               y         y
!
   %LET HA_FX=('$WINDOW2-'$WINDOW1)*0.01
   %LET HA_FY=('$WINDOW4-'$WINDOW3)*0.01
   %LET HA_TX=('$WINDOW1*100.0)/('$WINDOW2-'$WINDOW1)
   %LET HA_TY=('$WINDOW3*100.0)/('$WINDOW4-'$WINDOW3)
!
! transform HA_H to screen mm
   %LET HA_FH='UT_SCREEN_HEIGHT*0.01
!
.RECORD:
   %FILE READ
   %TEST $EOF
      %JTRUE .END
!
! first character
   %ASK STRING ASCII '$FILELINE
   %LET HA_I='$ASK_INT1-48
! comment line?
   %TEST HA_I=-15
      %JTRUE .RECORD
!
! second digit
   %ASK STRING EXTRACT 2 2 "'$FILELINE "
   %ASK STRING ASCII "'$ASK_CHAR1"
! space or tab?
   %TEST $ASK_INT1=32
   %OR   $ASK_INT1=9
      %ELSE %LET HA_I='HA_I*10+'$ASK_INT1-48
!
! force substitution of variables
   %TEST I='I#%THEN %LET STRING='$FILELINE
!
   %TEST HA_I<5
   %OR   HA_I>6
      %JTRUE .NOT_TEXT
!
! 5 or 6 - text
   %ASK STRING LENGTH "'STRING"
!
   %TEST $P2="SCAN"
      %JTRUE .SCAN_TEXT
!
   %ASK STRING EXTRACT 3 '$ASK_INT1 "'STRING"
   %TEST HA_I=6
      %THEN %LET HA_Y='HA_Y - 'HA_L*('HA_H/'$SCRFACTOR)
      %THEN %DRAW MOVE 'HA_X 'HA_Y
!
   %DRAW TITLE "'$ASK_CHAR1"
   %JUMP .RECORD
!
.SCAN_TEXT:
   %TEST HA_I=6
      %THEN %LET HA_Y='HA_Y - 'HA_L*'HA_H
!
! get text box size
   %ASK STRING ANNOTATION "'STRING"
   %LET _REAL1='$ASK_REAL1*100.0/('$WINDOW2-'$WINDOW1)
   %LET _REAL2='$ASK_REAL2*100.0/('$WINDOW4-'$WINDOW3)
   %LET W='HA_X + ('HA_JMINX * '_REAL1)
   %LET Z='HA_Y + ('HA_JMINY * '_REAL2)
   %TEST W<'HA_MINX
      %THEN %LET HA_MINX='W
   %TEST Z<'HA_MINY
      %THEN %LET HA_MINY='Z
   %LET W='HA_X + (('HA_JMINX+1.0) * '_REAL1)
   %LET Z='HA_Y + (('HA_JMINY+1.0) * '_REAL2)
   %TEST W>'HA_MAXX
      %THEN %LET HA_MAXX='W
   %TEST Z>'HA_MAXY
      %THEN %LET HA_MAXY='Z
   %JUMP .RECORD
!
.NOT_TEXT:
   %ASK STRING COMPRESS 'STRING
   %LET STRING='$ASK_CHAR1
   %ASK STRING ELEMENT 2 " " " 'STRING "
   %JUMP .P_'HA_I
!
! move
.P_0:
   %LET HA_X='$ASK_CHAR1   
   %ASK STRING ELEMENT 3 " " " 'STRING "
   %LET HA_Y='$ASK_CHAR1   
   %TEST $P2="SCAN"
      %JTRUE .SCAN_P_0
! transform HA_X,HA_Y to IFF co-ordinates
   %LET HA_X='HA_FX*('HA_X+'HA_TX)
   %LET HA_Y='HA_FY*('HA_Y+'HA_TY)
   %DRAW MOVE 'HA_X 'HA_Y
   %JUMP .RECORD
!
.SCAN_P_0:
   %TEST HA_X<'HA_MINX
      %THEN %LET HA_MINX='HA_X
   %TEST HA_X>'HA_MAXX
      %THEN %LET HA_MAXX='HA_X
   %TEST HA_Y<'HA_MINY
      %THEN %LET HA_MINY='HA_Y
   %TEST HA_Y>'HA_MAXY
      %THEN %LET HA_MAXY='HA_Y
   %JUMP .RECORD
!
! draw
.P_1:
   %LET HA_X='$ASK_CHAR1   
   %ASK STRING ELEMENT 3 " " " 'STRING "
   %LET HA_Y='$ASK_CHAR1   
   %TEST $P2="SCAN"
      %JTRUE .SCAN_P_0
! transform HA_X,HA_Y to IFF co-ordinates
   %LET HA_X='HA_FX*('HA_X+'HA_TX)
   %LET HA_Y='HA_FY*('HA_Y+'HA_TY)
   %DRAW VECTOR 'HA_X 'HA_Y
   %JUMP .RECORD
!
.SCAN_P_1:
   %TEST HA_X<'HA_MINX
      %THEN %LET HA_MINX='HA_X
   %TEST HA_X>'HA_MAXX
      %THEN %LET HA_MAXX='HA_X
   %TEST HA_Y<'HA_MINY
      %THEN %LET HA_MINY='HA_Y
   %TEST HA_Y>'HA_MAXY
      %THEN %LET HA_MAXY='HA_Y
   %JUMP .RECORD
!
.P_4:
! Draw fill area rectangle, width rw, height rh
   %LET W='$ASK_CHAR1   
   %ASK STRING ELEMENT 3 " " " 'STRING "
   %LET Z='$ASK_CHAR1   
   %TEST $P2="SCAN"
      %JTRUE .SCAN_P_4
! transform width (W) and height (Z) to IFF co-ordinates
   %LET W='W*'HA_FX
   %LET Z='Z*'HA_FY
   %TEST $REGION 10
      %THEN %CANCEL REGION 10
   %REGION 10 POINT 'HA_X 'HA_Y
   %LET HA_X='HA_X+'W
   %REGION 10 POINT 'HA_X 'HA_Y
   %LET HA_Y='HA_Y+'Z
   %REGION 10 POINT 'HA_X 'HA_Y
   %LET HA_X='HA_X-'W
   %REGION 10 POINT 'HA_X 'HA_Y
   %LET HA_Y='HA_Y-'Z
   %DRAW AREAREGION 10
   %JUMP .RECORD
!
.SCAN_P_4:
   %TEST HA_MAXX<'HA_X + 'W
      %THEN %LET HA_MAXX='HA_X + 'W
   %TEST HA_MAXY<'HA_Y + 'Z
      %THEN %LET HA_MAXY='HA_Y + 'Z
   %JUMP .RECORD
!
! set size
.P_11:
   %LET Z='$ASK_CHAR1
   %TEST $P2="SCAN"
      %THEN %LET HA_H='Z
      %ELSE %LET HA_H='Z*'HA_FH
   %ANNOTATION SIZE 'HA_H
   %JUMP .RECORD
!
! set overlay
.P_12:
   %ANNOTATION OVERLAY '$ASK_CHAR1   
   %JUMP .RECORD
!
! set colour
.P_13:
   %ANNOTATION COLOUR '$ASK_CHAR1   
   %JUMP .RECORD
!
! set justification
.P_14:
   %LET HA_J='$ASK_CHAR1
   %ANNOTATION LOCATION 'HA_J
   %LET _INT1='HA_J/3
   %LET HA_JMINX=-0.5*'_INT1
   %LET _INT1='HA_J-(3*('HA_J/3))
   %LET HA_JMINY=-0.5*'_INT1
   %JUMP .RECORD
!
! set line spacing
.P_18:
   %LET HA_L='$ASK_CHAR1   
   %JUMP .RECORD
!
! set output size (MINX,MINY,MAXX,MAXY) and terminate on scan
.P_20:
   %TEST $P2="SCAN"
      %JFALSE .RECORD
   %LET Z='$ASK_CHAR1   
   %TEST Z<'HA_MINX
      %THEN %LET HA_MINX='Z
   %ASK STRING ELEMENT 3 " " " 'STRING "
   %LET Z='$ASK_CHAR1   
   %TEST Z<'HA_MINY
      %THEN %LET HA_MINY='Z
   %ASK STRING ELEMENT 4 " " " 'STRING "
   %LET Z='$ASK_CHAR1   
   %TEST Z>'HA_MAXX
      %THEN %LET HA_MAXX='Z
   %ASK STRING ELEMENT 5 " " " 'STRING "
   %LET Z='$ASK_CHAR1   
   %TEST Z>'HA_MAXY
      %THEN %LET HA_MAXY='Z
   %JUMP .END
!
.END:
   %FILE CLOSE 2
   %POSITION
   %ANNOTATION LOCATION 0
   %JUMP _XGIS_END
%ENDMACRO
