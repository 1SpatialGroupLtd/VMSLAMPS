	LOGICAL FUNCTION DSR_INIT
C
***	IDENT	06MY92
***	MODULE	DSR_INIT
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					CCB	 2-Mar-1989
C
C	Function to initialise DSR
C	Returns .TRUE. if successful, .FALSE. otherwise
C
	IMPLICIT NONE
C
C overlay on KERN P2PLIB Pascal common block
	INTEGER*4	P3_IO
	COMMON/P3_IO/	P3_IO
C
C workspace
	LOGICAL	OK
C
	DSR_INIT = .TRUE.	! assume OK
C
C initialise, if not done already
	IF (P3_IO.EQ.0) THEN
	   CALL SUP_START_USER_PROGRAM
C
C Get Model/Ground transformations
	   CALL GMTGTM_GET_MODEL_TO_GROUND_TRAN(OK)
	   IF (.NOT.OK) GOTO 9990
	   CALL GGTMTM_GET_GROUND_TO_MODEL_TRAN(OK)
	   IF (.NOT.OK) GOTO 9990
	ENDIF
	GOTO 9999
C
9990	DSR_INIT = .FALSE.
C
9999	RETURN
	END
C
	SUBROUTINE DSR_MOVE(X,Y,Z,WAIT)
C
***	IDENT	02MR89
***	MODULE	DSR_MOVE
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					CCB	 2-Mar-1989
C
C	Move DSR to position
C
	IMPLICIT NONE
C
C arguments
	REAL*8	X,Y,Z
	LOGICAL	WAIT
C
C workspace
	REAL*8	XM,YM,ZM
	LOGICAL	OK
C
C transform ground to model
	CALL TGM_TRANSFORM_GROUND_TO_MODEL(X,Y,Z,XM,YM,ZM)
C
C move to position
C SPOT plate processor program has not had the 'track' routine added
***	UNLESS SPOT
	IF (WAIT) THEN
***	IFTF
	   CALL P2MPFM_P2_MOVE_PLATES_FAST_WITH(OK,XM,YM,ZM)
***	IFF
	ELSE
	   CALL P2TM_P2_TRACK_WITH_MODEL_COORDI(OK,XM,YM,ZM)
	ENDIF
***	ENDC
C
C OK if point in range
C define stage moving possibility to be "move in Model System"
	CALL P2DSM_P2_DEFINE_STAGE_MOTIONS(4)
C
	RETURN
	END
C
	SUBROUTINE DSR_READ(X,Y,Z)
C
***	IDENT	02MR89
***	MODULE	DSR_READ
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					CCB	 2-Mar-1989
C
C	Read DSR position
C
	IMPLICIT NONE
C
C arguments
	REAL*8	X,Y,Z
C
	CALL RGC_READ_GROUND_COORDINATES(X,Y,Z)
C
	RETURN
	END
C
	LOGICAL FUNCTION DSR_START(EFN,IOSB,AST,BUFF)
C
***	IDENT	02MR89
***	MODULE	DSR_START
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					CCB	 2-Mar-1989
C
C	Function to put up a read to DSR
C	Returns .TRUE. if successful, .FALSE. otherwise (error in QIO)
C
	IMPLICIT NONE
C
C overlay on KERN P2PLIB Pascal common block
	INTEGER*4	P3_IO
	COMMON/P3_IO/	P3_IO
C
C Arguments
	INTEGER*4	EFN		! event flag to wait for
	INTEGER*2	IOSB(4)
	EXTERNAL	AST
	BYTE		BUFF		! buffer for output	
C
C system parameters
	INCLUDE		'($IODEF)'
C
C functions called
	INTEGER*4	SYS$QIO
C
C workspace
	INTEGER*4	IERR
C
	DSR_START=.TRUE.			! assume OK
C
	IERR=SYS$QIO(%VAL(EFN),			! efn
     &	             %VAL(P3_IO),		! channel
     &	             %VAL(IO$_TTYREADALL.OR.IO$M_NOECHO),! function
     &	             IOSB,			! iosb
     &	             AST,			! astadr
     &	             ,				! astprm
     &	             BUFF,			! buffer for output
     &	             %VAL(1),			! length of buffer
     &	             ,				! p3
     &	             ,				! p4
     &	             ,				! p5
     &	              )				! p6
C
	IF (IERR) GOTO 9999			! OK
	DSR_START=.FALSE.			! QIO failed??
C
9999	RETURN
	END
C
	INTEGER FUNCTION DSR_TRN(CH)
C
***	IDENT	02MR89
***	MODULE	DSR_TRN
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					CCB	 2-Mar-1989
C
C	Translate a character from DSR P3 to button number
C
	IMPLICIT NONE
C
C Arguments
	BYTE		CH		! the character
C
	DSR_TRN = -1			! assume naff
C
C pedals return 16-19 for left down/up, right down/up
	IF (CH.GE.'A'.AND.CH.LE.'D') THEN
	   DSR_TRN = 84-CH
	ELSEIF (CH.GE.'0'.AND.CH.LE.'9') THEN
	   DSR_TRN = CH-'0'
	ELSEIF (CH.EQ.127) THEN
	   DSR_TRN = 10
	ELSEIF (CH.EQ.':') THEN
	   DSR_TRN = 11
	ELSEIF (CH.EQ.'E') THEN
	   DSR_TRN = 12
	ELSEIF (CH.EQ.13) THEN
	   DSR_TRN = 13
	ELSEIF (CH.EQ.'-') THEN
	   DSR_TRN = 14
	ELSEIF (CH.EQ.'.') THEN
	   DSR_TRN = 15
	ENDIF
C
9999	RETURN
	END
