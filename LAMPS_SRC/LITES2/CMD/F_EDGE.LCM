! F_EDGE.LCM - example command file using macros to edge match along a feature
!
! Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
!
! written by Ron Russell,				23-Nov-1987
!
! read this command file into LITES2 (while in INITIAL or READY state)
! giving the command @F_EDGE
!
! main commands are:
!
! 1) F_EDGE - a macro to find base feature, and edge match to it
!
! set up environment
!
%ENABLE SUBSTITUTION
%DISA LEARN
!
! declaration of variables
!
%DECLARE INTEGER _BASEFSN
!!!%DECLARE INTEGER _BASELYR
%DECLARE INTEGER _BASEMAP
!!!%DECLARE INTEGER _BASEFC
%DECLARE INTEGER _BASEID	!!!!!!!
%DECLARE INTEGER _CURVRTEX
%DECLARE CHARACTER _EDGE_TYPE
%DECLARE INTEGER _EDGEQ
!
! macro to find feature to edge match to, and to set up edge matching
!
!........................................................................
%MACRO F_EDGE
  !
  %ABANDON
  %ENABLE SUBSTITUTION
  !
  %MESSAGE "Select type of edgematching required"
  %MESSAGE "Enter 1 for TIE"
  %MESSAGE "      2 for JOIN"
  %MESSAGE "      3 for EXTEND"
  %INQUIRE _edgeq "Selection (1,2 or 3): "
  %TEST _EDGEQ .LT.1
  %OR _EDGEQ.GT.3
  %THEN %MESSAGE "Invalid selection"
  %THEN %PING
  %JTRUE F_EDGE
  %TEST _EDGEQ.EQ.1
  %THEN %LET _EDGE_TYPE = TIE
  %TEST _EDGEQ.EQ.2
  %THEN %LET _EDGE_TYPE = JOIN
  %TEST _EDGEQ.EQ.3
  %THEN %LET _EDGE_TYPE = EXTEND
  !
  %MESSAGE Move to feature to edge match to and enter CONTINUE
  %RESPOND
  %ENABLE CONTINUE
  %FIND
  %DISABLE CONTINUE
  %TEST $FOUND
  %JFALSE _F_EDGE_END
  %LET _BASEFSN = '$FSN
  !!!!%LET _BASELYR = '$LAYER
  %LET _BASEMAP = '$MAP
  !!!%LET _BASEFC  = '$FC
  %LET _BASEID = '$IFFADDR
  !
  ! initialise walk along this feature
  !
  %LET _CURVRTEX = 1
  %DISABLE APPEND		!!!!!!!
  !
  ! and start edge matching
  !
  %JUMP _F_EDGE
%ENDMACRO
!
!........................................................................
! macros to find base
!
%MACRO _FND_BASE
  %ENABLE CONTINUE
  %SEARCH FSN '_BASEFSN
  %DISABLE CONTINUE
  %TEST $FOUND
  %JFALSE _F_EDGE_ERROR
  ! is it the correct feature
  !!!!%TEST _BASELYR = '$LAYER
  %TEST _BASEID = '$IFFADDR	!!!!!!!!
  %AND  _BASEMAP = '$MAP
  !!!!!%AND  _BASEFC  = '$FC
  !
  %JFALSE _FND_BASE_AGAIN
%ENDMACRO
!
%MACRO _FND_BASE_AGAIN
  %ENABLE CONTINUE
  %SEARCH 
  %DISABLE CONTINUE
  %TEST $FOUND
  %JFALSE _F_EDGE_ERROR
  ! is it the correct feature
  !!!!%TEST _BASELYR = '$LAYER
  %TEST _BASEID = '$IFFADDR	!!!!!!!!
  %AND  _BASEMAP = '$MAP
  !!!!!%AND  _BASEFC  = '$FC
  !
  %JFALSE _FND_BASE_AGAIN
%ENDMACRO
! 
!........................................................................
! macro to move to next vector on base, and edge match to it
%MACRO _F_EDGE
  !
  ! FIND BASE FEATURE
  _FND_BASE
  !
  ! move to current vertex
  !
  %VERTEX '_CURVRTEX
  %TEST $LAST
  %THEN %ABANDON
  %JTRUE _F_EDGE_END
  %TEST _CURVRTEX .EQ. 2		!!!!!!
  %THEN %ENABLE APPEND		!!!!!!
  %LET _CURVRTEX = '_CURVRTEX + 1
  %BASE EDGE
  %EDGE '_EDGE_TYPE
  %JUMP _F_EDGE
%ENDMACRO
!
!........................................................................
! error exit
MACRO _F_EDGE_ERROR
  %MESSAGE "Error while edgematching"
  %JUMP _F_EDGE_END
ENDMACRO
!
!
!........................................................................
! normal exit
MACRO _F_EDGE_END
  %DISABLE APPEND
  %MESSAGE "Edge matching completed"
ENDMACRO
!
!
