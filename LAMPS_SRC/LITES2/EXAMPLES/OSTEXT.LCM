!
! OS text hidden line deletion 
!
! LITES2 commands required :-
!
!	@OSTEXT		to read the macros in
!	OSTEXTINI	to initialise hidden line deletion
!	OSTEXT		to delete all line segments hidden by texts
!
! delete or clip the current feature, i.e. scratch it out !
!
MACRO SCRATCH
%TEST $INREGION 1		! is it inside the region ?
%THEN DELETE			! if so, delete it
%ELSE CLIP OUTREGION 1		! if not, it must cut the region, so clip
ENDMACRO
!
! deal with the current feature if necessary, and find the next feature
!
MACRO NEXTFEAT
%TEST $STATE="LINE"		! is it a line ?
%THEN SCRATCH			! get rid of the relevant bit
%SEARCH NEXT
%TEST $FOUND			! got another line ?
%JTRUE NEXTFEAT			! if so deal with it
ENDMACRO
!
! deal with a single text
!
MACRO SINTEXT
%REGION 1			! define a region round the text
%LET FSN = '$FSN
%ABANDON			! abandon the text now
%DESELECT FSN 'FSN		! not interested in it any more
%DESELECT FC 28
%SELECT FC 0-27,29-32767
%SELECT INREGION 1
%SELECT CUTREGION 1
%SEARCH ALL			! find first feature
%TEST $FOUND			! check that there is one
%THEN NEXTFEAT			! and do something with it
%CANCEL REGION 1		! cancel the region 
%SELECT FC 28			! reselect texts
%JUMP DELHIDDEN			! and go to do another text
ENDMACRO
!
! locate a text
!
MACRO DELHIDDEN
%DESELECT FC 0-27,29-32767
%LOCATE ALL			! find a text
%TEST $FOUND
%JTRUE SINTEXT			! if there is one, deal with it
ENDMACRO
!
! start it all off
!
MACRO OSTEXT
%SELECT ALL
DELHIDDEN
%SELECT ALL
%DRAW SCREEN
ENDMACRO
!
! initialise
!
MACRO OSTEXTINI
%ENABLE SUBSTITUTION
%ENABLE CONTINUE
%DECLARE INTEGER FSN
ENDMACRO
