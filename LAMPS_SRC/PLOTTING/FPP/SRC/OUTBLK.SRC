	SUBROUTINE OUTBLK
C
***	MODULE	OUTBLK
***	IDENT	20FE92
C
C Copyright Laser Scan Laboratories Ltd, Cambridge, England.
C Author R.Daly.
C
C Created.					RD	12-Sep-1983
C
C Routine to flush out a block of coords which have been buffered
C up by routine ADDBLK. Routine is called when the buffer is full
C and when the end of the feature (EF entry) has been found.
C
	IMPLICIT NONE
C
***	COMMON/CMN:GRABUF/
C
C this is a jacket routine that passes the routine that does the work
C the arrays in the common block
	CALL OUTBLK_SUB(OUTPTR,BLOSIZ,
     &	                %VAL(XOUT_ADDR),%VAL(YOUT_ADDR))
C
	RETURN
	END

	SUBROUTINE OUTBLK_SUB(OUTPTR,BLOSIZ,XOUT,YOUT)
C
***	MODULE	OUTBLK_SUB
***	IDENT	20FE92
C
C Copyright Laser Scan Laboratories Ltd, Cambridge, England.
C Author R.Daly.
C
C Created.					RD	12-Sep-1983
C
C Routine to flush out a block of coords which have been buffered
C up by routine ADDBLK. Routine is called when the buffer is full
C and when the end of the feature (EF entry) has been found.
C
	IMPLICIT NONE
C
***	COMMON/CMN:CURFT/
***	COMMON/CMN:DRAWIN/
***	COMMON/CMN:FPPDAT/
***	COMMON/LSL$CMNFRT:FRTCOM/
C
C arguments
	INTEGER	OUTPTR
	INTEGER	BLOSIZ
	REAL	XOUT(BLOSIZ)
	REAL	YOUT(BLOSIZ)
C
C workspace
	LOGICAL		OK(2)		! had error substituting symbols?
	INTEGER		SYMB(2)
	INTEGER		PATT		! in pattern
C
	IF (PATLIN.AND.FRTSC.NE.0.AND.PATHWL.EQ.0
     &	      .AND.(FRTLIN.OR.FRTARC.OR.FRTCUR).AND.OUTPTR.GT.0) THEN
	   IF (INVSEG.AND.FRTCLP) THEN		! maintain patt phase
	      INVSEG = .FALSE.
	      CALL PATACT(.FALSE.)
	      CALL PATGPL(1,XOUT,YOUT)		! invis to first point
	      CALL PATACT(.TRUE.)
	   ENDIF
	   IF (OUTPTR.GT.1) THEN
	      CALL PATGPL(OUTPTR,XOUT,YOUT)	! patterned line
C
C check for error
	      CALL PATERR(OK,SYMB,PATT)
C
C check if symbol substitution failed
	      IF (.NOT.OK(1)) CALL WRITEF(
     &	'Major symbol substitution failed in line pattern %N',PATT)
	      IF (.NOT.OK(2)) CALL WRITEF(
     &	'Minor symbol substitution failed in line pattern %N',PATT)
	      IF (.NOT.(OK(1).AND.OK(2))) CALL WRITEF(
     &	'Layer %^W%I6 - Ft No %^L%I6 - ISQ %I6',LAYNUM,FTNUM,ISQ)
	   ENDIF
C
	ELSEIF (FRTARE.AND.OUTPTR.GT.0) THEN
	   IF (PATLIN.AND.FRTSC.LT.-1) THEN	! patterned fill
	      CALL FILLGFA(OUTPTR,XOUT,YOUT)
	   ELSE					! ordinary fill
	      CALL GFA(OUTPTR,XOUT,YOUT)
	   ENDIF
C
	ELSEIF (FRTCUR.AND.INTLIN.AND.		! curves allowed
     &		HWCUR.AND.FRTHWS.AND.		! hardware curve
     &		OUTPTR.GT.1) THEN
	   CALL GGDP(OUTPTR,XOUT,YOUT,6)
C
	ELSEIF (OUTPTR.GT.1) THEN
	   CALL GPL(OUTPTR,XOUT,YOUT)		! plain line
	ENDIF
C
	OUTPTR=0		! reset buffer pointer
C
	RETURN
	END
