	SUBROUTINE BUFFER
C
***	MODULE	BUFFER
***	IDENT	09NO88
C
C Copyright Laser Scan Laboratories Limited, Cambridge, England.
C Author R.Daly.
C
C Created.					RD	22-Jun-1983
C
C This routine is used as a buffer between IFFBDY and STDRAW.
C STDRAW needs to know if the block of coords it is sent is the
C final block for that feature but IFFBDY doesn't know whether it
C is or not until it reaches an EF entry. Consequently this
C routine keeps a block in hand until LAST is set (ie. EF found).
C Scaling and offsetting is performed on the data while it is
C being buffered.
C
	IMPLICIT NONE
C
***	COMMON/CMN:DRAWIN/
***	COMMON/CMN:SCALES/
***	COMMON/LSL$CMNFRT:FRTCOM/
C
	REAL	X,Y
	REAL	XX
	INTEGER	I,J
C
C No more coords so only output the last block (don't want
C to buffer anything.
	IF (LAST) GOTO 10
C
C If not the 1st call to BUFFER then output the previous block.
	IF (FRSTST) GOTO 20
C
10	IF (INDX.LE.0) GOTO 1000
C
C if drawing offset line, doctor last point in XCOORD and YCOORD
C
	IF (.NOT.LAST.AND.OFFSETF) THEN
C
C rotate and scale first point in this buffer
C
	   X  = BLKBUF(1,1)*SCALE
	   Y  = BLKBUF(2,1)*SCALE
	   XX = X*COSTHETA - Y*SINTHETA + TOTOFX
	   Y  = X*SINTHETA + Y*COSTHETA + TOTOFY 
	   X  = XX
C
C and use it to alter last point in X/YCOORD
C
	   CALL SRTOFF(XCOORD(INDX),YCOORD(INDX),X,Y)
	ENDIF
C
C output to drawing buffer
C
	CALL STDRAW
	IF (LAST) GOTO 1000
C
C Buffer the coordinates, rotating if required
20	DO 30 I=1,BLKPTS
	   X=BLKBUF(1,I)*SCALE
	   Y=BLKBUF(2,I)*SCALE
	   XCOORD(I) = X*COSTHETA - Y*SINTHETA + TOTOFX
	   YCOORD(I) = X*SINTHETA + Y*COSTHETA + TOTOFY
C
C transfer the attributes aswell - if necessary
	   IF (SYMBAD.NE.0.AND.FRTSYM) THEN
	      DO 25 J = 1,BLKNATT
	         XYATTV(J,I) = BLKATTV(J,I)
25	      CONTINUE
	   ENDIF
30	CONTINUE
C
C column headers
	IF (SYMBAD.NE.0.AND.FRTSYM) THEN
	   DO 35 I = 1,BLKNATT
	      XYATTC(I) = BLKATTC(I)
35	   CONTINUE
	   XYNATT = BLKNATT
	ENDIF
C
	INDX=BLKPTS
C
C offset if required
C
	IF (OFFSETF) CALL OFF(INDX,XCOORD,YCOORD)
C
	FRSTST=.FALSE.
C
1000	RETURN
C
	END
