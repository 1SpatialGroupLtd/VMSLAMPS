	SUBROUTINE DRAW_PRIO_LIST(FINISH)
C
***	MODULE	DRAW_PRIO_LIST
***	IDENT	08DE94
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author:  S Townrow
C
C Created					 ST	11 March 1992
C
C Simply calls DRAW_PRIO_LIST_SUB which sees addresses as arrays
C
	IMPLICIT NONE
C
C Argument
C
	LOGICAL		FINISH
C
***	COMMON/CMN:DRAWLIST/
C
C
C***********************************************************************
C
	IF (LIST_PTR.GT.0) THEN
	   CALL DRAW_PRIO_LIST_SUB(%VAL(PRIOLIST_LOC),
     &				   %VAL(PRIOLIST_LOC),
     &				   FINISH)
	ELSE
	   FINISH = .TRUE.
	ENDIF
C
	RETURN
	END



	SUBROUTINE DRAW_PRIO_LIST_SUB(PRIOLIST,ADDRLIST,FINISH)
C
***	MODULE	DRAW_PRIO_LIST_SUB
***	IDENT	10MA92
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author:  S Townrow
C
C Created					 ST	11 March 1992
C
C Loops thro whole priority list, calling DRAFTR to position to each feature
C to draw/plot. An opaque layer is generated for electrostatic devices between
C features of different priority. It is possible to pause after n parts and
C abort using crtl-c.
C
C
	IMPLICIT NONE
C
***	PARAMETER/CMN:GKSPAR/
***	COMMON/CMN:DRAWLIST/
***	COMMON/CMN:FPPDAT/
C
C Arguments
C
	INTEGER*2	PRIOLIST(4,LIST_PTR)
	INTEGER*4	ADDRLIST(2,LIST_PTR)
	LOGICAL		FINISH
C
C Workspace
C
	INTEGER*4	I
	INTEGER*2	PREPRI
	LOGICAL		LAB
	LOGICAL		HADCTRLC	! had a CTRL/C
C
C***********************************************************************
C
C Set the previous priority to be the maximum to force the first overlay
C
	PREPRI = LABEL_PRIO
	FINISH = .FALSE.
C
	DO 100 I = PAUSE_POS,LIST_PTR
C	   call writef('i=%^W%I0',i)
C
C Generate a hardware layer for some devices
C
	   IF (PRIOLIST(2,I).NE.PREPRI) THEN
	      CALL OVERLAY()
	   ENDIF
C
C Is this a labelling component?
C
	   LAB = PRIOLIST(2,I).EQ.LABEL_PRIO
C
	   CALL DRAFTR(	ADDRLIST(2,I),		! address of NF
     &			PRIOLIST(1,I),		! FC of part
     &			.TRUE.,			! use the PRIO rep FC
     &			LAB)			! labelling ?
C
	   PREPRI = PRIOLIST(2,I)
C
C if there's been a CTRL/C then finish
	   IF (HADCTRLC()) GOTO 150
C
C do we want to pause here?
	   IF (PAUSE_NOW) THEN
	      PAUSE_POS = I+1
	      GOTO 200
	   ENDIF

100	CONTINUE
150	FINISH = .TRUE.
C
C Have a hardware layer between IFF files
C
200	CALL GUWK(WKID,GSUPP)
C
	RETURN
	END
