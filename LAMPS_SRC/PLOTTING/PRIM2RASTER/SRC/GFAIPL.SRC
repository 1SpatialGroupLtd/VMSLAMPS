	SUBROUTINE GFAIPL(XS,XE,Y)
C
**	MODULE	GFAIPL
**	IDENT	13SE91
C
C Copyright Laser-Scan Labs., Ltd., Cambridge, England.
C
C Author Paul Hardy			PGH	29-Sep-1989
C
C	Part of the IFF-to-Raster conversion program
C
C	 - takes scan lines from area fill routine, and rasterises them
C
C
	IMPLICIT NONE
C
C
C Common blocks:
C
***	COMMON/CMN:CLIP/
***	COMMON/CMN:PRIM2RASTER/
***	COMMON/CMN:RASBUF/
C
C Arguments:
C
	REAL		XS,XE		! arrays of coords
	REAL		Y
C
C w/s:
C
	INTEGER*4	ISX,IEX,IY,ITX	! integer pixel coords
C
C code:
C
C...first check that this line is in the swathe, then convert to pixel units
C
	IY = INT(Y/RESOLUTION)
	IF (IY.LT.CLIPLO) GOTO 999	! below swathe
	IF (IY.GT.CLIPHI) GOTO 999	! above swathe
	IY = IY - CLIPLO
	ISX = NINT(XS/RESOLUTION)
	IEX = NINT(XE/RESOLUTION)
C
C get ends the right way round
	IF (IEX.LT.ISX) THEN
	   ITX = IEX
	   IEX = ISX
	   ISX = ITX
	ENDIF
C
C reduce right end by one so that e.g. a line of width 1.0 does not
C come out 2 pixels wide
	IEX = IEX-1
C
C ensure we get at least something for an intended 1 pixel line
	IF (IEX.LT.ISX) THEN
	   IF (ABS(XE-XS).LE.0.99*RESOLUTION) GOTO 999
	   ISX = NINT((XS+XE)/2.0/RESOLUTION-0.5)
	   IEX = ISX
	ENDIF
C
C mirror it if required
	IF (MIRROR) THEN
	   ITX = IEX
	   IEX = LINE_END-ISX-1
	   ISX = LINE_END-ITX-1
	ENDIF
C
C...Now rasterise it
C
	CALL RASTER_HORZ_LINE(
     &		  %VAL(ISX)			! x start
     &	        , %VAL(IEX)			! x end
     &		, %VAL(IY)			! y
     &		, %VAL(V_VALUE)			! value to set pixel
     &		, %VAL(BUFADR)			! the buffer...
     &		, %VAL(LINE_END)		!...it's width..
     &		, %VAL(LINES_PER_SWATHE))	!...it's height.
C
C End:
C
999	RETURN
	END
