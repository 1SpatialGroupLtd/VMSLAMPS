C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1986-09-12 19:15:10.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION NEW_PRIO(CURR_PRIO,CURR_DOUBT,NEW_HEIGHT,TOTAL)
C
***	IDENT	12SE86
***	MODULE	NEW_PRIO
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RJH	11-Sep-1986
C
C	OS RELHT Program - Reorder priorities and check if in doubt
C
C Description
C
C	Returns true if the relative heights are in doubt
C
	IMPLICIT NONE
C
C Arguments
C
	INTEGER*2	TOTAL			! total arms
	INTEGER*4	CURR_PRIO(TOTAL)	! priorities
	LOGICAL		CURR_DOUBT(TOTAL)	! doubts
	INTEGER*4	NEW_HEIGHT(TOTAL)	! final heights
C
C Common Blocks
C
***	COMMON/HERE:PRIO_TABLE/		! feature code priority table
C
C Local Variables
C
	INTEGER*4	FINAL_PRIO	! final relative height
	INTEGER*4	THIS_PRIO	! current priority
	INTEGER*4	THIS_ARM	! current arm
	LOGICAL		IN_DOUBT	! junction in doubt ?
	LOGICAL		HAD_THIS_PRIO	! already had current priority ?
C
C Initialisation
C
	FINAL_PRIO = -1
	IN_DOUBT = .FALSE.
C
C Go through all the possible priority values, ignoring 0 
C
	DO 200 THIS_PRIO=1,HIGH_PRIO
	   HAD_THIS_PRIO = .FALSE.	! not had the current priority
C
C For this priority value, go through each arm and check if it is the 
C same
C
	   DO 100 THIS_ARM=1,TOTAL
	      IF (CURR_PRIO(THIS_ARM) .EQ. THIS_PRIO) THEN
C
C If we haven't had an arm with the same priority, set the flag and 
C increment the final priority value
C
	         IF (.NOT. HAD_THIS_PRIO) THEN
	            HAD_THIS_PRIO = .TRUE.
	            FINAL_PRIO = FINAL_PRIO + 1
	         ENDIF
C
C See if this junction is in doubt
C
	         IF (.NOT. IN_DOUBT) THEN
	            IN_DOUBT = CURR_DOUBT(THIS_ARM)
	         ENDIF
C
C Set the final height 
C
	         NEW_HEIGHT(THIS_ARM) = FINAL_PRIO
	      ENDIF
100	   CONTINUE
200	CONTINUE
C
C Assign function value
C
	NEW_PRIO = IN_DOUBT
C
C Return
C
	RETURN
	END
