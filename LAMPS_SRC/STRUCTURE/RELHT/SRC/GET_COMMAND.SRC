	LOGICAL FUNCTION GET_COMMAND
C
***	IDENT	05SE86
***	MODULE	GET_COMMAND
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RJH	02-Sep-1986
C
C	Relative Heighting Program RELHT - Get and decode command line
C
C Description
C
C
	IMPLICIT NONE
C
C Included Parameter Files
C
	INCLUDE '($STSDEF)/NOLIST'
C
C Common Blocks
C
***	COMMON/HERE:CONTROL/		! control variables
***	COMMON/LSL$CMNLSL:CLD/		! DCL command decoding
***	COMMON/LSL$CMNLSL:FILENAME/	! LSLLIB filename expansion
C
C Functions called
C
        INTEGER*4       DCL_STARTUP	! looks for command line
	INTEGER*4	DCL_FILE	! reads & parses file title
	INTEGER*4	DCL_QUAL	! looks for specified qualifier
	INTEGER*4	PARFILN		! parse filename
	INTEGER*4	EXPFLN		! expand filename
C
C Local variables
C
	INTEGER*4	OK		! error return
	LOGICAL*4	ABSENT		! set if file param. not found
	LOGICAL*4	LOCAL		! set if qual. is local
	LOGICAL*4	LOCNEG		! set if qual. locally negated
        LOGICAL*4	WHINGE		! set-local error reporting
	CHARACTER*(C_NAM_SIZ)	DEF_NAM	! default text file name part 
C
C Command Language Definitions
C
	EXTERNAL	RELHT_CLD
C
C Initialisation for command line decoding
C
	WHINGE = .TRUE.			! report errors locally
C
C Unset all qualifier flags
C
	HAD_LOG   = .FALSE.
	HAD_LIS   = .FALSE.
	HAD_LCM   = .FALSE.
C
C Get command line. DCL_STARTUP will return SS$NORMAL if all well.
C
	OK=DCL_STARTUP('RELHT'		! command verb
     &      ,.FALSE.			! don't split parameter lists
     &      ,RELHT_CLD			! CLD module name
     &      ,WHINGE)			! set -local error reporting
        IF(.NOT.OK) GOTO 999
C
C Get input file-name 
C
	OK=DCL_FILE('IN_FILESPEC'	! parameter label
     &		,IN_TEM			! default file-name
     &		,ABSENT			! set if no file-name
     &		,.FALSE.		! don't expect list of files
     &          ,WHINGE)		! set-local error reporting
	IF(.NOT.OK) GOTO 999
C
C Transfer file-spec 
C
	IN_FILE = FILARY(1)(1:FIL_LEN(1))
	IN_LEN = FIL_LEN(1)
C
C Set up the default output filename - the input filename is already in 
C the FILENAME common block, as a result of calling DCL_FILE
C
	HAD_VER = .FALSE.
	OK = EXPFLN(OUT_FILE,OUT_LEN)
	IF (.NOT. OK) CALL LSL_PUTMSG(OK)	! just a warning
C
C Get output file-name if it is there
C
	OK=DCL_FILE('OUT_FILESPEC'	! parameter label
     &		,OUT_FILE(1:OUT_LEN)	! default is input filename
     &		,ABSENT			! set if no file-name
     &		,.FALSE.		! don't expect list of files
     &          ,WHINGE)		! set-local error reporting
	IF(.NOT.OK) GOTO 999
C
C Set file-spec 
C
	IF (.NOT. ABSENT) THEN
	   OUT_FILE = FILARY(1)(1:FIL_LEN(1))
	   OUT_LEN = FIL_LEN(1)
	ENDIF
C
C Whether or not an output file was explicitly given, its name part 
C will still be in the filename parsing common block, so save it for 
C later use
C
	DEF_NAM = STR_NAM(1:LEN_NAM)
C
C /LITES2 qualifier
C
	OK=DCL_QUAL('LITES2'		! qual. name
     &        		,HAD_LCM	! returned .T. if qual. found
     &			,LOCAL		! set if qual local to parameter
     &			,LOCNEG		! set if qual. negated locally
     &			,WHINGE)	! set-local error reporting
	IF(.NOT.OK) GOTO 999
C
C If we have the qualifier, first parse the output name part against the
C template to get the default.
C
	IF (HAD_LCM) THEN
	   OK = PARFILN(LCM_FILE,LCM_LEN,DEF_NAM,LCM_TEM,.FALSE.)
	   IF (.NOT. OK) THEN
	      CALL LSL_PUTMSG(OK)
	      IF ((OK .AND. STS$M_SEVERITY) .NE. STS$K_WARNING) 
     &	         GO TO 999
	   ENDIF
C
C Now get the actual filename if given
C
	   OK=DCL_FILE('LITES2'		! qualifier
     &		,LCM_FILE(1:LCM_LEN)	! default file-name
     &		,ABSENT			! set if no file-name
     &		,.FALSE.		! don't expect list of files
     &          ,WHINGE)		! set-local error reporting
	   IF(.NOT.OK) GOTO 999
C
C Set file-spec 
C
	   IF (.NOT. ABSENT) THEN
	      LCM_FILE = FILARY(1)(1:FIL_LEN(1))
	      LCM_LEN = FIL_LEN(1)
	   ENDIF
	ENDIF
C
C /LOG qualifier
C
	OK=DCL_QUAL('LOG'		! qual. name
     &        		,HAD_LOG	! returned .T. if qual. found
     &			,LOCAL		! set if qual local to parameter
     &			,LOCNEG		! set if qual. negated locally
     &			,WHINGE)	! set-local error reporting
	IF(.NOT.OK) GOTO 999
C
C /NODE_LIST qualifier
C
	OK=DCL_QUAL('NODE_LIST'		! qual. name
     &        		,HAD_LIS	! returned .T. if qual. found
     &			,LOCAL		! set if qual local to parameter
     &			,LOCNEG		! set if qual. negated locally
     &			,WHINGE)	! set-local error reporting
	IF(.NOT.OK) GOTO 999
C
C If we have the qualifier, first parse the output name part against the
C template to get the default.
C
	IF (HAD_LIS) THEN
	   OK = PARFILN(LIS_FILE,LIS_LEN,DEF_NAM,LIS_TEM,.FALSE.)
	   IF (.NOT. OK) THEN
	      CALL LSL_PUTMSG(OK)
	      IF ((OK .AND. STS$M_SEVERITY) .NE. STS$K_WARNING) 
     &	         GO TO 999
	   ENDIF
C
C Now get the actual filename if given
C
	   OK=DCL_FILE('NODE_LIST'	! qualifier
     &		,LIS_FILE(1:LIS_LEN)	! default file-name
     &		,ABSENT			! set if no file-name
     &		,.FALSE.		! don't expect list of files
     &          ,WHINGE)		! set-local error reporting
	   IF(.NOT.OK) GOTO 999
C
C Set file-spec 
C
	   IF (.NOT. ABSENT) THEN
	      LIS_FILE = FILARY(1)(1:FIL_LEN(1))
	      LIS_LEN = FIL_LEN(1)
	   ENDIF
	ENDIF
C
C Assign function value
C
999	GET_COMMAND = OK
C
C Return
C
	RETURN
	END
