	LOGICAL*4 FUNCTION GET_CB (CBH_OUT, CBD_OUT)

***	IDENT	16AU89
***	MODULE	GET_CB

C	Copyright : Laser-Scan Laboratories Ltd., Cambridge, England
C	Created   : 				ACM	Apr 1988

CMOD794 Make ILINK.CMN and ILINK.STR consistent, and increase max no. 
C	of points per feature to 15000
C	To stop CB processing with a sensible message when maximum no. 
C	of points has been reached		JB	14-Aug-1989

	IMPLICIT NONE

	INCLUDE	'LSL$CMNILINK:ILINKMSG.PAR'
	INCLUDE	'LSL$CMNIFF:CB.STR'		! define structures
	INCLUDE	'LSL$CMNILINK:ILINK.STR'

C Data out
	RECORD/	IFF_CBH/	CBH_OUT	! header for CB being built
	RECORD/	STR_ILINK_CBD/	CBD_OUT	! data for CB being built

C Workspace
	RECORD/	IFF_CBH/	CBH	! CB header
	RECORD/	IFF_CBD/	CBD	! CB data

	INTEGER*4	NPTS,ICOL	! num of pts in CB read in
	INTEGER*4	NCOLS		! num of columns in CB
	INTEGER*4	CB_LEN		! point count
	INTEGER*2	IECODE,IELEN	! for IFFNXT

C **********************************************************************

C	Reads (sequence of) CB(s) unbroken by a pen-up CB into
C	CBH_OUT/CBD_OUT.

C	Returns .TRUE. when there are no further CBs in feature.

C **********************************************************************

	GET_CB = .FALSE.
	CB_LEN = 0

10	CALL EIHRCB_HEAD(CBH)
C
	IF (CB_LEN.EQ.0) THEN			! set CB header
	  CBH_OUT.FLAGS = 0
	  CBH_OUT.GTYPE = CBH.GTYPE
	  CBH_OUT.NROW = 0
	  CBH_OUT.NCOL = CBH.NCOL
C
	  DO 20 ICOL = 1,CBH_OUT.NCOL
	    CBH_OUT.COLH(ICOL) = CBH.COLH(ICOL)
20	  CONTINUE
C
	  CBH_OUT.NATT = CBH.NATT
C
	  DO 30 ICOL = 1,CBH_OUT.NATT
	    CBH_OUT.ATT(ICOL).ATTC = CBH.ATT(ICOL).ATTC
	    CBH_OUT.ATT(ICOL).IATTV = CBH.ATT(ICOL).IATTV
30	  CONTINUE
C
	END IF
C
	CALL EIHRCB_DATA(CBD)
C
	NPTS  = CBH.NROW
	NCOLS = CBH.NCOL

C Return if pen-up break
	IF (CB_LEN.GT.0.AND.CBH.FLAGS.EQ.0) GOTO 40

	IF (CB_LEN+NPTS.GT.ILINK_MAX_CB_ROW) THEN  ! warn if too many pnts 
	  NPTS = ILINK_MAX_CB_ROW - CB_LEN
	  IF (NPTS.LE.0) GOTO 40		   ! no room for more data
	  CALL LSL_PUTMSG(ILINK__TOOMNYPPF,ILINK_MAX_CB_ROW)
	END IF

C Accumulate points in CBD_OUT

	CALL IFS_CB_WRITE (CBH_OUT, CBD_OUT, CBD.I, CBH.NCOL, CBH.COLH,
     &			   CB_LEN+1, CB_LEN+NPTS)

	CB_LEN = CB_LEN + NPTS		! sum total no. of pts in CB
	CBH_OUT.NROW = CB_LEN

C See if any more CBs follow

	CALL IFFNXT(IECODE,IELEN)
	IF (IECODE.EQ.'CB') GOTO 10

	GET_CB = .TRUE.

40	RETURN
	END

