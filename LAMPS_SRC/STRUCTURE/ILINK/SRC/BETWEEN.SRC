C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1993-04-21 18:16:16.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL*4 FUNCTION BETWEEN(XA,YA,XB,YB,XC,YC,TOL)

***	IDENT	21AP93
***	MODULE	BETWEEN

C	Copyright : Laser-Scan Ltd., Cambridge, England
C	Created   : ACM					      Feb 1987

CMOD1218 Test for the equivalence of point B  to point A or C, and 
C	 return FALSE immediately as then not between exclusive.
C	 Prevents returning TRUE or FALSE depending on TOL for
C	 identical points				JB 20-Apr-1993

	IMPLICIT NONE

C Parameters in
	REAL*4		XA,YA		! pt 1
	REAL*4		XB,YB		! pt to be tested
	REAL*4		XC,YC		! pt 2
	REAL*4		TOL		! tolerance

C Workspace
	REAL*4		XAB,YAB
	REAL*4		XAC,YAC
	REAL*4		ALONG		! dist of B along AC
	REAL*4		DIST		! dist of B to line AC
	REAL*4		DAC		! dist from A to C

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C	Returns truth of :
C		B is on straight line from A to C, exclusive
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

	BETWEEN = .FALSE.

C Test equivalence of B and A
	IF (XB.EQ.XA .AND. YB.EQ.YA) GOTO 999

C Test equivalence of B and C
	IF (XB.EQ.XC .AND. YB.EQ.YC) GOTO 999

	XAB = XB-XA
	YAB = YB-YA

	XAC = XC-XA
	YAC = YC-YA

	DAC = SQRT(XAC*XAC+YAC*YAC)
	IF (DAC.LT.TOL) GOTO 999

c ab^ac = |ab||ac| sin(ang)
c dist = |ab| sin(ang) = ab^ac / |ac|

	DIST = ABS(XAB*YAC-YAB*XAC)/DAC
	IF (DIST.GT.TOL) GOTO 999

	ALONG = (XAB*XAC+YAB*YAC)/DAC
	IF (ALONG.LT.TOL .OR. ALONG.GT.DAC-TOL) GOTO 999

	BETWEEN = .TRUE.

999	RETURN
	END

