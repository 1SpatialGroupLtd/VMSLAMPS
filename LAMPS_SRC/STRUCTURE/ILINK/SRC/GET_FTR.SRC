	INTEGER*4 FUNCTION GET_FTR(
     &		PR_FTR,FTR_INFTR,IN_FTR_ADDR,
     &  	MAXINFTRS,MAXOUTFTRS)

***	IDENT	08AP88
***	MODULE	GET_FTR
 
C	Copyright : Laser-Scan Laboratories Ltd., Cambridge, England

CMOD	Converted to accept CB in place of ST entries	ACM 06-Apr-1988
CMOD	Made a function					TJH 28-May-1987
C	Created   : ACM					       Dec 1986

	IMPLICIT NONE

C Parameters
	INCLUDE		'($SSDEF)'
	INCLUDE		'LSL$CMNILINK:ILINKMSG.PAR'

C Commons
	INCLUDE		'LSL$CMNILINK:ILINK.CMN'
	INCLUDE		'LSL$CMNILINK:ILINKCHAR.CMN'

C Data in
	INTEGER*4	MAXINFTRS,MAXOUTFTRS	! array dimensions
	INTEGER*4	FTR_INFTR(MAXOUTFTRS)	! pr-ftr in-ftr index
	INTEGER*4	IN_FTR_ADDR(MAXINFTRS)	! ftr addr in input file
	INTEGER*4	PR_FTR			! processed-ftr index

C Workspace
	INTEGER*4	ADDR			! IFF address
	INTEGER*4	IN_FTR			! input-ftr index
 
	INTEGER*2	IECODE,IELEN		! for IFFPKE
 
	LOGICAL*4	LAST_CB			! last pen-up CB in ftr

C Functions
	LOGICAL*4	GET_CB			! get CB string
	INTEGER*4	PICK_CB

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C	Puts (X,Y)s for feature PR_FTR into CBH/BIG_CBD in ILINK.CMN
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

	GET_FTR = SS$_NORMAL

	CALL IFFSEL(INLUN)
	IN_FTR = FTR_INFTR(PR_FTR)
	ADDR = IN_FTR_ADDR(IN_FTR)

	CALL IFFPKE(IECODE,IELEN,ADDR)
	IF (IECODE.NE.'NF') THEN
	  GET_FTR = ILINK__BADADDR
	  CALL LSL_PUTMSG(GET_FTR)
	  GOTO 999
	END IF
 
	CALL IFFNXC('CB',IELEN)
	IF (IELEN.EQ.-1) THEN
	  GET_FTR = ILINK__UNEXPEOF
	  CALL LSL_PUTMSG(GET_FTR,INFILE(:IN_LEN))
	  GOTO 999
	END IF
 
	LAST_CB = GET_CB(CBH,BIG_CBD)

C Because more than one separate pen-up string is allowed within each
C feature, if the string you now have is not the last it may not be the
C one you want. In such cases it is necessary to find which pen-up
C string sharing this same feature NF address is the one you want.

	IF (.NOT.LAST_CB) THEN
	  GET_FTR
     &	  = PICK_CB(CBH,BIG_CBD,IN_FTR,MAXINFTRS,IN_FTR_ADDR)
	  IF (.NOT.GET_FTR) THEN
	    GET_FTR = ILINK__INTRNLERR
	    CALL LSL_PUTMSG(GET_FTR,1,'GET_FTR')
	    GOTO 999
	  END IF
	END IF

999	RETURN
	END

