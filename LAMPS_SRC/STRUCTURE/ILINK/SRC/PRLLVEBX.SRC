C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1989-08-17 16:13:36.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL*4 FUNCTION PRLLVEBX(
     &		PROCESS_VECTORS,NEXT_PT,MAX_NEXT,
     &		LINE_FC,N_LINES,SEC,MAX_SECS,
     &		CHECK,MAX_FCS,CHECK_FCS,SELF_CHECK)

***	IDENT	17AU89
***	MODULE	PRLLVEBX

C	Copyright : Laser-Scan Laboratories Ltd., Cambridge, England
C	Created   : ACM					Feb 1986

	IMPLICIT NONE

***	COMMON/CMN:QUADCMN/

C Parameters
	INCLUDE	'($SSDEF)'


C Data in/out
	LOGICAL*4	PROCESS_VECTORS	! routine to process vec pairs
	INTEGER*4	MAX_NEXT	! NEXT_PT dimension
	INTEGER*4	N_LINES		! LINE dimension
	INTEGER*4	MAX_SECS	! SEC dimension
	INTEGER*4	NEXT_PT(MAX_NEXT) ! linked-list ptrs for PT
	INTEGER*2	LINE_FC(N_LINES)! FC for each line
	INTEGER*4	SEC(3,MAX_SECS,4)
C			box section PT sec start index, PT sec end
C			index and line index, in box NBOX
	INTEGER*4	MAX_FCS		! CHECK dimension
	LOGICAL*1	CHECK(MAX_FCS,MAX_FCS)	! FC pairs to be checked
	LOGICAL*4	CHECK_FCS	! use FC-pair check?
	LOGICAL*4	SELF_CHECK	! check lines against themselvs?

C Workspace
	INTEGER*4	IPT,JPT		! PT index in line ILIN & JLIN
	INTEGER*4	NEXTIPT,NEXTJPT	! next PT indices
	INTEGER*4	IPTSTA,IPTFIN	! sec pt start/fin PT indices
	INTEGER*4	JPTSTA,JPTFIN	! same
	INTEGER*4	ISEC,JSEC	! SEC indices
	INTEGER*4	ISECSTA,ISECFIN	! start and fin SEC INDICES
	INTEGER*4	JSECSTA,JSECFIN	! same
	INTEGER*4	ILIN,JLIN	! line indices
	INTEGER*4	IFC,JFC		! line FC indices

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C	Process all vectors in current linked-list vector quad-tree box.
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

	PRLLVEBX = SS$_NORMAL

	ISECSTA = SECSTA(NBOX,DEP)
	ISECFIN = ISECSTA+NSECS(NBOX,DEP)-1

	DO 80 ISEC = ISECSTA,ISECFIN		! for each sec in box do
	  IPTSTA = SEC(SEC_STA,ISEC,NBOX)	! get first pt index
	  IPTFIN = SEC(SEC_FIN,ISEC,NBOX)	! get last pt index

	  ILIN = SEC(SEC_LIN,ISEC,NBOX)		! get LINE index
	  IFC = LINE_FC(ILIN)			! get FC index for line
	  IPT = IPTSTA
	  GOTO 20

10	  IPT = NEXTIPT				! set next pt index
20	  NEXTIPT = NEXT_PT(IPT)		! get next pt index

	  IF (SELF_CHECK) THEN
	    JSECSTA = ISEC
	  ELSE
	    JSECSTA = ISEC+1
	  END IF

	  DO 70 JSEC = JSECSTA,ISECFIN		! for each foll sec do
	    JLIN = SEC(SEC_LIN,JSEC,NBOX)
	    IF (ILIN.EQ.JLIN.AND..NOT.SELF_CHECK) GOTO 70
	    JFC = LINE_FC(JLIN)
	    IF (CHECK_FCS.AND..NOT.CHECK(IFC,JFC)) GOTO 70

	    IF (ISEC.EQ.JSEC) THEN
	      IF (NEXTIPT.EQ.IPTFIN) GOTO 80
	      JPTSTA = NEXTIPT			! get first pt index
	    ELSE
	      JPTSTA = SEC(SEC_STA,JSEC,NBOX)
	    END IF

	    JPTFIN = SEC(SEC_FIN,JSEC,NBOX)	! get last pt index

	    JPT = JPTSTA
	    GOTO 40
30	    JPT = NEXTJPT
40	    NEXTJPT = NEXT_PT(JPT)

	    PRLLVEBX = PROCESS_VECTORS(ILIN,JLIN,IPT,JPT)
	    IF (.NOT.PRLLVEBX) GOTO 90		! return

	    IF (NEXTJPT.NE.JPTFIN) GOTO 30  ! loop for next pt in JLIN
70	  CONTINUE

	  IF (NEXTIPT.NE.IPTFIN) GOTO 10    ! loop for next pt in ILIN
80	CONTINUE

90	RETURN
	END
