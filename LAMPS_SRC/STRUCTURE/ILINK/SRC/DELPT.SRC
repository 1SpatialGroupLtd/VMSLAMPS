C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1988-04-15 10:38:40.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE DELPT(	IPT,
     &				NXT,PRV,PT_PR_FTR,
     &				FTR_PTS,PNT_FTR,
     &				MAXPTS,MAXOUTFTRS)

***	IDENT	29MY87
***	MODULE	DELPT

C	Copyright : Laser-Scan Laboratories Ltd., Cambridge, England
C	Created   : ACM					July 1986

	IMPLICIT NONE

	INCLUDE		'LSL$CMNILINK:ILINKMSG.PAR'

	INCLUDE		'LSL$CMNILINK:ILINK.CMN'

C Data in
	INTEGER*4	IPT		! PT index of pt to be deleted
	INTEGER*4	MAXPTS,MAXOUTFTRS
	INTEGER*4	NXT(MAXPTS),PRV(MAXPTS)
	INTEGER*4	PT_PR_FTR(MAXPTS)
	INTEGER*4	FTR_PTS(2,MAXOUTFTRS)

	LOGICAL*1	PNT_FTR(MAXOUTFTRS)

C Workspace
	INTEGER*4	IPTPRV,IPTNXT	! prev/next pts
	INTEGER*4	IPTSTA,IPTFIN	! start/fin pt indices
	INTEGER*4	I_PR_FTR	! ftr index
	LOGICAL  	CLOSED		! is ftr closed closed loop?
	LOGICAL  	STA,FIN		! is pt start/fin pt?

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C	Delete point IPT from linked list
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

C Do not try to delete if already deleted

	IF (PRV(IPT).LT.0) RETURN

C Find neighbouring pt indices

	IPTPRV = PRV(IPT)
	IPTNXT = NXT(IPT)

C Find whether ftr start or end pt

	I_PR_FTR = PT_PR_FTR(IPT)
	IF (PNT_FTR(I_PR_FTR)) RETURN		! don't delete pt-ftrs
	IPTSTA = FTR_PTS(1,I_PR_FTR)
	IPTFIN = FTR_PTS(2,I_PR_FTR)

	IF (IPTSTA.EQ.IPTFIN) THEN		! pt ftr being lost
	  IF (I_PR_FTR.GT.N_ORIG_PR_FTRS) THEN
	    FTR_PTS(1,I_PR_FTR) = NIL
	    FTR_PTS(2,I_PR_FTR) = NIL
	  END IF
	  RETURN
	END IF

	STA = IPT.EQ.IPTSTA
	FIN = IPT.EQ.IPTFIN

C Special attention must be taken if deleting a ftr start or end pt

	CLOSED = NXT(IPTFIN).EQ.IPTSTA

	IF (STA) FTR_PTS(1,I_PR_FTR) = IPTNXT
	IF (CLOSED.OR..NOT.STA) NXT(IPTPRV) = IPTNXT

	IF (FIN) FTR_PTS(2,I_PR_FTR) = IPTPRV
	IF (CLOSED.OR..NOT.FIN) PRV(IPTNXT) = IPTPRV

	PRV(IPT) = -1			! mark as deleted
	END

