C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1993-05-25 19:43:54.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE DEL1(	NXT,PRV,NEAREST,PT_PR_FTR,PT_SEEN,PDAT,
     &				QDAT,FTR_PTS,PNT_FTR,CHECK_ALL,USE_SEEN,
     &				PT,MAXPTS,MAXOUTFTRS)

***	IDENT	25MY93
***	MODULE	DEL1

C	Copyright : Laser-Scan Ltd., Cambridge, England
C	Created   : ACM					Month 1986

CMOD1232						JB 25-May-1993
C	Give warning ORIGPNTDEL if had /WARNING

	IMPLICIT NONE

	INCLUDE 	'LSL$CMNILINK:ILINKMSG.PAR'
	INCLUDE		'LSL$CMNILINK:ILINK.CMN'

C Data in
	INTEGER*4	MAXPTS,MAXOUTFTRS
	INTEGER*4	NXT(MAXPTS),PRV(MAXPTS)
	INTEGER*4	NEAREST(MAXPTS)
	INTEGER*4	FTR_PTS(2,MAXOUTFTRS)
	INTEGER*4	PT_PR_FTR(MAXPTS)

	LOGICAL*1	PT_SEEN(MAXPTS)
	LOGICAL*1	PDAT(MAXPTS)
	LOGICAL*1	QDAT(MAXPTS)
	LOGICAL*1	PNT_FTR(MAXOUTFTRS)
	LOGICAL*1	CHECK_ALL		! not only inserted pts?
	LOGICAL*1	USE_SEEN		! use PT_SEEN?

	REAL*4		PT(2,MAXPTS)

C Workspace
	INTEGER*4	IPT,JPT			! pt indices

	LOGICAL*1	DELETE			! delete pt?
	LOGICAL*1	POINTING		! pt points to pt?
	LOGICAL*1	POINTED_TO		! pt pointed-to by pt?

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C	Deletes all pts which are seen but not pointing or pointed to,
C	and are marked as part of an aligned ftr section.
C
C	QDAT currently marks pts which are part of aligned ftr sections.
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C Use PDAT to mark all points which are pointed to

	DO 10 IPT = 1,N_PTS
	  PDAT(IPT) = .FALSE.
10	CONTINUE

	DO 20 IPT = 1,N_PTS
	  IF (PRV(IPT).EQ.-1) GOTO 20		! ignore deleted pts
	  JPT = NEAREST(IPT)
	  IF (JPT.NE.NIL) PDAT(JPT) = .TRUE.
20	CONTINUE

	DO 30 IPT = 1,N_PTS
	  IF (PRV(IPT).EQ.-1) GOTO 30		! ignore deleted pts
	  POINTED_TO = PDAT(IPT)
	  POINTING   = NEAREST(IPT).NE.NIL

	  IF (.NOT.(POINTING .OR. POINTED_TO)) THEN
	    DELETE = .TRUE.
	    IF (USE_SEEN) DELETE = PT_SEEN(IPT).AND.QDAT(IPT)
	    IF (.NOT.CHECK_ALL) DELETE = DELETE.AND.IPT.GT.N_ORIG_PTS

	    IF (DELETE) THEN
		IF (HAD_WARNING .AND. IPT.GT.N_ORIG_PTS) 
     &		    CALL LSL_PUTMSG
     &			(ILINK__ORIGPNTDEL,PT(1,IPT),PT(2,IPT))

		CALL DELPT(IPT,NXT,PRV,PT_PR_FTR,FTR_PTS,PNT_FTR,
     &			   MAXPTS,MAXOUTFTRS)
	    ENDIF
	  ENDIF
30	CONTINUE
 
	END

