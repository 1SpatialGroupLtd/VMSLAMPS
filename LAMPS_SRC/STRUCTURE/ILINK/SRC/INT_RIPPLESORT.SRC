	SUBROUTINE INT_RIPPLESORT(A,INDEX,N,INCR)
C
***	IDENT	12AU87
***	MODULE	INT_RIPPLESORT
C
C	Copyright : Laser-Scan Laboratories Ltd., Cambridge, England
C	Created   : ACM					Feb 1986
C
	IMPLICIT NONE
C
C Data in
	INTEGER*4	N		! size of array A
	LOGICAL		INCR		! increasing order?
C
C Data in/out
	INTEGER*4	A(N)		! integer array to be sorted
C
C Data out
	INTEGER*4	INDEX(N)	! array of sorted A indices
C
C Workspace
	INTEGER*4	I,J,K
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C	Uses ripple-sort to sort array A into increasing or decreasing
C	order.
C
C	Also returns array of A indices sorted accordingly so that
C	other arrays can subsequently be sorted to correspond
C	with A if required.
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
	DO 10 I = 1,N
10	INDEX(I) = I
C
	IF (INCR) THEN
	  DO 30 I = 2,N
	    DO 20 J = I,2,-1
	      IF (A(J).GE.A(J-1)) GOTO 30
	      K = A(J)
	      A(J) = A(J-1)
	      A(J-1) = K
	      K = INDEX(J)
	      INDEX(J) = INDEX(J-1)
	      INDEX(J-1) = K
20	    CONTINUE
30	  CONTINUE
	ELSE
	  DO 50 I = 2,N
	    DO 40 J = I,2,-1
	      IF (A(J).LE.A(J-1)) GOTO 50
	      K = A(J)
	      A(J) = A(J-1)
	      A(J-1) = K
	      K = INDEX(J)
	      INDEX(J) = INDEX(J-1)
	      INDEX(J-1) = K
40	    CONTINUE
50	  CONTINUE
	END IF
C
	RETURN
	END
