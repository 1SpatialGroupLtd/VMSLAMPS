	SUBROUTINE UNDO_END_JOINS(
     &		PT,NEAREST,NXT,PT_PR_FTR,PT_DST,FTR_PTS,PNT_FTR,
     &		MAXPTS,MAXOUTFTRS)

***	MODULE	UNDO_END_JOINS
***	IDENT	01DE88

C	Copyright : Laser-Scan Laboratories Ltd., Cambridge, England

CMOD	Bug relating to circular linked lists fixed	ACM 22-Nov-1988
C	Created   : ACM					       Feb 1987

	IMPLICIT NONE

	INCLUDE		'LSL$CMNILINK:ILINK.CMN'

C Data in/out
	INTEGER*4	MAXPTS,MAXOUTFTRS
	INTEGER*4	NEAREST(MAXPTS)
	INTEGER*4	NXT(MAXPTS)
	INTEGER*4	PT_PR_FTR(MAXPTS)
	INTEGER*4	FTR_PTS(2,MAXOUTFTRS)
	REAL*4		PT(2,MAXPTS)
	REAL*4		PT_DST(MAXPTS)
	LOGICAL*1	PNT_FTR(MAXOUTFTRS)

C Workspace
	INTEGER*4	IPTSTA,IPTFIN		! start/fin pt indices
	INTEGER*4	JPTSTA,JPTFIN		! start/fin pt indices
	INTEGER*4	IFTR,JFTR		! ftr indices
	INTEGER*4	JPT			! pt index
	REAL*4		DIST_FROM_STA
	REAL*4		DIST_FROM_FIN
	REAL*4		DX,DY

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C	Disconnects joins within JNTOL of ftr ends, along ftr, which
C	should be joined by PPJOIN in a later run of ILINK
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

	DO 10 IFTR = 1,N_PR_FTRS
	  IPTSTA = FTR_PTS(1,IFTR)
	  IPTFIN = FTR_PTS(2,IFTR)
	  IF (IPTSTA.EQ.NIL) GOTO 10		! ftr deleted
	  IF (PNT_FTR(IFTR)) GOTO 10		! 1-point ftr

	  JPT = NEAREST(IPTSTA)
	  IF (JPT.NE.NIL) THEN			! pt is connected
	    JFTR = PT_PR_FTR(JPT)
	    JPTSTA = FTR_PTS(1,JFTR)
	    JPTFIN = FTR_PTS(2,JFTR)
	    IF (NXT(JPTFIN).EQ.JPTSTA) THEN	! circular list?
	      DX = PT(1,JPT)-PT(1,JPTSTA)
	      DY = PT(2,JPT)-PT(2,JPTSTA)
	      DIST_FROM_STA = SQRT(DX**2+DY**2)
	      DIST_FROM_FIN = DIST_FROM_STA
	    ELSE
	      DIST_FROM_STA = ABS(PT_DST(JPT)-PT_DST(JPTSTA))
	      DIST_FROM_FIN = ABS(PT_DST(JPT)-PT_DST(JPTFIN))
	    END IF
	    IF (DIST_FROM_STA.LT.JNTOL.OR.DIST_FROM_FIN.LT.JNTOL)
     &	      NEAREST(IPTSTA) = NIL
	  END IF

	  JPT = NEAREST(IPTFIN)
	  IF (JPT.NE.NIL) THEN			! pt is connected
	    JFTR = PT_PR_FTR(JPT)
	    JPTSTA = FTR_PTS(1,JFTR)
	    JPTFIN = FTR_PTS(2,JFTR)

	    IF (NXT(JPTFIN).EQ.JPTSTA) THEN	! circular list?
	      DX = PT(1,JPT)-PT(1,JPTSTA)
	      DY = PT(2,JPT)-PT(2,JPTSTA)
	      DIST_FROM_STA = SQRT(DX**2+DY**2)
	      DIST_FROM_FIN = DIST_FROM_STA
	    ELSE
	      DIST_FROM_STA = ABS(PT_DST(JPT)-PT_DST(JPTSTA))
	      DIST_FROM_FIN = ABS(PT_DST(JPT)-PT_DST(JPTFIN))
	    END IF
	    IF (DIST_FROM_STA.LT.JNTOL.OR.DIST_FROM_FIN.LT.JNTOL)
     &	      NEAREST(IPTFIN) = NIL
	  END IF

10	CONTINUE

	RETURN
	END

