	SUBROUTINE UNDO_SMALL_LOOPS(
     &		NEAREST,PT_PR_FTR,PT_DST,FTR_PTS,PNT_FTR,
     &		MAXPTS,MAXOUTFTRS)

***	IDENT	29MY87
***	MODULE	UNDO_SMALL_LOOPS

C	Copyright : Laser-Scan Laboratories Ltd., Cambridge, England
C	Created   : ACM					Dec 1986

	IMPLICIT NONE

	INCLUDE		'LSL$CMNILINK:ILINK.CMN'

C Data in
	INTEGER*4	MAXPTS,MAXOUTFTRS
	INTEGER*4	NEAREST(MAXPTS)
	INTEGER*4	PT_PR_FTR(MAXPTS)
	INTEGER*4	FTR_PTS(2,MAXOUTFTRS)

	REAL*4		PT_DST(MAXPTS)

	LOGICAL*1	PNT_FTR(MAXOUTFTRS)

C Workspace
	INTEGER*4	IPTSTA,IPTFIN		! start/fin pt indices
	INTEGER*4	IFTR,JFTR		! ftr indices
	INTEGER*4	IPT			! pt index

	REAL*4		ARCLEN			! distance along ftr

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C	Disconnects line ends where line-end joining would form a small
C	loop of arc length < 3*EXTOL
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

	DO 10 IFTR = 1,N_PR_FTRS
	  IPTSTA = FTR_PTS(1,IFTR)
	  IPTFIN = FTR_PTS(2,IFTR)
	  IF (IPTSTA.EQ.NIL) GOTO 10		! ftr deleted
	  IF (PNT_FTR(IFTR)) GOTO 10		! 1-point ftr

	  IPT = NEAREST(IPTSTA)
	  IF (IPT.NE.NIL) THEN			! pt is connected
	    JFTR = PT_PR_FTR(IPT)
	    IF (JFTR.EQ.IFTR) THEN		! loop
	      ARCLEN = ABS(PT_DST(IPT)-PT_DST(IPTSTA))
	      IF (ARCLEN.LT.3*EXTOL) NEAREST(IPTSTA) = NIL
	    END IF
	  END IF

	  IPT = NEAREST(IPTFIN)
	  IF (IPT.NE.NIL) THEN			! pt is connected
	    JFTR = PT_PR_FTR(IPT)
	    IF (JFTR.EQ.IFTR) THEN		! loop
	      ARCLEN = ABS(PT_DST(IPT)-PT_DST(IPTFIN))
	      IF (ARCLEN.LT.3*EXTOL) NEAREST(IPTFIN) = NIL
	    END IF
	  END IF

10	CONTINUE
 
	END
