	SUBROUTINE COUNT_JOIN(P1X,P1Y,P2X,P2Y)

***	IDENT	13JL92
***	MODULE	COUNT_JOIN

C	Copyright : Laser-Scan Ltd., Cambridge, England

CMOD1060 Use absolute coords if requested		JB 07-Nov-1991

C	Created   : ACM					Sep 1986

	IMPLICIT NONE

	INCLUDE		'LSL$CMNILINK:ILINK.CMN'
	INCLUDE		'LSL$CMNIFF:MD2DES.CMN'

C Data in
	REAL*4		P1X,P1Y			! pos moved to
	REAL*4		P2X,P2Y			! pos moved from

C Workspace
	REAL*4		DX,DY			! vec from p1 to p2
	REAL*4		DIST			! dist from pt
	REAL*4		SQDIST			! sq dist from pt
	INTEGER*4	IDIV			! range division index

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C	Notes distribution of join distances over 10 subdivisions
C	of the verification range.
C
C	If /VERIFY has been specified, then writes a message to
C	LITES2 command file for each join counted.
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

	DX = P2X-P1X
	DY = P2Y-P1Y
	SQDIST = DX*DX+DY*DY
	IF (SQDIST.LT.VRTOL_SQ.OR.SQDIST.GT.EXJNTOL_SQ) GOTO 999
	DIST = SQRT(SQDIST)
	IF (VER_RANGE.LT.ZEROPLUS) THEN
	  IDIV = 1
	ELSE
	  IDIV = INT((NDIVS*(DIST-VRTOL))/VER_RANGE)+1
	ENDIF

	IF (IDIV.GE.1.AND.IDIV.LE.NDIVS) THEN
	  JOIN_COUNT(IDIV) = JOIN_COUNT(IDIV)+1
	  IF (HAD_VERIFY) THEN
	    CALL FLWSEL(LITES2LUN)

	    IF (HAD_ABS) THEN
	      CALL FLWRTF(
     &		'%%MESSAGE Point moved to (%^D%F10.3, %^D%F10.3)',
     &					     P1X+MD2LOC(1),P1Y+MD2LOC(2))
	      CALL FLWRTF('%%ABSOLUTE %^D%F %^D%F',
     &					     P2X+MD2LOC(1),P2Y+MD2LOC(2))
	    ELSE
	      CALL FLWRTF(
     &		'%%MESSAGE Point moved to (%F10.3, %F10.3)',P1X,P1Y)
	      CALL FLWRTF('%%POSITION %F %F',		    P2X,P2Y)
	    ENDIF

	    CALL FLWRTF('%%TEST $CURSINWIN')
	    CALL FLWRTF('%%ELSE %%ZOOM 1')
	    CALL FLWRTF('%%PING')
	    CALL FLWRTF('%%RESPOND')
	    CALL FLWRTF('%%ABANDON')
	    CALL FLWRTF('%%ABANDON')
	  END IF
	END IF

999	END
