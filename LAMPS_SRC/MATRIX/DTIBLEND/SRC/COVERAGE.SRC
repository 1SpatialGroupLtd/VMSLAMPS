	SUBROUTINE COVERAGE(DTILUN)
C
***	MODULE	COVERAGE
***	IDENT	18AU88
C
C	Copyright : Laser-Scan Laboratories, Cambridge, England
C
CMOD	DTI_DTED replaced by parameter DTI_TED4         DRC 18-Aug-1988
C Created						TJH 17-May-1988
C
C Routine to calculate the row and column coverage of the blend
C zone defined in BLEND_ZONE for the DTILUN'th input file. The
C result is returned in  /DTIHDR/WINDOW_* 
C
	IMPLICIT NONE
C
C Argument:
	INTEGER	DTILUN		! the lun of the file for which
C				! the row/col coverage is required
C
C Parameters:
	INCLUDE 'LSL$CMNDTIBLEND:PARAMS.PAR'	
	INCLUDE 'LSL$CMNDTI:DTIDEF.PAR'	! DTILIB parameters
	INCLUDE 'LSL$CMNDTI:DTIPAR.PAR'	! DTILIB parameters
C	
C Common:
	INCLUDE 'LSL$CMNDTI:DTIHDR.CMN'	! DTILIB common
	INCLUDE 'LSL$CMNDTIBLEND:PROCESS.CMN'
	INCLUDE 'LSL$CMNDTIBLEND:COMMAND.CMN'
	INCLUDE 'LSL$CMNDTIBLEND:FILES.CMN'
C W/s
	INTEGER*4	X1,Y1,X2,Y2	! window values in matrix units
C
C ......................................................................
C
	IF (HEADER_TYPE(DTILUN).EQ.DTI_TED4) THEN
	   X1=BLEND_ZONE(1,2)
	   Y1=BLEND_ZONE(2,2)
	   X2=BLEND_ZONE(1,4)
	   Y2=BLEND_ZONE(2,4)
	   X1=((X1-(LONREC(1,DTILUN)-MINXOF))/LONINT(DTILUN))+1
	   Y1=((Y1-(LATREC(1,DTILUN)-MINYOF))/LATINT(DTILUN))+1
	   X2=((X2-(LONREC(1,DTILUN)-MINXOF))/LONINT(DTILUN))+1
	   Y2=((Y2-(LATREC(1,DTILUN)-MINYOF))/LATINT(DTILUN))+1
	ELSE
	   X1=NINT((BLEND_ZONE(1,2)-(X_OFFSET(DTILUN)-MINXOF))
     &					/X_GRID(DTILUN))+1
	   Y1=NINT((BLEND_ZONE(2,2)-(Y_OFFSET(DTILUN)-MINYOF))
     &					/Y_GRID(DTILUN))+1
	   X2=NINT((BLEND_ZONE(1,4)-(X_OFFSET(DTILUN)-MINXOF))
     &					/X_GRID(DTILUN))+1
	   Y2=NINT((BLEND_ZONE(2,4)-(Y_OFFSET(DTILUN)-MINYOF))
     &					/Y_GRID(DTILUN))+1
	ENDIF
C
C Do the values need truncating?
	X1=MAX(1,MIN(X1,X_EXTENT(DTILUN)))
	X2=MAX(1,MIN(X2,X_EXTENT(DTILUN)))
	Y1=MAX(1,MIN(Y1,Y_EXTENT(DTILUN)))
	Y2=MAX(1,MIN(Y2,Y_EXTENT(DTILUN)))
C
C Transfer to the DTI common block variables
	DTI_WINDOW_SWX(DTILUN) = X1
	DTI_WINDOW_SWY(DTILUN) = Y1
	DTI_WINDOW_NEX(DTILUN) = X2	
	DTI_WINDOW_NEY(DTILUN) = Y2
	RETURN
	END
