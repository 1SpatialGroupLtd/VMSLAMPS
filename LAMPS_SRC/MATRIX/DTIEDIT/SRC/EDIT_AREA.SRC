	LOGICAL FUNCTION EDIT_AREA(DTI_LUN)
C
***	IDENT	19AP93
***	MODULE  EDIT_AREA
C
C	Copyright : Laser-Scan, Cambridge, England
C	Author    : D R Catlow
C	
	IMPLICIT NONE
C
C------------------------------------------------------------------------
C
C	Function to edit a all matrix points in a rectangular area in 
C	a DTI file to the specified value.
C	The function checks that a file is opened on the LUN, that
C	the coordinates of the rectangle are specified in the correct
C	order, and are in range, and that the data value is within
C	range for the DTI data type.
C	It then calls DTI_INIT_DATA to perform the edit on the DTI file.
C
C	The function returns TRUE on success, and FALSE if an error
C	has occurred.
C
C-------------------------------------------------------------------------
C	DTILIB commons and parameter files
C
***	COMMON/LSL$CMNDTI:DTIHDR/
***	PARAMETER/LSL$CMNDTI:DTIPAR/
***	PARAMETER/LSL$CMNDTI:DTILIBMSG/
C
***	COMMON/LSL$CMNLSL:CMDCOM/
C
***	COMMON/HERE:DTICOM/
C
C	Argument:
	INTEGER*4	DTI_LUN		! DTI file number
C
C	Local:
	REAL		REAL_VALUE	! real data value
	INTEGER*4	INT_VALUE	! integer data value
	LOGICAL*4	DATA_VALUE	! passed to DTI_INIT_DATA
	INTEGER*4	RETVAL		! function return
C
	EQUIVALENCE	(DATA_VALUE,REAL_VALUE,INT_VALUE)
C
C 	DTILIB functions
	INTEGER*4	DTI_INIT_DATA	! set rectangular area in a DTI
C					! file to a given value
C****************************************************************************
C	Set function status
	EDIT_AREA =.FALSE.	
C
C----------------------------------------------------------------------------
C	Check that a DTI file is opened on DTI_LUN
	IF(.NOT.DTILUN_INUSE(DTI_LUN))THEN
	  CALL WRITEF('%S Specifying command EDIT AREA',ERROR_BELL)
	  CALL WRITEF('No DTI file has been defined')
	  GOTO 999
	ENDIF
C
C	Check that the option to edit the file is selected
	IF(.NOT.ALLOW_EDIT)THEN
	  CALL WRITEF('%S Specifying command EDIT AREA',ERROR_BELL)
	  CALL WRITEF('EDIT option is not enabled')
	  GOTO 999
	ENDIF
C
C	Check for 5 arguments
	IF(CMDACT.NE.5)THEN
	  CALL WRITEF('%S Specifying command EDIT AREA',ERROR_BELL)
	  IF(CMDACT.EQ.4)THEN
	    CALL WRITEF('Data value is missing')
	  ELSE
	    CALL WRITEF('Command requires 5 arguments')
	  ENDIF
	  GOTO 999
	ENDIF
C
C	Check rectangle is correctly specified
	IF (INTARG(3).LE.INTARG(1) .OR. INTARG(4).LE.INTARG(2)) THEN
	  CALL WRITEF('%S Specifying command EDIT AREA',ERROR_BELL)
	  CALL WRITEF
     &  ('The top righthand point must exceed the bottom 
     &lefthand point')
	  GOTO 999
	ENDIF
C
C	Check coordinates are within the DTI coordinate range
	IF (INTARG(1).LT.1.OR.INTARG(1).GT.X_EXTENT(DTI_LUN) .OR.
     &      INTARG(3).LT.1.OR.INTARG(3).GT.X_EXTENT(DTI_LUN) .OR.
     &      INTARG(2).LT.1.OR.INTARG(2).GT.Y_EXTENT(DTI_LUN) .OR.
     &      INTARG(4).LT.1.OR.INTARG(4).GT.Y_EXTENT(DTI_LUN)) THEN
	  CALL WRITEF('%S Specifying command EDIT AREA',ERROR_BELL)
	  CALL WRITEF
     &    ('The coordinates of the rectangle are out of range')
	  GOTO 999
	ENDIF
C
C	Check data value is valid for DTI data type
	IF (DATA_TYPE(DTI_LUN).EQ.DATA_BIT) THEN		!bit data
	  IF (REALAR(1).NE.1 .AND. REALAR(1).NE.0) THEN
	    CALL WRITEF('%S Specifying command EDIT AREA',ERROR_BELL)
	    CALL WRITEF('The data value is invalid for the DTI data type')
	    GOTO 999
	  ENDIF
C
	ELSEIF (DATA_TYPE(DTI_LUN).EQ.DATA_BYTE) THEN	!byte data
	  IF (REALAR(1).LT.0 .OR. REALAR(1).GT.255) THEN
	    CALL WRITEF('%S Specifying command EDIT AREA',ERROR_BELL)
	    CALL WRITEF('The data value is invalid for the DTI data type')
	    GOTO 999
	  ENDIF
C
	ELSEIF (DATA_TYPE(DTI_LUN).EQ.DATA_WORD) THEN	!word data
	  IF (REALAR(1).LT.-32767 .OR. REALAR(1).GT.32767) THEN
	    CALL WRITEF('%S Specifying command EDIT AREA',ERROR_BELL)
	    CALL WRITEF('The data value is invalid for the DTI data type')
	    GOTO 999
	  ENDIF
C
	ELSEIF (DATA_TYPE(DTI_LUN).EQ.DATA_LONG) THEN	!longword data
	  IF (REALAR(1).LT.-2147483648 .OR.
     &        REALAR(1).GT. 2147483647) THEN
	    CALL WRITEF('%S Specifying command EDIT AREA',ERROR_BELL)
	    CALL WRITEF('The data value is invalid for the DTI data type')
	    GOTO 999
	  ENDIF
	ENDIF
C
	IF (DATA_TYPE(DTI_LUN).NE.DATA_REAL) THEN
	  INT_VALUE=REALAR(1)
	ELSE
	  REAL_VALUE=REALAR(1)
	ENDIF
C
C	Write the data value to the DTI file
	RETVAL=DTI_INIT_DATA(
     &                       DTI_LUN,
     &                       DATA_VALUE,
     &			     INTARG(1),
     &			     INTARG(2),
     &                       (INTARG(3)-INTARG(1))+1,
     &                       (INTARG(4)-INTARG(2))+1)
	IF(.NOT.RETVAL)THEN
	  CALL LSL_PUTMSG(RETVAL)
	  GOTO 999
	ENDIF
C
C------------------------------------------------------------------------
C Assume the range needs re-calculating
	NEWRAN = .TRUE.
C
C If new value still in range, turn off new range calculation
C
	IF (DATA_TYPE(DTI_LUN).EQ.DATA_BIT) THEN
C
	ELSEIF (DATA_TYPE(DTI_LUN).EQ.DATA_REAL) THEN
	  IF (REAL_VALUE.LE.MAX_RVALUE(DTI_LUN) .AND.
     &        REAL_VALUE.GE.MIN_RVALUE(DTI_LUN)) NEWRAN=.FALSE.
C
	ELSEIF (DATA_TYPE(DTI_LUN).EQ.DATA_LONG) THEN
	  IF (INT_VALUE.LE.MAX_LVALUE(DTI_LUN) .AND.
     &        INT_VALUE.GE.MIN_LVALUE(DTI_LUN)) NEWRAN=.FALSE.
C
	ELSE
	  IF (INT_VALUE.LE.MAX_WVALUE(DTI_LUN) .AND.
     &        INT_VALUE.GE.MIN_WVALUE(DTI_LUN)) NEWRAN=.FALSE.
	ENDIF
C
C----------------------------------------------------------------------------
C	Signal success
	EDIT_AREA=.TRUE.
C
999	RETURN
	END
C
