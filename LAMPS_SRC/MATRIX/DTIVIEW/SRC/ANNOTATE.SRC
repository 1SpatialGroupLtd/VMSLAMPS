	SUBROUTINE ANNOTATE
C
***	MODULE	ANNOTATE
***	IDENT	13AP88
C
C	Copyright : Laser-Scan Laboratories Ltd, Cambridge, England
C	Author    : DRC
C	Created   : Jan 85
C
C	Modified  : DRC                                 Jan-1988
C                   For use with primary and secondary
C		    graphics devices and extended GKSLDLIB
C	Modified  : DRC                                 Jul-1986
C                   DEVICE common block added
C	Modified  : ACM                                 May-1985
C                   To use GKS graphics calls	
C
C---------------------------------------------------------------------
C
	IMPLICIT NONE
C
***	COMMON/LSL$CMNLSL:TXTC/
***	COMMON/HERE:DEVICE/
***	COMMON/HERE:GKSCMN/
***	PARAMETER/HERE:GKSPAR/
C
C workspace
	INTEGER*4	DESC(2)
	REAL		TX,TY		! text device position
	REAL		WX,WY		! text world position
	REAL		WXR,WYR		! world ranges
	REAL		HGT		! character height
C
C--------------------------------------------------------------------
C
C	Is a graphics device enabled?
	IF (.NOT.(PRIMARY.OR.SECONDARY))GOTO 999
C
C--------------------------------------------------------------------
	CALL PLTPOS(.FALSE.,.FALSE.)
	CALL GSSGT
C
C	Label position is specified by the user in
C	normalised device coordinates, therefore we must
C	translate text position specified in normalised device
C	coordinates into world coordinates.
C
	IF (PRIMARY) THEN
	  WXR=SCRNDX(1)		!world x range
	  WYR=SCRNDY(1)		!world y range
	  TX =(LABX*WXR)-(WXR*0.5)
	  TY =(LABY*WYR)-(WYR*0.5)
	  CALL VSTKD2W(1,TX,TY,WX,WY)
	ELSE
	  WXR=SCRNDX(2)	!world x range
	  WYR=SCRNDY(2)	!world y range
	  TX =(LABX*WXR)-(WXR*0.5)
	  TY =(LABY*WYR)-(WYR*0.5)
	  CALL VSTKD2W(2,TX,TY,WX,WY)
	ENDIF
C
C-------------------------------------------------------------------
C	Labels are drawn into the second set of planes
C	except when 4 planes are selected, or the graphics
C	device is a TEKTRONIX 4014
	IF(PRIMARY)THEN
          IF(NOPLNS.EQ.4.OR.WTYPE(1).EQ.9900)THEN
	    CALL LSG_PLANE_SELECT(1,PLANE_MASK)
	    CALL LSG_PLANE_OFFSET(1,0)
	    CALL LSG_PLANE_VISIBLE(1,-1)
	  ELSE
	    CALL LSG_PLANE_SELECT(1,.NOT.PLANE_MASK)
	    CALL LSG_PLANE_OFFSET(1,NOPLNS)
	    CALL LSG_PLANE_VISIBLE(1,-1)
	  ENDIF
	ENDIF
C
	IF(SECONDARY)THEN
          IF(NOPLNS.EQ.4)THEN
	    CALL LSG_PLANE_SELECT(2,PLANE_MASK)
	    CALL LSG_PLANE_OFFSET(2,0)
	    CALL LSG_PLANE_VISIBLE(2,-1)
	  ELSE
	    CALL LSG_PLANE_SELECT(2,.NOT.PLANE_MASK)
	    CALL LSG_PLANE_OFFSET(2,NOPLNS)
	    CALL LSG_PLANE_VISIBLE(2,-1)
	  ENDIF
	ENDIF
C
C------------------------------------------------------------------------
C	Set label colour
C	If its a 4 plane device then use colour 1 in the
C	colour table, otherwise use the user specified
C	colour.
	IF(NOPLNS.EQ.4)THEN
	  CALL GSTXCI(1)
	ELSE
          CALL GSTXCI(LABCOL)
	ENDIF
C
C-----------------------------------------------------------------------
C	Set linewidth and text angle
	CALL GSLWSC(1.0)
	CALL GSCHUP(0.0,1.0)
C
C-----------------------------------------------------------------------
C	Set label height
	IF(PRIMARY)  HGT=(WYMAX-WYMIN)*(0.0025*LABSIZ)/SCRNDY(1)
	IF(SECONDARY)HGT=(WYMAX-WYMIN)*(0.0025*LABSIZ)/SCRNDY(2)
	CALL GSCHH(HGT)
C
C-----------------------------------------------------------------------
C	Write out the text string to the screen
	DESC(1) = TXTPTR-DCPTR+1
	DESC(2) = %LOC(TXTBUF(DCPTR))
	CALL G3TX(WX,WY,DESC)
C
C	Update workstation
	IF(PRIMARY)CALL GUWK(1,GSUPP)
	IF(SECONDARY)CALL GUWK(2,GSUPP)
C------------------------------------------------------------------------
C
999	RETURN
	END

