	SUBROUTINE CORDTF(X,Y,Z, XX,YY)
C
***	MODULE CORDTF
***	IDENT 27FE88
C
	IMPLICIT NONE
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England
C Author R. Fairbairns
C
C Coded June 1982
C
C To transform a coordinate in model space to one in view space,
C and to update view limits
C
C MOD	Common block VIEW added				Jul-1986
C
C arguments:
	REAL		X, Y, Z		! coordinate in model space
	REAL		XX, YY		! (returned) coordinate in view
C					! space
C
C workspace:
	REAL		ZZZ		! perspective intermediary
	REAL		PROJF		! projection factor
C
***	COMMON/HERE:VIEW/
C
C------------------------------------------------------------------------
C
C transform
	IF (PRSPVW) THEN		! perspective view
	   ZZZ = Z*A2-X*A1		!  - rotate and -
	   PROJF = DIST/(DOV+Z0-ZZZ)
	   XX = (X*A2+Z*A1)*PROJF	!     project
	   YY = (Y-HOV)*PROJF
	ELSE				! isometric view
	   XX = A11*X+A13*Z		!  - simply rotate
	   YY = A21*X+A22*Y+A23*Z	!    (perhaps not so simply!)
	ENDIF
C
C update limits	
	IF (XX.LT.VWMINX) VWMINX = XX
	IF (XX.GT.VWMAXX) VWMAXX = XX
	IF (YY.LT.VWMINY) VWMINY = YY
	IF (YY.GT.VWMAXY) VWMAXY = YY
C
	RETURN
C
	END
