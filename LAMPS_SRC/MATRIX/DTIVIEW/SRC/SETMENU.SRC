C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1988-01-22 12:36:30.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE SET_MENU
C
***	IDENT	22JA88
***	MODULE	SET_MENU
C
C	Copyright : Laser-Scan Laboratories, Cambridge, England
C
C	Author	  : Andrew Morris
C	Created   : Jan 1985
C
	IMPLICIT NONE
C
***	COMMON/HERE:TRANS/		! has TAB_MNU
***	COMMON/HERE:DTICMN/		! has MENU_SET,NX_MNU,etc.
C
C workspace
	INTEGER		SW(2),NW(2),SE(2),NE(2)! map corner table coords
	REAL		X_EW,Y_EW	! east to west vector
	REAL		X_NS,Y_NS	! north to south vector
	REAL		L_EW 		! east to west length
	REAL		L_NS 		! north to south length
	REAL		MYCOS,MYSIN	! a cosine and a sine
	REAL		FX,FY		! X and Y scale factors
	LOGICAL		ERR		! error
	CHARACTER*6	BELL
C
	PARAMETER	(BELL 		= CHAR(7)//CHAR(7)//' ')
C
C functions
	LOGICAL		GET_TAB_COORDS	! get a point from the table
C
C:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
C Prompts for menu NW, SW, SE and NE corners to set up TAB_MNU
C transformation
C
C Will not accept a menu which is more than 2 degrees off rectangular
C:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
C
	MENU_SET = .FALSE.		! unset any previous menu
C
10	CALL WRITEF('%S',BELL)
	IF(.NOT.GET_TAB_COORDS(NW(1),NW(2),'Menu NW corner')) GOTO 20
	CALL WRITEF('%S',BELL)
 	IF(.NOT.GET_TAB_COORDS(SW(1),SW(2),'Menu SW corner')) GOTO 20
	CALL WRITEF('%S',BELL)
	IF(.NOT.GET_TAB_COORDS(SE(1),SE(2),'Menu SE corner')) GOTO 20
	CALL WRITEF('%S',BELL)
	IF(.NOT.GET_TAB_COORDS(NE(1),NE(2),'Menu NE corner')) GOTO 20
C
	X_EW = SE(1)-SW(1)
	Y_EW = SE(2)-SW(2)
	X_NS = NW(1)-SW(1)
	Y_NS = NW(2)-SW(2)
	L_EW = SQRT(X_EW**2+Y_EW**2)
	L_NS = SQRT(X_NS**2+Y_NS**2)
C
C near enough rectangular?
	IF (ABS(X_EW*Y_NS-Y_EW*X_NS).LT.
     +	    L_EW*L_NS*SIN(82*2*3.1459265/360)) THEN
	  CALL WRITEF('%S %S',BELL,BELL)
	  CALL WRITEF('*** ERROR *** Setting up MENU')
	  CALL WRITEF('Badly digitised corner points; try again')
	  GOTO 10
	END IF
C
C set up TAB_MNU transformation matrix
	MYCOS = X_EW/L_EW
	MYSIN = -Y_EW/L_EW
C
	FX = NX_MNU/L_EW
	FY = NY_MNU/L_NS
C
	TAB_MNU(1,1) = FX*MYCOS
	TAB_MNU(1,2) = -FX*MYSIN
	TAB_MNU(1,3) = FX*(-SW(1)*MYCOS+SW(2)*MYSIN)
	TAB_MNU(2,1) = FY*MYSIN
	TAB_MNU(2,2) = FY*MYCOS
	TAB_MNU(2,3) = FY*(-SW(1)*MYSIN-SW(2)*MYCOS)
	TAB_MNU(3,1) = 0
	TAB_MNU(3,2) = 0
	TAB_MNU(3,3) = 1
C
	MENU_SET = .TRUE.
C
20	RETURN
	END
