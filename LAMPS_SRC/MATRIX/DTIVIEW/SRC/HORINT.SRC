	SUBROUTINE HORINT(X, Y,  MASKX, MASKY)
C
***	MODULE HORINT
***	IDENT 11JL86
C
	IMPLICIT NONE
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England
C Author R. Fairbairns
C
C Coded June 1982
C
C To find the intersection (MASKX,MASKY) of a vector with
C		the existing horizon
C
C arguments:
	REAL		X, Y		! other end of vector starting
C					!  (NEWX1,NEWY1)
	REAL		MASKX,MASKY	! intersection (masking point)
C					! (returned result)
C
C The routine, as well as calculating MASKX,Y, puts them into
C  a node of the horizon vector (which it creates)
C
C
***	COMMON/HORIZN/
C
C workspace:
	REAL		DXN, DYN	! ) vector
	REAL		DXO, DYO	! )  components
	REAL		XPN, XPO	! cross products
	REAL		XPD		! denominator
C
C evaluate vector components
	DXN = X-NEWX1
	DYN = Y-NEWY1
	DXO = OLDX2-OLDX1
	DYO = OLDY2-OLDY1
C
C and cross products
	XPN = NEWY1*X-NEWX1*Y
	XPO = OLDY1*OLDX2-OLDX1*OLDY2
C
C and, finally, denominator
	XPD = DXO*DYN-DXN*DYO
C
C calculate results
	MASKX = (XPO*DXN-XPN*DXO)/XPD
	MASKY = (XPO*DYN-XPN*DYO)/XPD
C
C make up the new node
	CALL ADDNOD(MASKX, MASKY)
C
	RETURN
C
	END
