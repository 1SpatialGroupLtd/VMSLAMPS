	SUBROUTINE GEVTM(XREF,YREF,DX,DY,THT,XMAG,YMAG)
C
***	MODULE	GEVTM
***	IDENT	15FE88
C
C	Copyright : Laser Scan Laboratories Ltd., Cambrdige, England
C	Created   : ACM			May 1985
C
C	Sets up NDC coeffs of transformation to scale about REF,
C	rotate about REF and then shift by (DX,DY)
C
C	Derivation : MAT = W2NDC*WMAT*W2NDC_INV
C
	IMPLICIT NONE
C
***	COMMON/HERE:GKSCMN/		!contains MAT,WXMIN,WYMIN
C					!WXMAX,WYMAX
C
C	Arguments
	REAL		XREF,YREF	!Reference point
	REAL		DX,DY		!Shift
	REAL		THT		!Rotation
	REAL		XMAG,YMAG	!Scale factors
C
	REAL		WXR,WYR		!World x and y ranges
	REAL		C,S		!cos and sin values
C
C---------------------------------------------------------------------
C
C	Calculate world x,y range
	WXR = WXMAX-WXMIN
	WYR = WYMAX-WYMIN
C
C	Rotation values
	C = COS(THT)
	S = SIN(THT)
C	
C	Scale and rotate
	MAT(1) =  C*XMAG
	MAT(2) =  S*XMAG*WXR/WYR
	MAT(3) = -S*YMAG*WYR/WXR
	MAT(4) =  C*YMAG
C
C	Scale, rotate and shift
	MAT(5) = (C*XMAG*(WXMIN-XREF)-S*YMAG*(WYMIN-YREF)+XREF+DX-WXMIN)
     +	/WXR
	MAT(6) = (S*XMAG*(WXMIN-XREF)+C*YMAG*(WYMIN-YREF)+YREF+DY-WYMIN)
     +	/WYR
C
	RETURN
C
	END
