C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1990-06-04 13:45:56.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE SET_COLOUR
C
***	MODULE SET_COLOUR
***	IDENT  04JN90
C
C	Copyright : Laser-Scan Laboratories, Cambridge, England.
C	Created   : 19 March 1988
C	Author    : D R Catlow
C
C	Modified  : P Pan		4 June 1990
C		    to reset the colour index stuff
C
C	Routine to set the line colour if outputting to a
C	graphics device, or the line feature code if writing to
C	an IFF file.  In the later case, a change in feature code
C	causes the current IFF feature to be flushed.
C	Currently a check is made for a change in sea / land
C	colour, so that sea and land profiles may be differently
C	coded in the output IFF file.
C	If COLOUR_IFF is true, then a new feature is started
C	each time the colour changes, and the feature code is
C	made the same as the colour index.  The primary use for
C	a multi-feature coded file is for plotting on a colour raster
C	plotter.
C	
C---------------------------------------------------------------------
C
	IMPLICIT NONE
C
***	COMMON/HERE:DRCTRL/	!contains COLOUR_INDEX,PREV_INDEX
C				!and CURFC
***	COMMON/HERE:DEVICE/
***	COMMON/HERE:CULTRE/	!contains LAND_FC, SEA_FC
C
C---------------------------------------------------------------------
C
C	Has the colour index value changed since the previous vector
C	was output?
	IF(COLOUR_INDEX.EQ.PREV_INDEX)GOTO 999		!return
C
C	Is a graphics device selected.  In this case, a call is made
C	to GSPLCI to set the line colour index
	IF(PRIMARY.OR.SECONDARY)THEN
	  CALL GSPLCI(COLOUR_INDEX)
C
	  IF((COLOUR_INDEX.EQ.2).AND.BATH)THEN
	     CALL GSPLCI(15)
	  ENDIF
C
	ENDIF
C
C	Is output to an IFF file selected?
	IF(PLOT_IFF.AND.ACTIVE_IFF)THEN
C
	NULL_FEATURE=.FALSE.
C	Deal with special case of colour index of 0.  This indicates
C	a NULL data value has been encountered.
	  IF(COLOUR_INDEX.EQ.0)THEN
	    CALL FLUSH(.TRUE.)		!terminate current IFF feature
	    NULL_FEATURE=.TRUE. 
	    GOTO 900
	  ENDIF
C
C	If COLOUR_IFF is true, then start a new feature, with
C	the feature code equal to the colour index.
	  IF(COLOUR_IFF)THEN
	    CALL FLUSH(.TRUE.)
C
	    IF (COLOUR_INDEX.EQ.2) THEN
	       IF (BATH) THEN
		  CURFC = LAND_FC
	       ELSE
                  CURFC = SEA_FC
	       ENDIF
	    ELSEIF (COLOUR_INDEX.GT.2) THEN
	       CURFC = COLOUR_INDEX
	    ELSE			! colour_index = 1
	       CURFC = NULL_FC
	    ENDIF
C
	    GOTO 900
	  ENDIF
C	  	  
C	Are the profile vectors outside the Z range
	  IF(COLOUR_INDEX.EQ.1)THEN
	    CALL FLUSH(.TRUE.)
	    CURFC=NULL_FC
	  ENDIF
C
C	Does the colour index indicate sea.  (ie. colour index 2)
	  IF(COLOUR_INDEX.EQ.2)THEN
	    CALL FLUSH(.TRUE.)
	    CURFC=SEA_FC
	    IF(BATH)CURFC=LAND_FC
	  ENDIF
C
C	Does the colour index indicate land.
	  IF(COLOUR_INDEX.GT.2)THEN
	    CALL FLUSH(.TRUE.)
	    CURFC=LAND_FC
	    IF(BATH)CURFC=SEA_FC
	  ENDIF
	ENDIF
C
C	Update previous colour index
900	PREV_INDEX = COLOUR_INDEX
C	
C
999	RETURN
C
	END

