C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1988-03-21 14:47:46.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE LEGBAT
C
***	MODULE	LEGBAT
***	IDENT	21MR88
C
C	Copyright : Laser-Scan Laboratories Ltd, Cambridge, England
C	Author    : DRC
C	Created   : September 1985
C
C	Routine to output legend on colour display relating to
C	bathymetric colour option
C	Modified version of LEGEND.
C
	IMPLICIT NONE
C
***	COMMON/LSL$CMNLSL:EXPC/
***	COMMON/HERE:DRCTRL/
***	COMMON/HERE:DEVICE/
***	COMMON/HERE:DTICMN/
***	COMMON/HERE:GKSCMN/
C
C workspace
	INTEGER*4	I		! counter
	INTEGER*4	BOX		! number of boxes required
	INTEGER*4	ISEG		! saved quadrant value
	INTEGER*4	DEVICE
	REAL		HGT0
	REAL		SDX,SDY
	REAL		RDUMMY
	REAL		LS		! legend scaling factor
	REAL		LX,LY		! legend position in device units
	REAL		XLL,YLL,XUR,YUR	! box ll and ur corners in WC
	REAL		WXR,WYR		! world range
	REAL		WX,WY		! world coords
C
C
C**********************************************************************
C
C	Is an appropriate device selected?
	IF (.NOT.(PRIMARY.OR.SECONDARY))GOTO 999
C
C	Deactivate TEK 4014 since a legend on a non-colour device
C	is meaningless
	IF ((PRIMARY).AND.(WTYPE(1).EQ.9900))CALL GDAWK(1)
C	
C----------------------------------------------------------------------
C	Take into account a split screen
	IF (SPLIT)THEN
	   ISEG=QUAD
	   QUAD=QUAD-1
	   IF(QUAD.EQ.0)QUAD=4
	ENDIF
C
C	Set text characteristics
	CALL GSTXCI(1)		!colour
	CALL GSLWSC(1.0)	!thin lines
	CALL GSCHUP(0.0,1.0)	!angle of text
	IF(PRIMARY)THEN
	  HGT0=(WYMAX-WYMIN)*0.0025/SCRNDY(1)
	  CALL GSCHH(HGT0)
	ENDIF
C
	IF(SECONDARY)THEN
	  HGT0=(WYMAX-WYMIN)*0.0025/SCRNDY(2)
	  CALL GSCHH(HGT0)
	ENDIF
C
C	Translate legend position specified in normalised device
C	coordinates into world coordinates.
C
	IF (PRIMARY) THEN
	  WXR=SCRNDX(1)	!world x range
	  WYR=SCRNDY(1)	!world y range
	  LX =(LEGX*WXR)-(WXR*0.5)
	  LY =(LEGY*WYR)-(WYR*0.5)
	  LS =LEGSIZ*0.01
	  DEVICE=1
C	Otherwise 
	ELSE
	  WXR=SCRNDX(2)	!world x range
	  WYR=SCRNDY(2)	!world y range
	  LX =(LEGX*WXR)-(WXR*0.5)
	  LY =(LEGY*WYR)-(WYR*0.5)
	  LS =LEGSIZ*0.01
	  DEVICE=2
	END IF
C
	BOX=0
C	Generate legend boxes and label with height
C	Legend may have up to 14 steps + 1 for land
	DO 100 I=STEPS,1,-1
	   BOX=BOX+1
	   CALL VSTKD2W(DEVICE,LS+LX,LS*BOX*5+LY,XLL,YLL)
	   CALL VSTKD2W(DEVICE,LS*2.0+LX,
     &			LS*(BOX+1)*5.0+LY,XUR,YUR)
	   CALL G3FILL(XLL,YLL,XUR,YUR,HGTSTP(2,I))
C
C	Label with height
	   CALL VSTKD2W(DEVICE,LS*2.5+LX,LS*BOX*5.0+LY,WX,WY)
C
	   IF(I.EQ.STEPS)THEN
	     CALL EXPAND('%I',-SEABOT)
	     CALL G3TX(WX,WY,EXPDSC)
	   ELSE
	      CALL EXPAND('%I',HGTSTP(1,I))
	      CALL G3TX(WX,WY,EXPDSC)
	   ENDIF
C
100	CONTINUE		   
C
	BOX=BOX+1
C	Finally generate a box for the land, and label
	CALL VSTKD2W(DEVICE,LS+LX,LS*BOX*5+LY,XLL,YLL)
	CALL VSTKD2W(DEVICE,LS*2.0+LX,
     &			LS*(BOX+1)*5.0+LY,XUR,YUR)
	CALL G3FILL(XLL,YLL,XUR,YUR,15)! fill in brown
	CALL EXPAND('   Land')
	CALL VSTKD2W(DEVICE,LS*2.5+LX,LS*BOX*5.0+LY,WX,WY)
	CALL G3TX(WX,WY,EXPDSC)
C
C
	IF (SPLIT) QUAD = ISEG
C
C	Reactivate TEK if nec
	IF ((PRIMARY).AND.(WTYPE(1).EQ.9900))CALL GACWK(1)
C
C------------------------------------------------------------------------------	
C
999	RETURN
C
	END
