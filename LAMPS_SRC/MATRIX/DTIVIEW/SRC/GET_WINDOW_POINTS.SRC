	LOGICAL FUNCTION GET_WINDOW_POINTS (MCMD)
C
***	IDENT	28NO88
***	MODULE	GET_WINDOW_POINTS
C
C	Copyright : Laser-Scan Laboratories, Cambridge, England
C	Author    : D R Catlow
C	Created   : 22 March 1988
C	
CMOD	Use projection units				MWRS 23-Nov-1988
CMOD	Incorporate HAD_ABSOLUTE for metre values 	MWSR 22-Nov-1988
C
C	Function to get the SW and NE window coordinate values.
C	
C
	IMPLICIT NONE
C	
C DTILIB common
***	COMMON/LSL$CMNDTI:DTIHDR/
***	COMMON/LSL$CMNDTI:DTIPROJ/
C
C LSLLIB common
***	COMMON/LSL$CMNLSL:CMDCOM/
C
***	COMMON/HERE:MODEL/		! contains UNITS etc.
***	COMMON/HERE:DTIQIO/		! 
***	COMMON/HERE:TAB/		! contains HAD_VDU
***	COMMON/HERE:TRANS/		! has TAB_DTI transformation
***	COMMON/HERE:DTICMN/		! contains MAP_SET
C
C	Argument
	LOGICAL		MCMD		! true if the command is
C					! issued from a menu box.
C
C	Local
	INTEGER*4	X1,Y1,X2,Y2	! window coordinate values
	INTEGER*4	LLDEGS(4)	! values in seconds
	INTEGER*4	X_TAB,Y_TAB	! table coordinates
	INTEGER*4	CMD		! menu command number
C
C	Function
	LOGICAL		READLL		! reads lat/long
	LOGICAL		RDREAL		! read real value
	LOGICAL		WAIT_INPUT	! to wait for input
	LOGICAL		READ_PUCK	! read puck data
	LOGICAL		IN_MENU		! test if in menu area
C
C************************************************************************
C
	GET_WINDOW_POINTS = .FALSE.
C
C	Deal with WINDOW command from terminal
C	
C	If no arguments are supplied, treat as though menu command
	IF(CMDACT.EQ.0)MCMD=.TRUE.
C	  	
	IF(.NOT.MCMD)THEN
	  IF(CMDACT.NE.4.AND.UNITS.NE.4)THEN
	    CALL WRITEF('%S Specifying command WINDOW',ERROR_BELL)
	    CALL WRITEF('Command requires 4 arguments')
	    GOTO 999
	  ENDIF
C
C	Matrix units
	  IF     (UNITS.EQ.1)THEN
	    X1=NINT(REALAR(1))
	    Y1=NINT(REALAR(2))
	    X2=NINT(REALAR(3))
	    Y2=NINT(REALAR(4))
C	Metre units
	  ELSE IF(UNITS.EQ.2)THEN
	    IF (HAD_ABSOLUTE) THEN
	      X1=NINT((REALAR(1)-X_OFFSET(1))/X_GRID(1))+1
	      Y1=NINT((REALAR(2)-Y_OFFSET(1))/Y_GRID(1))+1
	      X2=NINT((REALAR(3)-X_OFFSET(1))/X_GRID(1))+1
	      Y2=NINT((REALAR(4)-Y_OFFSET(1))/Y_GRID(1))+1
	    ELSE
	      X1=NINT(REALAR(1)/X_GRID(1))+1
	      Y1=NINT(REALAR(2)/Y_GRID(1))+1
	      X2=NINT(REALAR(3)/X_GRID(1))+1
	      Y2=NINT(REALAR(4)/Y_GRID(1))+1
	    ENDIF
C	Latlong units
	  ELSE IF(UNITS.EQ.4)THEN
	    IF(READLL(LLDEGS,4,.FALSE.))THEN
	      CALL WRITEF('%S Specifying command WINDOW',ERROR_BELL)
	      CALL WRITEF
     &        ('Latitude and longitude values supplied in wrong format')
	      GOTO 999
	    ENDIF
	    REALAR(1)=LLDEGS(1)
	    REALAR(2)=LLDEGS(2)
	    REALAR(3)=LLDEGS(3)
	    REALAR(4)=LLDEGS(4)
	  ELSEIF(UNITS .EQ. 5)THEN
C
C projection units
	    IF (HAD_ABSOLUTE) THEN 
	      X1=NINT((REALAR(1)-DTIPROJ_ORIGIN(1,1))
     &             /DTIPROJ_SAMPLE(1,1))+1
	      Y1=NINT((REALAR(2)-DTIPROJ_ORIGIN(2,1))
     &             /DTIPROJ_SAMPLE(2,1))+1
	      X2=NINT((REALAR(3)-DTIPROJ_ORIGIN(1,1))
     &             /DTIPROJ_SAMPLE(1,1))+1
	      Y2=NINT((REALAR(4)-DTIPROJ_ORIGIN(2,1))
     &             /DTIPROJ_SAMPLE(2,1))+1
	    ELSE
	      X1=NINT(REALAR(1)/DTIPROJ_SAMPLE(1,1))+1
	      Y1=NINT(REALAR(2)/DTIPROJ_SAMPLE(2,1))+1
	      X2=NINT(REALAR(3)/DTIPROJ_SAMPLE(1,1))+1
	      Y2=NINT(REALAR(4)/DTIPROJ_SAMPLE(2,1))+1
	    ENDIF
	  ENDIF	      	      
C
	  IF(UNITS.EQ.3.OR.UNITS.EQ.4)THEN
	    X1=NINT(REALAR(2)*10)
	    Y1=NINT(REALAR(1)*10)
	    X1=((X1-LONREC(1,1))/LONINT(1))+1
	    Y1=((Y1-LATREC(1,1))/LATINT(1))+1
	    X2=NINT(REALAR(4)*10)
	    Y2=NINT(REALAR(3)*10)
	    X2=((X2-LONREC(1,1))/LONINT(1))+1
	    Y2=((Y2-LATREC(1,1))/LATINT(1))+1
	  ENDIF
C	Check that start coordinates are within the DTM
	  IF(X1.LT.1.OR.X1.GT.X_EXTENT(1).OR.
     &       Y1.LT.1.OR.Y1.GT.Y_EXTENT(1).OR.
     &       X2.LT.1.OR.X2.GT.X_EXTENT(1).OR.
     &       Y2.LT.1.OR.Y2.GT.Y_EXTENT(1))THEN
	     CALL WRITEF('%S Specifying command WINDOW',ERROR_BELL)
	     CALL WRITEF('Supplied values outside DTM bounds')
	     GOTO 999
	  ENDIF	   
C	Check that the SW corner value is less than the NE value
	  IF(X2.LE.X1.OR.Y2.LE.Y1)THEN
	     CALL WRITEF('%S Specifying command WINDOW',ERROR_BELL)
	     CALL WRITEF('NE corner values must exceed SW corner values')
	     GOTO 999				!reprompt
	  ENDIF	   
	  GOTO 900
	ENDIF
C
C	If the command is issued via the menu, then we need to
C	prompt for start and end coordinate values
C	This is also done, if the WINDOW command is given from the
C	terminal, without any arguments.
C
	IF(MCMD)THEN
C
C	First prompt for the SW window x and y coordinate values
100	   IF(.NOT.WAIT_INPUT('Window SW corner',.TRUE.,.TRUE.))
     &     GOTO 999
C
C	If input is from the terminal and expected format is not LATLONG
C	then........
	   IF(HAD_VDU)THEN
             IF(UNITS.NE.4)THEN
	       IF(RDREAL(REALAR(1)))GOTO 100	!reprompt
	       IF(RDREAL(REALAR(2)))GOTO 100	!reprompt
C
C	If input is from the terminal and expected format is LATLONG
C	then........
	     ELSE
C	Convert to seconds of arc 
               IF(READLL(LLDEGS,2,.FALSE.))THEN
	         CALL WRITEF
     &	   ('Latitude and longitude values supplied in wrong format')
	         GOTO 100				!reprompt
	       ELSE
	         REALAR(1)=LLDEGS(1)		!lat in seconds
	         REALAR(2)=LLDEGS(2)		!long in seconds
	       ENDIF
	     ENDIF
C
C	Convert coordinates to nearest DTI value
	     IF     (UNITS.EQ.1)THEN			!matrix
	        X1=NINT(REALAR(1))
	        Y1=NINT(REALAR(2))
	     ELSE IF(UNITS.EQ.2)THEN			!metres
		IF (HAD_ABSOLUTE)THEN
	           X1=NINT((REALAR(1)-X_OFFSET(1))/X_GRID(1))+1
	           Y1=NINT((REALAR(2)-Y_OFFSET(1))/Y_GRID(1))+1
		ELSE
	           X1=NINT((REALAR(1))/X_GRID(1))+1
	           Y1=NINT((REALAR(2))/Y_GRID(1))+1
		ENDIF
	     ELSE IF (UNITS.EQ.3 .OR. UNITS.EQ.4)THEN	!secs or LATLONG
	        X1=NINT(REALAR(2)*10)
	        Y1=NINT(REALAR(1)*10)
	        X1=((X1-LONREC(1,1))/LONINT(1))+1
	        Y1=((Y1-LATREC(1,1))/LATINT(1))+1
	     ELSE				     !projection units
	        IF (HAD_ABSOLUTE) THEN 
	           X1=NINT((REALAR(1)-DTIPROJ_ORIGIN(1,1))
     &             /DTIPROJ_SAMPLE(1,1))+1
	           Y1=NINT((REALAR(2)-DTIPROJ_ORIGIN(2,1))
     &             /DTIPROJ_SAMPLE(2,1))+1
	        ELSE
	           X1=NINT(REALAR(1)/DTIPROJ_SAMPLE(1,1))+1
	           Y1=NINT(REALAR(2)/DTIPROJ_SAMPLE(2,1))+1
	        ENDIF
	     ENDIF
C
C	If input is from the table puck then......
	   ELSE 
C	First check that a map has been set up on the table
	     IF(.NOT.MAP_SET)THEN
	       CALL WRITEF('%S Inputting coordinates from table',ERROR_BELL)
	       CALL WRITEF('No map setup has been performed')
	       GOTO 999				!return
	     ENDIF
C	Read the table coordinates
	     IF(READ_PUCK(BUTTON,X_TAB,Y_TAB,IERR))THEN
               CALL SHOW_ERROR(IERR)
	       GOTO 999
	     ENDIF
C	Test we are not in the menu area
	     IF(IN_MENU(X_TAB,Y_TAB,CMD))THEN
	       CALL WRITEF('%S Inputting coordinates from table',ERROR_BELL)
	       CALL WRITEF('Digitised point lies inside the menu')
	       GOTO 100				!reprompt
	     ENDIF
C	Convert from table coordinates to DTI coordinates
	     X1=NINT(TAB_DTI(1,1)*X_TAB+TAB_DTI(1,2)*Y_TAB+TAB_DTI(1,3))
	     Y1=NINT(TAB_DTI(2,1)*X_TAB+TAB_DTI(2,2)*Y_TAB+TAB_DTI(2,3))
C       Output coordinates to follow the prompt
	     TTOBEL = .TRUE.	!signal to VIO$PUT_OUTPUT
	     CALL WRITEF('%N %N',X1,Y1)
	     TTOBEL = .FALSE.
	  ENDIF
C
C	Check that the SW coordinates are within the DTM
	  IF(X1.LT.1.OR.X1.GT.X_EXTENT(1).OR.
     &       Y1.LT.1.OR.Y1.GT.Y_EXTENT(1))THEN
	     CALL WRITEF
     &    ('%S Specifying WINDOW SW corner',ERROR_BELL)
	     CALL WRITEF('Point is outside DTM bounds')
	     GOTO 100				!reprompt
	  ENDIF	   
C	
C	Now prompt for the NE window x and y coordinate values
200	  IF(.NOT.WAIT_INPUT('Window NE corner',.TRUE.,.TRUE.))
     &    GOTO 999
C
C	If input is from the terminal and expected format is not LATLONG
C	then........
	  IF(HAD_VDU)THEN
            IF(UNITS.NE.4)THEN
	      IF(RDREAL(REALAR(3)))GOTO 200	!reprompt
	      IF(RDREAL(REALAR(4)))GOTO 200	!reprompt
C
C	If input is from the terminal and expected format is LATLONG
C	then........
	    ELSE
C	Convert to seconds of arc 
              IF(READLL(LLDEGS,2,.FALSE.))THEN
	        CALL WRITEF
     &	     ('Latitude and longitude values supplied in wrong format')
	        GOTO 200				!reprompt
	      ELSE
	        REALAR(3)=LLDEGS(1)		!lat in seconds
	        REALAR(4)=LLDEGS(2)		!long in seconds
	      ENDIF
	    ENDIF
C
C	Convert coordinates to nearest DTI value
	    IF     (UNITS.EQ.1)THEN			!matrix
	        X2=NINT(REALAR(3))
	        Y2=NINT(REALAR(4))
	    ELSE IF(UNITS.EQ.2)THEN			!metres
		IF (HAD_ABSOLUTE) THEN
	           X2=NINT((REALAR(3)-X_OFFSET(1))/X_GRID(1))+1
	           Y2=NINT((REALAR(4)-Y_OFFSET(1))/Y_GRID(1))+1
		ELSE
	           X2=NINT((REALAR(3))/X_GRID(1))+1
	           Y2=NINT((REALAR(4))/Y_GRID(1))+1
		ENDIF
	    ELSEIF (UNITS .EQ. 3 .OR. UNITS .EQ. 4)THEN	!secs or LATLONG
	        X2=NINT(REALAR(4)*10)
	        Y2=NINT(REALAR(3)*10)
	        X2=((X2-LONREC(1,1))/LONINT(1))+1
	        Y2=((Y2-LATREC(1,1))/LATINT(1))+1
	    ELSE					!projection
	        IF (HAD_ABSOLUTE) THEN 
	           X2=NINT((REALAR(3)-DTIPROJ_ORIGIN(1,1))
     &             /DTIPROJ_SAMPLE(1,1))+1
	           Y2=NINT((REALAR(4)-DTIPROJ_ORIGIN(2,1))
     &             /DTIPROJ_SAMPLE(2,1))+1
	        ELSE
	           X2=NINT(REALAR(3)/DTIPROJ_SAMPLE(1,1))+1
	           Y2=NINT(REALAR(4)/DTIPROJ_SAMPLE(2,1))+1
	        ENDIF
	    ENDIF
C
C	If input is from the table puck then......
	  ELSE 
C	Read the table coordinates
	    IF(READ_PUCK(BUTTON,X_TAB,Y_TAB,IERR))THEN
               CALL SHOW_ERROR(IERR)
	       GOTO 999
	    ENDIF
C	Test we are not in the menu area
	    IF(IN_MENU(X_TAB,Y_TAB,CMD))THEN
	       CALL WRITEF('%S Inputting coordinates from table',ERROR_BELL)
	       CALL WRITEF('Digitised point lies inside the menu')
	       GOTO 200				!reprompt
	    ENDIF
C	Convert from table coordinates to DTI coordinates
	     X2=NINT(TAB_DTI(1,1)*X_TAB+TAB_DTI(1,2)*Y_TAB+TAB_DTI(1,3))
	     Y2=NINT(TAB_DTI(2,1)*X_TAB+TAB_DTI(2,2)*Y_TAB+TAB_DTI(2,3))
C       Output coordinates to follow the prompt
	     TTOBEL = .TRUE.	!signal to VIO$PUT_OUTPUT
	     CALL WRITEF('%N %N',X2,Y2)
	     TTOBEL = .FALSE.
	  ENDIF
C
C	Check that coordinates are within the DTM
	  IF(X2.LT.1.OR.X2.GT.X_EXTENT(1).OR.
     &       Y2.LT.1.OR.Y2.GT.Y_EXTENT(1))THEN
	     CALL WRITEF
     &       ('%S Specifying WINDOW NE corner',ERROR_BELL)
	     CALL WRITEF('Point is outside DTM bounds')
	     GOTO 200				!reprompt
	  ENDIF
	ENDIF	   
C
C	Signal success
900	GET_WINDOW_POINTS=.TRUE.
C
C	Transfer to the DTI common block variables
	DTI_WINDOW_SWX(1) = X1
	DTI_WINDOW_SWY(1) = Y1
	DTI_WINDOW_NEX(1) = X2	
	DTI_WINDOW_NEY(1) = Y2
C
C
999	MCMD=.FALSE.
	RETURN
C
	END	  
