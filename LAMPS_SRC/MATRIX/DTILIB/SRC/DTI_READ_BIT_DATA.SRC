C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1991-07-17 19:06:30.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	INTEGER*4 FUNCTION DTI_READ_BIT_DATA
     &                     (DTI_LUN, BYTE_READ, X_POSN ,Y_POSN )
C
***	IDENT	17JL91
***	MODULE	DTI_READ_BIT_DATA
C
C	Copyright : Laser-Scan Laboratories, Cambridge, England
C	Author    : D R Catlow
C
C	DTILIB Function.   
C
C	Reads a bit value from the DTI data area at position 
C	X_POSN,Y_POSN, and returns the value 0 or 1 in BYTE_READ
C
C	The function returns true if successful.
C
C----------------------------------------------------------------------------
C
	IMPLICIT NONE
C
C parameters
	INCLUDE '($SSDEF)'
C
***	PARAMETER/LSL$CMNDTI:DTIDEF/
***	PARAMETER/LSL$CMNDTI:DTIPAR/
***	PARAMETER/LSL$CMNDTI:DTILIBMSG/
C
***	COMMON/LSL$CMNDTI:DTIHDR/
C
C arguments
	INTEGER*4		DTI_LUN		!which DTI file?
	INTEGER*4		X_POSN		!Column value
	INTEGER*4		Y_POSN		!Row value
	BYTE			BYTE_READ	!Value of bit
C
C****************************************************************************
C
C	Test if the supplied LUN number is valid
	IF(DTI_LUN.LT.1.OR.DTI_LUN.GT.MAX_DTI)THEN
	  DTI_READ_BIT_DATA=DTI__BADLUN		!LUN out of range
	  GOTO 999					!Return
	ENDIF
C
C	Test if the supplied LUN is in use
	IF(.NOT.DTILUN_INUSE(DTI_LUN))THEN
	  DTI_READ_BIT_DATA=DTI__LUNUNUSED		!LUN not in use
	  GOTO 999					!Return
	ENDIF
C
C	Test if the file is set up to contain bit data
	IF(DATA_TYPE(DTI_LUN).NE.DATA_BIT)THEN
	  DTI_READ_BIT_DATA=DTI__DATAMISMATCH		!Data type mismatch
	  GOTO 999
	ENDIF
C
C	Test if the supplied x and y positions are within the data area
	IF(X_POSN.LE.0.OR.X_POSN.GT.X_EXTENT(DTI_LUN).OR.
     &	   Y_POSN.LE.0.OR.Y_POSN.GT.Y_EXTENT(DTI_LUN))THEN
	  DTI_READ_BIT_DATA=DTI__BADDATAPOS		!Bad data position
	  GOTO 999					!Return
	ENDIF
C
C----------------------------------------------------------------------------
C
C	O.K. now retrive the value of the bit
	CALL DTI_RBIT_DATA (%VAL(SEC_ADDR(DTI_LUN)+DATA_OFFSET(DTI_LUN)),
     & 		          X_POSN,Y_POSN,BYTE_READ,DTI_LUN)
C
	DTI_READ_BIT_DATA=DTI__NORMAL	!Signal success
C
999	RETURN
C
	END
C
C****************************************************************************
C
	SUBROUTINE DTI_RBIT_DATA
     &  (DATA_ARRAY,COL_POSN,ROW_POSN,VALUE,WHICH_DTI)
C
***	IDENT	02AP87
***	MODULE	DTI_RBIT_DATA
C
C	Copyright : Laser-Scan Laboratories, Cambridge, England
C	Author    : D R Catlow
C	Mod	  : PGH/JB	Redefine DATA_ARRAY
C
	IMPLICIT NONE
C
***	COMMON/LSL$CMNDTI:DTIHDR/
C
C arguments
	INTEGER*4	WHICH_DTI		!which DTI file?
CCC	BYTE		DATA_ARRAY(Y_EXTENT(WHICH_DTI)/8,!data array
CCC  &				   X_EXTENT(WHICH_DTI))
C
	BYTE		DATA_ARRAY(*)
C Above declaration used to work, but violates the Fortran standard,
C in that an array bound expression may not refer to another array!
C The compiler now objects, so change to assumed size array (*).
C
	INTEGER*4	COL_POSN		!x coordinate
	INTEGER*4	ROW_POSN		!y coordinate
	BYTE		VALUE			!byte value
C
C local
	INTEGER*4	N
	INTEGER*4	MAX_Y,MAX_X
	INTEGER*4	Y_BYTE			!byte we are interested in
	INTEGER*4	BIT_OFFSET		!bit offset in required byte
	BYTE		BYTE_VALUE		!byte data value
	INTEGER*2	WORD_VALUE		!unsigned data value
C
	EQUIVALENCE (BYTE_VALUE,WORD_VALUE)
C
C---------------------------------------------------------------------------
	MAX_Y = Y_EXTENT(WHICH_DTI)/8
C	MAX_X = X_EXTENT(WHICH_DTI)
C
C First work out which BYTE we are interested in
	Y_BYTE = 1 + (ROW_POSN-1)/8
C	BYTE_VALUE=DATA_ARRAY(Y_BYTE,COL_POSN)
C
	N = MAX_Y*(COL_POSN-1) + Y_BYTE
	BYTE_VALUE=DATA_ARRAY(N)
C
C and the bit offset in the byte
	BIT_OFFSET = MOD(ROW_POSN,8)
	IF(BIT_OFFSET.EQ.0)BIT_OFFSET=8
C
C Test if the appropriate bit is set
	IF(BTEST(WORD_VALUE,BIT_OFFSET-1))THEN
	  VALUE=1
	ELSE
	  VALUE=0
	ENDIF
C
C--------------------------------------------------------------------------
C
	RETURN
	END
C
