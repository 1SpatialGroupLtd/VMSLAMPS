C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1988-08-03 16:31:20.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	INTEGER*4 FUNCTION DTI_WRITE_DSI ( DTI_LUN, RETVAL )
C
***	IDENT	03AU88
***	MODULE	DTI_WRITE_DSI
C
C	Copyright : Laser-Scan Laboratories, Cambridge, England
C	Author    : D R Catlow
C	Created   : 23 June 1987
C
C	Modified  : 01AU88 to test for write access to the file
C	Modified  : 03AU88 to replace DTI_DTED with DTI_TED4
C
C	DTILIB Function.   
C
C	Modifies the part of the DSI record containing 
C	LATLONG origin, corners and grid interval.
C
C	The LATLONG header details are transferred from the 
C	common block DTIHDR.
C
C	The function returns true if successful.
C	Any system error is returned in RETVAL.
C
C-----------------------------------------------------------------------------
C
	IMPLICIT NONE
C
C parameters
	INCLUDE '($SSDEF)'
C
***	PARAMETER/LSL$CMNDTI:DTIDEF/
***	PARAMETER/LSL$CMNDTI:DTIPAR/
***	PARAMETER/LSL$CMNDTI:DTILIBMSG/
C
***	COMMON/LSL$CMNDTI:DTIHDR/
C
C local
	INTEGER*4	ERROR			!READSI system 
C						!error return code
C arguments
	INTEGER*4	DTI_LUN			!LUN on which to open file
	INTEGER*4	RETVAL			!system error return code
C
C****************************************************************************
C
C	Test if the supplied LUN number is valid
	IF(DTI_LUN.LT.1.OR.DTI_LUN.GT.MAX_DTI)THEN
	  DTI_WRITE_DSI=DTI__BADLUN		!LUN out of range
	  GOTO 999				!Return
	ENDIF
C
C	Test of the supplied LUN is in use
	IF(.NOT.DTILUN_INUSE(DTI_LUN))THEN
	  DTI_WRITE_DSI=DTI__LUNUNUSED		!no file opened on LUN
	  GOTO 999				!Return
	ENDIF
C
C	Test if the file is a TED4 file
	IF(HEADER_TYPE(DTI_LUN).NE.DTI_TED4)THEN
	  DTI_WRITE_DSI=DTI__BADHEADER		!bad header
	  GOTO 999
	ENDIF
C
C	Test for write access to the file
	IF(READ_ONLY(DTI_LUN))THEN
	  DTI_WRITE_DSI=DTI__READONLY		!read only access
	  GOTO 999
	ENDIF
C
C--------------------------------------------------------------------------
C
	CALL DTI_WRIDSI(DATA_OFFSET(DTI_LUN)-1,%VAL(SEC_ADDR(DTI_LUN)),
     &			DTI_LUN,ERROR)
C
C	Check for any error
	IF(ERROR.NE.SS$_NORMAL)THEN
	  DTI_WRITE_DSI=DTI__SYSERR
	  RETVAL=ERROR				!Pass back system error
	ELSE
	  DTI_WRITE_DSI=DTI__NORMAL		!Signal success
	ENDIF
C
999	RETURN
C
	END	   
C
C****************************************************************************
C
	SUBROUTINE DTI_WRIDSI (HEADER_SIZE,HEADER_ARRAY,WHICH_DTI,IERR)
C
***	IDENT	22OC86
***	MODULE	DTI_WRIDSI
C
C	Copyright : Laser-Scan Laboratories, Cambridge, England
C	Author    : D R Catlow
C
	IMPLICIT NONE
C
***	PARAMETER/LSL$CMNDTI:DTILIBMSG/
***	COMMON/LSL$CMNDTI:DTIHDR/
***	COMMON/LSL$CMNLSL:EXPC/
C
C arguments
	INTEGER*4	HEADER_SIZE	!Size of header area
	BYTE		HEADER_ARRAY(0:HEADER_SIZE)
	INTEGER*4	WHICH_DTI	!Which DTI file
	INTEGER*4	IERR		!system error code
C locals
	INTEGER*4	I		!counter
	INTEGER*4	DEGR,MINU,SECO	!converted latlong values
	LOGICAL		NEGA		!neg or pos seconds value
C
C function
	INTEGER*4	VIOMV3		!transfer bytes
C
C***************************************************************************
C
C	Write origin values
        CALL DTI_CVTANG(LATORI(WHICH_DTI),
     &  NEGA,DEGR,MINU,SECO)
	CALL EXPAND('%P0%I2%P0%I2%P0%I2.0',DEGR,MINU,SECO)
	IF(NEGA)THEN
	  CALL APPEND('S')
	ELSE
	  CALL APPEND('N')
	ENDIF
C
	CALL DTI_CVTANG(LONORI(WHICH_DTI),
     &	NEGA,DEGR,MINU,SECO)
	CALL APPEND('%P0%I3%P0%I2%P0%I2.0',DEGR,MINU,SECO)
	IF(NEGA)THEN
	  CALL APPEND('W')
	ELSE
	  CALL APPEND('E')
	ENDIF
C		
C-----------------------------------------------------------------
	DO 200 I=1,4
C
          CALL DTI_CVTANG(LATREC(I,WHICH_DTI),
     &    NEGA,DEGR,MINU,SECO)
	  CALL APPEND('%P0%I2%P0%I2%P0%I2',DEGR,MINU,SECO)
	  IF(NEGA)THEN
	    CALL APPEND('S')
	  ELSE
	    CALL APPEND('N')
	  ENDIF
C
	  CALL DTI_CVTANG(LONREC(I,WHICH_DTI),
     &	  NEGA,DEGR,MINU,SECO)
	  CALL APPEND('%P0%I3%P0%I2%P0%I2',DEGR,MINU,SECO)
	  IF(NEGA)THEN
	    CALL APPEND('W')
	  ELSE
	    CALL APPEND('E')
	  ENDIF
C
200	CONTINUE
C
	IERR = VIOMV3(EXPBUF,79,HEADER_ARRAY(421))
	IF(.NOT.IERR)GOTO 999	
C
C--------------------------------------------------------------------------
C
C	Write the LATINT and LONINT records
	CALL EXPAND('%P0%I4',LATINT(WHICH_DTI))
	CALL APPEND('%P0%I4',LONINT(WHICH_DTI))
C
	IERR= VIOMV3(EXPBUF,8,HEADER_ARRAY(509))
	IF(.NOT.IERR)GOTO 999	
C
C--------------------------------------------------------------------------
C
C	Modify the number of lines and rows
C	Format NNNN NNNN
C
	CALL EXPAND('%P0%I4',Y_EXTENT(WHICH_DTI))
	CALL APPEND('%P0%I4',X_EXTENT(WHICH_DTI))
C
	IERR= VIOMV3(EXPBUF,8,HEADER_ARRAY(517))
	IF(.NOT.IERR)GOTO 999
C
999	RETURN
C
	END
