	INTEGER*4 FUNCTION DTI_READ_WORD_HEADER 
     &                     ( DTI_LUN, WORD_READ, BYTE_OFFSET, RETVAL)
C
***	IDENT	29JL88
***	MODULE	DTI_READ_WORD_HEADER
C
C	Copyright : Laser-Scan Laboratories, Cambridge, England
C	Author    : D R Catlow
C	Created   : 13 November 1986
C
C	Modified  : DRC 29JL88  To return an error if the supplied byte
C		    offset would result in data partly being transferred
C		    from the data area.
C
C	DTILIB Function.   
C
C	Reads a word from the DTI header, and returns it via WORD_READ
C
C	The function returns true if successful.
C	Any system error is returned in RETVAL.
C
C------------------------------------------------------------------------------
C
	IMPLICIT NONE
C
C parameters
	INCLUDE '($SSDEF)'
C
***	PARAMETER/LSL$CMNDTI:DTIDEF/
***	PARAMETER/LSL$CMNDTI:DTIPAR/
***	PARAMETER/LSL$CMNDTI:DTILIBMSG/
C
***	COMMON/LSL$CMNDTI:DTIHDR/
C
C arguments
	INTEGER*2		WORD_READ	!Value of word (returned)
	INTEGER*4		BYTE_OFFSET	!Offset in header
	INTEGER*4		DTI_LUN		!which DTI file?
	INTEGER*4		RETVAL		!error return code
C
C local
	INTEGER*4		IERR		!function return status
C
C****************************************************************************
C
C	Test if the supplied LUN number is valid
	IF(DTI_LUN.LT.1.OR.DTI_LUN.GT.MAX_DTI)THEN
	  DTI_READ_WORD_HEADER=DTI__BADLUN	!LUN out of range
	  GOTO 999				!Return
	ENDIF
C
C	Test of the supplied LUN is in use
	IF(.NOT.DTILUN_INUSE(DTI_LUN))THEN
	  DTI_READ_WORD_HEADER=DTI__LUNUNUSED	!LUN not in use
	  GOTO 999				!Return
	ENDIF
C
C	Test if the supplied byte offset is sensible.
C	ie that it lies within the header area, and allows 2 bytes to
C	be read without reading a byte from the data area.
	IF(BYTE_OFFSET.LT.0.OR.BYTE_OFFSET.GT.DATA_OFFSET(DTI_LUN)-2)THEN
	  DTI_READ_WORD_HEADER=DTI__BADHEADOFF	!Bad data offset
	  GOTO 999				!Return
	ENDIF
C
C----------------------------------------------------------------------------
C	O.K. now extract the value of the word
	CALL DTI_RW (DATA_OFFSET(DTI_LUN)-1,%VAL(SEC_ADDR(DTI_LUN)),
     & 		     BYTE_OFFSET,WORD_READ,IERR)
C
C	Check for any error - highly unlikely!
	IF(IERR.NE.SS$_NORMAL)THEN
	  DTI_READ_WORD_HEADER=DTI__SYSERR
	  RETVAL=IERR				!Pass back system error
	ELSE
	  DTI_READ_WORD_HEADER=DTI__NORMAL	!Signal success
	ENDIF
C
999	RETURN
C
	END
C
C****************************************************************************
C
	SUBROUTINE DTI_RW 
     &  (HEADER_SIZE,HEADER_ARRAY,B_OFF,VALUE,ERROR)
C
***	IDENT	07OC86
***	MODULE	DTI_RW
C
C	Copyright : Laser-Scan Laboratories, Cambridge, England
C	Author    : D R Catlow
C
	IMPLICIT NONE
C
C arguments
	INTEGER*4	HEADER_SIZE		!Size of header
	BYTE		HEADER_ARRAY(0:HEADER_SIZE)!Byte header
	INTEGER*4	B_OFF			!Byte offset in header
	INTEGER*2	VALUE			!word value
	INTEGER*4	ERROR			!VIOMV3 return code
C
C function
	INTEGER*4	VIOMV3		!to transfer 2 bytes from byte header
C					!array into VALUE
C
C---------------------------------------------------------------------------
C
C	Transfer 2 bytes
	ERROR=VIOMV3(HEADER_ARRAY(B_OFF),2,VALUE)
C	
999	RETURN
C
	END
