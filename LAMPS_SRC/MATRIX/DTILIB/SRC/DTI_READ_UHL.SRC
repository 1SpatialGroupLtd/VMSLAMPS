	INTEGER*4 FUNCTION DTI_READ_UHL ( DTI_LUN )
C
***	IDENT	27SE91
***	MODULE	DTI_READ_UHL
C
C	Copyright : Laser-Scan, Cambridge, England
C	Author    : D R Catlow
C
C	DTILIB Function.   
C
C	Reads the UHL (User Header Label )record contained in the 
C	header of a UHL1 type DTI header, and extracts the latlong 
C	corner and origin values, and latlong grid intervals.  
C	Since the record only contains latlong values for the SW corner, 
C	the dataset origin is taken to be equal to the SW corner, and 
C	the remainder of the corner values are calculated from the 
C	SW corner values and latlong grid interval values.
C	The header details are transferred to the common block DTIHDR.
C
C	The function returns true if successful.
C	Any system error is returned in RETVAL.
C
C----------------------------------------------------------------------------
C
	IMPLICIT NONE
C
C parameters
***	PARAMETER/LSL$CMNDTI:DTIDEF/
***	PARAMETER/LSL$CMNDTI:DTIPAR/
***	PARAMETER/LSL$CMNDTI:DTILIBMSG/
C
***	COMMON/LSL$CMNDTI:DTIHDR/
C
C arguments
	INTEGER*4	DTI_LUN			!LUN on which to open file
C
C local
	INTEGER*4	ERROR			!System error code
C
C****************************************************************************
C	Test if the supplied LUN number is valid
	IF(DTI_LUN.LT.1.OR.DTI_LUN.GT.MAX_DTI)THEN
	  DTI_READ_UHL=DTI__BADLUN		!LUN out of range
	  GOTO 999				!Return
	ENDIF
C
C	Test of the supplied LUN is in use
	IF(.NOT.DTILUN_INUSE(DTI_LUN))THEN
	  DTI_READ_UHL=DTI__LUNUNUSED		!no file opened on LUN
	  GOTO 999				!Return
	ENDIF
C
C	Test if the file is a UHL1 file
	IF(HEADER_TYPE(DTI_LUN).NE.DTI_UHL1)THEN
	  DTI_READ_UHL=DTI__BADHEADER		!bad header
	  GOTO 999
	ENDIF
C
C--------------------------------------------------------------------------
C
	CALL DTI_READUHL(DATA_OFFSET(DTI_LUN)-1,%VAL(SEC_ADDR(DTI_LUN)),
     &			DTI_LUN,ERROR)
C
C	Check for any error
	IF(ERROR.NE.DTI__NORMAL)THEN
	  DTI_READ_UHL=ERROR
	ELSE
	  DTI_READ_UHL=DTI__NORMAL		!Signal success
	ENDIF
C
999	RETURN
	END	   
C
C****************************************************************************
C
	SUBROUTINE DTI_READUHL (HEADER_SIZE,HEADER_ARRAY,WHICH_DTI,IERR)
C
***	IDENT   25SE87
***	MODULE	DTI_READUHL
C
C	Copyright : Laser-Scan, Cambridge, England
C	Author    : D R Catlow
C
CMOD1045	  : J Barber			17 Sep 1991
C		    Take angles mod 2*PI to avoid later integer overflows
C
	IMPLICIT NONE
C
***	PARAMETER/LSL$CMNDTI:DTILIBMSG/
***	COMMON/LSL$CMNDTI:DTIHDR/
C
C arguments
	INTEGER*4	HEADER_SIZE	!Size of header area
	BYTE		HEADER_ARRAY(0:HEADER_SIZE)
	INTEGER*4	WHICH_DTI	!Which DTI file
	INTEGER*4	IERR		!error code
C function
	INTEGER*4	DTI_RDFANG	!reads an angle in 10ths secs.
	INTEGER*4	RDLONG		!reads a longword
C
	REAL*8		TEMP
	REAL*8		D_MOD
	INTEGER		I_MOD
C
C***************************************************************************
C
	CALL SETAUX(HEADER_ARRAY(4),24)
C
C	Longitude of S.W. Corner
	IF (DTI_RDFANG(
     &		.TRUE.,.FALSE.,.TRUE., LONREC(1,WHICH_DTI))) GOTO 900
C	Latitude of S.W. Corner
	IF (DTI_RDFANG(
     &		.TRUE.,.FALSE.,.TRUE., LATREC(1,WHICH_DTI))) GOTO 900
C
C	Grid Intervals
C	Longitude ( in tenths of seconds)
	CALL SETWIN(17,4)
	IF (RDLONG(LONINT(WHICH_DTI))) GOTO 900
	CALL SETWIN(,4)
	IF (RDLONG(LATINT(WHICH_DTI))) GOTO 900
C
C	Fill out the rest of the variables in DTIHDR with latlong details
C	Origin values are made same as SW corner
C
C Check that angles (in 0.1 secs) are mod (2*PI)
C
	I_MOD = 360*3600*10
	D_MOD = I_MOD
	LATREC(1,WHICH_DTI) = MOD(LATREC(1,WHICH_DTI),I_MOD)
	LONREC(1,WHICH_DTI) = MOD(LONREC(1,WHICH_DTI),I_MOD)
	LATINT(WHICH_DTI)   = MOD(LATINT(WHICH_DTI),I_MOD)
	LONINT(WHICH_DTI)   = MOD(LONINT(WHICH_DTI),I_MOD)
C
	LATORI(WHICH_DTI) = LATREC(1,WHICH_DTI)
	LONORI(WHICH_DTI) = LONREC(1,WHICH_DTI)
C
C	N.W. Corner
C
	TEMP = DBLE(LATREC(1,WHICH_DTI)) + DBLE(LATINT(WHICH_DTI)) *
     &					 DBLE(Y_EXTENT(WHICH_DTI)-1)
	LATREC(2,WHICH_DTI) = DMOD(TEMP,D_MOD)
	LONREC(2,WHICH_DTI) = LONREC(1,WHICH_DTI)
C
C	N.E. Corner
C
	LATREC(3,WHICH_DTI) = LATREC(2,WHICH_DTI)
	TEMP = DBLE(LONREC(1,WHICH_DTI)) + DBLE(LONINT(WHICH_DTI)) *
     &					 DBLE(X_EXTENT(WHICH_DTI)-1)
	LONREC(3,WHICH_DTI) = DMOD(TEMP,D_MOD)
C
C	S.E. Corner
C
	LATREC(4,WHICH_DTI) = LATREC(1,WHICH_DTI)
	LONREC(4,WHICH_DTI) = LONREC(3,WHICH_DTI)
C
C	Set up two offset values
C	X_OFFSET(WHICH_DTI) = LONREC(1,WHICH_DTI)
C	Y_OFFSET(WHICH_DTI) = LATREC(1,WHICH_DTI)
C
	IERR=DTI__NORMAL	! signal success 
	GOTO 999
C
C-------------------------------------------------------------------------
C
900	IERR=DTI__BADUHL
C
999	RETURN
	END
C
