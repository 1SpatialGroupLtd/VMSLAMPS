C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1988-11-16 08:56:36.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	INTEGER*4 FUNCTION DTI_WRITE_HEADER ( DTI_LUN, RETVAL )
C
***	IDENT	16NO88
***	MODULE	DTI_WRITE_HEADER
C
C	Copyright : Laser-Scan Laboratories, Cambridge, England
C	Author    : D R Catlow
C
C	Modified  : DRC 25SE87 to call DTI_WRITE_UHL to update the UHL
C		    record of a UHL1 style DTI file
C	Modified  : DRC 01AU88 to test for write access to the file
C		    and for use with new LSLA header
C	Modified  : DRC 03AU88 to replace DTI_DTED with DTI_TED4
C	Modified  : DRC 09SE88 To write data order values from
C		    DTIHDR to the header of a LSLA type DTI file,
C		    and write DATA_TYPE as a byte.
CMOD	Modified  : DRC 15NO88 to ensure data order values are
C		    only written to LSLA files
C	
C
C	DTILIB Function.   
C
C	Writes the header details (x,y extent, grid intervals,
C	min/max values, data_type, data_length ) from common DTIHDR
C	to a file opened on DTI_LUN.  
C
C	The function returns true if successful.
C	Any system error is returned in RETVAL.
C
C----------------------------------------------------------------------------
C
	IMPLICIT NONE
C
C parameters
***	PARAMETER/LSL$CMNDTI:DTIDEF/
***	PARAMETER/LSL$CMNDTI:DTIPAR/
***	PARAMETER/LSL$CMNDTI:DTILIBMSG/
C
***	COMMON/LSL$CMNDTI:DTIHDR/
C
C arguments
	INTEGER*4	DTI_LUN			!LUN on which to open file
	INTEGER*4	RETVAL			!System error return code
C
C local
	INTEGER*4	X_EXTENT_BYTE_OFFSET	!x extent byte offset
	INTEGER*4	Y_EXTENT_BYTE_OFFSET	!y extent byte offset
	INTEGER*4	X_GRID_BYTE_OFFSET	!x grid interval byte offset
	INTEGER*4	Y_GRID_BYTE_OFFSET	!y grid interval byte offset
	INTEGER*4	MIN_WVALUE_BYTE_OFFSET	!min byte/word data b offset
	INTEGER*4	MAX_WVALUE_BYTE_OFFSET	!max byte/word data b offset
	INTEGER*4	MIN_LVALUE_BYTE_OFFSET	!min real/long data b offset
	INTEGER*4	MAX_LVALUE_BYTE_OFFSET	!max real/long data b offset
	INTEGER*4	DATA_TYPE_BYTE_OFFSET	!data type ident byte offset
	INTEGER*4	DATA_ORDER_BYTE_OFFSET	!data order vals byte offset
C
	INTEGER*4	LONGWORD_VALUE		!signed word value	
	INTEGER*2	WORD_VALUE		!word value
	EQUIVALENCE	(WORD_VALUE,LONGWORD_VALUE)
	BYTE		BYTE_VALUE
C
	INTEGER*4	IERR			!function return code
	INTEGER*4	ERROR			!system error return code
C
C functions
	INTEGER*4	DTI_WRITE_WORD_HEADER 	!writes word to header
	INTEGER*4	DTI_WRITE_LONG_HEADER 	!writes longword to header
	INTEGER*4	DTI_WRITE_REAL_HEADER 	!writes real to header
	INTEGER*4	DTI_WRITE_BYTE_HEADER	!writes a byte to header
	INTEGER*4	DTI_WRITE_DSI		!writes back DSI block
	INTEGER*4	DTI_WRITE_UHL		!writes back UHL record
C
C****************************************************************************
C
C	Test if the supplied LUN number is valid
	IF(DTI_LUN.LT.1.OR.DTI_LUN.GT.MAX_DTI)THEN
	  DTI_WRITE_HEADER=DTI__BADLUN		!LUN out of range
	  GOTO 999				!Return
	ENDIF
C
C	Test of the supplied LUN is in use
	IF(.NOT.DTILUN_INUSE(DTI_LUN))THEN
	  DTI_WRITE_HEADER=DTI__LUNUNUSED	!no file opened on LUN
	  GOTO 999				!Return
	ENDIF
C
C	Test for write access to the file
	IF(READ_ONLY(DTI_LUN))THEN
	  DTI_WRITE_HEADER=DTI__READONLY	!read only access
	  GOTO 999
	ENDIF
C
C----------------------------------------------------------------------------
C	First set up byte offset values 
C
	IF	(HEADER_TYPE(DTI_LUN).EQ.DTI_MIKE)THEN
	  X_EXTENT_BYTE_OFFSET  =DTI_PAN_EEXT
	  Y_EXTENT_BYTE_OFFSET  =DTI_PAN_NEXT
	  X_GRID_BYTE_OFFSET    =DTI_PAN_EINT
	  Y_GRID_BYTE_OFFSET    =DTI_PAN_NINT
          MIN_WVALUE_BYTE_OFFSET=DTI_PAN_WMIN
          MAX_WVALUE_BYTE_OFFSET=DTI_PAN_WMAX
	  MIN_LVALUE_BYTE_OFFSET=DTI_PAN_LMIN
	  MAX_LVALUE_BYTE_OFFSET=DTI_PAN_LMAX
	  DATA_TYPE_BYTE_OFFSET	=DTI_PAN_DTYP
C
	ELSE IF (HEADER_TYPE(DTI_LUN).EQ.DTI_UHL1)THEN	  
	  X_EXTENT_BYTE_OFFSET  =DTI_DMA_EEXT
	  Y_EXTENT_BYTE_OFFSET  =DTI_DMA_NEXT
	  X_GRID_BYTE_OFFSET    =DTI_DMA_EINT
	  Y_GRID_BYTE_OFFSET    =DTI_DMA_NINT
          MIN_WVALUE_BYTE_OFFSET=DTI_DMA_WMIN
          MAX_WVALUE_BYTE_OFFSET=DTI_DMA_WMAX
	  MIN_LVALUE_BYTE_OFFSET=DTI_DMA_LMIN
	  MAX_LVALUE_BYTE_OFFSET=DTI_DMA_LMAX
	  DATA_TYPE_BYTE_OFFSET	=DTI_DMA_DTYP
C
	ELSE IF (HEADER_TYPE(DTI_LUN).EQ.DTI_TED4)THEN	  
	  X_EXTENT_BYTE_OFFSET  =DTI_TED_EEXT
	  Y_EXTENT_BYTE_OFFSET  =DTI_TED_NEXT
	  X_GRID_BYTE_OFFSET    =DTI_TED_EINT
	  Y_GRID_BYTE_OFFSET    =DTI_TED_NINT
          MIN_WVALUE_BYTE_OFFSET=DTI_TED_WMIN
          MAX_WVALUE_BYTE_OFFSET=DTI_TED_WMAX
	  MIN_LVALUE_BYTE_OFFSET=DTI_TED_LMIN
	  MAX_LVALUE_BYTE_OFFSET=DTI_TED_LMAX
	  DATA_TYPE_BYTE_OFFSET	=DTI_TED_DTYP
C	
	ELSE IF (HEADER_TYPE(DTI_LUN).EQ.DTI_ALVY.OR.
     &           HEADER_TYPE(DTI_LUN).EQ.DTI_LSLA)THEN	  
	  X_EXTENT_BYTE_OFFSET  =DTI_LSLA_EEXT
	  Y_EXTENT_BYTE_OFFSET  =DTI_LSLA_NEXT
	  X_GRID_BYTE_OFFSET    =DTI_LSLA_EINT
	  Y_GRID_BYTE_OFFSET    =DTI_LSLA_NINT
          MIN_WVALUE_BYTE_OFFSET=DTI_LSLA_WMIN
          MAX_WVALUE_BYTE_OFFSET=DTI_LSLA_WMAX
	  MIN_LVALUE_BYTE_OFFSET=DTI_LSLA_LMIN
	  MAX_LVALUE_BYTE_OFFSET=DTI_LSLA_LMAX
	  DATA_TYPE_BYTE_OFFSET	=DTI_LSLA_DTYP
	  DATA_ORDER_BYTE_OFFSET=DTI_LSLA_DORD
C
	ENDIF
C
C----------------------------------------------------------------------------
C
C	Use byte offsets to write details to header
C
C	First write the header type identifier
	IF	(HEADER_TYPE(DTI_LUN).EQ.DTI_MIKE)THEN
	  IERR=DTI_WRITE_LONG_HEADER
     &	       (DTI_LUN,MIKE_MAKE,0,ERROR)
	  IF(IERR.NE.DTI__NORMAL)GOTO 900
	ELSE IF (HEADER_TYPE(DTI_LUN).EQ.DTI_UHL1)THEN
	  IERR=DTI_WRITE_LONG_HEADER
     &	       (DTI_LUN,UHL1_MAKE,0,ERROR)
	  IF(IERR.NE.DTI__NORMAL)GOTO 900
	ELSE IF (HEADER_TYPE(DTI_LUN).EQ.DTI_TED4)THEN
	  IERR=DTI_WRITE_LONG_HEADER
     &	       (DTI_LUN,TED4_MAKE,0,ERROR)
	  IF(IERR.NE.DTI__NORMAL)GOTO 900
	ELSE IF (HEADER_TYPE(DTI_LUN).EQ.DTI_ALVY.OR.
     &	         HEADER_TYPE(DTI_LUN).EQ.DTI_LSLA)THEN
	  IERR=DTI_WRITE_LONG_HEADER
     &	       (DTI_LUN,LSLA_MAKE,0,ERROR)
	  IF(IERR.NE.DTI__NORMAL)GOTO 900
	ENDIF
C
C	X Extent
	IERR=DTI_WRITE_WORD_HEADER
     &	     (DTI_LUN,X_EXTENT(DTI_LUN),X_EXTENT_BYTE_OFFSET,ERROR)
	IF(IERR.NE.DTI__NORMAL)GOTO 900
C
C	Y Extent	  
	IERR=DTI_WRITE_WORD_HEADER
     &	     (DTI_LUN,Y_EXTENT(DTI_LUN),Y_EXTENT_BYTE_OFFSET,ERROR)
	IF(IERR.NE.DTI__NORMAL)GOTO 900
C
C	X Grid Interval	  
	IERR=DTI_WRITE_REAL_HEADER
     &	     (DTI_LUN,X_GRID(DTI_LUN),X_GRID_BYTE_OFFSET,ERROR)
	IF(IERR.NE.DTI__NORMAL)GOTO 900
C
C	Y Grid Interval	  
	IERR=DTI_WRITE_REAL_HEADER
     &	     (DTI_LUN,Y_GRID(DTI_LUN),Y_GRID_BYTE_OFFSET,ERROR)
	IF(IERR.NE.DTI__NORMAL)GOTO 900
C
C	Minimum byte/word data value	  
	IERR=DTI_WRITE_WORD_HEADER
     &	     (DTI_LUN,MIN_WVALUE(DTI_LUN),MIN_WVALUE_BYTE_OFFSET,ERROR)
	IF(IERR.NE.DTI__NORMAL)GOTO 900
C
C	Maximum byte/word data value	  
	IERR=DTI_WRITE_WORD_HEADER
     &	     (DTI_LUN,MAX_WVALUE(DTI_LUN),MAX_WVALUE_BYTE_OFFSET,ERROR)
	IF(IERR.NE.DTI__NORMAL)GOTO 900
C
C	Data type identifier	  
	BYTE_VALUE=DATA_TYPE(DTI_LUN)
	IERR=DTI_WRITE_BYTE_HEADER
     &	     (DTI_LUN,BYTE_VALUE,DATA_TYPE_BYTE_OFFSET,ERROR)
	IF(IERR.NE.DTI__NORMAL)GOTO 900
C
C	If LSLA header also write data order values
	IF(HEADER_TYPE(DTI_LUN).EQ.DTI_LSLA.OR.
     &     HEADER_TYPE(DTI_LUN).EQ.DTI_ALVY)THEN
	  CALL LIB$INSV(DTI_ORDER_CORNER(DTI_LUN),0,4,BYTE_VALUE)
	  CALL LIB$INSV(DTI_ORDER_DIRECTION(DTI_LUN),4,4,BYTE_VALUE)
	  IERR=DTI_WRITE_BYTE_HEADER
     &	       (DTI_LUN,BYTE_VALUE,DATA_ORDER_BYTE_OFFSET,ERROR)
	  IF(IERR.NE.DTI__NORMAL)GOTO 900
	ENDIF
C
C	Minimum or maximum longword value (only if data type = data_long)
	IF(DATA_TYPE(DTI_LUN).EQ.DATA_LONG)THEN
	  IERR=DTI_WRITE_LONG_HEADER
     &	       (DTI_LUN,MIN_LVALUE(DTI_LUN),MIN_LVALUE_BYTE_OFFSET,ERROR)
	  IF(IERR.NE.DTI__NORMAL)GOTO 900
C
	  IERR=DTI_WRITE_LONG_HEADER
     &	       (DTI_LUN,MAX_LVALUE(DTI_LUN),MAX_LVALUE_BYTE_OFFSET,ERROR)
	  IF(IERR.NE.DTI__NORMAL)GOTO 900
	ENDIF
C
C	Minimum or maximum real data value (only if data type = data_real)
	IF(DATA_TYPE(DTI_LUN).EQ.DATA_REAL)THEN
	  IERR=DTI_WRITE_REAL_HEADER
     &	       (DTI_LUN,MIN_RVALUE(DTI_LUN),MIN_LVALUE_BYTE_OFFSET,ERROR)
	  IF(IERR.NE.DTI__NORMAL)GOTO 900
C
	  IERR=DTI_WRITE_REAL_HEADER
     &	       (DTI_LUN,MAX_RVALUE(DTI_LUN),MAX_LVALUE_BYTE_OFFSET,ERROR)
	  IF(IERR.NE.DTI__NORMAL)GOTO 900
	ENDIF
C
C	Write size of header if we're dealing with an ALVY or LSLA header
	IF(HEADER_TYPE(DTI_LUN).EQ.DTI_ALVY.OR.
     &     HEADER_TYPE(DTI_LUN).EQ.DTI_LSLA)THEN
	  LONGWORD_VALUE=DATA_OFFSET(DTI_LUN)
	  IERR=DTI_WRITE_WORD_HEADER
     &	       (DTI_LUN,WORD_VALUE,DTI_LSLA_DATA,ERROR)
	  IF(IERR.NE.DTI__NORMAL)GOTO 900
	ENDIF
C
C	For TED4 file, also write the latlong values to the DSI record.
	IF(HEADER_TYPE(DTI_LUN).EQ.DTI_TED4)THEN
	  IERR=DTI_WRITE_DSI(DTI_LUN,ERROR)
	  IF(IERR.NE.DTI__NORMAL)GOTO 900
	ENDIF
C
C	For UHL1 file, also write the latlong values to the UHL record.
	IF(HEADER_TYPE(DTI_LUN).EQ.DTI_UHL1)THEN
	  IERR=DTI_WRITE_UHL(DTI_LUN,ERROR)
	  IF(IERR.NE.DTI__NORMAL)GOTO 900
	ENDIF
C
	DTI_WRITE_HEADER=DTI__NORMAL	! signal success
	GOTO 999
C
C--------------------------------------------------------------------------
C	Error occured while writing the header details
900	DTI_WRITE_HEADER=DTI__SYSERR
	RETVAL=ERROR
C
999	RETURN
C
	END
