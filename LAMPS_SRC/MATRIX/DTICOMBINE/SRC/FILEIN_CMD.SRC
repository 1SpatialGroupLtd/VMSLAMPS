	LOGICAL FUNCTION FILEIN_CMD(LUN)
C
***	MODULE	FILEIN_CMD
***	IDENT	18OC88
C
C	Copyright (C), Laser-Scan Laboratories Ltd., Cambridge, England
C
C	Created						TJH 18-Jan-1988
C
C Function to get input file-spec from FILEx commands.
C
C Function gets a FILEA etc argument and opens the DTI file.
C If input file-spec argument is missing the user is
C prompted.
C
C Function returns .TRUE. if all is well, otherwise .FALSE.
C
	IMPLICIT NONE
C
C Argument (in):
	INTEGER	LUN			! selected DTI lun
C
C Parameters:
	INCLUDE	'LSL$DTMCREATESYS:MESPAR.PAR'
	INCLUDE 'LSL$CMNDTI:DTIDEF.PAR'
	INCLUDE 'LSL$CMNDTI:DTIPAR.PAR'
C
C Common:
	INCLUDE	'LSL$CMNLSL:TXTC.CMN'
	INCLUDE	'LSL$CMNDTICOMBINE:CONTROL.CMN'
	INCLUDE	'LSL$CMNDTICOMBINE:FILES.CMN'
	INCLUDE 'LSL$CMNDTI:DTIHDR.CMN'
C
C W/s
	LOGICAL		COMPER	! had a DTI comparison error?
	BYTE		CH	! a character
	LOGICAL		BLANK	! blank line?
	LOGICAL		OK,IERR	! for error returns
	CHARACTER*1	ALPFIL(8)! FILEx idents
	DATA 		ALPFIL/'A','B','C','D','E','F','G','H'/
C
C Functions
	LOGICAL		GETFILNAM ! get file-spec
	LOGICAL		OPNDTI	  ! open specified DTI file
	LOGICAL		RDCHS	  ! read char. skipping spaces
	LOGICAL		TTRSTR	  ! read from TT:
C
C ......................................................................
C
	FILEIN_CMD=.FALSE.		! assume failure
	COMPER=.TRUE.			! no file comparison errs - yet
C
C ......................................................................
C
	IF (DTILUN_INUSE(LUN)) THEN 	! already successful FILEIN?
	   CALL WRITEF(
     &	   '%S You have already got %S open as file %S',WARNING,
     &		DTI_NAME(LUN)(1:DTI_NAME_LENGTH(LUN)),ALPFIL(LUN))
	   GOTO 10
	ENDIF
C
	BLANK=RDCHS(CH)			! empty line ...
	IF (BLANK.OR.CH.EQ.'!') THEN	! or comment?
	   CALL WRITEF(
     &	   '%S You must specify a file-spec '//
     &		'argument to the FILE%S command',WARNING,ALPFIL(LUN))
	   IF (TTIN) THEN
	      CALL WRITEF('For example FILEA SW301.DTI')
	      OK=TTRSTR(,,'Read from which DTI file? ',IERR)
	      IF (OK) THEN
	         CALL BSLN
	      ELSE
	         CALL LSL_PUTMSG(OK)
	         CALL LSL_ADDMSG(IERR)
	         GOTO 10
	      ENDIF
	      IF (TXTPTR.EQ.0) GOTO 10		! empty line
	   ELSE
	      GOTO 10  ! someone got their indirect file wrong !!
	   ENDIF
	ENDIF
C
C ......................................................................
C
C Try interpreting the file-spec
	CALL BSCH
	OK=GETFILNAM(DTI_NAME(LUN),DTI_NAME_LENGTH(LUN),
     &		'LSL$DTI:DTI.DTI;0',.TRUE.,%REF('!'))
	IF (.NOT.OK) THEN
	   CALL WRITEF('%S Unable to interpret input file-spec',ERROR)
	   CALL LSL_PUTMSG(OK)
	ELSE
C
C Uppercase the file-spec
	   CALL STR$UPCASE(DTI_NAME(LUN),DTI_NAME(LUN))
C
C Attempt to open the DTI file (readonly) returns true if OK
	   FILEIN_CMD=OPNDTI(LUN)
	ENDIF
10	RETURN
	END
