	LOGICAL FUNCTION TST_CLASS(INDEX,VAL,DATATYPE)
C
***	IDENT	22SE88
***	MODULE	TST_CLASS
C
C	Copyright (C), Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created from CCB's original in LITES2	JHR	19-Sep-1988
C
C To test whether classification value VAL is selected
C
C
C.......................................................................
C
	IMPLICIT NONE
C
C Common:
	INCLUDE 'LSL$CMNDTICOMBINE:CLASS_SEL.CMN'
C
C Arguments
	INTEGER		INDEX		! CLASS index
	REAL*4		VAL		! CLASS value
	INTEGER		DATATYPE	! CLASS datatype
C
C Functions
	INTEGER		CMP_CLASS
C
C Local Variables
	INTEGER		I,T
C
	IF (CLSSEL(INDEX).NTOG.EQ.0) THEN	! no toggle points
	   TST_CLASS = CLSSEL(INDEX).ALL	! so return overall selection
	   GOTO 999
	ENDIF
C
C find first toggle .GE. VAL
	DO 10 T=1,CLSSEL(INDEX).NTOG
	   I = CMP_CLASS(CLSSEL(INDEX).TOG(T).VAL,VAL,DATATYPE)
	   IF (I.GE.0) GOTO 20
10	CONTINUE
C
C all toggles .LT. VAL (so I = -1)
	T = CLSSEL(INDEX).NTOG+1
C
C If exactly on a toggle point, then result from selection on the point
20	IF (I.EQ.0) THEN
	   TST_CLASS = CLSSEL(INDEX).TOG(T).SEL(2)
	ELSE
C
C If VAL .LT. first toggle point, then result from left of first point
	   IF (T.EQ.1) THEN
	      TST_CLASS = CLSSEL(INDEX).TOG(1).SEL(1)
	   ELSE
C
C otherwise result from right of preceding toggle point
	      TST_CLASS = CLSSEL(INDEX).TOG(T-1).SEL(3)
	   ENDIF
	ENDIF
C
999	RETURN
	END
