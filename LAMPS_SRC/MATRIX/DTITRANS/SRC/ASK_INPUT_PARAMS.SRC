C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1995-01-19 18:15:24.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
        INTEGER*4 FUNCTION ASK_INPUT_PARAMETERS()
C
***	MODULE	ASK_INPUT_PARAMETERS
***	IDENT	19JA95
C
C 	Copyright Laser-Scan Ltd., Cambridge, England
C 	Author  : D R Catlow
C
C	Created : 28 September 1987
C	Modified: 26 April 1988 - D J Gugan for IMAGEMAP option.
C
C	Function for DTITRANS to request the projection details 
C	for the input DTI file.
C	If the input DTI file already contains a projection record,
C	details from this record are output to the terminal, and 
C	the user is NOT prompted for any projection data.
C	If the user has specified /PROMPT 
C	or /DESCRIPTOR on the command line, 
C	he is prompted for all projection values irrespective of whether
C	values already exist in the Projection Record.
C	If /NOTRANSFORM is specified on the command line, no prompting
C	for values is necessary, as straightforward resampling is all
C	that is required.
C	Similarly, if /IMAGEMAP is specified then no projection details 
C	for the input DTI file are required.
C
C----------------------------------------------------------------------
	IMPLICIT NONE
C
C	DTILIB commons
***	COMMON   	/LSL$CMNDTI:DTIHDR/
***	COMMON	 	/LSL$CMNDTI:DTIPROJ/
***	PARAMETER	/LSL$CMNDTI:DTIPAR/
C
C	DTITRANS commons
***	COMMON   	/HERE:DTITRANS/
***	PARAMETER	/HERE:DTITRANSMSG/
C
C Functions:
	INTEGER*4	PRINT_PROJ_REC	!print out Proj. Rec. details
	INTEGER*4	ASK_SPHEROID	!asks for speheroid code
	INTEGER*4	ASK_PROJECTION	!asks for projection code
	INTEGER*4	ASK_UNITS	!asks for units type code
	INTEGER*4	ASK_ORIGIN	!asks for origin values
	INTEGER*4	ASK_SAMPLE	!asks for sample values
C
C----------------------------------------------------------------------
C----------------------------------------------------------------------
C If /IMAGEMAP or CONTROL then no need to go any further since we are
C not interested in details of the input projection system.
C
	IF ( IMAGEMAP.OR.CONTROL ) GOTO 900
C
C----------------------------------------------------------------------
C If we've found a Projection Record in the 
C input DTI file, then print out details on the terminal.
C
	IF (HAD_PROJ_RECORD(IN_LUN))THEN
	  CALL WRITEF(' ')
	  CALL WRITEF('Existing Projection Data for source DTI file:')
	  CALL WRITEF(' ')
	  ASK_INPUT_PARAMETERS = PRINT_PROJ_REC(IN_LUN)
	  IF (.NOT.ASK_INPUT_PARAMETERS) GOTO 999
C
C If transformation has been requested, check that the units
C and projection code are sensible.
C
	  IF (DO_TRANSFORM .AND. .NOT.DO_PROMPT) THEN
	    IF (DTIPROJ_UNITS(IN_LUN).EQ.102 .OR.
     &          DTIPROJ_UNITS(IN_LUN).EQ.104) THEN
	       CALL LSL_PUTMSG(DTRANS__INVUNTRANS)
	       ASK_INPUT_PARAMETERS=DTRANS__INVUNTRANS
	       GOTO 999
	    ENDIF
C
	    IF(DTIPROJ_PROJECTION(IN_LUN).EQ.0)THEN
               CALL LSL_PUTMSG(DTRANS__INVPROJTRANS)
	       ASK_INPUT_PARAMETERS=DTRANS__INVPROJTRANS
	       GOTO 999
	    ENDIF
	  ENDIF
C
C Unless /PROMPT is present on the command line, then we will
C use these values in the transformation operations.
C
          IF (.NOT. (DO_PROMPT.OR.HAD_DESCRIPTOR)) GOTO 900
C
	ELSE
C
          IF (.NOT. (DO_TRANSFORM.OR.HAD_DESCRIPTOR)) GOTO 900
C
	ENDIF
C
C---------------------------------------------------------------------
C Prompt for details of the input spheroid, projection system
C and associated values.
C
	CALL WRITEF(' ')
	CALL WRITEF('Enter projection data for source DTI file')
	CALL WRITEF('*****************************************')
	CALL WRITEF(' ')
C
C Spheroid code
C
	ASK_INPUT_PARAMETERS=ASK_SPHEROID(IN_LUN)
	IF(.NOT.ASK_INPUT_PARAMETERS)GOTO 999
C
C Projection code and parameters
C
	ASK_INPUT_PARAMETERS=ASK_PROJECTION(IN_LUN)
	IF(.NOT.ASK_INPUT_PARAMETERS)GOTO 999
C
C Units
C
	ASK_INPUT_PARAMETERS=ASK_UNITS(IN_LUN)
	IF(.NOT.ASK_INPUT_PARAMETERS)GOTO 999
C
C Origin Values
C
	ASK_INPUT_PARAMETERS=ASK_ORIGIN(IN_LUN)
	IF(.NOT.ASK_INPUT_PARAMETERS)GOTO 999
C
C Sample values ie. grid intervals
C
	ASK_INPUT_PARAMETERS=ASK_SAMPLE(IN_LUN)
	IF(.NOT.ASK_INPUT_PARAMETERS)GOTO 999
C
C----------------------------------------------------------------------
C And set up the absolute window
C
900	ABSOLUTE_SWX(IN_LUN)=DTIPROJ_ORIGIN(1,IN_LUN)
	ABSOLUTE_SWY(IN_LUN)=DTIPROJ_ORIGIN(2,IN_LUN)
	ABSOLUTE_NEX(IN_LUN)=ABSOLUTE_SWX(IN_LUN)+((X_EXTENT(IN_LUN)-1)
     &                       *DTIPROJ_SAMPLE(1,IN_LUN))
	ABSOLUTE_NEY(IN_LUN)=ABSOLUTE_SWY(IN_LUN)+((Y_EXTENT(IN_LUN)-1)
     &                       *DTIPROJ_SAMPLE(2,IN_LUN))
C
C	Signal success
C
	ASK_INPUT_PARAMETERS=DTRANS__NORMAL
C
C----------------------------------------------------------------------
999	RETURN
	END
C
