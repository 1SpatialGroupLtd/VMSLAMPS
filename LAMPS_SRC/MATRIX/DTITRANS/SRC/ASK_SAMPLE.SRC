C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1995-01-19 17:54:14.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
        INTEGER*4 FUNCTION ASK_SAMPLE(DTILUN)
C
***	MODULE	ASK_SAMPLE
***	IDENT	19JA95
C
C 	Copyright Laser-Scan Ltd., Cambridge, England
C 	Author  : D R Catlow
c
C	Created : 28 September 1987
C	Modified:
C
C	Function for DTITRANS to request the x and y sample values
C	for the file opened on DTILUN.
C	The values are written to the relevant variables in
C	DTIPROJ.
C
C----------------------------------------------------------------------
	IMPLICIT NONE
C
***	COMMON		/LSL$CMNLSL:EXCEPTION/
***	COMMON		/LSL$CMNLSL:EXPC/
***	PARAMETER	/LSL$CMNLSL:LSLLIBMSG/
C
***	COMMON		/LSL$CMNDTI:DTIPROJ/
***	COMMON		/LSL$CMNDTI:DTIHDR/
C
***	COMMON		/HERE:DTITRANS/
***	PARAMETER	/HERE:DTITRANSMSG/
C
C	Arguments:
	INTEGER*4	DTILUN		!which DTI file
C
C	Locals:
	REAL*8		XVALUE		!x sample value
	REAL*8		YVALUE		!y sample value
	INTEGER*4	LEN		!number of characters read
	INTEGER*4	ERROR		!system error code
	BYTE		CH		!single character
C
C	Functions:
	INTEGER		TTRSTR		!read response from terminal
	INTEGER		RDDBLE		!read REAL*8
	INTEGER		RDCHS		!read a single character
C
C----------------------------------------------------------------------
C----------------------------------------------------------------------
C Set up default values
C
50	IF ( (DTILUN.EQ.OUT_LUN .AND. DTIPROJ_UNITS(OUT_LUN).NE.
     &        DTIPROJ_UNITS(IN_LUN)) .OR. DO_PROMPT) THEN
	   XVALUE = 1.0
	   YVALUE = 1.0
C
	ELSE
	   IF(DTIPROJ_PROJECTION(IN_LUN).EQ.100)THEN
	     XVALUE=DTIPROJ_SAMPLE(2,IN_LUN)
	     YVALUE=DTIPROJ_SAMPLE(1,IN_LUN)
	   ELSE
	     XVALUE=DTIPROJ_SAMPLE(1,IN_LUN)
	     YVALUE=DTIPROJ_SAMPLE(2,IN_LUN)
	   ENDIF
	ENDIF
C
C scale up if doing plane transformation - (should perhaps do this for
C imagemap as well)
C
	IF (DTILUN.EQ.OUT_LUN.AND.CONTROL) THEN
	   XVALUE = XVALUE * CONTROL_SF
	   YVALUE = YVALUE * CONTROL_SF
	ENDIF
C	   
C----------------------------------------------------------------------
C
	CALL WRITEF(' ')
C
C Set up prompt
C
100	IF(DTIPROJ_PROJECTION(DTILUN).EQ.100)THEN
	  CALL EXPAND
     &    ('Define sample intervals as Latitude Longitude '//
     &	            '(%^D%F0.3, %F0.3): ',XVALUE,YVALUE)
C
	ELSE
          CALL EXPAND
     &    ('Define sample intervals as Eastings Northings '//
     &	            '(%^D%F0.3, %F0.3): ',XVALUE,YVALUE)
	ENDIF
C
	ASK_SAMPLE=TTRSTR(,LEN,EXPDSC,ERROR)
	IF(.NOT.ASK_SAMPLE) THEN		!  error
	  IF (ASK_SAMPLE.NE.LSL__EOF) THEN
	     CALL LSL_PUTMSG(ERROR)
	  ELSE
	     ASK_SAMPLE=DTRANS__EOF
	     CALL LSL_PUTMSG(ASK_SAMPLE)
	  ENDIF
C
	  GOTO 999
	ENDIF
C
	IF(LEN.EQ.0)THEN
	  IF(XVALUE.LE.0.AND.YVALUE.LE.0)THEN
	    CALL LSL_PUTMSG(DTRANS__BADSAMPLE)
	    CALL LSL_PUTMSG(DTRANS__REENTER)
            GOTO 100 ! reprompt
	  ENDIF
	  GOTO 300
	ENDIF
C
C----------------------------------------------------------------------
C	Decode the response
	CALL BSLN
C
	IF(RDCHS(CH))THEN
	  CALL LSL_PUTMSG(DTRANS__UNEXPEOL)
	  GOTO 100
	ENDIF
C
C----------------------------------------------------------------------
C	Check for ?
C
	IF(CH.EQ.'?')THEN
	  IF(DTIPROJ_PROJECTION(DTILUN).EQ.100) THEN
	    CALL WRITEF('Enter the Latitude and Longitude grid '//
     &	                     'values as two real numbers')
	  ELSE
	    CALL WRITEF('Enter the X and Y grid interval '//
     &	                     'values as two real numbers')
	  ENDIF
	  GOTO 100		!reprompt
C
	ELSE
	  CALL BSCH
	ENDIF	
C
C----------------------------------------------------------------------
C 	Read the first value.  This is the X value except when the 
C	projection system is geographicals, in which case it is the
C	latitude or Y value.
C
	IF (RDDBLE(XVALUE)) THEN
	   CALL LSL_PUTMSG(ERRNUM)
	   GOTO 100
	ENDIF	
C
C----------------------------------------------------------------------
C	Check for anything else on the command line
C
	IF(RDCHS(CH))THEN
150	  IF(DTIPROJ_PROJECTION(DTILUN).EQ.100)THEN
	    CALL EXPAND('Longitude (%^D%F0.3): ',YVALUE)
	  ELSE
            CALL EXPAND('Northings (%^D%F0.3): ',YVALUE)
	  ENDIF
C
	  ASK_SAMPLE=TTRSTR(,LEN,EXPDSC,ERROR)
	  IF(.NOT.ASK_SAMPLE) THEN		!  error
	    IF (ASK_SAMPLE.NE.LSL__EOF) THEN
	      CALL LSL_PUTMSG(ERROR)
C
	    ELSE
	      ASK_SAMPLE=DTRANS__EOF
	      CALL LSL_PUTMSG(ASK_SAMPLE)
	    ENDIF
	    GOTO 999
	  ENDIF
C
	  IF(LEN.EQ.0)THEN
	    IF(YVALUE.LE.0)THEN
	      CALL LSL_PUTMSG(DTRANS__BADSAMPLE)
	      CALL LSL_PUTMSG(DTRANS__REENTER)
              GOTO 150 ! reprompt
	    ENDIF
	    GOTO 300
	  ENDIF
C
	  CALL BSLN
C
	ELSE
	  CALL BSCH
	ENDIF
C
C---------------------------------------------------------------------
C	Read the second value.  This is the Y value except when the
C	projection system is geographicals, in which case it is the
C	longitude or X value
C
	IF (RDDBLE(YVALUE)) THEN
	   CALL LSL_PUTMSG(ERRNUM)
	   GOTO 150
	ENDIF	
C
C----------------------------------------------------------------------
C	Validate the sample values
C
300	IF (XVALUE.LE.0.OR.YVALUE.LE.0)THEN
	   CALL LSL_PUTMSG(DTRANS__BADSAMPLE)
	   CALL LSL_PUTMSG(DTRANS__REENTER)
	   GOTO 100
	ENDIF
C----------------------------------------------------------------------
C	Transfer values to DTIPROJ common block
C	If Geographicals swap the X and Y values around
C
	IF(DTIPROJ_PROJECTION(DTILUN).EQ.100)THEN
	  DTIPROJ_SAMPLE(1,DTILUN)=YVALUE
	  DTIPROJ_SAMPLE(2,DTILUN)=XVALUE
	  X_GRID(DTILUN) = YVALUE
	  Y_GRID(DTILUN) = XVALUE
C
	ELSE
	  DTIPROJ_SAMPLE(1,DTILUN)=XVALUE
	  DTIPROJ_SAMPLE(2,DTILUN)=YVALUE
	  X_GRID(DTILUN) = XVALUE
	  Y_GRID(DTILUN) = YVALUE
	ENDIF
C
C---------------------------------------------------------------------
C	Signal success
	ASK_SAMPLE=DTRANS__NORMAL
C
C----------------------------------------------------------------------
C
999	RETURN
	END
C
