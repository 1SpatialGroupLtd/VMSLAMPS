        INTEGER*4 FUNCTION ASK_OUTPUT_PARAMETERS()
C
***	MODULE	ASK_OUTPUT_PARAMETERS
***	IDENT	15MY91
C
C 	Copyright Laser-Scan Laboratories Ltd.,Cambridge,England
C 	Author  : D R Catlow
C	Created : 28 September 1987
C	Modified: 26 April 1988 - D J Gugan for SATMAP
C
C	Function for DTITRANS to request the projection details 
C	for the output DTI file.
C	If /NOTRANSFORM is specified on the command line, the
C	function only prompts for details of the sample grid 
C	intervals.
C	Note: for SATMAP (i.e. generation of an IMAGEMAP) we would
C	like to know the output map projection, but this is not
C	actually used for any of the processing.
C
C----------------------------------------------------------------------
	IMPLICIT NONE
C
C	DTILIB commons
***	COMMON   /LSL$CMNDTI:DTIHDR/
***	COMMON	 /LSL$CMNDTI:DTIPROJ/
***	PARAMETER/LSL$CMNDTI:DTIPAR/
C
C	DTITRANS commons
***	COMMON   /HERE:DTITRANS/
***	PARAMETER/HERE:DTITRANSMSG/
C
C Functions:
	INTEGER*4	ASK_SPHEROID	!asks for speheroid code
	INTEGER*4	ASK_PROJECTION	!asks for projection code
	INTEGER*4	ASK_UNITS	!asks for units type code
	INTEGER*4	ASK_ORIGIN	!asks for origin values
	INTEGER*4	ASK_SAMPLE	!asks for sample values
C
C Locals:
	INTEGER*4	I		!loop counter
C
C----------------------------------------------------------------------
C----------------------------------------------------------------------
C
	CALL WRITEF(' ')
	CALL WRITEF('Enter projection data for destination DTI file')
	CALL WRITEF('**********************************************')
	CALL WRITEF(' ')
C
C----------------------------------------------------------------------
C If /NOTRANSFORM then copy projection details from the input
C file, and then go to the ASK_SAMPLE section since we are
C not interested in details of the output projection system.
C
	IF (.NOT.DO_TRANSFORM) THEN
C
C	Copy any projection information from the output file
C
	  IF (DTIPROJ_UNITS(IN_LUN).EQ.0) THEN
	    DTIPROJ_UNITS(OUT_LUN) = 2			! metres by default
	  ELSE
	    DTIPROJ_UNITS(OUT_LUN) = DTIPROJ_UNITS(IN_LUN)
	  ENDIF
C
	  DTIPROJ_ORIGIN(1,OUT_LUN) = DTIPROJ_ORIGIN(1,IN_LUN)
	  DTIPROJ_ORIGIN(2,OUT_LUN) = DTIPROJ_ORIGIN(2,IN_LUN)
	  X_OFFSET(OUT_LUN) = DTIPROJ_ORIGIN(1,OUT_LUN)
	  Y_OFFSET(OUT_LUN) = DTIPROJ_ORIGIN(2,OUT_LUN)
C
	  IF (DTIPROJ_SAMPLE(1,IN_LUN).EQ.0) THEN
	    DTIPROJ_SAMPLE(1,OUT_LUN) = 1
	  ELSE
	    DTIPROJ_SAMPLE(1,OUT_LUN) = DTIPROJ_ORIGIN(1,IN_LUN)
	  ENDIF
C
	  IF (DTIPROJ_SAMPLE(2,IN_LUN).EQ.0) THEN
	    DTIPROJ_SAMPLE(2,OUT_LUN) = 1
	  ELSE
	    DTIPROJ_SAMPLE(2,OUT_LUN) = DTIPROJ_ORIGIN(2,IN_LUN)
	  ENDIF
	  X_GRID(OUT_LUN) = DTIPROJ_SAMPLE(1,OUT_LUN)
	  Y_GRID(OUT_LUN) = DTIPROJ_SAMPLE(2,OUT_LUN)
C
	  ABSOLUTE_SWX(OUT_LUN) = ABSOLUTE_SWX(IN_LUN) 
	  ABSOLUTE_SWY(OUT_LUN) = ABSOLUTE_SWY(IN_LUN) 
C
	  DTIPROJ_PROJECTION(OUT_LUN) = DTIPROJ_PROJECTION(IN_LUN)
C
	  DO 50 I=1,15
	     DTIPROJ_PROJ_PARAMS(I,OUT_LUN)=
     &       DTIPROJ_PROJ_PARAMS(I,IN_LUN)
50	  CONTINUE
C
	  DTIPROJ_SPHEROID(OUT_LUN) = DTIPROJ_SPHEROID(IN_LUN)
	  IF(DTIPROJ_SPHEROID(OUT_LUN).EQ.101)THEN
	    DTIPROJ_USER_SPHEROID(1,OUT_LUN)=
     &      DTIPROJ_USER_SPHEROID(1,IN_LUN)
	    DTIPROJ_USER_SPHEROID(2,OUT_LUN)=
     &      DTIPROJ_USER_SPHEROID(2,IN_LUN)
	  ENDIF
C
	  GOTO 800
	ENDIF
C
C----------------------------------------------------------------------
C---------------------------------------------------------------------
C	Prompt for details of the output spheroid, projection system
C	and associated values.
C	
C	Get Spheroid code - only required for IMAGEMAP option.
	IF(IMAGEMAP.OR.CONTROL)THEN
	  ASK_OUTPUT_PARAMETERS=ASK_SPHEROID(OUT_LUN)
	  IF(.NOT.ASK_OUTPUT_PARAMETERS)GOTO 999
C
	ELSE
C For the present until perhaps a datum shift option is
C added, this must be the same as the input spheroid.
C
	  DTIPROJ_SPHEROID(OUT_LUN)=DTIPROJ_SPHEROID(IN_LUN)
	  IF(DTIPROJ_SPHEROID(IN_LUN).EQ.101)THEN
	     DTIPROJ_USER_SPHEROID(1,OUT_LUN)=
     &       DTIPROJ_USER_SPHEROID(1,IN_LUN)
	     DTIPROJ_USER_SPHEROID(2,OUT_LUN)=
     &       DTIPROJ_USER_SPHEROID(2,IN_LUN)
	  ENDIF
	ENDIF
C
C Projection code and parameters
	ASK_OUTPUT_PARAMETERS=ASK_PROJECTION(OUT_LUN)
	IF(.NOT.ASK_OUTPUT_PARAMETERS)GOTO 999
C
C Units
	ASK_OUTPUT_PARAMETERS=ASK_UNITS(OUT_LUN)
	IF(.NOT.ASK_OUTPUT_PARAMETERS)GOTO 999
C
C Origin Values
C Not requested, since these are determined as part of the
C the transformation operation ?
C
C	ASK_OUTPUT_PARAMETERS=ASK_ORIGIN(OUT_LUN)
C	IF(.NOT.ASK_OUTPUT_PARAMETERS)GOTO 999
C
C Sample values ie. grid intervals
C
800	ASK_OUTPUT_PARAMETERS=ASK_SAMPLE(OUT_LUN)
	IF(.NOT.ASK_OUTPUT_PARAMETERS)GOTO 999
C
C----------------------------------------------------------------------
C And set up the absolute window
C
900	ABSOLUTE_SWX(OUT_LUN)=DTIPROJ_ORIGIN(1,OUT_LUN)
	ABSOLUTE_SWY(OUT_LUN)=DTIPROJ_ORIGIN(2,OUT_LUN)
C
	ASK_OUTPUT_PARAMETERS=DTRANS__NORMAL
C
C----------------------------------------------------------------------
999	RETURN
	END
C
