        SUBROUTINE EQRAD(DATA)
C
***	MODULE EQRAD
***	IDENT 19JL88
C
C Copyright Laser-Scan Laboratories Ltd.,Cambridge,England
C 
C Author    Ron Russell				 9-Dec-1985
C Modified  Dave Catlow for use with DTITRANS	19-Jul-1988
C
C       This subroutine computes the radius of a sphere of equal
C	surface area to the specified shperoid
C
	IMPLICIT NONE
C
C arguments
C
	REAL*8		DATA(2)
C
C on input DATA(1) contains the semi-major axis of the shperoid
C      and DATA(2) either the semi-minor axis or the eccentricity squared
C
C on output DATA(1) contains the radius of the sphere, and DATA(2) 
C      contains 0.0
C
C W/S
C
	REAL*8		A,B,E,ES,Q
C
	A=DATA(1)
	B=DATA(2)
C
	IF (B.LE.0.0) THEN		! starting with sphere
	   E=0.0
	   ES=0.0
	ELSEIF(B.LT.1.0) THEN		! got eccentricity squared
	   E=SQRT(B)
	   ES=B
	ELSE
	   ES=1.0-(B/A)**2
	   E=SQRT(ES)
	ENDIF
C
C compute radius of sphere, if DATA does not already represent one
C
	IF (E.GT.0.0) THEN
	   Q=(1.0-ES)*(1.0/(1-ES)-1.0/(2.0*E)*LOG((1.0-E)/(1.0+E)))
	   Q=SQRT(Q/2.0)
C
	   DATA(1)=Q*DATA(1)	
C
C remember this radius for message
C
C	   IF (.NOT.SPH_MESS) THEN
C	      SPH_RAD=DATA(1)
C	      SPH_MESS=.TRUE.		! don't output message again
C	   ENDIF
	ENDIF
C
	DATA(2)=0.0
C
	RETURN
   	END
