	LOGICAL*4 FUNCTION GETSPEC(GIVEN,GVNLEN,RESULT,RESLEN,DEFFIL,
     &				  RELFIL,AMBIG)
C
***	MODULE	GETSPEC
***	IDENT	11AU93
C
C Copyright Laser-Scan Limited, Cambridge, England.
C
CMOD	Plagiarised from the below mentioned's
C	GETFIL.SRC						TJH 08-JUN-1985
C	Created as GETFIL.SRC					 DM 23-Aug-1983
C
CMOD1265 Copied from DTMCREATE service to matrix service 	 JB 11-Aug-1993 
C
C Parses and hunts for a specified file name.
C The function returns .TRUE. if we succeed, and .FALSE. if we fail
C (the error code is returned such that CALL EXIT(GETSPEC) will cause
C it to be reported).
C An overriding error return is AMBIG; this should be tested BEFORE
C GETSPEC (N.B.). If AMBIG is .TRUE., the specified file name is
C ambiguous.
C
	IMPLICIT NONE
C 
	CHARACTER*(*) DEFFIL	! default file name
	CHARACTER*(*) GIVEN	! specified file name
	CHARACTER*(*) RELFIL	! related file name
	CHARACTER*(*) RESULT	! resulting (expanded) file name
	CHARACTER*80  TMPFIL	! temporary file name
	INTEGER*4 GVNLEN	! actual length of GIVEN
	INTEGER*4 RESLEN	! actual length of RESULT
	INTEGER*4 I,J,K		! temporary variables
	LOGICAL*1 AMBIG		! is the specified file name ambiguous ?
C
C function:
	LOGICAL*4 LIB$FIND_FILE
C
C ......................................................................
C
	AMBIG=.FALSE.				! assume not ambiguous
	K=0					! clear file 'context'
	GETSPEC=LIB$FIND_FILE(GIVEN(:GVNLEN),RESULT,K,DEFFIL,RELFIL)
C
C ... get the 'actual' length of character variable RESULT.
C ... N.B. the terminating character is assumed to be a space.
	J=LEN(RESULT)				! get the implied length
	DO 10 I=1,J
	   IF (RESULT(I:I).EQ.' ') GO TO 20
10	CONTINUE
	I=J + 1					! no space found
20	RESLEN=I - 1
C
C ... did we succeed in finding a file ?
	IF (GETSPEC) THEN
	   IF (LIB$FIND_FILE(GIVEN(:GVNLEN),TMPFIL,K,DEFFIL,RESULT))
     &	      AMBIG=.TRUE.			! ambiguous file name ?
	ELSE
	   IF (RESLEN.LE.0) THEN		! no good - tell him
	      RESULT=GIVEN			! if not translated ...
	      RESLEN=GVNLEN			! use original name
	   ENDIF
	ENDIF
C
	RETURN
	END
C
