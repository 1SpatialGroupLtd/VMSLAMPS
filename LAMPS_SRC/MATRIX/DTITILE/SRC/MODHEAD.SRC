	LOGICAL FUNCTION MODHEAD()
C
***	MODULE	MODHEAD
***	IDENT	27JL92
C
C	Copyright: Laser-Scan Ltd., Cambridge, England.
C
CMOD1129 Fix calculation of origin to be most SOUTH and
C	 WEST DTI files					  JB 23-Jul-1992
CMOD1066 Calculate range of final file with real values 
C	 of LAT/LONINT to avoid rounding errors		  JB 27-Nov-1991
CMOD	 Update projection record origin		MWSR 16-Nov-1988
C
C Modified from WRIDSI in module PANTILE		 ALB 25-FEB-1987
C
C	Function for DTITILE to update the latlong corner point
C	values etc. in the DSI part of the DTED header of the
C	output file.
C
	IMPLICIT NONE
C
C Parameters:
	INCLUDE 'LSL$DTMCREATESYS:MESPAR.PAR'
	INCLUDE 'LSL$CMNDTI:DTIPAR.PAR'  	! DTI parameters
	INCLUDE 'LSL$CMNDTI:DTIDEF.PAR'  	! DTI parameters
C
C Common block:
	INCLUDE	'LSL$CMNDTI:DTIHDR.CMN'		!DTILIB common
	INCLUDE	'LSL$CMNDTI:DTIPROJ.CMN'		!DTILIB common
	INCLUDE	'LSL$CMNDTITILE:CONTROL.CMN'
C
	REAL*8	DLATINT,DLONINT
	REAL*8	D_MOD,TEMP
	INTEGER	I_MOD
C
C***********************************************************************
C
	MODHEAD = .TRUE.	! assume success
C
C***********************************************************************
C Update LAT/LONREC to new extents 
C
	IF (HAD_PROJ_RECORD(DTIOUT_LUN)) THEN
C
	   IF (GEOGRAPHICALS(DTIOUT_LUN)) THEN
C
C Take mod 2*PI
	     I_MOD = 360*3600*10
	     D_MOD = I_MOD
C
	     LATORI(DTIOUT_LUN) = MOD(LATORI(DTIOUT_LUN),I_MOD) 
	     LONORI(DTIOUT_LUN) = MOD(LONORI(DTIOUT_LUN),I_MOD) 
	     LATINT(DTIOUT_LUN) = MOD(LATINT(DTIOUT_LUN),I_MOD) 
	     LONINT(DTIOUT_LUN) = MOD(LONINT(DTIOUT_LUN),I_MOD) 
C
C Get real versions of LAT/LONINT to avoid rounding errors from the
C first file, against which all projection variables are compared, with
C warnings given if any differences
C
	     DLONINT = DBLE(LONREC(3,1)-LONORI(1)) / DBLE(X_EXTENT(1)-1)
	     DLONINT = DMOD(DLONINT,D_MOD)
C
	     DLATINT = DBLE(LATREC(3,1)-LATORI(1)) / DBLE(Y_EXTENT(1)-1)
	     DLATINT = DMOD(DLATINT,D_MOD)
C
C Modify DSI header values.
C
C	     IF (HEADER_TYPE(DTIOUT_LUN).EQ.DTI_TED4 .OR. 
C    &	     	 HEADER_TYPE(DTIOUT_LUN).EQ.DTI_UHL1) THEN
C
	     TEMP = LATREC(1,SOUTH_FILE) - 
     &                DLATINT*(Y_ORIGIN(SOUTH_FILE)-1)
	     LATREC(1,DTIOUT_LUN) = DMOD(TEMP,D_MOD)
C
	     TEMP = LONREC(1,WEST_FILE) - 
     &                DLONINT*(X_ORIGIN(WEST_FILE)-1)
	     LONREC(1,DTIOUT_LUN) = DMOD(TEMP,D_MOD)
C
	     IF(DTI_WINDOW_SWY(1).NE.1) THEN
	        TEMP = LATREC(1,DTIOUT_LUN) + 
     &			DLATINT*(DTI_WINDOW_SWY(SOUTH_FILE)-1)
	        LATREC(1,DTIOUT_LUN) = DMOD(TEMP,D_MOD)
	     ENDIF
C
	     IF (DTI_WINDOW_SWX(1).NE.1) THEN
	        TEMP = LONREC(1,DTIOUT_LUN) + 
     &			DLONINT*(DTI_WINDOW_SWX(WEST_FILE)-1)
	        LONREC(1,DTIOUT_LUN) = DMOD(TEMP,D_MOD)
	     ENDIF
C
C Calculate the NE corner LAT/LONG values
C
	     TEMP = LATREC(1,DTIOUT_LUN) + 
     &				DLATINT*(Y_EXTENT(DTIOUT_LUN)-1)
	     LATREC(3,DTIOUT_LUN) = DMOD(TEMP,D_MOD)
	     TEMP = LONREC(1,DTIOUT_LUN) + 
     &				DLONINT*(X_EXTENT(DTIOUT_LUN)-1)
	     LONREC(3,DTIOUT_LUN) = DMOD(TEMP,D_MOD)
C
C Fill in LATREC and LONREC for NW and SE
C
	     LATREC(2,DTIOUT_LUN) = LATREC(3,DTIOUT_LUN)
	     LONREC(2,DTIOUT_LUN) = LONREC(1,DTIOUT_LUN)
	     LATREC(4,DTIOUT_LUN) = LATREC(1,DTIOUT_LUN)
	     LONREC(4,DTIOUT_LUN) = LONREC(3,DTIOUT_LUN)
C
C Make the origin equivalent to the SW corner
C
	     LATORI(DTIOUT_LUN) = LATREC(1,DTIOUT_LUN)
	     LONORI(DTIOUT_LUN) = LONREC(1,DTIOUT_LUN)
	   ENDIF
C
C do similar stuff for projection record
C
	   DTIPROJ_ORIGIN(1,DTIOUT_LUN) = 
     &				 DTIPROJ_ORIGIN(1,WEST_FILE) -
     &				(DTIPROJ_SAMPLE(1,WEST_FILE) *
     &				(X_ORIGIN(WEST_FILE)-1) )
	   DTIPROJ_ORIGIN(2,DTIOUT_LUN) = 
     &				 DTIPROJ_ORIGIN(2,SOUTH_FILE) -
     &				(DTIPROJ_SAMPLE(2,SOUTH_FILE) *
     & 				(Y_ORIGIN(SOUTH_FILE)-1))
C
C Set origin to most SOUTH and WEST file
C
	   IF (DTI_WINDOW_SWX(WEST_FILE).NE.1) THEN
	      DTIPROJ_ORIGIN(1,DTIOUT_LUN) = 
     &				    DTIPROJ_ORIGIN(1,DTIOUT_LUN) +
     &				   (DTIPROJ_SAMPLE(1,WEST_FILE) *
     &				   (DTI_WINDOW_SWX(WEST_FILE)-1))
	   ENDIF
C
	   IF (DTI_WINDOW_SWY(SOUTH_FILE).NE.1) THEN
	      DTIPROJ_ORIGIN(2,DTIOUT_LUN) = 
     &				    DTIPROJ_ORIGIN(2,DTIOUT_LUN) +
     &				   (DTIPROJ_SAMPLE(2,SOUTH_FILE) *
     &				   (DTI_WINDOW_SWY(SOUTH_FILE)-1))
	   ENDIF
C
	   X_OFFSET(DTIOUT_LUN) = DTIPROJ_ORIGIN(1,DTIOUT_LUN)
	   Y_OFFSET(DTIOUT_LUN) = DTIPROJ_ORIGIN(2,DTIOUT_LUN)
	ENDIF
C
999	RETURN
	END
C
