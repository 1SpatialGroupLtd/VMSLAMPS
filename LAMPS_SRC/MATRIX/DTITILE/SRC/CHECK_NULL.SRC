	LOGICAL FUNCTION CHECK_NULL(COL_NO,ROW_NO,NUM_ROWS)
C
***	MODULE	CHECK_NULL
***	IDENT	06AU92
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England
C
C Author M.W.S. Reid
C
C 	Created					MWSR    15-Nov-1988
C
C Checks each pixel in COLUMN. If it is null then retrieve the
C existing value for the pixel position from the output DTI file. This
C avoids null values overwriting non-null values already in the output 
C file.
C
	IMPLICIT NONE
C
C Parameters and commons
	INCLUDE 'LSL$CMNDTI:DTILIBMSG.PAR'
	INCLUDE 'LSL$CMNDTI:DTIHDR.CMN'
	INCLUDE 'LSL$CMNDTI:DTIPAR.PAR'
	INCLUDE 'LSL$CMNDTITILE:CONTROL.CMN'
	INCLUDE 'LSL$CMNDTITILE:NULLCHK.CMN'
C
C Arguments
	INTEGER*4	COL_NO		!IN - col number
	INTEGER*4	ROW_NO		!IN - row number
	INTEGER*4	NUM_ROWS	!IN - number of rows
C
C Functions
	INTEGER*4	DTI_COPY_DATA
	LOGICAL		CHECK_NULL_BYTE
	LOGICAL		CHECK_NULL_WORD
	LOGICAL		CHECK_NULL_LONG
	LOGICAL		CHECK_NULL_REAL
C
C Local Variables
	INTEGER*4	RET_CODE
	LOGICAL*4	EXIST_COLUMN(DTI_MAX_ROWS)
	BYTE		BVAL(DTI_MAX_ROWS)
	INTEGER*2	WVAL(DTI_MAX_ROWS)
	INTEGER*4	LVAL(DTI_MAX_ROWS)
	REAL		RVAL(DTI_MAX_ROWS)
	INTEGER*4	PIXNO
	LOGICAL		USE_EXISTING		
	INTEGER*4	IERR
	EQUIVALENCE	(EXIST_COLUMN,LVAL,RVAL,WVAL,BVAL)
C
C assume OK
	CHECK_NULL = .TRUE.
C
C copy the existing column from the file
	RET_CODE = DTI_COPY_DATA(DTIOUT_LUN,	! lun of output DTI
     &  			EXIST_COLUMN,	! Target array
     &  			.FALSE.,	! Copy into array
     &  			COL_NO,		! first column
     &  			ROW_NO,		! first row
     &  			1,		! number of columns
     &  			NUM_ROWS,	! number of rows
     &  			IERR)		! error code
C
C Check on error
	IF (RET_CODE.NE.DTI__NORMAL)THEN
	   CALL LSL_PUTMSG(RET_CODE)
	   IF(RET_CODE.EQ.DTI__SYSERR) CALL LSL_PUTMSG(IERR)
	   CHECK_NULL = .FALSE.
	   GOTO 999
	ENDIF

C loop for each pixel in the column
C
	DO 10 PIXNO = 1,NUM_ROWS
C
C call the checking routine appropriate to the datatype
	   IF(DATA_TYPE(DTIOUT_LUN).EQ.DATA_BYTE)THEN
	      IF (CHECK_NULL_BYTE(BCOL(PIXNO))) THEN
	         BCOL(PIXNO) = BVAL(PIXNO)
	      ENDIF
	   ELSEIF(DATA_TYPE(DTIOUT_LUN).EQ.DATA_WORD)THEN	      
	      IF (CHECK_NULL_WORD(WCOL(PIXNO))) THEN
	         WCOL(PIXNO) = WVAL(PIXNO)
	      ENDIF
	   ELSEIF(DATA_TYPE(DTIOUT_LUN).EQ.DATA_LONG)THEN	      
	      IF (CHECK_NULL_LONG(LCOL(PIXNO))) THEN
	         LCOL(PIXNO) = LVAL(PIXNO)
	      ENDIF
	   ELSEIF(DATA_TYPE(DTIOUT_LUN).EQ.DATA_REAL)THEN	      
	      IF (CHECK_NULL_REAL(RCOL(PIXNO))) THEN
	         RCOL(PIXNO) = RVAL(PIXNO)
	      ENDIF
	   ENDIF
C
10	CONTINUE	
C
999	RETURN
	END
C
	LOGICAL FUNCTION CHECK_NULL_BYTE(BVAL)
C
***	MODULE	CHECK_NULL_BYTE
***	IDENT	15NO88
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England
C
C Author M.W.S. Reid
C
C 	Created					MWSR    15-Nov-1988
C
C Returns true if the value is equal to the null value for a byte
C
	IMPLICIT NONE
C
C Arguments
	BYTE	BVAL		! byte value
C
C Common Blocks
	INCLUDE 'LSL$CMNDTITILE:CONTROL.CMN'
C
	CHECK_NULL_BYTE = .FALSE.
	IF (BVAL .EQ. NULL_BYTE_VAL) THEN
	   CHECK_NULL_BYTE = .TRUE.
	ENDIF
C
	RETURN
	END
C
	LOGICAL FUNCTION CHECK_NULL_WORD(WVAL)
C
***	MODULE	CHECK_NULL_WORD
***	IDENT	15NO88
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England
C
C Author M.W.S. Reid
C
C 	Created					MWSR    15-Nov-1988
C
C Returns true if the value is equal to the null value for a word
C
	IMPLICIT NONE
C
C Arguments
	INTEGER*2	WVAL		! word value
C
C Common Blocks
	INCLUDE 'LSL$CMNDTITILE:CONTROL.CMN'
C
	CHECK_NULL_WORD = .FALSE.
	IF (WVAL .EQ. NULL_WORD_VAL) THEN
	   CHECK_NULL_WORD = .TRUE.
	ENDIF
C
	RETURN
	END
C
	LOGICAL FUNCTION CHECK_NULL_LONG(LVAL)
C
***	MODULE	CHECK_NULL_LONG
***	IDENT	15NO88
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England
C
C Author M.W.S. Reid
C
C 	Created					MWSR    15-Nov-1988
C
C Returns true if the value is equal to the null value for a longword
C
	IMPLICIT NONE
C
C Arguments
	INTEGER*4	LVAL		! longword value
C
C Common Blocks
	INCLUDE 'LSL$CMNDTITILE:CONTROL.CMN'
C
	CHECK_NULL_LONG = .FALSE.
	IF (LVAL .EQ. NULL_LONG_VAL) THEN
	   CHECK_NULL_LONG = .TRUE.
	ENDIF
C
	RETURN
	END
C
	LOGICAL FUNCTION CHECK_NULL_REAL(RVAL)
C
***	MODULE	CHECK_NULL_REAL
***	IDENT	15NO88
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England
C
C Author M.W.S. Reid
C
C 	Created					MWSR    15-Nov-1988
C
C Returns true if the value is equal to the null value for a real
C
	IMPLICIT NONE
C
C Arguments
	REAL	RVAL		! real value
C
C Common Blocks
	INCLUDE 'LSL$CMNDTITILE:CONTROL.CMN'
C
	CHECK_NULL_REAL= .FALSE.
	IF (RVAL .EQ. NULL_REAL_VAL) THEN
	   CHECK_NULL_REAL = .TRUE.
	ENDIF
C
	RETURN
	END


