	LOGICAL FUNCTION FILEIN_CMD()
C
***	MODULE	FILEIN_CMD
***	IDENT	23JL92
C
C	Copyright (C), Laser-Scan Ltd., Cambridge, England
C
C Modified from FILEIN_CMD in DTI2TEXT			ALB 04-Feb-1988
C
C Function to get input file-spec from FILEIN command. If argument is
C missing the user is prompted.  Checks that a FILEIN command is
C appropriate at this stage of interaction.
C
C Function returns .TRUE. if all is well, otherwise .FALSE.
C
	IMPLICIT NONE
C
C Parameters :
	INCLUDE 'LSL$DTMCREATESYS:MESPAR.PAR'
C
C Common:
	INCLUDE	'LSL$CMNLSL:TXTC.CMN'
	INCLUDE 'LSL$CMNDTITILE:CONTROL.CMN'
C
C W/s
	BYTE		CH	! a character
	LOGICAL		BLANK	! blank line?
	LOGICAL		OK	! for error returns
	LOGICAL		IERR	! for error returns
C
C Functions
	LOGICAL		GETFILNAM ! get file-spec
	LOGICAL		OPNDTI	! open specified DTI file
	LOGICAL		RDCHS	! read char. skipping spaces
	LOGICAL		TTRSTR	! read from TT:
C
C ......................................................................
C
	FILEIN_CMD=.TRUE.		! assume success
C
C ......................................................................
C
C Check that no. input DTI file does not exceed 8
C
	IF (NUM_DTIIN.GE.8) THEN 		 ! eight file openned
	   CALL WRITEF('%S Specifying command FILEIN',ERROR)
	   CALL WRITEF(
     &	   'You have already got eight DTI files open for input')
	   FILEIN_CMD=.FALSE.
	   GOTO 999 
	ENDIF
C
C Look for filename, taking appropriate action if none found
C
	BLANK=RDCHS(CH)			! empty line ...
	IF (BLANK.OR.CH.EQ.'!') THEN	! or comment?
C
C Display warning/error for no filename present
C
	   IF (TTIN) THEN
	      CALL WRITEF('%S Specifying command FILEIN',WARNING)
	      CALL WRITEF('You must specify a file-spec argument'//
     &		' to the FILEIN command')
	   ELSE
	      CALL WRITEF('%S Specifying command FILEIN',ERROR)
	      CALL WRITEF('You must specify a file-spec argument'//
     &		'to the FILEIN command')
	   ENDIF	   
C
C If terminal input, prompt for filename
C
	   IF (TTIN) THEN
	      CALL WRITEF('For example FILEIN SW301.DTI')
	      OK=TTRSTR(,,'Read from which DTI file? ',IERR)
	      IF (OK) THEN
	         CALL BSLN			! reset decode ptr
	      ELSE
	         CALL LSL_PUTMSG(OK)		! problem with input
	         CALL LSL_ADDMSG(IERR)
	         FILEIN_CMD=.FALSE.
	         GOTO 999
	      ENDIF
	      IF (TXTPTR.EQ.0) THEN
	   	 FILEIN_CMD=.FALSE.
		 GOTO 999		! empty line
	      ENDIF
	   ELSE
	      FILEIN_CMD=.FALSE.
	      GOTO 999  ! someone got their indirect file wrong !!
	   ENDIF
	ENDIF
C
C ......................................................................
C Try interpreting the file-spec
C
	CALL BSCH				! move back one chr
	OK=  GETFILNAM(	DTIFILE(NUM_DTIIN+1),	! filename
     &			DTILEN(NUM_DTIIN+1),	! filename length
     &			'LSL$DTI:DTI.DTI;0',	! default
     &			.TRUE.,			! allow version numbers
     &			%REF('!') )		! termination character
C
C Deal with any error in interpreting file-spec
C
	IF (.NOT.OK) THEN
	   CALL WRITEF('%S Specifying command FILEIN',ERROR)
	   CALL WRITEF('Unable to interpret input file-spec')
	   CALL LSL_PUTMSG(OK)
	   FILEIN_CMD=.FALSE.
	   GOTO 999
	ELSE
C
C ......................................................................
C attempt to open the DTI file (readonly)
C
	   OK = OPNDTI()
	ENDIF
	IF(.NOT.OK) THEN
	   FILEIN_CMD=.FALSE.
	   GOTO 999
	ELSE
	   DTIFSPEC(NUM_DTIIN) = .TRUE.
	ENDIF
C
999	RETURN
	END
C
