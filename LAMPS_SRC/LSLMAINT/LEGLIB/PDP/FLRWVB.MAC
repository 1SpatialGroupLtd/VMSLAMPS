	.TITLE	FLRWVB
	.IDENT	/14MY80/
	.ENABL	LC

; Copyright Laser Scan Laboratories Ltd., Cambridge, England.
; Author  R. Fairbairns		Date as .IDENT

; Read blocks from, or write blocks to, files accessed
;  (one way or another) under the file i/o procedures
;  of LIOLIB

	.MCALL	QIO$, DIR$, WTSE$S

; read block from file

;	FLRBLK(memadr, [size], filadr[, ierr])

; <size> defaults to 512

FLRBLK:: MOV	#-1,   R1	; no default for memory address
	MOV	#.512, R2	; default size is 512 bytes
	MOV	#-1,   R3	; no default for file address
	MOV	#-1,   R4	; default no error return address
	CALL	LI$ARG		; decode arguments
	MOV	(R3), QIBKLS	; copy file address to ls of block no.
	MOV	FLRLUN, R0	; LUN of file currently selected
	MOV	R0, QILUN	; to LUN for transfer
	MOVB	R0, QIEFN	; and to EFN
	MOV	R1, QIBLKB	; set block buffer pointer
	MOV	(R2), QIBYTC	; set byte count
	MOV	#IO.RVB, QIOP	; set function
	DIR$	#QIODPB		; execute QIO transfer
	BCS	20$		; failed
	WTSE$S	QILUN		; wait for transfer to complete
	BCS	20$		; why did that fail, then?
	MOVB	FLBKST, R0	; recover error/success flag
5$:	MOV	R0, FLRERR	; save error in common block regardless
	CMP	R4, #-1		; is he interested in the result?
	BEQ	10$		; if EQ, he ain't
	MOV	R0, (R4)	; return error to the user
10$:	RTS	PC

; directive rejected - return error code as error for whole operation
20$:	MOV	$DSW, R0	; directive reject
	BR	5$		; go to return it

; QIO parameters

QIODPB:	QIO$	,,,,FLBKST,,<-1,512.,,0,1>

QIOP	= QIODPB+Q.IOFN
QILUN	= QIODPB+Q.IOLU
QIEFN	= QIODPB+Q.IOEF
QIBLKB	= QIODPB+Q.IOPL
QIBYTC	= QIBLKB+2
QIBKLS	= QIBLKB+10

; workspace for these routines only
FLBKST:	.BLKW	2		; two words for iosb
FLBKVB:	.WORD	0		; high order 15 bits will always be 0
	.BLKW	1		; low order 16 bits from call
.512:	.WORD	^D512		; default transfer size

	.PSECT	FLRCOM, D,OVR,GBL

; interface to rest of LIOLIB
FLRERR:	.WORD	0		; error marker
FLRFDB:	.BLKW	1		; currently selected FDB
FLRLUN:	.WORD	0		; currently selected LUN

	.PSECT

	.END
