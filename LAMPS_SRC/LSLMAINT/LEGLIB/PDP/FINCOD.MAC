	.TITLE	FINCOD
	.IDENT	/JUL 77/
	.ENABL	LC

; Copyright Laser Scan Laboratories Ltd., Cambridge, England.
; Author  R. Fairbairns		Date as .IDENT

	.PSECT

; FORTRAN INTEGER FUNCTION FINCOD(GT)

; Find the last graphical code in the code table whose type is GT
; (NB the last in the table is, of course, the first in the code file)

; Result is the code if found, or -1

; (unlike FINLEG, this routine doesn't set up the auxiliary
;  variables in /LEGEND/)

FINCOD:: MOVB	@2(R5), R4	; the type required
	MOV	#-1, R0		; initial guess - failure
	.IF NE	ONELEG
	MOV	LEGRT, R1	; base of the code table
	.IFF
	MOV	LEGRT+2, R1	; base of the IT legenda table
	.ENDC

1$:	MOV	(R1)+, R3	; next code
	MOV	(R1)+, R2	; table chain, for later

	CMPB	R4, (R1)+	; compare type
	BNE	3$		; not this one
	MOV	R3, R0		; better bet than the last
3$:	MOV	R2, R1		; follow chain
	BNE	1$		; not yet end

	RTS	PC

	.END

