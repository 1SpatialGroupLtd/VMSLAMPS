	.TITLE	RDLEGS
	.ENABL	LC
	.IDENT	/MAY 76/

; Copyright Laser Scan Laboratories Ltd., Cambridge, England.
; Author  R. Fairbairns		Date as .IDENT

; (MS marked 'version 2', and dated 30.iii.47 (!))
; Upgraded 22.iii.77 for MTC system

; FORTRAN logical function to read and 'convert' one word of a 
;  legenda symbol. Arguments are:
;	symbol index (incremented by this routine)
;	current within-symbol X, and Y
;	increment generated by this step: X, and Y
; The result is TRUE if the result is to be visible.

	.PSECT

F0 = %0
F1 = %1
F2 = %2
F3 = %3


; the subroutine
RDLEGS::TST	(R5)+		; assume correct number of arguments
	MOV	@(R5)+, R2	; symbol index, current value
	MOV	(R2)+, R1	; actual data word
	SXT	R0		; set up visibility switch for result
	MOV	R2, @-2(R5)	; step symbol index
	ADD	(PC)+, R1
	.BYTE	40, 40
	MOV	R1, R3
	SWAB	R3

; R1, R3 now hold X, Y, resp., for a bit
	BIC	#177700, R1	; sign extend
	SUB	#40, R1		;  for X
	BIC	#177700, R3	; and
	SUB	#40, R3		;  for Y

; float both halves
	LDCIF	R1, F1
	LDCIF	R3, F3

; now scale and rotate
	MOV	#LEGSCF, R1
	MULF	(R1), F1	; scale
	MULF	(R1), F3
	TSTB	LEGSHR
	BEQ	1$		; not to shear
	LDF	F3, F2		; copy for shear
	MULF	SHRFAC, F2	; take up shear factor
	ADDF	F2, F1		; and shear
1$:	LDF	F1, F0		; copy for
	LDF	F3, F2		;  rotation operation
	MULF	-(R1), F0	; rotate
	MULF	-(R1), F1
	MULF	(R1)+, F3
	MULF	(R1), F2
	SUBF	F2, F1		; X result
	STF	F1, @(R5)+
	ADDF	F0, F3		; Y result
	STF	F3, @(R5)+
	RTS	PC

	.END
