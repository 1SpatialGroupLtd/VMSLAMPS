	INTEGER*4 FUNCTION RDNAME(STRING)
C
***	MODULE RDNAME
***	IDENT 26AP83
C
	IMPLICIT CHARACTER*1 (A-Z)
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England
C Author R. Fairbairns
C
C Coded October 1981
C
C To read a 'name' from the current line in TXTBUF
C Permitted characters are alpha/numerics, '$', and '_'
C All alpha chars are upper-cased
C
	CHARACTER*(*)	STRING	! to read into
C
C Function result is length of name read (0 if none found)
C
C w/s
	INTEGER*4  LENGTH	! to accumulate length of string
	INTEGER*4  ILNGTH	! length of 'input' string (in argument)
	INTEGER*4  LSTNSP	! last character of string not a space
	BYTE	   CH		! for reading characters
C
C functions called
	LOGICAL	   RDCH		! read one character
	LOGICAL	   RDCHS	! read one character, ignoring spaces
C
C preset data
	BYTE	SPACE, TAB, UPA, UPZ, LWA, LWZ
	DATA	SPACE, TAB/' ', 9/
	DATA	UPA, UPZ/'A', 'Z'/
	DATA	LWA, LWZ/'a', 'z'/
C
C initialise
	LENGTH = 0
	ILNGTH = LEN(STRING)
	LSTNSP = 0
C
C look for start of name
	IF (RDCHS(CH)) GOTO 50
	IF (.NOT.(CH.EQ.'$' .OR. CH.EQ.'_' .OR.
     &		 (CH.GE.'0' .AND. CH.LE.'9') .OR.
     &		 (CH.GE.UPA .AND. CH.LE.UPZ) .OR.
     &		 (CH.GE.LWA .AND. CH.LE.LWZ)) ) GOTO 40
C
C loop to add to string
20	IF (LENGTH.GE.ILNGTH) GOTO 40	! filled arg string up
	LENGTH = LENGTH+1
	IF (CH.GE.LWA .AND. CH.LE.LWZ)
     &		CH = CH-LWA+UPA		! upper-case
	STRING(LENGTH:LENGTH) = CHAR(CH)
	IF (CH.NE.SPACE .AND. CH.NE.TAB) LSTNSP = LENGTH
C
C get next character of string
	IF (RDCH(CH)) GOTO 50		! j if end of line
C
C name terminates on non-alpha/numeric, non-$ non-_
	IF (CH.EQ.'$' .OR. CH.EQ.'_' .OR.
     &     (CH.GE.'0' .AND. CH.LE.'9') .OR.
     &     (CH.GE.UPA .AND. CH.LE.UPZ) .OR.
     &     (CH.GE.LWA .AND. CH.LE.LWZ)) GOTO 20
	GOTO 40				! met terminator condition
C
C terminated, but we overshot by one character
40	CALL BSCH
C
C terminated, character pointer correct
50	RDNAME = LENGTH
	RETURN
C
	END
