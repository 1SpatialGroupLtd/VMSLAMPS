C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1984-11-23 20:32:50.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL*4 FUNCTION GENTRN(NPTS,PTS,COEFFS,RESID,XMAX,YMAX,
     &				  XRMS,YRMS)
C
***	MODULE	GENTRN
***	IDENT	23NO84
C
C Copyright Laser-Scan Laboratories Limited, Cambridge, England.
C
C Author: D. Meader
C
C	Created 				 DM	23-Nov-1984
C
C Performs a 'four-point' (affine) transformation.
C This routine uses a stripped-down version of DM4PT which performs a
C least squares fit of the specified points to functions of the form:
C		X=a0 + a1*x + a2*y
C		Y=b0 + b1*x + b2*y
C (i.e. no x*y terms).
C This transformation is sufficient to correct for shifting, scaling,
C shearing and rotation.
C
C GENTRN takes an array of NPTS sets of coordinates, with each set
C arranged in the order:
C
C		 Xmeasured Ymeasured Xtarget Ytarget
C
C A set of coefficients (COEFFS) is calculated which will transform the
C 'measured' coordinate system into the 'target' one. X and Y residuals
C are returned in the user array RESID, and the maximum and RMS errors
C are also calculated.
C
C If GENTRN is .TRUE. then we may have tried to do the impossible. For
C example the point distribution may be inappropriate (e.g. all points
C nearly collinear) or the 'measured' and 'target' spaces simply may not
C be related by an affine transformation. In any case the transformation
C coefficients are suspect.
C
CCC	IMPLICIT NONE
C
	INTEGER*4 NPTS			! number of points
	REAL	  PTS(4,NPTS)		! measured & target coordinates
	REAL*8	  COEFFS(3,2)		! returned transform coeffs
	REAL	  RESID(2,NPTS)		! X,Y residuals (target space)
	REAL	  XMAX,YMAX		! max residuals    "      "
	REAL	  XRMS,YRMS		! RMS residuals    "      "
C
	INTEGER	  I,J
	REAL	  DX,DY			! temporary X,Y residuals
	REAL*8	  A(3,3)		! temporary matrix for SOLVE
	REAL*8	  CRIT			! criterion for rejection of fit
	REAL*8	  XM,YM			! measured X,Y (e.g. m/c space)
	REAL*8	  XT,YT			! target X,Y (e.g. map space)
C
C ......................................................................
C
C ... first set up the transform.
C ... zero all the arrays.
	DO 20 J=1,3
	   COEFFS(J,1)=0.0D0
	   COEFFS(J,2)=0.0D0
	   DO 10 I=1,3
	      A(I,J)=0.0D0
10	   CONTINUE
20	CONTINUE
C
C ... set up the matrix equation AC=B, where B is COEFFS initially.
	DO 30 I=1,NPTS
	   XM=PTS(1,I)				! measured values
	   YM=PTS(2,I)
	   XT=PTS(3,I)				! 'true' values
	   YT=PTS(4,I)
C
	   COEFFS(1,1)=COEFFS(1,1) + XT		! r.h.s. of equation
	   COEFFS(1,2)=COEFFS(1,2) + YT
	   COEFFS(2,1)=COEFFS(2,1) + XT*XM
	   COEFFS(2,2)=COEFFS(2,2) + YT*XM
	   COEFFS(3,1)=COEFFS(3,1) + XT*YM
	   COEFFS(3,2)=COEFFS(3,2) + YT*YM
C
	   A(2,1)=A(2,1) + XM			! coefficient matrix A
	   A(3,1)=A(3,1) + YM
	   A(2,2)=A(2,2) + XM*XM
	   A(3,2)=A(3,2) + XM*YM
	   A(3,3)=A(3,3) + YM*YM
30	CONTINUE
C
C ... finish setting up the coefficient matrix A.
	A(1,1)=DFLOAT(NPTS)
	A(1,2)=A(2,1)
	A(1,3)=A(3,1)
	A(2,3)=A(3,2)
C
C ... now call SOLVE to give us the transform array C in COEFFS.
	CALL SOLVE(A,COEFFS,3,2,0,3,3)
C
C ... analyse the eigenvalues of A (now triangular).
C ... examine the first and last eigenvalues; if the fit is good these
C ... will have 'similar' magnitudes (one day I may get round to proving
C ... this, until then take my word for it !!)
C ... return .TRUE. if we don't believe the fit is any good.
C ... N.B. we are more tolerant than in FIT, since we must allow fairly
C ... weird points to be used if required.
	CRIT=1.0D6			! rather arbitrary, this !
	GENTRN=DABS(A(1,1)/A(3,3)).GT.CRIT
C
C ... now test the goodness of fit by applying the transform to the
C ... measured values.
	XRMS=0.0
	YRMS=0.0
	XMAX=0.0
	YMAX=0.0
	DO 40 I=1,NPTS
	   DX=ABS(
     &	      COEFFS(1,1) + PTS(1,I)*COEFFS(2,1) + PTS(2,I)*COEFFS(3,1)
     &	      - PTS(3,I))			! error in target space
	   DY=ABS(
     &	      COEFFS(1,2) + PTS(1,I)*COEFFS(2,2) + PTS(2,I)*COEFFS(3,2)
     &	      - PTS(4,I))
	   RESID(1,I)=DX			! return all residuals
	   RESID(2,I)=DY
	   XRMS=XRMS + DX*DX			! and RMS and MAX errors
	   YRMS=YRMS + DY*DY
	   IF (DX.GT.XMAX) XMAX=DX
	   IF (DY.GT.YMAX) YMAX=DY
40	CONTINUE
	XRMS=SQRT(XRMS/FLOAT(NPTS))
	YRMS=SQRT(YRMS/FLOAT(NPTS))
	RETURN
	END
