C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1990-09-04 12:56:02.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL*4 FUNCTION RWR2PT(NPTS,PTS,COEFFS,RESID,XMAX,YMAX,
     &				  XRMS,YRMS)
C
***	MODULE	RWR2PT
***	IDENT	04SE90
C
C Copyright Laser-Scan Laboratories Limited, Cambridge, England.
C
C Author: R. Russell (with help from D Meader)
C
C	Created 				 RWR	21-Mar-1986
C
C Performs a 'two-point' (orthogonal) transformation.
C This routine performs a least squares fit of the specified points
C to functions of the form:
C		X=a0 + a1*x - a2*y
C		Y=b0 + a2*x + a1*y
C 
C This transformation is sufficient to correct for shifting, scaling
C and rotation.
C
C RWR2PT takes an array of NPTS sets of coordinates, with each set
C arranged in the order:
C
C		 Xmeasured Ymeasured Xtarget Ytarget
C
C A set of coefficients (COEFFS) is calculated which will transform the
C 'measured' coordinate system into the 'target' one. X and Y residuals
C are returned in the user array RESID, and the maximum and RMS errors
C are also calculated.
C
C If RWR2PT is .TRUE. then we may have tried to do the impossible. For
C example the point distribution may be inappropriate (e.g. all points
C nearly collinear) or the 'measured' and 'target' spaces simply may not
C be related by an orthogonal transformation. In any case the 
C transformation coefficients are suspect.
C
CCCCCC	IMPLICIT NONE
C
	INTEGER*4 NPTS			! number of points
	REAL	  PTS(4,NPTS)		! measured & target coordinates
	REAL*8	  COEFFS(4)		! returned transform coeffs
	REAL	  RESID(2,NPTS)		! X,Y residuals (target space)
	REAL	  XMAX,YMAX		! max residuals    "      "
	REAL	  XRMS,YRMS		! RMS residuals    "      "
C
	INTEGER	  I,J
	REAL	  DX,DY			! temporary X,Y residuals
	REAL*8	  A(4,4)		! temporary matrix for SOLVE
	REAL*8	  CRIT			! criterion for rejection of fit
	REAL*8	  XM,YM			! measured X,Y (e.g. m/c space)
	REAL*8	  XT,YT			! target X,Y (e.g. map space)
	REAL*8	  SIGXM,SIGYM		! temporary sums
	REAL*8	  SIG2XM,SIG2YM
	REAL*8	  SIGXT,SIGYT
	REAL*8	  SIGXTXM,SIGYTYM
	REAL*8	  SIGXTYM,SIGYTXM
C
C ......................................................................
C
C ... first set up the transform.
C ... zero all the arrays.
	DO 10 J=1,4
	   COEFFS(J)=0.0D0
10	CONTINUE
C
	SIGXM=0.0D0
	SIGYM=0.0D0
	SIG2XM=0.0D0
	SIG2YM=0.0D0
C	SIGXMYM=0.0D0
	SIGXT=0.0D0
	SIGYT=0.0D0
	SIGXTXM=0.0D0
	SIGYTYM=0.0D0
	SIGXTYM=0.0D0
	SIGYTXM=0.0D0
C
C ... set up the matrix equation AC=B, where B is COEFFS initially.
	DO 30 I=1,NPTS
	   XM=PTS(1,I)				! measured values
	   YM=PTS(2,I)
	   XT=PTS(3,I)				! 'true' values
	   YT=PTS(4,I)
C
	   SIGXM=SIGXM + XM
	   SIGYM=SIGYM + YM
	   SIG2XM=SIG2XM + XM*XM
	   SIG2YM=SIG2YM + YM*YM	   
C
	   SIGXT=SIGXT + XT
	   SIGYT=SIGYT + YT
C
	   SIGXTXM=SIGXTXM + XT*XM
	   SIGYTYM=SIGYTYM + YT*YM
	   SIGXTYM=SIGXTYM + XT*YM
	   SIGYTXM=SIGYTXM + YT*XM
C
30	CONTINUE
C
C ... set up the coefficient matrix A.
	A(1,1)=DFLOAT(NPTS)
	A(4,4)=A(1,1)
	A(2,2)=SIG2XM+SIG2YM
	A(3,3)=A(2,2)
C
	A(2,1)=SIGXM
	A(1,2)=SIGXM
	A(4,3)=SIGXM
	A(3,4)=SIGXM
C
	A(3,1)=-SIGYM
	A(1,3)=-SIGYM
	A(4,2)=SIGYM
	A(2,4)=SIGYM
C
	A(4,1)=0.0D0
	A(3,2)=0.0D0
	A(2,3)=0.0D0
	A(1,4)=0.0D0
C
C ... and the constants
	COEFFS(1)=SIGXT
	COEFFS(2)=SIGXTXM+SIGYTYM
	COEFFS(3)=-SIGXTYM+SIGYTXM
	COEFFS(4)=SIGYT
C
C ... now call SOLVE to give us the transform array C in COEFFS.
	CALL SOLVE(A,COEFFS,4,1,0,4,4)
C
C ... analyse the eigenvalues of A (now triangular).
C ... examine the first and last eigenvalues; if the fit is good these
C ... will have 'similar' magnitudes (one day I may get round to proving
C ... this, until then take my word for it !!)
C ... return .TRUE. if we don't believe the fit is any good.
C ... N.B. we are more tolerant than in FIT, since we must allow fairly
C ... weird points to be used if required.
	CRIT=1.0D7			! rather arbitrary, this !
C
C
	RWR2PT=DABS(A(1,1)/A(4,4)).GT.CRIT
C
C ... now test the goodness of fit by applying the transform to the
C ... measured values.
	XRMS=0.0
	YRMS=0.0
	XMAX=0.0
	YMAX=0.0
	DO 40 I=1,NPTS
	   DX=ABS(
     &	      COEFFS(1) + PTS(1,I)*COEFFS(2) - PTS(2,I)*COEFFS(3)
     &	      - PTS(3,I))			! error in target space
	   DY=ABS(
     &	      COEFFS(4) + PTS(1,I)*COEFFS(3) + PTS(2,I)*COEFFS(2)
     &	      - PTS(4,I))
	   RESID(1,I)=DX			! return all residuals
	   RESID(2,I)=DY
	   XRMS=XRMS + DX*DX			! and RMS and MAX errors
	   YRMS=YRMS + DY*DY
	   IF (DX.GT.XMAX) XMAX=DX
	   IF (DY.GT.YMAX) YMAX=DY
40	CONTINUE
	XRMS=SQRT(XRMS/FLOAT(NPTS))
	YRMS=SQRT(YRMS/FLOAT(NPTS))
	RETURN
	END
