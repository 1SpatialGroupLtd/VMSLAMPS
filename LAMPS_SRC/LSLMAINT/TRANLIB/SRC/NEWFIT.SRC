	LOGICAL*4 FUNCTION NEWFIT(NPTS,SHOULD,SAID,COEFF,
     &				  A,B,V,NORDER,NGAP)
C
***	MODULE	NEWFIT
***	IDENT	21MR85
C
CMOD	Now called NEWFIT			TJI	22-Mar-1985
CMOD	Arrays A,B,V passed as arguments	 DM	21-Mar-1985
CMOD	CRIT put in common block		 DM	20-Jun-1983
CMOD	Now a function - .TRUE. if fit is duff	 DM	01-Jun-1983
C
C Takes NPTS points in SHOULD and SAID and returns transform
C coefficients of order NORDER in COEFF using every NGAPth point.
C (Gets SHOULD as a function of SAID).
C
	REAL*8	CRIT			! criterion for rejection of fit
	COMMON /FITCOM/ CRIT
C
	INTEGER I,NGAP,NORDER,NPTS,NTERMS
	REAL	SHOULD(2,NPTS),SAID(2,NPTS)
	REAL	COEFF(((NORDER+1)*(NORDER+2))/2,2)
	REAL*8	A(((NORDER+1)*(NORDER+2))/2,((NORDER+1)*(NORDER+2))/2)
	REAL*8	B(((NORDER+1)*(NORDER+2))/2,2)
	REAL*8	V(((NORDER+1)*(NORDER+2))/2)
	REAL*8	XD,YD,XD1,YD1
C
C ......................................................................
C
	NTERMS=((NORDER+1)*(NORDER+2))/2
C
C ... clear arrays.
	DO 20 I=1,NTERMS
	   B(I,1)=0.0D0
	   B(I,2)=0.0D0
	   DO 10 J=1,NTERMS
	      A(I,J)=0.0D0
10	   CONTINUE
20	CONTINUE
C
C ... form sums.
	DO 50 I=1,NPTS
	   IF (MOD(I,NGAP).NE.0) GO TO 50
	   XD=SAID(1,I)				! measured values
	   YD=SAID(2,I)
	   XD1=SHOULD(1,I)			! true values
	   YD1=SHOULD(2,I)
CCC	   IF (XD.GT.1.0D29.OR.XD1.GT.1.0D29) GO TO 60
	   CALL POLGEN(V,XD,YD,NORDER)		! get 1,X,Y,XX,XY,YY etc
	   DO 40 J=1,NTERMS
	      B(J,1)=B(J,1) + V(J)*XD1		! r.h.s. of equation
	      B(J,2)=B(J,2) + V(J)*YD1
	      DO 30 K=1,NTERMS
		 A(J,K)=A(J,K) + V(J)*V(K)	! l.h.s. of equation
30	      CONTINUE
40	   CONTINUE
50	CONTINUE
C
C ... solve matrix equation AC=B (returning coefficients C in B).
C ... note that if the points are *really* duff this will fall over !!
	CALL SOLVE(A,B,NTERMS,2,0,NTERMS,NTERMS)
C
C ... return results.
	DO 60 I=1,NTERMS
	   COEFF(I,1)=B(I,1)
	   COEFF(I,2)=B(I,2)
60	CONTINUE
C
C ... analyse the eigenvalues of A (now triangular).
C ... examine the first and last eigenvalues; if the fit is good these
C ... will have similar magnitudes (one day I may get round to proving
C ... this, until then take my word for it !!)
	NEWFIT=DABS(A(1,1)/A(NTERMS,NTERMS)).GT.CRIT
C
	RETURN
	END
