	.TITLE	WAIT
	.IDENT  /05AP84/
	.LIBRARY/LSL$LIBRARY:LSLMACLIB/
;
; VAX Fortran WAIT routine
; mimics PDP11 IAS WAIT.
;
;	CALL WAIT(ITIME, IUNITS [,ISTATUS] )
;
; where ITIME is the magnitude of the wait
;	IUNITS are :	0	ticks , 50 per second
;			1	milliseconds
;			2	seconds
;			3	minutes
;			4	hours
;	ISTATUS is NOT used .
;
; ITIME is always read as INTEGER*2. Beware large INTEGER*4 nos.
; The maximum time for a wait is 2**31-1 milliseconds ( 23 days ).
;

; workspace
	DATA_PSECT	VIO

WAITTM:	.BLKQ	1		; quad-word delta time put here

; conversion constants

TIMTAB:	.LONG	20.		; convert ticks to ms.
	.LONG	1.		;	    ms.    ms.
	.LONG	1000.		;	   secs.   ms.
	.LONG	60.*1000.	;	   mins.   ms.
	.LONG	3600.*1000.	;	    hrs.   ms.

	CODE_PSECT	VIO

; entry point for WAIT

	.ENTRY	WAIT, ^M<>
	MOVZWL	@4(AP), R0		; get time interval
	MOVZWL	@8(AP), R1		;  and time units
	CMPL	#4., R1			; check magnitude of units
	BLSS	90$			; don't wait if bad units
	MULL2	TIMTAB[R1],R0		; convert time to milliseconds
	BVS	90$			; don't wait if too large time
	EMUL	#-10000., R0, #0., WAITTM  ; convert to 100ns units
	$CANWAK_S			; cancel anything outstanding
	$SCHDWK_S	DAYTIM=WAITTM	; schedule to wake in time
	BLBC	R0,90$			; don't wait if error
	$HIBER_S			; hibernate until time-up
90$:	RET

;
	.PSECT
	.END
