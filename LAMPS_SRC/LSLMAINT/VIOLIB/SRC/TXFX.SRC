C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1985-05-01 13:44:16.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE TXORGN(IX0,IY0)
C
***	MODULE TXORGN
***	IDENT  01MY85
C
	IMPLICIT NONE
C
C Fortran interface to Tektronix 4010 display terminal
C
CMOD	TXFLSH added				AJW	21-Feb-1984
CMOD	Tidied					AJW	01-Jun-1981
CMOD	Improved				MCF	   Jul-1976
CMOD	Created					PGH	07-May-1976
C
***	COMMON/LSL$CMNVIO:TXCOM/
C
	INTEGER IX0,IY0
C
	X0=IX0
	Y0=IY0
C
	RETURN
	END
C
C**********************************************************************
C 
	SUBROUTINE TXSCLE(F)
C
***	MODULE TXSCLE
***	IDENT  24JN83
C
	IMPLICIT NONE
C
	REAL F
C
***	COMMON/LSL$CMNVIO:TXCOM/
C
	SCALE=F
C
	RETURN
	END
C
C**********************************************************************
C
	SUBROUTINE TXMBY(IDX,IDY)
C
***	MODULE TXMBY
***	IDENT  24JN83
C
	IMPLICIT NONE
C
C invisible line by (IDX,IDY)
C
	INTEGER IDX,IDY
	INTEGER IX,IY
C
***	COMMON/LSL$CMNVIO:TXCOM/
C
	IX=IXP+IDX
	IY=IYP+IDY
	CALL TXMTO(IX,IY)
C
	RETURN
	END
C
C**********************************************************************
C
	SUBROUTINE TXLBY(IDX,IDY)
C
***	MODULE TXLBY
***	IDENT  24JN83
C
	IMPLICIT NONE
C
C visible line by (IDX,IDY)
C
	INTEGER IDX,IDY
	INTEGER IX,IY
C
***	COMMON/LSL$CMNVIO:TXCOM/
C
	IX=IXP+IDX
	IY=IYP+IDY
	CALL TXLTO(IX,IY)
C
	RETURN
	END
C
C**********************************************************************
C
	SUBROUTINE TXLTO(IX,IY)
C
***	MODULE TXLTO
***	IDENT  24JN83
C
	IMPLICIT NONE
C
C visible line to (IX,IY)
C
	INTEGER IX,IY
	INTEGER I,J
	INTEGER JX,JY
C
***	COMMON/LSL$CMNVIO:TXCOM/
C
	BYTE NCH(4),MCH(4)
	IF(.NOT.GRAFIX) CALL TXMTO(IXP,IYP)
	JX=IX*SCALE+X0
	JY=IY*SCALE+Y0
	CALL TXCODE(JX,JY,NCH)
C rules for what can be missed out
	IF(NCH(1).EQ.CH(1)) NCH(1)=0
	IF(NCH(3).EQ.CH(3)) NCH(3)=0
	IF(NCH(2).EQ.CH(2).AND.NCH(3).EQ.0) NCH(2)=0
	J=0
	DO 10 I=1,4
	  IF(NCH(I).EQ.0) GOTO 10
	  CH(I)=NCH(I)
	  J=J+1
	  MCH(J)=NCH(I)
10	CONTINUE
	CALL TXWT(MCH,J)
	IXP=IX
	IYP=IY
C
	RETURN
	END
C
C**********************************************************************
C
	SUBROUTINE TXMTO(IX,IY)
C
***	MODULE TXMTO
***	IDENT  24JN83
C
	IMPLICIT NONE
C
C invisible line to (IX,IY)
C
	INTEGER IX,IY
	INTEGER JX,JY
C
***	COMMON/LSL$CMNVIO:TXCOM/
C
	GRAFIX=.TRUE.
	CALL TXWT(29,1)
	JX=IX*SCALE+X0
	JY=IY*SCALE+Y0
	CALL TXCODE(JX,JY,CH)
	CALL TXWT(CH,4)
	IXP=IX
	IYP=IY
C
	RETURN
	END
C
C**********************************************************************
C
	SUBROUTINE TXLOC(ICH,IX,IY)
C
***	MODULE TXLOC
***	IDENT  21FE84
C
CMOD	TXFLSH added				AJW	21-Feb-1984
C
	IMPLICIT NONE
C
C after the operator has typed a printing character
C returns posn of cross hair cursor if TXCUR has been called
C otherwise returns posn of alpha cursor .
C
	INTEGER ICH,IX,IY
	BYTE NCH(8)
C
***	COMMON/LSL$CMNVIO:TXCOM/
C
	INTEGER M32
	DATA M32/-32/
C
	CALL TXRD(NCH,5)
C  appropriate coercion forced in the next 2 by "M32"
	IX=32*(NCH(2)+M32)+(NCH(3)+M32)
	IY=32*(NCH(4)+M32)+(NCH(5)+M32)
	IX=(IX-X0)/SCALE
	IY=(IY-Y0)/SCALE
	ICH=NCH(1)
	GRAFIX=.FALSE.
C
C flush anything in the type-ahead buffer
	CALL TXFLSH
C
	RETURN
	END
C
C**********************************************************************
C
	SUBROUTINE TXCUR
C
***	MODULE TXCUR
***	IDENT  24JN83
C
	IMPLICIT NONE
C
C sends ESC SUB to start cursor
C
***	COMMON/LSL$CMNVIO:TXCOM/
C
	BYTE NCH(4)
C
	NCH(1)=27
	NCH(2)=26
	CALL TXWT(NCH,2)
C
	RETURN
	END
C
C**********************************************************************
C
	SUBROUTINE TXCLR
C
***	MODULE TXCLR
***	IDENT  24JN83
C
	IMPLICIT NONE
C
C sends ESC FF to erase, reurn to ALPHA and go HOME.
C
***	COMMON/LSL$CMNVIO:TXCOM/
C
	CH(1)=27
	CH(2)=12
	CALL TXWT(CH,2)
	GRAFIX=.FALSE.
	CALL WAIT(1,2)		! one second delay
C
	RETURN
	END
C
C**********************************************************************
C
	SUBROUTINE TXEND
C
***	MODULE TXEND
***	IDENT  24JN83
C
	IMPLICIT NONE
C
C sends US to return to ALPHA mode
C
***	COMMON/LSL$CMNVIO:TXCOM/
C
	CALL TXWT(31,1)
	GRAFIX=.FALSE.
	RETURN
	END
