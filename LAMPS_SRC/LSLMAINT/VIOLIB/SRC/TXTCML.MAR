;
; * This file is part of the LAMPS distribution, released as a software
; * preservation project to archive digital history for future historians.
; * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
; *
; * Timestamp of this file for the 2002 release was: 1982-08-16 10:04:56.000000000 +0100
; *
; * This program is free software: you can redistribute it and/or modify
; * it under the terms of the GNU General Public License as published by
; * the Free Software Foundation, version 3.
; *
; * This program is distributed in the hope that it will be useful, but
; * WITHOUT ANY WARRANTY; without even the implied warranty of
; * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
; * General Public License for more details.
; *
; * You should have received a copy of the GNU General Public License
; * along with this program. If not, see http://www.gnu.org/licenses/.
;
	.TITLE	TXTCML
	.IDENT	/16AU82/
	CODE_PSECT	VIO
 
; Copyright Laser-Scan Laboratories Ltd, Cambridge, England
; Author Paul Hardy
; Created Jun 80
; revised for EOF after CLI line 	PGH	18-Mar-1982
;
; Must be assembled with TXTC.MAR
;
; Get a command line from CLI or SYS$INPUT
;
	.ENTRY	TXTCML,^M<R11>
	MOVW	TXTLIM,TXTPTR		; buffer size
	TSTW	TXTCLI			; have we already had CLI line ?
	BNEQ	40$			; yes - go get from SYS$INPUT
	PUSHAQ	TXTDSC			; address of destination
	CALLS	#1, G^GETCLI		; get command line if any
	TSTL	R0			; length in R0
	BEQL	7$			; nothing there
5$:	MOVW	R0,TXTPTR		; update length
	CMPW	R0,TXTLIM		; full buffer ?
	BGEQ	6$			; yes - nowt to do
	CLRB	TXTBUF[R0]		; add terminating null
6$:	INCW	TXTCLI			; mark as read CLI now
	MOVW	#1,DCPTR		; nothing decoded
	RET
 
; here on blank CLI line - prompt for something
7$:	INCW	TXTCLI			; we have tried the CLI line
; here to get line from SYS$INPUT
10$:	JSB	VIO$1ST			; arg handling init
	MOVL	#1,R1			; at least one arg
	JSB	VIO$GNA			; get arg
	BCS	15$			; missing
	INCL	R1			; one more arg for GET_INPUT
	PUSHL	R0			; prompt string address
15$:	PUSHAQ	TXTDSC			; destination string descriptor address
	CALLS	R1, G^VIO$GET_INPUT	; read from SYS$INPUT, or whatever
	BLBC	R0,99$			; error in read
	MOVL	#TXTBUF,R1		; address of buffer
	DECL	R1			; to allow 1st byte = 1
	MOVZWL	TXTLIM,R0		; maximum string size
20$:	CMPB	(R1)[R0],#^A/ /		; space ?
	BNEQ	30$			; jump at first non-space from end
	SOBGTR	R0,20$			; more to scan
30$:	BRB	5$			; common exit
 
; here after a CLI line to fake an EOF if necessary
; for compatability with RSX GCML$
40$:	CMPW	TXTCLI,#1		; first time ?
	BNEQ	10$			; no so ok

; here with error in read or to fake an EOF
99$:	MNEGL	#10,R0			; return negative value
	CLRL	TXTPTR			; nothing read
	BRB	6$			; common exit
 
	.PSECT
	.END
