;
; * This file is part of the LAMPS distribution, released as a software
; * preservation project to archive digital history for future historians.
; * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
; *
; * Timestamp of this file for the 2002 release was: 1984-04-05 16:31:02.000000000 +0100
; *
; * This program is free software: you can redistribute it and/or modify
; * it under the terms of the GNU General Public License as published by
; * the Free Software Foundation, version 3.
; *
; * This program is distributed in the hope that it will be useful, but
; * WITHOUT ANY WARRANTY; without even the implied warranty of
; * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
; * General Public License for more details.
; *
; * You should have received a copy of the GNU General Public License
; * along with this program. If not, see http://www.gnu.org/licenses/.
;
; Copyright Laser-Scan Labs Ltd, Cambridge, England.
; Author Paul Hardy	Created June 80
; Last edited 10/June/80

; Unformatted write to terminal

	.TITLE	UTOUT
	.IDENT	/05AP84/
	.LIBRARY/LSL$LIBRARY:LSLMACLIB/

; local storage

	DATA_PSECT	VIO

TTDESC:	.ASCID	/TT/			; string descriptor for default device
EQVDSC:					; string descriptor for translation
EQVLEN:	.LONG	40			; length of translated string
EQVPTR:	.LONG	EQVBUF			; pointer to name
EQVBUF:	.BLKB	40			; space for logical name translation
UTWISB:	.LONG	0			; IO status block
UTWSTS:	.LONG	0

; Directive Parameter Block

UTWDPB:	$QIO		FUNC=IO$_WRITEVBLK!IO$M_NOFORMAT,-
			IOSB=UTWISB
UTWCHAN=UTWDPB+QIO$_CHAN
UTWEFN=UTWDPB+QIO$_EFN
UTWP1=UTWDPB+QIO$_P1
UTWP2=UTWDPB+QIO$_P2

	CODE_PSECT	VIO

; initialisation routine CALL UTWINI(['dev'][,efn])

	.ENTRY	UTWINI,^M<>
	MOVAL	TTDESC,R1		; default device
	CMPB	(AP),#1			; first arg ?
	BLSS	10$			; missing
	MOVL	4(AP),R1		; string descriptor pointer for device
10$:	$TRNLOG_S	LOGNAM=(R1),-	; translate logical name for device
			RSLLEN=EQVLEN,-	; length
			RSLBUF=EQVDSC	; destination
	BLBC	R0,EXIT
	$ASSIGN_S	DEVNAM=EQVDSC,-	
			CHAN=UTWCHAN
	BLBC	R0,EXIT
	CMPB	(AP),#2			; second arg is efn
	BLSS	20$			; missing
	MOVZBL	@8(AP),UTWEFN		; use given efn
	BRB	30$
20$:	PUSHAL	UTWEFN			; place to put it
	CALLS	#1, G^LIB$GET_EF	; get event flag no
	BLBC	R0,EXIT
30$:	$SETEF_S	EFN=UTWEFN	; for later
	BLBC	R0,EXIT
	RET

; write character string argument to the terminal

	.ENTRY	UTWCHS,^M<>
	$WAITFR_S	EFN=UTWEFN	; wait for previous write
	BLBC	R0,EXIT
	MOVL	4(AP),R1		; address of string descriptor
	MOVL	(R1)+,R0		; string length and crud
	MOVZWL	R0,UTWP2		; just the length
	MOVL	(R1),UTWP1		; address of string
	$QIO_G	UTWDPB			; queue the request
	BLBC	R0,EXIT
	RET

EXIT:	$EXIT_S	R0			; error exit 

	.PSECT
	.END
