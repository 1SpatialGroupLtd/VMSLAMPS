;
; * This file is part of the LAMPS distribution, released as a software
; * preservation project to archive digital history for future historians.
; * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
; *
; * Timestamp of this file for the 2002 release was: 1984-09-10 14:26:56.000000000 +0100
; *
; * This program is free software: you can redistribute it and/or modify
; * it under the terms of the GNU General Public License as published by
; * the Free Software Foundation, version 3.
; *
; * This program is distributed in the hope that it will be useful, but
; * WITHOUT ANY WARRANTY; without even the implied warranty of
; * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
; * General Public License for more details.
; *
; * You should have received a copy of the GNU General Public License
; * along with this program. If not, see http://www.gnu.org/licenses/.
;
	.TITLE		VIOMV3
	.IDENT		/10SE84/
	.LIBRARY	/LSL$LIBRARY:LSLMACLIB/
;
; Copyright Laser-Scan Laboratories Limited, Cambridge, England.
;
; Author: D. Meader
;
;	Created from VIOCLR			 DM	16-Aug-1983
;
 
; CALL VIOMV3(ARRAY1.ab,LENGTH.rl,ARRAY2.ab)
 
; Copies LENGTH bytes starting at ARRAY1 to ARRAY2.
 
	$SSDEF
 
	CODE_PSECT	VIO
 
	.ENTRY	VIOMV3, ^M<R2,R3,R4,R5,R6,R7,R8,R9,R11>
 
; R2 - R5 are stepped on by MOVC3
; R6 - one word full
; R7 - current length
; R8 - address of ARRAY1
; R9 - address of ARRAY2
; R11- used by VIO$...
 
	JSB	VIO$1ST			; ready for 1st arg
 
	JSB	VIO$GNA			; and get it
	BCS	10$
	MOVL	R0, R8			; remember address in R8
 
	JSB	VIO$GNA			; get LENGTH
	BCS	10$
	MOVL	(R0), R7		; and dereference it
 
	JSB	VIO$GNA			; get ARRAY2
	BCS	10$
	MOVL	R0, R9			; remember address in R9
 
; now, MOVC3 takes a WORD length, so we cannot move more than 65535
; bytes at a time. However, we have just taken a longword argument,
; so we must check to see how much we need to move
 
	MOVL	#^XFFFF,R6		; useful number
 
1$:	CMPL	R7,R6			; is length more than one word ?
	BLEQU	5$			; no - we can do it in one go
 
	MOVC3	R6,(R8),(R9)		; copy this section of array
 
	SUBL2	R6,R7			; get remainder in R7
	ADDL	R6,R8			; and new start of arrays
	ADDL	R6,R9
	BRB	1$
 
5$:	MOVC3	R7,(R8),(R9)		; copy the whole of ARRAY
 
	MOVL	#SS$_NORMAL,R0		; success
	BRB 20$
 
10$:	MOVL	#SS$_INSFARG,R0		; insufficient args
 
20$:	RET
 
	.END
