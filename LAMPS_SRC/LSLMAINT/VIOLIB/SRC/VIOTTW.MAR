	.TITLE	VIOTTW
	.IDENT	/16AU82/
;
; Copyright Laser-Scan Laboratories Ltd,Cambridge,England
; Author Paul Hardy
;
; terminal output routines for VIOLIB

; TTWLIN & TTWSTR write a line using LIB$PUT_OUTPUT
	; CALL TTWSTR([STRING])		; write character string
; CALL TTWLIN([BUF], [NCHS])		; write byte array
; the default buffer is EXPC as used by EXPLODE

; MUST be assembled with EXPC.MAR

	DATA_PSECT	VIO

TTWDSC:					; string descriptor for LIB$PUT_OUTPUT
TTWLEN:	.BLKW	1			; string length
	.BYTE	0,0			; class & type
TTWPTR:	.BLKL	1			; pointer to buffer (unset)

	CODE_PSECT	VIO

WRTEXP::				; entry alias used by WRITEF
MSG::					; alias for old times sake
LINOUT::				; ditto
PUTLIN::
WRTOUT::
MSGOUT::
	.ENTRY	TTWSTR,^M<R11>		; write string
	JSB	VIO$1ST			; basic init for arg handling
	MOVAL	EXPDSC,R0		; default string is in EXPC
	JSB	VIO$GNA			; first arg is string to write
	PUSHL	R0			; arg is string descriptor address
; here with address of descriptor on stack
GOTDSC:	CALLS	#1, G^VIO$PUT_OUTPUT	; print it
	RET				; return code from LIB$ call

	.ENTRY	TTWLIN,^M<R11>		; write byte array
	JSB	VIO$1ST			; basic init for arg handling
	MOVAB	EXPBUF,R0		; default buffer is EXPBUF
	JSB	VIO$GNA			; first arg is buffer to read into
	MOVL	R0,TTWPTR		; set buffer address
	MOVAW	EXPLEN,R0		; default for second arg is EXPLEN
	JSB	VIO$GNA			; get second arg - length
	MOVW	(R0),TTWLEN		; set in descriptor
	PUSHAL	TTWDSC			; use our manufactured descriptor
	BRW	GOTDSC			; common code

	.PSECT
	.END
