	SUBROUTINE ICLHDR(SERNO,NAME,SEQNO,GENNO,KEEP,BYTARR)
C
**	MODULE ICLHDR
**	IDENT  25JN97
C
C Created by Alan J. Wylie 31.Mar.80
C
C Last edited 28/Jan/81	- made SERNO integer*4		SCM
C
C Copyright Laser-Scan Laboratories Limited,Cambridge,England
C
C Creates in BYTARR an I.C.L. standard 9 track header
C
C arguments:
	INTEGER*4 SERNO		! tape serial number
	BYTE NAME(12)		! 12 character name
	INTEGER SEQNO		! tape sequence number
	INTEGER GENNO		! tape generation number
	INTEGER KEEP		! number of days tape is to be kept
	BYTE BYTARR(60)		! array into which header is put
C
C workspace:
	INTEGER*4 QUAD		! we only want 24 bits,but 32 will do
	BYTE BYTE(4)
	EQUIVALENCE (QUAD,BYTE)	! so we can put QUAD into BYTARR
C
	BYTE HDDR(4)		! guess what this is
	INTEGER INDX		! where we are in BYTARR
	INTEGER DAY,MONTH,YEAR
	CHARACTER*11 CDATE
	INTEGER*4 QSHIFT	! for shifting 6 bit bytes around in
	INTEGER DM(12)		! cumulative days since 1 Jan
	INTEGER I,J
C
	DATA HDDR/'H','D','D','R'/
	DATA DM/0,31,59,90,120,151,181,212,243,273,304,334/
C
C code:
	INDX=1
	QUAD=0
	DO 10 I=1,4		! first I.C.L. word is "HDDR"
	   QSHIFT=HDDR(5-I)
	   IF ((QSHIFT.AND."100).EQ.0) QSHIFT=QSHIFT.XOR."20
	   QSHIFT=(QSHIFT.XOR."40).AND."77
	   QSHIFT=ISHFT(QSHIFT,6*I-6)
	   QUAD=QUAD.OR.QSHIFT
10	CONTINUE
	DO 20 I=3,1,-1		! put it into 3*8 bit bytes
	   BYTARR(INDX)=BYTE(I)
	   INDX=INDX+1
20	CONTINUE
C
	QUAD=SERNO
	DO 30 I=3,1,-1
	   BYTARR(INDX)=BYTE(I)
	   INDX=INDX+1
30	CONTINUE
C
	DO 60 I=1,3		! 3 I.C.L. words of name
	   QUAD=0
	   DO 40 J=1,4		! 4 I.C.L. bytes per word
	      QSHIFT=NAME(4*I-J+1)
	      IF ((QSHIFT.AND."100).EQ.0) QSHIFT=QSHIFT.XOR."20
	      QSHIFT=(QSHIFT.XOR."40).AND."77
	      QSHIFT=ISHFT(QSHIFT,6*J-6)
	      QUAD=QUAD.OR.QSHIFT
40	   CONTINUE
	   DO 50 J=3,1,-1	! put it into 3*8 bit bytes
	      BYTARR(INDX)=BYTE(J)
	      INDX=INDX+1
50	   CONTINUE
60	CONTINUE
C
	QUAD=SEQNO
	DO 70 I=3,1,-1
	   BYTARR(INDX)=BYTE(I)
	   INDX=INDX+1
70	CONTINUE
C
	QUAD=GENNO
	DO 80 I=3,1,-1
	   BYTARR(INDX)=BYTE(I)
	   INDX=INDX+1
80	CONTINUE
C
	QUAD=KEEP
	DO 90 I=3,1,-1
	   BYTARR(INDX)=BYTE(I)
	   INDX=INDX+1
90	CONTINUE
C
	CALL IDATE(MONTH,DAY,YEAR)
C
C	Year 2000 compliancy patch
C
	CALL LIB$DATE_TIME(CDATE)
	READ (CDATE(8:11), '(I4)') YEAR
	YEAR = YEAR - 1900
	QUAD=YEAR*365+YEAR/4+DM(MONTH)+DAY
	IF (YEAR.EQ.(YEAR/4)*4.AND.MONTH.LE.2) QUAD=QUAD-1
	DO 100 I=3,1,-1
	   BYTARR(INDX)=BYTE(I)
	   INDX=INDX+1
100	CONTINUE
C
	DO 110 I=28,60		! fill up rest with nothings
	   BYTARR(I)=0
110	CONTINUE
C
	RETURN
	END
