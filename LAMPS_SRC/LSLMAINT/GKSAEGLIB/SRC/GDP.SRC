C
C***********************************************************************
C
C	GDP PRIMITIVE
C	=============
C
	SUBROUTINE	GGDP(N,PX,PY,ID)
C
**	MODULE	GGDP
**	IDENT	21FE91
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C Created				Ron Russell	16-July-1987
C
C GENERALISED DRAWING PRIMITIVE
C
	IMPLICIT NONE
C
C Arguments:
	INTEGER N			! number of points
	REAL	PX(N)			! X coords of points in WC
	REAL	PY(N)			! Y coords of points in WC
	INTEGER	ID			! GDP identifier
C
C Workspace
	INTEGER	I
	REAL	QX(3),QY(3)
	REAL	PX1,PX2,PY1,PY2		! X and Y coordinates
	REAL	CURX,CURY		! current position
C
***	COMMON/GKSDAT1/
C
C Functions:
	LOGICAL	GKCLIP			! clips a straight line to
					! rectangular boundaries
C
	IF ((ID.LE.0.OR.ID.GT.3).AND.(ID.NE.6)) THEN
	   CALL GKSERR(102,'GGDP')
	   GOTO 9999
	ENDIF
	IF ((ID.EQ.1.AND.N.NE.2).OR.
     &	    ((ID.EQ.2.OR.ID.EQ.3).AND.N.NE.3).OR.
     &	    (ID.EQ.6.AND.N.LT.2)) THEN
	   CALL GKSERR(100,'GGDP')
	   GOTO 9999
	ENDIF
C
C Is this workstation active?
	IF (.NOT.WSACT) GOTO 100
C
C Make sure transformation is up to date
	IF (UPTRN) CALL GUPTRN
C
C Is everything clipped ?
	IF (CLPALL) GOTO 100
C
C Make sure polyline attributes are up to date
	IF (UPLIN) CALL GUPLIN
C
	IF (ID.NE.6) THEN		! circle
C
C Loop for each point
	   DO 90 I = 1, N
	      QX(I) = PX(I)
	      QY(I) = PY(I)
	      IF (ENCLIP) THEN
	         IF (QX(I).LT.CLIPLO(1).OR.QX(I).GT.CLIPHI(1) .OR.
     &		     QY(I).LT.CLIPLO(2).OR.QY(I).GT.CLIPHI(2) ) GOTO 100
	      ENDIF
C transform World Coordinates to Workstation Device Coordinates
	      QX(I) = (QX(I)*SCALEX)+OFFX
	      QY(I) = (QY(I)*SCALEY)+OFFY
90	   CONTINUE
C
C Draw the circle or arc
C
	   CALL AEG_CIRCLE(QX,QY,ID)
C
	ELSE				! curve
C
C unset current position to force move
	   CURX = 1E38
	   CURY = 1E38
C
C Loop for each segment of this polyline.
	   DO 95 I = 1, N-1
	      PX1 = PX(I)
	      PY1 = PY(I)
	      PX2 = PX(I+1)
	      PY2 = PY(I+1)
	      IF (ENCLIP) THEN
	         IF (GKCLIP(PX1,PY1,PX2,PY2)) GOTO 95
	      ENDIF
C transform World Coordinates to Workstation Device Coordinates
	      PX1 = (PX1*SCALEX)+OFFX
	      PX2 = (PX2*SCALEX)+OFFX
	      PY1 = (PY1*SCALEY)+OFFY
	      PY2 = (PY2*SCALEY)+OFFY
	      IF (PX1.NE.CURX.OR.PY1.NE.CURY) CALL AEG_MOVTO(PX1,PY1)
	      CALL AEG_CURTO(PX2,PY2)
	      CURX = PX2		! update current position
	      CURY = PY2
95	   CONTINUE
	ENDIF
C
	IF (DEFER .EQ. GASAP) CALL GUWK(WKIDS,0)
100	CONTINUE
C
9999	RETURN
	END
