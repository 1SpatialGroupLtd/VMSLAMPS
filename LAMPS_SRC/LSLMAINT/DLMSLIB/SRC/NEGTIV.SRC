C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1983-11-08 09:43:54.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION NEGTIV
C
***	IDENT	08NO83
***	MODULE	NEGTIV
C
C	Copyright LaserScan Laboratories Ltd., Cambridge, England
C
C Author TJ Ibbs, 11 March 1983
C
C This routine looks at the current 36 bit word, and if it is negative,
C it converts it to 2s complement. It also removes -0 !!
C If it was -ve and NOT -0, then it returns TRUE, otherwise FALSE
C
	IMPLICIT NONE
C
***	COMMON/CMNDFAD:DFAD/	! for buffer
C
	NEGTIV = .FALSE.
C
C we look at the 18th bit of the high 1/2 word to determine if the
C thing is positive...
C
	IF ( (DFABUF(DFAPTR+1).AND.'20000'X).EQ.0 ) RETURN
C
C add one to the low 1/2 word
C
	DFABUF(DFAPTR) = DFABUF(DFAPTR)+1		! add one
C
C do we need to carry into the high 1/2 word?
C
	IF ( DFABUF(DFAPTR).GT.'3FFFF'X ) THEN	! carry up
C
C	 yes - low 1/2 word is thus zero, do the carry
C
	   DFABUF(DFAPTR)   = 0
	   DFABUF(DFAPTR+1) = DFABUF(DFAPTR+1)+1
C
C	 and if the high 1/2 word overflows, was -ve zero
C
	   IF ( DFABUF(DFAPTR+1).GT.'3FFFF'X ) THEN
	      DFABUF(DFAPTR+1) = 0		! zero high 1/2 too
	      RETURN
	   ENDIF
	ENDIF
C
C now we have to convert our 2s complement (but 36 bit) value into
C the equivalent +ve number...
C
C in 2s complement, we must complement and add 1
C first we complement (nice 1/2 words)
C
	DFABUF(DFAPTR)   = NOT( DFABUF(DFAPTR) )
	DFABUF(DFAPTR+1) = NOT( DFABUF(DFAPTR+1) )
C
C and reduce to 18 bits again
C
	DFABUF(DFAPTR)   = DFABUF(DFAPTR).AND.'3FFFF'X
	DFABUF(DFAPTR+1) = DFABUF(DFAPTR+1).AND.'3FFFF'X
C
C and now do the adding one...
C
	DFABUF(DFAPTR)   = DFABUF(DFAPTR) + 1
	IF ( DFABUF(DFAPTR) .GT. '3FFFF'X ) THEN
	   DFABUF(DFAPTR)   = 0
	   DFABUF(DFAPTR+1) = DFABUF(DFAPTR+1) + 1
	   IF (DFABUF(DFAPTR+1).GT.'3FFFF'X) THEN
	      CALL WRITEF('*** Overflow in -ve number - DFABUF %N,%N',
     &						    DFAPTR, DFAPTR+1 )
	   ENDIF
	ENDIF
C
	NEGTIV = .TRUE.				! and it was -ve
C
	RETURN
	END
