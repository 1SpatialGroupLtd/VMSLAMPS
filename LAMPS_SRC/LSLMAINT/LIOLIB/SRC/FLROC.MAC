;
; * This file is part of the LAMPS distribution, released as a software
; * preservation project to archive digital history for future historians.
; * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
; *
; * Timestamp of this file for the 2002 release was: 1980-06-25 11:53:42.000000000 +0100
; *
; * This program is free software: you can redistribute it and/or modify
; * it under the terms of the GNU General Public License as published by
; * the Free Software Foundation, version 3.
; *
; * This program is distributed in the hope that it will be useful, but
; * WITHOUT ANY WARRANTY; without even the implied warranty of
; * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
; * General Public License for more details.
; *
; * You should have received a copy of the GNU General Public License
; * along with this program. If not, see http://www.gnu.org/licenses/.
;
	.TITLE	FLROC
	.IDENT	/28SP79/
	.ENABL	LC
;
; Copyright Laser-Scan Laboratories Ltd,Cambridge,England
;
; Authors M.C.Fulford & P.G.Hardy
;
; read records from disc file using Fortran FDBs & FCS
; file open and close section


	.MCALL	OFNB$R,CLOSE$

; ERROR=FLROPN(LUN, [IERR]) opens file on LUN using FORTRAN FDB

FLROPN::MOV	#FLRLUN, R1
	MOV	#-1,     R2
	CALL	LI$ARG
	MOV	@R1, R1
	CALL	FL$RSL		; find appropriate FDB and select
	MOV	R2, -(SP)
	OFNB$R	R0,FLRLUN
	MOV	(SP)+, R2
	MOVB	F.ERR(R0),R0	; sign extend error code
	CMP	R2, #-1
	BEQ	20$
	MOV	R0, @R2
20$:	MOV	R0,FLRERR	; save it
	SXT	R0
	RETURN

; FLRSEL(LUN) finds FORTRAN FDB for LUN and selects it for reading

FLRSEL::MOV	#FLRLUN, R1
	CALL	LI$ARG
	MOV	@R1, R1
FL$RSL:	MOV	FLRFDB,R0	; current fdb
	MOV	R2, -(SP)
	MOV	R1, R2
	CMP	R2,FLRLUN	; same as before ?
	BEQ	1$		; yes
	MOV	R2,FLRLUN	; save selection
	CALL	$FCHNL		; find FDB
	ADD	#14,R0		; to get FDB address
	MOV	R0,FLRFDB	; save it
1$:	MOV	(SP)+, R2
	RETURN		; return it in R0

; FLRCLO closes the selected file

FLRCLO::TST	FLRLUN
	BEQ	3$		; no selection
	CLOSE$	FLRFDB		; and close the file
	CLR	FLRLUN		; null selection
3$:	RETURN


	.PSECT	FLRCOM,D,GBL,OVR

FLRERR:	.WORD	0	; error marker
FLRFDB:	.BLKW	1	; currently selected FDB
FLRLUN:	.WORD	0	; currently selected LUN

	.END
