;
; * This file is part of the LAMPS distribution, released as a software
; * preservation project to archive digital history for future historians.
; * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
; *
; * Timestamp of this file for the 2002 release was: 1980-06-25 12:53:42.000000000 +0100
; *
; * This program is free software: you can redistribute it and/or modify
; * it under the terms of the GNU General Public License as published by
; * the Free Software Foundation, version 3.
; *
; * This program is distributed in the hope that it will be useful, but
; * WITHOUT ANY WARRANTY; without even the implied warranty of
; * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
; * General Public License for more details.
; *
; * You should have received a copy of the GNU General Public License
; * along with this program. If not, see http://www.gnu.org/licenses/.
;
	.TITLE	FLWLIN
	.IDENT	/28SP79/
	.ENABL	LC
;
; Copyright Laser-Scan Laboratories Ltd,Cambridge,England
;
; Authors M.C.Fulford & P.G.Hardy
;
;
; write record to disc file using Fortran FDBs & FCS

	.MCALL	PUT$

; ERROR=FLWLIN([LINE], [NCHS], [IERR]) writes to current LUN

FLWLIN::MOV  #EXPBUF, R1
	MOV  #-1,     R2
	MOV  #-1,     R3
	CALL LI$ARG
	CMP  R2, #-1			; length given ?
	BNE  10$			; j if so
	ADD  R1, R2			; compute length
5$:	INC  R2
	TSTB 1(R2)
	BNE  5$
	SUB  R1,R2
	INC  R2
	BR	15$

10$:	MOV	@R2, R2
15$:	MOV	R3, -(SP)
	PUT$	FLWFDB,R1,R2	; write record
	MOV	(SP)+, R3
	MOVB	F.ERR(R0),R0	; sign extend error code
	CMP	R3, #-1		; error code ?
	BEQ	20$
	MOV	R0, @R3
20$:	MOV	R0,FLWERR	; save error code
	SXT	R0
	RETURN

	.PSECT	FLWCOM,D,GBL,OVR

FLWERR:	.WORD	0	; error marker
FLWFDB:	.BLKW	1	; currently selected FDB
FLWLUN:	.WORD	0	; currently selected LUN

	.PSECT	EXPC	GBL,OVR,D
EXPLEN:	.BLKW	1
EXPVFC:	.BLKW	1
EXPBUF:	.BLKB	80.

	.END
