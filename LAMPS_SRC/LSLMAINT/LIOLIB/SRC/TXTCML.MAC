	.TITLE TXTCML
	.IDENT /02JN82/
	.ENABL LC
;
;Mod	Set DCPTR to 1				WJJ	 2-Jun-1982
;
; Written by Paul Hardy
;
; LIOLIB interface to GETCML mechanism.
; Version leaving results in COMMON/TXTC/ for LIOLIB
;
; length/err-code=TXTCML ([PROMPT], [LUN] )
;
;	PROMPT	prompt string in .ASCIZ format
;		If PROMPT is ommitted, then a prompt is constructed
;		from the task's name.
;	LUN	unit number to use, defaults to 5
;


; data
	.PSECT	TXTC	GBL,OVR,D
DCPTR:	.BLKW	1
TXTPTR:	.BLKW	1
TXTLIM:	.BLKW	1
TXTBUF:	.BLKB	80.

	.PSECT
	.EVEN
IDS:	.WORD	-1		; argument address
FIVE:	.WORD	5		; default LUN

; arg list for GETCML
GCMARG:	.WORD	4		; arg count
BUFARG:	.WORD	TXTBUF		; address of buffer for result
IDSARG:	.WORD	IDS		; address of place for count/error
PRMARG:	.WORD	0		; address of prompt string
LUNARG:	.WORD	0		; address of lun

; the prog itself
TXTCML::
	CLR	R1		; default is no prompt
	MOV	#FIVE,R2	; and LUN 5
	CALL	LI$ARG
	MOV	R1,PRMARG	; address of given prompt string
	MOV	R2, LUNARG	; address of LUN arg
	MOV	#GCMARG,R5	; set up argument list for GETCML
	JSR	PC,GETCML	; and call it
	MOV	IDS,R0		; function value is no of chars read
	MOV	R0,TXTPTR	; ditto (or error)
	MOV	#1,DCPTR	; set up for reading line
	RTS	PC

	.END
