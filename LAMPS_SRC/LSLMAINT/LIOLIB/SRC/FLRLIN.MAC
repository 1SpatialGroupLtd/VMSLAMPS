;
; * This file is part of the LAMPS distribution, released as a software
; * preservation project to archive digital history for future historians.
; * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
; *
; * Timestamp of this file for the 2002 release was: 1980-06-25 11:53:02.000000000 +0100
; *
; * This program is free software: you can redistribute it and/or modify
; * it under the terms of the GNU General Public License as published by
; * the Free Software Foundation, version 3.
; *
; * This program is distributed in the hope that it will be useful, but
; * WITHOUT ANY WARRANTY; without even the implied warranty of
; * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
; * General Public License for more details.
; *
; * You should have received a copy of the GNU General Public License
; * along with this program. If not, see http://www.gnu.org/licenses/.
;
	.TITLE	FLRLIN
	.IDENT	/12MY80/
	.ENABL	LC
;
; Copyright Laser-Scan Laboratories Ltd,Cambridge,England
;
; Authors M.C.Fulford & P.G.Hardy
;
;
; read record from disc file using Fortran FDBs & FCS

	.MCALL	GET$

; ERROR=FLRLIN([LINE], [NCHS], [LEN], [IERR]) reads from current LUN

FLRLIN::MOV	#TXTBUF, R1	; default arguments
	MOV	#TXTPTR, R2
	MOV	#TXTLIM, R3
	MOV	#-1,     R4
	CALL	LI$ARG		; fetch real arguments
	MOV	@R3, R3		; buffer length
	DEC	R3
	MOV	R4, -(SP)
	MOV	R2, -(SP)
	MOV	R1, -(SP)
	GET$	FLRFDB,R1,R3	; write record
	MOV	(SP)+, R1
	MOV	(SP)+, R2
	MOV	(SP)+, R4
	MOV	F.NRBD(R0), @R2	; return length
	ADD	F.NRBD(R0), R1	; last byte plus one
	CLRB	@R1		; zero it
	MOVB	F.ERR(R0),R0	; sign extend error code
	CMP	R4, #-1		; error code wanted ?
	BEQ	20$
	MOV	R0, @R4		; provide it
20$:	MOV	R0,FLRERR	; save error code
	SXT	R0		; result is "error occurred"
	RETURN


	.PSECT	FLRCOM,D,GBL,OVR

FLRERR:	.WORD	0	; error marker
FLRFDB:	.BLKW	1	; currently selected FDB
FLRLUN:	.WORD	0	; currently selected LUN

	.PSECT	TXTC	GBL,OVR,D
DCPTR:	.BLKW	1
TXTPTR:	.BLKW	1
TXTLIM:	.BLKW	1
TXTBUF:	.BLKB	80.

	.END
