C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1989-02-08 12:56:36.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE MCONAL3D(XY,ZZ)
C
**	MODULE	MCONAL3D
**	IDENT	08FE89
C
C Copyright Laser Scan Laboratories Ltd, Cambridge, England.
C Developed by R.W. Russell from MCONAL			8-Feb-1989
C
	IMPLICIT NONE
C
C McConalogues interpolation routine. 
C
C
C arguments
	REAL	XY(2,6)		! XY hold the master points
	INTEGER	ZZ(6)		! and Z values (as integers, so that
				! they can be assigned and compared with
				! IFF_ABSENT)
C
	INCLUDE 'LSL$CMNIFF:CB.STR'
***	COMMON/CMN:INTCOM/
C
C functions
	REAL	INCSIZ		! calculates increment size
C
C w/s
	INTEGER I
	REAL DFX,DFY
	REAL R,SINTH(2),COSTH(2),SUMSIN,SUMCOS
	REAL INC,SUMINC,G,F,E,T,B1,C1,TEMP1,TEMP2
	REAL UV(2)
	REAL H,DH
	INTEGER HI,DHI
	EQUIVALENCE (H,HI),(DH,DHI)
C
C Work out gradients at points 3 and 4 for the start, middle 
C or end of line.
	IF (ENDCON.EQ.0) THEN
C Middle
	   CALL GRAD(XY(1,2),2,SINTH(1),COSTH(1),R)
	   CALL GRAD(XY(1,3),2,SINTH(2),COSTH(2),TEMP1)
	ELSEIF (ENDCON.GT.0) THEN
C Start
	   CALL GRAD(XY(1,3),1,SINTH(1),COSTH(1),R)
	   CALL GRAD(XY(1,3),2,SINTH(2),COSTH(2),TEMP1)
	ELSE
C End
	   CALL GRAD(XY(1,2),2,SINTH(1),COSTH(1),R)
	   CALL GRAD(XY(1,2),3,SINTH(2),COSTH(2),TEMP1)
	ENDIF
C
C Now work out constants involved in the curve generation
20	SUMINC=0.0
	DFX=XY(1,4)-XY(1,3)
	DFY=XY(2,4)-XY(2,3)
	G=(DFX*DFX)+(DFY*DFY)
	SUMCOS=COSTH(1)+COSTH(2)
	SUMSIN=SINTH(1)+SINTH(2)
	F=(DFX*SUMCOS)+(DFY*SUMSIN)
	E=7-(COSTH(1)*COSTH(2))-(SINTH(1)*SINTH(2))
C Now work out the arc length
	T=(3*(SQRT((F*F)+(2*E*G))-F))/E
	TEMP1=6/(T**3)
	TEMP2=3/(T*T)
	B1=(TEMP1*DFX)-(TEMP2*SUMCOS)
	C1=(TEMP1*DFY)-(TEMP2*SUMSIN)
C Now get the increment size
C This is approximately distance between interpolated points
	INC = INCSIZ(TOL,R)
C
C calculate Z increments
	HI = ZZ(3)		! integer assignments
	DHI = ZZ(4)
C
C if either height is not defined, then use other height with an
C increment of 0.0
	IF (HI.NE.IFF_ABSENT.AND.DHI.NE.IFF_ABSENT) THEN
	   DH = (DH - H)*INC/T
	ELSEIF (HI.EQ.IFF_ABSENT) THEN
	   H = DH
	   DH = 0.0
	ELSEIF (DHI.EQ.IFF_ABSENT) THEN
	   DH = 0.0
	ENDIF
C
C Now generate the new points between points 3 and 4
30	SUMINC=SUMINC+INC
	IF (SUMINC.GE.T) GOTO 80
	TEMP1=(SUMINC*SUMINC)/(2*T)
	TEMP2=((T*SUMINC*SUMINC)/2)-((SUMINC**3)/3)
C Now get the generated point (U,V)
	UV(1)=XY(1,3)+(SUMINC*COSTH(1))+((COSTH(2)-COSTH(1))*TEMP1)
     &    +(B1*TEMP2)
	UV(2)=XY(2,3)+(SUMINC*SINTH(1))+((SINTH(2)-SINTH(1))*TEMP1)
     &    +(C1*TEMP2)
	IF (HI.NE.IFF_ABSENT) H = H + DH
	CALL ADDPTS3D(1,UV,H,ONSCR)
	GOTO 30
C
C finish off by falling through to draw last point
80	CALL ADDPTS3D(1,XY(1,4),ZZ(4),ONSCR)
	RETURN
	END

