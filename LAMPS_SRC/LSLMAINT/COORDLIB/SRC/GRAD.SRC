	OPTIONS/CHECK=NOUNDERFLOW
	SUBROUTINE GRAD(XY,POINT,SINANG,COSANG,R)
C
**	MODULE	GRAD
**	IDENT	30OC86
C
C Copyright Laser Scan Laboratories Limited, Cambridge, England
C Author R.Daly
C Coded and typed 25.iii.81
C Transferred to separate file for LITES2 	RJH	11-Mar-1985
C Copied with no changes for general use	RJH	22-Oct-1986
C OPTIONS statement added to prevent underflow	RJH	30-Oct-1986
C    of N/R on last line.
C
C Routine to work out the gradient at point POINT (1, 2, or 3).
C Result is given as direction cosines.
C Given points 1,2 and 3, then we usually want to
C work out the gradient at point 2. At the start or end 
C point of a line we require to find the gradient at
C points 1 and 3 respectively. Integer POINT indicates
C which is required.
C Formula derived from Lagrangian interpolating polynomials.
C
	IMPLICIT NONE
C
C arguments
	REAL XY(2,3),SINANG,COSANG,R
	INTEGER POINT
C
C w/s
	REAL DFX1,DFY1,DFX2,DFY2,ALPHA,BETA,GAMMA,DELTA,A,B,D,N
C
C First work out the differences between the points.
	DFX1=XY(1,2)-XY(1,1)
	DFY1=XY(2,2)-XY(2,1)
	DFX2=XY(1,3)-XY(1,2)
	DFY2=XY(2,3)-XY(2,2)
	D=2*((DFX1*DFY2)-(DFX2*DFY1))
	ALPHA=(DFX2*DFX2)+(DFY2*DFY2)
	BETA =(DFX1*DFX1)+(DFY1*DFY1)
	GAMMA=0.0
	DELTA=0.0
	GOTO (10,30,20),POINT
10	GAMMA=D
	GOTO 30
20	DELTA=D
30	A=(ALPHA*DFY1)+(BETA*DFY2)-(GAMMA*DFX1)+(DELTA*DFX2)
	B=(ALPHA*DFX1)+(BETA*DFX2)+(GAMMA*DFY1)-(DELTA*DFY2)
	N=SQRT((A*A)+(B*B))
C Now get the direction cosines.
	SINANG=A/N
	COSANG=B/N
C
C Work out radius of curvature.
C In order to avoid dividing by 0 (in the case of straight
C lines) or getting an overflow (nearly straight lines)
C R defaults to a large value
	R=1E38
	IF (ABS(D).GT.(N/R)) R=ABS(N/D)
C
	RETURN
	END
