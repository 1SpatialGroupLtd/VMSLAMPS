;
; * This file is part of the LAMPS distribution, released as a software
; * preservation project to archive digital history for future historians.
; * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
; *
; * Timestamp of this file for the 2002 release was: 1982-04-05 12:31:48.000000000 +0100
; *
; * This program is free software: you can redistribute it and/or modify
; * it under the terms of the GNU General Public License as published by
; * the Free Software Foundation, version 3.
; *
; * This program is distributed in the hope that it will be useful, but
; * WITHOUT ANY WARRANTY; without even the implied warranty of
; * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
; * General Public License for more details.
; *
; * You should have received a copy of the GNU General Public License
; * along with this program. If not, see http://www.gnu.org/licenses/.
;
	.TITLE	CCL02
	.IDENT	/05AP82/
	.ENABL	LC
 
 
; CLASSIFY CHARACTER R0 WRT TABLE R5
; uses R1, char in R1 on exit
 
;	char in R0
;	JSR R5,CCLAS
;	.BYTE addr1-./2,CHAR1
;	...
;	.BYTE ADDRD-./2,200	;default
;	.BYTE ADDRN-./2,ADDRL-./2	; DIGIT (0-9 in R0)/LETTER (1-26)
 
.GLOBL	GCLAS,GETC
; "get and classify"
GCLAS:	JSR	R5,GETC
.GLOBL	CCLAS
 
CCLAS:	CMPB	R0, #'a		; check
	BLT	10$		;  for
	CMPB	R0, #'z		;   lower-case
	BGT	10$		;    letter
	SUB	#'a-'A, R0	; upper-case
10$:	MOV	R0,-(SP)
CCL.L:	MOVB	(R5)+,R1	; PICK UP OFFSET
	CMPB	R0,(R5)+
	BVS	CCL.E		; J IF END OF INDIVIDUALS
	BNE	CCL.L
CCL.Y:	DEC	R5		; IND CHAR MATCH
CCL.X:	ASR	R5
	ADD	R1,R5
	ASL	R5
	MOV	(SP)+,R1	; RECOVER CHAR
	RTS	R5
 
CCL.E:	SUB	#'0,R0
	BMI	CCL.Y		; NON-DIGIT (LOW)
	CMP	R0,#'9-'0
	BLE	CCL.D		; J IF DIGIT
	SUB	#'A-'1,R0
	BLE	CCL.Y		; J NOT A LETTER
	CMP	R0,#'Z-'A+1
	BGT	CCL.Y
	INC	R5		; LETTER
CCL.D:				; DIGIT OR LETTER
	MOVB	(R5)+,R1	; PICK UP OFFSET
	BMI	CCL.Y		; CORRECT FOR NEG OFFSET
	BR	CCL.X
 
	.END
