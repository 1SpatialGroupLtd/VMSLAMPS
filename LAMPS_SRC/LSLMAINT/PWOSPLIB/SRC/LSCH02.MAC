	.IF EQ LCD14
	.TITLE	LSCH
	.IFF
	.TITLE	LSCH14
	.ENDC
	.IDENT	/15MY85/
	.ENABL LC
 
; Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
;
; Author: Clarke Brunt from originals by Susan Manning
 
 
; LCD character control routines
 
; CHSIZE(CH,CX,CY) set character height CH, and spacing CX,CY (in user units)
 
CHSIZE:: JSR	PC, FTNFPU
	LDF	@(R5)+, F0	; get character height
	LDF	@(R5)+, F1	; get character spacing vector
	LDF	@(R5)+, F3

$CHSIZ:: MULF	TKTR, F0	; transform CH to tek counts
	STCFI	F0, R0		; convert to integer counts
	BPL	5$
	NEG	R0		; height must be positive
5$:	CMP	R0, #CHEXT
	BLE	10$
	MOV	#CHEXT,R0	; force within limit if necessary
10$:	MOV	R0,CHCH		; store
	JSR	PC, TKTRMT	; transform CX, CY into F0, F2
	CLR	R0
	STCFI	F0, R1		; to integer counts
	BPL	15$
	NEG	R1		; make +ve so only one comparison
	INC	R0		; remember
15$:	CMP	R1,#CHEXT
	BLE	20$
	MOV	#CHEXT,R1	; force in range
20$:	TST	R0
	BEQ	25$
	NEG	R1		; restore sign
25$:	MOV	R1,CHCX		; store
	CLR	R0
	STCFI	F2,R1		; same for CY
	BEQ	30$
	NEG	R1
	INC	R0
30$:	CMP	R1,#CHEXT
	BLE	35$
	MOV	#CHEXT,R1
35$:	TST	R0
	BEQ	40$
	NEG	R1
40$:	MOV	R1,CHCY
;
	JSR	R5, $CHKBF	; check sequence contiguous
	.WORD	9.
	JSR	R5, $TKW2C	; output <esc=N> sequence
	.BYTE	AS.ESC, '=
	MOV	#'N, R0
	JSR	PC, $TKWCH
;
; transmit each of 3 words (CH,CX,CY) as 2*6-bit biased numbers
	MOV	#CHCH,R5	; start of words
	MOV	#3,R4		; no of words
50$:	MOV	(R5)+, R3	; get next word
.IF NE LCD14
	ASH	#-2, R3		; convert 14 to 12 bit
.ENDC
	ASHC	#12, R2		; bits 6 to 15 in R2
	BIC	#177700, R2	; mask to bottom 6
	MOV	R2, R1		; ready for TKWBN
	JSR	PC, $TKWBN
	ASHC	#6, R2		; bits 0 to 5 in R2
	BIC	#177700, R2
	MOV	R2, R1
	JSR	PC, $TKWBN
	SOB	R4, 50$

	MOV	#-1,TKCCHS	; mark not concerned about char size
				; so refresh wont reset
	RTS	PC
 
 
; CHFONT(NFONT,NITAL)	NFONT=0,1	NITAL=0,1 (normal,italicised)
;
CHFONT:: MOV	(R5)+,R0	; get argument count
	MOV	@(R5)+,R1	; get font number
	CLR	R2
	CMP	R0, #1		; only one argument, default to normal font
	BLE	$CHFNT
	MOV	@(R5)+,R2	; get italic switch
 
$CHFNT:: BIC	#177770,R1	; only fonts 0 to 7
	BIC	#177776,R2	; only italic or normal
	ASH	#3, R2		; move up 3 bits
	BIS	R2, R1		; and insert in font word
	JSR	R5, $CHKBF
	.WORD	4
	JSR	R5, $TKW2C	; send <escape = M <font>> sequence
	.BYTE	AS.ESC, '=
	MOV	#'M, R0
	JSR	PC, $TKWCH
	JSR	PC, $TKWBN
	RTS	PC


; TKCOL(N)	N=0-7	Set colour N
;
TKCOL::	MOV	@2(R5),R1	; get colour
	BIC	#177770,R1	; only  colors 0 to 7
	JSR	R5, $CHKBF
	.WORD	4
	JSR	R5, $TKW2C	; send <escape = K <colour>> sequence
	.BYTE	AS.ESC, '=
	MOV	#'K, R0
	JSR	PC, $TKWCH
	JSR	PC, $TKWBN
	RTS	PC
  
	.END
