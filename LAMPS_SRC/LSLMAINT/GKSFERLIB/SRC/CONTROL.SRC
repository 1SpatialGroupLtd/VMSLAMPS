C 	CONTROL FUNCTIONS
C	=================
C
	SUBROUTINE	GOPKS(ERRFIL)
C
**	MODULE	GOPKS
**	IDENT	19NO87
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C Created					CCB	9-May-1986
C
	IMPLICIT NONE
C
	EXTERNAL GKSBLK		! ensure that block data is linked in
C
C OPEN GKS	- includes initialising of data structures and variables
C
C Arguments:
	INTEGER	ERRFIL			! error message file
C
***	COMMON/GKSDAT1/			! GKS data structures
C
C Set up initial indices and  default modes
	TRNCUR	= 1		! use normalisation transformation 0
	PLIND	= 1		! use polyline bundle table 1
	PMIND	= 1		! use polymarker bundle table 1
	FAIND	= 1		! use fill area bundle table 1
	ENCLIP	= .TRUE.	! enable clipping
	RETURN
	END
C
C***********************************************************************
C
	SUBROUTINE	GCLKS
C
**	MODULE	GCLKS
**	IDENT	27JL83
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C Created					CCB	9-May-1986
C
C CLOSE GKS	
C
	IMPLICIT NONE
C
	RETURN
	END
C
C***********************************************************************
C
	SUBROUTINE	GOPWK(WKID,CONID,WTYPE)
C
**	MODULE	GOPWK
**	IDENT	27NO84
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C Created					CCB	9-May-1986
C
C OPEN WORKSTATION
C
	IMPLICIT NONE
C
***	PARAMETER/FERRANTI/
***	COMMON/GKSDAT1/
C
C Arguments:
	INTEGER	WKID			! workstation-identifier
	INTEGER	CONID			! connection-identifier
	INTEGER	WTYPE			! workstation type
C
	WKIDS = WKID	! save workstation identifier
	DEFER = GASTI	! deferral mode is At Some TIme
	REGEN = GSUPPD	! implicit regeneration suppressed
	UPTRN = .TRUE.	! update transformation
	UPLIN = .TRUE.	! update polyline attributes
	UPMAR = .TRUE.	! update polymarker attributes
	UPFA  = .TRUE.	! update fill area attributes
C
	CALL FER_OPEN	! open channel to plotter
C
C Set up size of plotting area (metres)
	DCMAXX = FER_MAXX
	DCMAXY = FER_MAXY
C
C Set up parameters controlling fill area
	GNOMLW  = FER_SPOT
	GNOMSEP = FER_SEP
C
C Set workstation viewport to entire area
	CALL GSWKVP(WKID,0.0,DCMAXX,0.0,DCMAXY)
C
C Select well defined symbol, magnification, and angle
	CMTYPE = LSYM-1		! start at 0
	CMAG = .FALSE.
	CALL FER_SYMBOL(CMTYPE)	! symbols start at 0
	CANG = 0.0
	CALL FER_SANG(CANG)
	CTANG = .FALSE.
C
C Current position (plotter counts) at origin, in centre
	FER_CURX = 0
	FER_CURY = 0
C
	RETURN
	END
C
C***********************************************************************
C
	SUBROUTINE	GCLWK(WKID)
C
**	MODULE	GCLWK
**	IDENT	27JL83
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C Created					CCB	9-May-1986
C
C CLOSE WORKSTATION
C
	IMPLICIT NONE
C
C Arguments:
	INTEGER	WKID			! workstation-identifier
C
***	COMMON/GKSDAT1/
C
	CALL FER_FLUSHP			! flush out any polylines
	CALL FER_CHECK(2)
	CALL FER_BUF(ICHAR('\'))	! escape char
	CALL FER_BUF(3)			! ETX
	CALL GUWK(WKID,0)		! update workstation
	CALL FER_CLOSE			! and close channel
C
	RETURN
	END
C
C***********************************************************************
C
	SUBROUTINE	GACWK(WKID)
C
**	MODULE	GACWK
**	IDENT	27JL83
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C Created					CCB	9-May-1986
C
C ACTIVATE WORKSTATION
C
	IMPLICIT NONE
C
C Arguments:
	INTEGER	WKID			! workstation-identifier
C
***	COMMON/GKSDAT1/
C
	WSACT = .TRUE.
	RETURN
	END
C
C***********************************************************************
C
	SUBROUTINE	GDAWK(WKID)
C
**	MODULE	GDAWK
**	IDENT	27JL83
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C Created					CCB	9-May-1986
C
C DEACTIVATE WORKSTATION
C
	IMPLICIT NONE
C
C Arguments:
	INTEGER	WKID			! workstation-identifier
C
***	COMMON/GKSDAT1/
C
	WSACT = .FALSE.
	RETURN
	END
C
C***********************************************************************
C
	SUBROUTINE	GCLRWK(WKID,COFL)
C
**	MODULE	GCLRWK
**	IDENT	29FE84
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C Created					CCB	9-May-1986
C
C CLEAR WORKSTATION
C
	IMPLICIT NONE
C
C Arguments:
	INTEGER	WKID			! workstation-identifier
	INTEGER COFL			! control flag
C
C No sensible action for clear
C
	RETURN
	END
C
C***********************************************************************
C
	SUBROUTINE	GUWK(WKID,REGFL)
C
**	MODULE	GUWK
**	IDENT	16MY84
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C Created					CCB	16-May-1984
C
C UPDATE WORKSTATION
C
	IMPLICIT NONE
C
C Arguments:
	INTEGER	WKID			! workstation-identifier
	INTEGER	REGFL			! regeneration flag
C
	CALL FER_FLUSHP			! flush out polylines
	CALL FER_FLUSH			! update
C
	RETURN
	END
C
C***********************************************************************
C
	SUBROUTINE	GSDS(WKID,DEFMOD,REGMOD)
C
**	MODULE	GSDS
**	IDENT	27JL83
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C Created					CCB	9-May-1986
C
C SET DEFERRAL STATE
C
	IMPLICIT NONE
C
C Arguments:
	INTEGER	WKID			! workstation-identifier
	INTEGER	DEFMOD			! deferral mode
	INTEGER	REGMOD			! implicit regeneration mode
C
***	COMMON/GKSDAT1/
C
	IF (DEFMOD .LT. GASAP .OR. DEFMOD .GT. GASTI) GOTO 10
	DEFER = DEFMOD
C
C nasty call to GUWK (second argument ignored)
	IF (DEFER.EQ.GASAP.AND.WSACT) CALL GUWK(WKID,0)
10	IF (REGMOD .LT. GSUPPD .OR. REGMOD .GT. GALLOW) GOTO 9999
	REGEN= REGMOD
C
9999	RETURN
	END

C
C***********************************************************************
C
	SUBROUTINE	GSASF(LASF)
C
**	MODULE	GSASF
**	IDENT	27NO84
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C Created				Clarke Brunt	26-April-1984
C
C SET ASPECT SOURCE FLAGS
C
	IMPLICIT NONE
C
***	COMMON/GKSDAT1/
C
C Arguments:
	INTEGER	LASF(ASFMAX)			! list of flags
C
	INTEGER	I				! loop counter
C
	DO 11 I=1,ASFMAX
	   GASF(I) = LASF(I)
11	CONTINUE
	UPLIN = .TRUE.		! polyline attributes need updating
	UPMAR  = .TRUE.		! and polymarker attributes
	UPFA  = .TRUE.		! and fill area attributes
C
	RETURN
	END
C
C***********************************************************************
C
	SUBROUTINE	GSCLIP(CLSW)
C
**	MODULE	GSCLIP
**	IDENT	08MY86
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C Created					CCB	31-July-1984
C
C SET CLIPPING INDICATOR
C
	IMPLICIT NONE
C
C Arguments:
	INTEGER	CLSW			! clipping indicator
C
***	COMMON/GKSDAT1/
C
	ENCLIP = (CLSW.EQ.GCLIP)
	RETURN
C
	END
C
C***********************************************************************
C
C	INQUIRY FUNCTIONS
C	=================
C
	SUBROUTINE GQMDS(WTYPE,ERRIND,COOUNI,RX,RY,LX,LY)
C
***	MODULE	GQMDS
***	IDENT	14SE83
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C Created					SCM	14-Sept-1983
C
C INQUIRE MAXIMUM DISPLAY SURFACE SIZE
C
	IMPLICIT NONE
C
C Arguments:
	INTEGER	WTYPE		! Workstation type
	INTEGER	ERRIND		! Error indicator
	INTEGER	COOUNI		! Device Coordinate Units
	REAL	RX,RY		! Max. display surface size in DC
	INTEGER	LX,LY		! no. of device units in X and Y
C
***	COMMON/GKSDAT1/		! GKS data structures
C
	ERRIND = 0		! no error indication
C
	COOUNI = GMETRE		! units are in metres
	RX = DCMAXX		! film size in metres
	RY = DCMAXY
	LX = 60800		! and in raster units (irrelevant)
	LY = 48800
C
	RETURN
	END
C
C***********************************************************************
C
C	ERROR ROUTINE
C	=============
C
	SUBROUTINE GKSERR(NUMBER,ROUTINE)
C
***	MODULE	GKSERR
***	IDENT	14SE83
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C Created					SCM	14-Sept-1983
C
C ERROR routine called from within this library
C May be replaced if required
C
	IMPLICIT NONE
C
C Arguments:
	INTEGER		NUMBER		! Error number
	CHARACTER*(*)	ROUTINE		! Routine name
C
	CALL WRITEF('GKS error %N in routine %S',NUMBER,ROUTINE)
C
	RETURN
	END
