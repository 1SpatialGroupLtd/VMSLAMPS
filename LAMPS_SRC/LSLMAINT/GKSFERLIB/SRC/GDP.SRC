C
C***********************************************************************
C
C	GDP PRIMITIVE
C	=============
C
	SUBROUTINE	GGDP(N,PX,PY,ID)
C
**	MODULE	GGDP
**	IDENT	13JL87
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C Created					CCB	9-May-1986
C
C GENERALISED DRAWING PRIMITIVE
C
	IMPLICIT NONE
C
C Arguments:
	INTEGER N			! number of points
	REAL	PX(N)			! X coords of points in WC
	REAL	PY(N)			! Y coords of points in WC
	INTEGER	ID			! GDP identifier
C
C Workspace
	INTEGER	I
	REAL	QX(3),QY(3)
C
***	COMMON/GKSDAT1/
C
	IF (ID.LE.0.OR.ID.GT.3) THEN
	   CALL GKSERR(102,'GGDP')
	   GOTO 9999
	ENDIF
	IF ((ID.EQ.1.AND.N.NE.2).OR.(ID.NE.1.AND.N.NE.3)) THEN
	   CALL GKSERR(100,'GGDP')
	   GOTO 9999
	ENDIF
C
C Is this workstation active?
	IF (.NOT.WSACT) GOTO 100
C
C Make sure transformation is up to date
	IF (UPTRN) CALL GUPTRN
C
C Is everything clipped ?
	IF (CLPALL) GOTO 100
C
C Make sure polyline attributes are up to date
	IF (UPLIN) CALL GUPLIN
C
C Loop for each point
	DO 90 I = 1, N
	   QX(I) = PX(I)
	   QY(I) = PY(I)
	   IF (ENCLIP) THEN
	      IF (QX(I).LT.CLIPLO(1).OR.QX(I).GT.CLIPHI(1) .OR.
     &            QY(I).LT.CLIPLO(2).OR.QY(I).GT.CLIPHI(2) ) GOTO 100
	   ENDIF
C transform World Coordinates to Workstation Device Coordinates
	   QX(I) = (QX(I)*SCALEX)+OFFX
	   QY(I) = (QY(I)*SCALEY)+OFFY
90	CONTINUE
C
	CALL FER_FLUSHP		! flush out any polylines
C
C Draw the circle or arc
C
	CALL FER_CIRCLE(QX,QY,ID)

	IF (DEFER .EQ. GASAP) CALL GUWK(WKIDS,0)
100	CONTINUE
C
9999	RETURN
	END
