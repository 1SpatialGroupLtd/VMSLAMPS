C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1995-10-30 15:36:20.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
C                   PJ28F
C **********************************************************************
C                       *  WINKEL III *
C **********************************************************************
C
CMOD    Converted to Fortran77 (WI/CC 1514)      T.J.Mulcahy 25-Oct-1995
C
	SUBROUTINE PJ28F
C
C ***	IDENT	25OC95
C ***	MODULE  PJ28F
C
C Copyright Laser-Scan Ltd., Cambridge, England
C
C Author                               R.W. Russell      24-Feb-93
CMOD Altered to call subroutine rather than two functions
CMOD as is now required by NEWT2D      R.W. Russell      19-Feb-94
C
C This pseudocylindrical projection is the arithmetic mean between Plate
C Carre and the Hammer Projection. It is only available in spherical
C form.
C
C arguments
C
C projection parameters
      DOUBLE PRECISION DATA(15)
C                      DATA(5) = Longitude of central meridian
C                      DATA(7) = False Eastings
C                      DATA(8) = False Northings
C geographical coordinates (radians)
      DOUBLE PRECISION GEOG(2)
C projected coordinates (usually meters)
      DOUBLE PRECISION PROJ(2)
C zone number (variable if using file)
      INTEGER    ZONE
C LUN for input file (if used)
      INTEGER   INFILE
C
C common blocks
C
      INTEGER IERROR,IPEMSG,IPPARM
      COMMON /ERRM/ IERROR
      COMMON /PRIN/ IPEMSG,IPPARM
C
C default sphere
C
      DOUBLE PRECISION AZZ
      COMMON/SPHR/ AZZ
C
C working area
C
      INTEGER ANGS(4,1)
      INTEGER SWITCH
      DOUBLE PRECISION A,LON0,X0,Y0,SQRT2,TSQRT2
      DOUBLE PRECISION BUFF(15)
      COMMON /WRK28F/BUFF,ANGS,A,LON0,X0,Y0,SQRT2,TSQRT2
      COMMON /SW28F/ SWITCH
C
      DOUBLE PRECISION BLOW1,BLOW2,BHIGH1,BHIGH2,TOL
      SAVE             BLOW1,BLOW2,BHIGH1,BHIGH2,TOL
C
C constants
C
      INTEGER          NIT
      DOUBLE PRECISION PI,HALFPI,EPSLN,ZERO,ONE,TWO,HALF
      PARAMETER        (NIT = 100)
      PARAMETER        (PI =3.14159265358979323846D0)
      PARAMETER        (HALFPI =1.5707963267948966D0)
      PARAMETER        (EPSLN = 1.0D-12)
      PARAMETER        (ZERO = 0.0, ONE = 1.0, TWO = 2.0, HALF = 0.5)
C
C functions called
C
      DOUBLE PRECISION PAKR
      DOUBLE PRECISION ADJL
      DOUBLE PRECISION DARSIN
C
C working storage
C
      DOUBLE PRECISION B,E,ES
      INTEGER I
      DOUBLE PRECISION DLON,PHI,X,Y
      DOUBLE PRECISION STEP1,STEP2
      LOGICAL OK
C
C initialisation
C
C      DATA SWITCH /0/
C
C ......................................................................
C      .  INITIALIZATION OF PROJECTION PARAMETERS (ENTRY INPUT)  .
C ......................................................................
C
      ENTRY IS28F (ZONE,DATA)
C
      IERROR = 0
      IF (SWITCH.NE.0 .AND. SWITCH.EQ.ZONE) RETURN
      DO 180 I = 1,8
      BUFF(I) = DATA(I)
  180 CONTINUE
      GO TO 020
C
C ......................................................................
C       .  INITIALIZATION OF PROJECTION PARAMETERS (FILE INPUT)  .
C ......................................................................
C
      ENTRY IF28F (INFILE)
C
      IERROR = 0
      READ (INFILE,END=160) ZONE,BUFF
      IF (SWITCH.NE.0 .AND. SWITCH.EQ.ZONE) RETURN
  020 A = BUFF(1)
      IF (A .LE. ZERO) A = AZZ
      LON0 = PAKR (BUFF(5))
      X0 = BUFF(7)
      Y0 = BUFF(8)
      SQRT2 = SQRT(TWO)
      TSQRT2 = SQRT2 * TWO
      BLOW1 = -PI * 3.0
      BLOW2 = BLOW1 * HALF 
      BHIGH1 = -BLOW1
      BHIGH2 = -BLOW2
      TOL = EPSLN * A
C
C LIST RESULTS OF PARAMETER INITIALIZATION.
C
      CALL RADD (LON0,ANGS(1,1))
      IF (IPPARM .EQ. 0) PRINT 2000, A,ANGS,X0,Y0
 2000 FORMAT ('0INITIALIZATION PARAMETERS (WINKEL III PROJECTION)'/
     .        ' RADIUS OF SPHERE             =',F12.2,' METERS'/
     .        ' LONGITUDE OF C. MERIDIAN     = ',A1,2I3,F7.3/
     .        ' FALSE EASTING                =',F12.2,' METERS'/
     .        ' FALSE NORTHING               =',F12.2,' METERS')
      DATA(1) = A
      SWITCH = ZONE
      RETURN
  160 IF (IPEMSG .EQ. 0) PRINT 2020
 2020 FORMAT ('0ERROR PJ28F'/
     .        ' MISSING PROJECTION PARAMETERS')
      IERROR = 281
      RETURN
C
C ......................................................................
C                      .  FORWARD TRANSFORMATION  .
C ......................................................................
C
      ENTRY PF28F (GEOG,PROJ)
C
      IERROR = 0
      IF (SWITCH .NE. 0) GO TO 220
      IF (IPEMSG .EQ. 0) PRINT 2020
      IERROR = 282
      RETURN
  220 CONTINUE
      DLON = ADJL (GEOG(1) - LON0)
C
      CALL WINKF(GEOG(2),DLON,X,Y)

      PROJ(1) = X0 + X
      PROJ(2) = Y0 + Y
      RETURN
C
      END
      SUBROUTINE WINKF(LAT,LON,X,Y)      
C
C ***	IDENT   25OC95
C ***   MODULE  WINKF
C
C      Copyright LaserScan Laboratories Ltd., Cambridge, England
C
C to calculate X and Y, from latitude and longitude
C
C Author                               R.W. Russell      24-Feb-93
C
      IMPLICIT DOUBLE PRECISION (A-Z)
C
C arguments
C
C latitude to convert
      DOUBLE PRECISION LAT
      DOUBLE PRECISION LON
C projected point
      DOUBLE PRECISION X
      DOUBLE PRECISION Y
C
C initialised values
C
      INTEGER ANGS(4,1)
      INTEGER SWITCH
      DOUBLE PRECISION A,LON0,X0,Y0,SQRT2,TSQRT2
      DOUBLE PRECISION BUFF(15)
      COMMON /WRK28F/BUFF,ANGS,A,LON0,X0,Y0,SQRT2,TSQRT2,
     &               SWITCH
C
C working storage
C
C constants
C
      DOUBLE PRECISION PI,HALFPI,EPSLN,ZERO,ONE,TWO,HALF
      PARAMETER        (PI =3.14159265358979323846D0)
      PARAMETER        (HALFPI =1.5707963267948966D0)
      PARAMETER        (EPSLN = 1.0D-12)
      PARAMETER        (ZERO = 0.0, ONE = 1.0, TWO = 2.0, HALF = 0.5)
C
C functions called
C
      DOUBLE PRECISION DARSIN
C
C working storage
C
      DOUBLE PRECISION COSPHI,SINPHI
      DOUBLE PRECISION TMP,LON2
      DOUBLE PRECISION CSLON2,SNLON2
C
C Hammer Projection

      COSPHI = COS(LAT)
      SINPHI = SIN(LAT)
      LON2 = LON/TWO
      CSLON2 = COS(LON2)
      SNLON2 = SIN(LON2)
      TMP = SQRT(ONE + COSPHI*CSLON2)
      TMP = ONE/TMP
      X = COSPHI * SNLON2 * TSQRT2 * TMP
      Y = SQRT2 * SINPHI * TMP
C
C now add Plate-Carre coordinates
C
      X = (X + LON) * HALF
      Y = (Y + LAT) * HALF
C
C result
C
      X = X * A
      Y = Y * A
C
      RETURN
      END
