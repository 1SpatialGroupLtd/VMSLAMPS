C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1995-10-27 12:43:04.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
C                   PJ25F
C **********************************************************************
C                       *  BONNE *
C **********************************************************************
C
CMOD    Converted to Fortran77 (WI/CC 1514)      T.J.Mulcahy 25-Oct-1995
C
      SUBROUTINE PJ25F
C ***   IDENT   25OC95
C ***   MODULE  PJ25F
C
C
C Copyright LaserScan Laboratories Ltd., Cambridge, England
C
C Author 					R.W. Russell	11-Feb-93
C
      IMPLICIT DOUBLE PRECISION (A-Z)
C
C This pseudoconical projection is equal area. It is available in both
C spherical and spheroidal form.
C
C The formulation comes from Snyder, pages 138 - 140
C
C arguments
C
C projection parameters
      DOUBLE PRECISION DATA(15)
C                      DATA(3) = latitude of standard parallel
C                      DATA(5) = Longitude of central meridian
C                      DATA(7) = False Eastings
C                      DATA(8) = False Northings
C geographical coordinates (radians)
      DOUBLE PRECISION GEOG(2)
C projected coordinates (usually meters)
      DOUBLE PRECISION PROJ(2)
C zone number (variable if using file)
      INTEGER   ZONE
C LUN for input file (if used)
      INTEGER   INFILE
C
C common blocks
C
      INTEGER IERROR,IPEMSG,IPPARM
      COMMON /ERRM/ IERROR
      COMMON /PRIN/ IPEMSG,IPPARM
C
C default spheroid
C
      DOUBLE PRECISION AZ,EZ,ESZ,E0Z,E1Z,E2Z,E3Z
      COMMON /ELLP/ AZ,EZ,ESZ,E0Z,E1Z,E2Z,E3Z
C
C working area
C
      INTEGER ANGS(4,2)
      INTEGER SWITCH
      DOUBLE PRECISION A,LON0,LAT1,CLAT1,ACLAT1,SGN,X0,Y0
      DOUBLE PRECISION E0,E1,E2,ML1,MS1,SNPHI1,TS1
      DOUBLE PRECISION BUFF(15)
      LOGICAL ISSPH
C
      COMMON /WRK25F/BUFF,ANGS,A,LON0,LAT1,CLAT1,ACLAT1,SGN,
     &               X0,Y0,E0,E1,E2,ML1,MS1,SNPHI1,TS1,
     &               ISSPH
      COMMON /SW25F/ SWITCH
C
C constants
C
      DOUBLE PRECISION HALFPI,EPSLN,ZERO,ONE,TWO
      PARAMETER       (HALFPI =1.5707963267948966D0)
      PARAMETER       (EPSLN = 1.0D-10)
      PARAMETER       (ZERO = 0.0, ONE = 1.0, TWO = 2.0)
C
C functions called
C
      DOUBLE PRECISION PAKR
      DOUBLE PRECISION ADJL
      DOUBLE PRECISION E0FN
      DOUBLE PRECISION E1FN
      DOUBLE PRECISION E2FN
      DOUBLE PRECISION MLFN
      DOUBLE PRECISION MSFN
      DOUBLE PRECISION PHI3
C
C working storage
C
      DOUBLE PRECISION B,E,ES
      INTEGER I
      DOUBLE PRECISION DLON,X,Y,RH,EE,TEMP
      DOUBLE PRECISION ML,MS,SINPHI,COSPHI
C
C initialisation
C
C      DATA SWITCH /0/
C
C ......................................................................
C      .  INITIALIZATION OF PROJECTION PARAMETERS (ENTRY INPUT)  .
C ......................................................................
C
      ENTRY IS25F (ZONE,DATA)
C
      IERROR = 0
      IF (SWITCH.NE.0 .AND. SWITCH.EQ.ZONE) RETURN
      DO 180 I = 1,8
      BUFF(I) = DATA(I)
  180 CONTINUE
      GO TO 020
C
C ......................................................................
C       .  INITIALIZATION OF PROJECTION PARAMETERS (FILE INPUT)  .
C ......................................................................
C
      ENTRY IF25F (INFILE)
C
      IERROR = 0
      READ (INFILE,END=160) ZONE,BUFF
      IF (SWITCH.NE.0 .AND. SWITCH.EQ.ZONE) RETURN
  020 IF (BUFF(1) .LE. ZERO) GO TO 100
      A = BUFF(1)
      B = BUFF(2)
      IF (B .GT. ZERO) GO TO 040
      E = ZERO
      ES = ZERO
      E0 = ONE
      E1 = ZERO
      E2 = ZERO
      GO TO 120
  040 IF (B .GT. ONE) GO TO 060
      E = DSQRT (B)
      ES = B
      GO TO 080
  060 ES = ONE - (B / A) ** 2
      E = DSQRT (ES)
  080 CONTINUE
      E0 = E0FN (ES)
      E1 = E1FN (ES)
      E2 = E2FN (ES)
      GOTO 120
  100 A = AZ
      E = EZ
      ES = ESZ
      E0 = E0Z
      E1 = E1Z
      E2 = E2Z
  120 LAT1 = PAKR (BUFF(3))
      LON0 = PAKR (BUFF(5))
      X0 = BUFF(7)
      Y0 = BUFF(8)
C
      IF (ABS(LAT1) .GE. EPSLN) GOTO 128
      IF (IPEMSG .EQ. 0) PRINT 2000
 2000 FORMAT ('0ERROR PJ25F'/
     .        ' FOR EQUATOR AS STANDARD PARALLEL, USE SINUSOIDAL')
      IERROR = 253
      RETURN
C
  128 ISSPH = E .LT. EPSLN
      SGN = ONE
      IF (LAT1 .LT. ZERO) SGN = -SGN
      IF (ISSPH) THEN
         IF (ABS(ABS(LAT1) - HALFPI) .GT. EPSLN) THEN
            CLAT1 = ONE/TAN(LAT1)
         ELSE
            CLAT1 = ZERO
         ENDIF
         ACLAT1 = A*CLAT1
      ELSE
         SNPHI1 = SIN(LAT1)
         ML1 = MLFN (E0,E1,E2,LAT1)
         MS1 = MSFN (E,SNPHI1,COS(LAT1))
         TS1 = MS1/SNPHI1
      ENDIF
C
C LIST RESULTS OF PARAMETER INITIALIZATION.
C
  130 CALL RADD (LAT1,ANGS(1,1))
      CALL RADD (LON0,ANGS(1,2))
      IF (IPPARM .EQ. 0) PRINT 2010, A,ES,ANGS,X0,Y0
 2010 FORMAT ('0INITIALIZATION PARAMETERS (BONNE',
     .        ' PROJECTION)'/
     .        ' SEMI-MAJOR AXIS OF ELLIPSOID  =',F12.2,' METERS'/
     .        ' ECCENTRICITY SQUARED          =',F12.9/
     .        ' LATITUDE OF STANDARD PARALLEL = ',A1,2I3,F7.3/
     .        ' LONGITUDE OF CENTRAL MERIDIAN = ',A1,2I3,F7.3/
     .        ' FALSE EASTING                 =',F12.2,' METERS'/
     .        ' FALSE NORTHING                =',F12.2,' METERS')
      DATA(1) = A
      DATA(2) = ES
      SWITCH = ZONE
      RETURN
  160 IF (IPEMSG .EQ. 0) PRINT 2020
 2020 FORMAT ('0ERROR PJ25F'/
     .        ' MISSING PROJECTION PARAMETERS')
      IERROR = 251
      RETURN
C
C ......................................................................
C                      .  FORWARD TRANSFORMATION  .
C ......................................................................
C
      ENTRY PF25F (GEOG,PROJ)
C
      IERROR = 0
      IF (SWITCH .NE. 0) GO TO 220
      IF (IPEMSG .EQ. 0) PRINT 2020
      IERROR = 252
      RETURN
  220 CONTINUE
      DLON = ADJL (GEOG(1) - LON0)
      IF (ABS(GEOG(2) - LAT1) .LT. EPSLN .AND. 
     &    ABS(ABS(LAT1) - HALFPI) .LT. EPSLN) THEN
         X = ZERO
         Y = ZERO
      ELSE
         IF (ISSPH) THEN
            RH = A*(CLAT1 + LAT1 - GEOG(2))
            EE = A*DLON*COS(GEOG(2))/RH
            X = RH * SIN(EE)
            Y = ACLAT1 - RH*COS(EE)
         ELSE
            SINPHI = SIN(GEOG(2))
            COSPHI = COS(GEOG(2))
            ML = MLFN (E0,E1,E2,GEOG(2))
            MS = MSFN (E,SINPHI,COSPHI)
            RH = TS1 + ML1 - ML
            EE = MS * DLON / RH
            X = A * RH * SIN(EE)
            Y = A * (TS1 - RH * COS(EE))
         ENDIF
      ENDIF
      PROJ(1) = X0 + X
      PROJ(2) = Y0 + Y
      RETURN
C
      END
