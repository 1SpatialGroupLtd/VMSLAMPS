C                   PJ06I
C **********************************************************************
C ** U.S.G.S. GENERAL MAP PROJECTION PACKAGE ...... DR. A. A. ELASSAL **
C **          MATHEMATICAL ANALYSIS BY JOHN SNYDER                    **
C ** MODULE I                VERSION 1.0.0            NOVEMBER 1,1980 **
C **********************************************************************
C                       *  POLAR STEREOGRAPHIC  *
C **********************************************************************
C
CMOD	PJ06I created from PJ06F for inverse transformation
C	Add CF06 to compare two projection parameter sets
C						    J Barber 20-Oct-1993
C
CMOD    Converted to Fortran77 (WI/CC 1514)      T.J.Mulcahy 24-Oct-1995
C
	SUBROUTINE PJ06I
C
C ***	IDENT   24OC95
C ***	MODULE  PJ06I
C
      IMPLICIT DOUBLE PRECISION (A-Z)
C
      DOUBLE PRECISION ARGS1(15),ARGS2(15)
      INTEGER IERROR,IPEMSG,IPPARM
      INTEGER SWITCH,IND,ZONE,ANGS,INFILE,I
      LOGICAL SAME
C
      COMMON /ELLP/ AZ,EZ,ESZ,E0Z,E1Z,E2Z,E3Z
C **** PARAMETERS **** A,E,ES,LON0,LATC,X0,Y0,E3,MCS,TCS,FAC,IND *******
      COMMON /ERRM/ IERROR
      COMMON /PRIN/ IPEMSG,IPPARM
      COMMON /WRK0/ BUFF(15),ANGS(4,2)
C
      DOUBLE PRECISION DATA(1),GEOG(1),PROJ(1)
C
      DATA NINTYD /90000000.0D0/
      DATA ZERO,ONE,TWO /0.0D0,1.0D0,2.0D0/
      DATA SWITCH /0/
C
C ......................................................................
C      .  INITIALIZATION OF PROJECTION PARAMETERS (ENTRY INPUT)  .
C ......................................................................
C
      ENTRY IS06I (ZONE,DATA)
C
      IERROR = 0
      IF (SWITCH.NE.0 .AND. SWITCH.EQ.ZONE) RETURN
      DO 180 I = 1,8
      BUFF(I) = DATA(I)
  180 CONTINUE
      GO TO 020
C
C ......................................................................
C       .  INITIALIZATION OF PROJECTION PARAMETERS (FILE INPUT)  .
C ......................................................................
C
      ENTRY IF06I (INFILE,DATA)
C
      IERROR = 0
      READ (INFILE,END=160) ZONE,BUFF
      IF (SWITCH.NE.0 .AND. SWITCH.EQ.ZONE) RETURN
  020 IF (BUFF(1) .LE. ZERO) GO TO 100
      A = BUFF(1)
      B = BUFF(2)
      IF (B .GT. ZERO) GO TO 040
      E = ZERO
      ES = ZERO
      E3 = ONE
      GO TO 120
  040 IF (B .GT. ONE) GO TO 060
      E = DSQRT (B)
      ES = B
      GO TO 080
  060 ES = ONE - (B / A) ** 2
      E = DSQRT (ES)
  080 E3 = E3FN (E)
      GO TO 120
  100 A = AZ
      E = EZ
      ES = ESZ
      E3 = E3Z
  120 LON0 = PAKR (BUFF(5))
      SAVE = BUFF(6)
      LATC = PAKR (SAVE)
      X0 = BUFF(7)
      Y0 = BUFF(8)
      FAC = ONE
      IF (SAVE .LT. ZERO) FAC =-ONE
      IND = 0
      IF (DABS(SAVE) .EQ. NINTYD) GO TO 130
      IND = 1
      CON1 = FAC * LATC
      SINPHI = DSIN (CON1)
      COSPHI = DCOS (CON1)
      MCS = MSFN (E,SINPHI,COSPHI)
      TCS = TSFN (E,CON1,SINPHI)
C
C LIST RESULTS OF PARAMETER INITIALIZATION.
C
  130 CALL RADD (LON0,ANGS(1,1))
      CALL RADD (LATC,ANGS(1,2))
      IF (IPPARM .EQ. 0) PRINT 2000, A,ES,ANGS,X0,Y0
 2000 FORMAT ('0INITIALIZATION PARAMETERS (POLAR STEREOGRAPHIC',
     .        ' PROJECTION)'/
     .        ' SEMI-MAJOR AXIS OF ELLIPSOID =',F12.2,' METERS'/
     .        ' ECCENTRICITY SQUARED         =',F12.9/
     .        ' LONGITUDE OF Y-AXIS          = ',A1,2I3,F7.3/
     .        ' LATITUDE OF TRUE SCALE       = ',A1,2I3,F7.3/
     .        ' FALSE EASTING                =',F12.2,' METERS'/
     .        ' FALSE NORTHING               =',F12.2,' METERS')
      DATA(1) = A
      DATA(2) = ES
      SWITCH = ZONE
      RETURN
  160 IF (IPEMSG .EQ. 0) PRINT 2010
 2010 FORMAT ('0ERROR PJ06I'/
     .        ' MISSING PROJECTION PARAMETERS')
      IERROR = 061
      RETURN
C
C ......................................................................
C                      .  INVERSE TRANSFORMATION  .
C ......................................................................
C
      ENTRY PI06I (PROJ,GEOG)
C
      IERROR = 0
      IF (SWITCH .NE. 0) GO TO 320
      IF (IPEMSG .EQ. 0) PRINT 2010
      IERROR = 063
      RETURN
  320 X = FAC * (PROJ(1) - X0)
      Y = FAC * (PROJ(2) - Y0)
      RH = DSQRT (X * X + Y * Y)
      IF (IND .EQ. 0) GO TO 340
      TS = RH * TCS / (A * MCS)
      GO TO 360
  340 TS = RH * E3 / (TWO * A)
  360 GEOG(2) = FAC * PHI2 (E,TS)
      IF (IERROR .EQ. 0) GO TO 380
      IERROR = 064
      RETURN
  380 IF (RH .NE. ZERO) GO TO 400
      GEOG(1) = FAC * LON0
      RETURN
  400 GEOG(1) = ADJL (FAC * DATAN2 (X , -Y) + LON0)
      RETURN
C
C ......................................................................
C                 .  TRANSFORMATION  COMPARISON  .
C ......................................................................
C
      ENTRY CF06(ARGS1,ARGS2,SAME)
C
      SAME = .TRUE.
C
C semi-major axis
      IF ( DABS(ARGS1(1)-ARGS2(1)) .GT. 0.1D-3) GOTO 810
C
C eccentricity or semi-minor axis converted to eccentricity
      IF (ARGS1(2).GT.1D0) ARGS1(2) = 1D0 - ARGS1(2)*ARGS1(2) /
     &						ARGS1(1)*ARGS1(1)
C
      IF (ARGS2(2).GT.1D0) ARGS2(2) = 1D0 - ARGS2(2)*ARGS2(2) /
     &						ARGS2(1)*ARGS2(1)
C
      IF ( DABS(ARGS1(2)-ARGS2(2)) .GT. 0.1D-11) GOTO 810
C
C central meridian, proj orig
C
      DO 801 I = 5,6
	 SEC1 = PAKS(ARGS1(I))
	 SEC2 = PAKS(ARGS2(I))
C
         IF ( DABS(SEC1-SEC2) .GT. 0.1D-3) GOTO 810
801   CONTINUE
C
C eastings, northings
C
      DO 802 I = 7,8
         IF ( DABS(ARGS1(I)-ARGS2(I)) .GT. 0.1D-3) GOTO 810
802   CONTINUE
      GOTO 820
C
810   SAME=.FALSE.
C
820   RETURN
      END
C
