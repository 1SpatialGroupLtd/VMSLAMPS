C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1995-10-25 16:58:38.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
C****                                                              *****
C ** U.S.G.S. GENERAL MAP PROJECTION PACKAGE ...... JOHN F. WAANANEN  **
C ** MODULE I                VERSION 1.0.0             APRIL 2, 1981  **
C ****                                                             *****
C
CMOD    Converted to Fortran77 (WI/CC 1514)              TJM 25-Oct-1995
C
      SUBROUTINE SPHD(ISPH,PARM)
C ***	IDENT   25OC95
C ***	MODULE  SPHD
C
CMOD Correct comments (two code 21s!)		RWR	11-Feb-1993
CMOD (mod 2/468) Add two more spheroids		RWR	 5-June-1987
C     and correct values for spheroid 11
CMOD Some spheroids refined to make 1/f correct RWR	21-Oct -1988
C
C     SUBROUTINE TO COMPUTE SPHEROID PARAMETERS
C     SUBROUTINE SPHD IS REQUIRED FOR PROGRAMS NO. L176 AND NO. L177
C
C     ISPH IS THE SPHEROID CODE FROM THE FOLLOWING LIST:
C     0 = CLARKE 1866           1 = CLARKE 1880
C     2 = BESSEL                3 = NEW INTERNATIONAL 1967
C     4 = INTERNATIONAL 1909    5 = WGS 72
C     6 = EVEREST               7 = WGS 66
C     8 = GRS 1980              9 = AIRY
C    10 = MODIFIED EVEREST     11 = MODIFIED AIRY
C    12 = WALBECK              13 = SOUTHEAST ASIA
C    14 = AUSTRALIAN NATIONAL  15 = KRASSOVSKY
C    16 = HOUGH                17 = MERCURY 1960
C    18 = MODIFIED MERC 1968   19 = SPHERE OF RADIUS 6370997 M
C    20 = SPHERE OF RADIUS 6371229.3 M
C    21 = CLARKE 1880 (IGN)    22 = WGS 84
C
C    PARM IS ARRAY OF PROJECTION PARAMETERS:
C       PARM(1) IS THE SEMI-MAJOR AXIS
C       PARM(2) IS THE ECCENTRICITY SQUARED
C
C     IF ISPH IS NEGATIVE, THE DEFAULT IS RESET FROM CLARKE 1866
C     TO THE POSITIVE OF "ISPH" SPHEROID.
C
C     IF ISPH = 0 , THE DEFAULT IS RESET TO CLARKE 1866
C
C ****                                                             *****
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DOUBLE PRECISION PARM(15),AXIS(23),BXIS(23)
C
      COMMON/ELLP/ AZ,EZ,ESZ,E0Z,E1Z,E2Z,E3Z
      COMMON/SPHR/ AZZ
      COMMON/ERRM/ IERROR
      COMMON/PRIN/ IPEMSG,IPPARM
      COMMON/PROJ/ IPROJ
      COMMON/AXES/ AXIS,BXIS
C
      DATA AXIS/6378206.4D0,6378249.145D0,6377397.155D0,6378157.5D0,
     . 6378388.0D0,6378135.0D0,6377276.3452D0,6378145.0D0,6378137.0D0,
     . 6377563.396D0,6377304.063D0,6377340.189D0,6376896.0D0,
     . 6378155.0D0,6378160.0D0,6378245.0D0,6378270.0D0,6378166.0D0,
     . 6378150.0D0,6370997.0D0,6371229.3D0,6378249.200D0,6378137.000D0/
C
      DATA BXIS/6356583.8D0,6356514.86955D0,6356078.96284D0,
     . 6356772.2D0,6356911.94613D0,6356750.52001609D0,6356075.4133D0,
     . 6356759.76948868D0,6356752.31414D0,6356256.91D0,6356103.039D0,
     . 6356034.448D0,6355834.8467D0,6356773.3205D0,6356774.71919531D0,
     . 6356863.01877305D0,6356794.343434D0,6356784.28360711D0,
     . 6356768.337303D0,6370997.0D0,6371229.3D0,6356515.0D0,
     . 6356752.31424518D0/
C
      IF (ISPH.LT.0) GO TO 5
      IF (PARM(1).NE.0.0D0.AND.IPROJ.NE.1) RETURN
    5 JSPH = IABS(ISPH) + 1
      IF (JSPH.LE.23) GO TO 10
      IERROR = 211
      IF (IPEMSG.EQ.0) WRITE(6,1) ISPH
    1 FORMAT('0ERROR SPHD:  SPHEROID CODE OF ',I5,' RESET TO 0')
      ISPH = 0
       JSPH = 1
   10 A = AXIS(JSPH)
      B = BXIS(JSPH)
      ES = (A*A-B*B)/(A*A)
C     IF ISPH LE 0 THEN RESET DEFAULT
      IF (ISPH.GT.0) GO TO 20
      AZZ = 6370997.0D0
      EZ  = DSQRT(ES)
      E0Z = E0FN(ES)
      E1Z = E1FN(ES)
      E2Z = E2FN(ES)
      E3Z = E3FN(EZ)
      AZ  = A
      ESZ = ES
      IF (ES.EQ.0.0D0) AZZ=A
      IF (IPPARM.NE.0) GO TO 20
      WRITE(6,2) ISPH,AZ,EZ,ESZ,E0Z,E1Z,E2Z,E3Z,AZZ
    2 FORMAT('0SPHEROID DEFAULT SET TO NUMBER ',I2/
     . ' SEMI-MAJOR AXIS  = ',D24.15/
     . ' ECCENTRICITY     = ',D24.15/
     . ' ECCENT. SQUARED  = ',D24.15/
     . ' E0               = ',D24.15/
     . ' E1               = ',D24.15/
     . ' E2               = ',D24.15/
     . ' E3               = ',D24.15/
     . ' REFERENCE SPHERE = ',D24.15)
C
   20 PARM(1) = A
      PARM(2) = ES
      RETURN
      END

