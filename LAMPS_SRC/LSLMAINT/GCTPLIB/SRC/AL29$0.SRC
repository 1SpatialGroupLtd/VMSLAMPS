C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1995-10-25 17:19:28.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
C                   AL29
C **********************************************************************
C ** U.S.G.S. GENERAL MAP PROJECTION PACKAGE ...... DR. A. A. ELASSAL **
C ** MODULE I                VERSION 1.0.0            NOVEMBER 1,1980 **
C **********************************************************************
C
CMOD    Converted to Fortran77 (WI/CC 1514)              TJM 25-Oct-1995
C
      SUBROUTINE AL29 (CRDIN,CRDOUT,ZONE,FLAG)
C ***	IDENT   26OC95
C ***	MODULE  AL29 
C
C SUBROUTINE TO COMPUTE TRANSFORMATION BETWEEN GEOGRAPHIC AND
C ALASKA STATE ZONES NO. 2 THROUGH 9.
C FLAG = 0, MEANS PLANE TO GEOGRAPHIC.
C FLAG = 1, MEANS GEOGRAPHIC TO PLANE.
C
      IMPLICIT DOUBLE PRECISION (A-Z)
C
      INTEGER ZONE,FLAG,IND
      DOUBLE PRECISION CRDIN(1),CRDOUT(1),CONST(2,8)
C
      DATA CONST /500000.0D0 , 511200.0D0,
     .            500000.0D0 , 525600.0D0,
     .            500000.0D0 , 540000.0D0,
     .            500000.0D0 , 554400.0D0,
     .            500000.0D0 , 568800.0D0,
     .            700000.0D0 , 583200.0D0,
     .            500000.0D0 , 597600.0D0,
     .            600000.0D0 , 612000.0D0/
      DATA RADSEC /206264.806247D0/
      DATA ONE,TWO /1.0D0,2.0D0/
C
      IND = ZONE - 1
      C = CONST(1,IND)
      CM = CONST(2,IND)
      IF (FLAG .EQ. 0) GO TO 020
C
C GEOGRAPHIC TO STATE PLANE TRANSFORMATION.
C
      GEOG1 = DABS(CRDIN(1))
      GEOG2 = CRDIN(2)
      C1 = DCOS(GEOG2)
      C2 = C1 * C1
      C3 = C2 * C2
      C4 = C2 * C3
      C5 = (CM - GEOG1 * RADSEC) * 1.0D-4
      C6 = C5 * C5
      C7 = C6 * C6
      C8 = DSQRT(ONE + 0.0068147849 * C2)
      C9 = DSQRT(ONE - C2) * C1
      CRDOUT(1) = C + (1017862.15D0 * C1 / C8) * C5 * (ONE -
     .            3.91740509D-4 * C6 * (ONE - TWO * C2 - 0.681478D-2 *
     .            C3) + 4.60382D-8 * C7 * (ONE - 20.0D0 * C2 +
     .            23.6047D0 * C3 + 0.4907D0 * C4))
      CRDOUT(2) = 101.269278503D0 * (GEOG2 * RADSEC - 193900.05442D0
     .            - (1052.893943D0 - 4.483386D0 * C2 + 2.3559D-2 * C3) *
     .            C9) + (24673.6748D0 * C9 * C6 / C8) * (ONE +
     .            1.958703D-4 * C6 * (-ONE + 6.0D0 * C2 + 6.133306D-2 *
     .            C3 + 1.8577D-4 * C4) + 1.5346D-8 * C7 * (ONE -
     .            60.0D0 * C2 + 117.75D0 * C3 + 4.089D0 * C4))
      RETURN
C
C STATE PLANE TO GEOGRAPHIC TRANSFORMATION.
C
  020 OMEGA = 193900.05442D0 + 0.00987466302498D0 * CRDIN(2)
      C1 = DCOS(OMEGA / RADSEC)
      C2 = C1 * C1
      C3 = C2 * C2
      PHI = OMEGA + (1047.546691D0 + 6.193011 * C2 + 5.0699D-2 * C3) *
     .      DSQRT(ONE - C2) * C1
      C1 = DCOS(PHI / RADSEC)
      C2 = C1 * C1
      C3 = C2 * C2
      C4 = (CRDIN(1) - C) * 1.0D-6
      C5 = C4 * C4
      C6 = C5 * C5
      C7 = ONE + 0.0068147849 * C2
      C8 = C7 * C7
      CRDOUT(2) = (PHI - 233.973645D0 * C5 * C8 * DSQRT((ONE / C2) -
     .            ONE) * (ONE - 1.8905604D-4 * C5 * (1.9591113D0 +
     .            (3.0D0 / C2) + 8.1359D-2 * C2 + 2.79D-4 * C3) +
     .            1.42969D-8 * C6 * C7 * (15.5D0 + (45.0D0 / C3) -
     .            (0.307D0 / C2) + 1.53D0 * C2))) / RADSEC
      CRDOUT(1) =-(CM - 9824.513072D0 * DSQRT(C7) * C4 / C1 *
     .            (ONE - 3.7811208D-4 * C7 * C5 * (-TWO + C7 + (TWO /
     .            C2)) + 4.2890624D-8 * C7 * C6 * (1.054 + (24.0D0 /
     .            C3) - (20.0D0 / C2) - 1.36D-2 * C2))) / RADSEC
      RETURN
C
      END

