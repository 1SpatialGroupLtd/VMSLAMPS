C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1995-10-27 11:02:44.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
C                   PJ09F
C **********************************************************************
C ** U.S.G.S. GENERAL MAP PROJECTION PACKAGE ...... DR. A. A. ELASSAL **
C **          MATHEMATICAL ANALYSIS BY JOHN SNYDER                    **
C ** MODULE I                VERSION 1.0.2                MAY 14,1981 **
C **********************************************************************
C                       *  TRANSVERSE MARCATOR  *
C **********************************************************************
C
CMOD    Converted to Fortran77 (WI/CC 1514)      T.J.Mulcahy 24-Oct-1995
C
      SUBROUTINE PJ09F
C ***	IDENT   24OC95
C ***	MODULE  PJ09F
C
      IMPLICIT DOUBLE PRECISION (A-Z)
C
      INTEGER IERROR,IPEMSG,IPPARM
      INTEGER SWITCH,I,ZONE,ANGS,INFILE,IND,NIT
C
      COMMON /ELLP/ AZ,EZ,ESZ,E0Z,E1Z,E2Z,E3Z
C **** PARAMETERS **** A,E,ES,KS0,LON0,LAT0,X0,Y0,E0,E1,E2,ESP,ML0,IND *
      COMMON /ERRM/ IERROR
      COMMON /PRIN/ IPEMSG,IPPARM
      COMMON /WRK0/ BUFF(15),ANGS(4,2)
C
C ********************** correction by RWR, Laserscan  13-Jan-85 ***
C
C As this transformation is used by the UTM subroutine
C SWITCH should be held in common with that transformation, so that
C the transformation parameters can be reset if the other half of the
C transformation also uses PJ09F
C
C      DATA SWITCH /0/
	COMMON /SW09F/SWITCH
C
C *************************end of correction **********************
C
      DOUBLE PRECISION DATA(15),GEOG(1),PROJ(1)
C
      DATA ZERO,HALF,ONE,TWO,THREE /0.0D0,0.5D0,1.0D0,2.0D0,3.0D0/
      DATA FOUR,FIVE,SIX,EIGHT,NINE /4.0D0,5.0D0,6.0D0,8.0D0,9.0D0/
      DATA HALFPI /1.5707963267948966D0/
      DATA TEN /10.0D0/
      DATA TOL,EPSLN,NIT /1.0D-5,1.0D-10,6/
C
C ......................................................................
C      .  INITIALIZATION OF PROJECTION PARAMETERS (ENTRY INPUT)  .
C ......................................................................
C
      ENTRY IS09F (ZONE,DATA)
C
      IERROR = 0
C$$$$$$$$$$$$$$$$$$$$$ ADDITIONS BY J F WAANANEN 5/1/81 $$$$$$$$$$$
      IF (DATA(1).NE.0.0D0.AND.DATA(1).NE.BUFF(1)) SWITCH=0
C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
      IF (SWITCH.NE.0 .AND. SWITCH.EQ.ZONE) RETURN
      DO 180 I = 1,8
      BUFF(I) = DATA(I)
  180 CONTINUE
      GO TO 020
C
C ......................................................................
C       .  INITIALIZATION OF PROJECTION PARAMETERS (FILE INPUT)  .
C ......................................................................
C
      ENTRY IF09F (INFILE)
C
      IERROR = 0
      READ (INFILE,END=160) ZONE,BUFF
      IF (SWITCH.NE.0 .AND. SWITCH.EQ.ZONE) RETURN
  020 IF (BUFF(1) .LE. ZERO) GO TO 100
      A = BUFF(1)
      B = BUFF(2)
      IF (B .GT. ZERO) GO TO 040
      E = ZERO
      ES = ZERO
      E0 = ONE
      E1 = ZERO
      E2 = ZERO
      GO TO 120
  040 IF (B .GT. ONE) GO TO 060
      E = DSQRT (B)
      ES = B
      GO TO 080
  060 ES = ONE - (B / A) ** 2
      E = DSQRT (ES)
  080 E0 = E0FN (ES)
      E1 = E1FN (ES)
      E2 = E2FN (ES)
      GO TO 120
  100 A = AZ
      E = EZ
      ES = ESZ
      E0 = E0Z
      E1 = E1Z
      E2 = E2Z
  120 KS0 = BUFF(3)
      LON0 = PAKR (BUFF(5))
      LAT0 = PAKR (BUFF(6))
      X0 = BUFF(7)
      Y0 = BUFF(8)
      ML0 = A * MLFN (E0,E1,E2,LAT0)
      IND = 1
      IF (E .LT. TOL) GO TO 130
      IND = 0
      ESP = ES / (ONE - ES)
C
C LIST RESULTS OF PARAMETER INITIALIZATION.
C
  130 CALL RADD (LON0,ANGS(1,1))
      CALL RADD (LAT0,ANGS(1,2))
      IF (IPPARM .EQ. 0) PRINT 2000, A,ES,KS0,ANGS,X0,Y0
 2000 FORMAT ('0INITIALIZATION PARAMETERS (TRANSVERSE MERCATOR',
     .        ' PROJECTION)'/
     .        ' SEMI-MAJOR AXIS OF ELLIPSOID =',F12.2,' METERS'/
     .        ' ECCENTRICITY SQUARED         =',F12.9/
     .        ' SCALE FACTOR AT C. MERIDIAN  =',F9.6/
     .        ' LONGITUDE OF C. MERIDIAN     = ',A1,2I3,F7.3/
     .        ' LATITUDE OF ORIGIN           = ',A1,2I3,F7.3/
     .        ' FALSE EASTING                =',F12.2,' METERS'/
     .        ' FALSE NORTHING               =',F12.2,' METERS')
      DATA(1) = A
      DATA(2) = ES
      SWITCH = ZONE
      RETURN
  160 IF (IPEMSG .EQ. 0) PRINT 2010
 2010 FORMAT ('0ERROR PJ09F'/
     .        ' MISSING PROJECTION PARAMETERS')
      IERROR = 091
      RETURN
C
C ......................................................................
C                      .  FORWARD TRANSFORMATION  .
C ......................................................................
C
      ENTRY PF09F (GEOG,PROJ)
C
      IERROR = 0
      IF (SWITCH .NE. 0) GO TO 220
      IF (IPEMSG .EQ. 0) PRINT 2010
      IERROR = 092
      RETURN
  220 DLON = ADJL (GEOG(1) - LON0)
      LAT = GEOG(2)
      IF (IND .EQ. 0) GO TO 240
      COSPHI = DCOS (LAT)
      B = COSPHI * DSIN (DLON)
      IF (DABS(DABS(B) - ONE) .GT. EPSLN) GO TO 230
      IF (IPEMSG .EQ. 0) PRINT 2020
 2020 FORMAT ('0ERROR PJ09F'/
     .        ' POINT PROJECTS INTO INFINITY')
      IERROR = 093
      RETURN
  230 PROJ(1) = HALF * A * KS0 * DLOG ((ONE + B) / (ONE - B))
      CON = DARCOS (COSPHI * DCOS (DLON) / DSQRT (ONE - B * B))
      IF (LAT .LT. ZERO) CON =-CON
      PROJ(2) = A * KS0 * (CON - LAT0)
C
C ********************** correction by RWR, Laserscan  19-Jun-91 ***
C
C apply false origin to transformed coordinates
C
      PROJ(1) = PROJ(1) + X0
      PROJ(2) = PROJ(2) + Y0
C
C *************************end of correction **********************
      RETURN
C
  240 SINPHI = DSIN (LAT)
      COSPHI = DCOS (LAT)
      AL = COSPHI * DLON
      ALS = AL * AL
      C = ESP * COSPHI * COSPHI
      TQ = DTAN (LAT)
      T = TQ * TQ
      N = A / DSQRT (ONE - ES * SINPHI * SINPHI)
      ML = A * MLFN (E0,E1,E2,LAT)
      PROJ(1) = KS0 * N * AL * (ONE + ALS / SIX * (ONE - T + C +
     .          ALS / 20.0D0 * (FIVE - 18.0D0 * T + T * T + 72.0D0 *
     .          C - 58.0D0 * ESP))) + X0
      PROJ(2) = KS0 * (ML - ML0 + N * TQ * (ALS * (HALF + ALS / 24.0D0 *
     .          (FIVE - T + NINE * C + FOUR * C * C + ALS / 30.0D0 *
     .          (61.0D0 - 58.0D0 * T + T * T + 600.0D0 * C -
     .          330.0D0 * ESP))))) + Y0
      RETURN
C
      END

