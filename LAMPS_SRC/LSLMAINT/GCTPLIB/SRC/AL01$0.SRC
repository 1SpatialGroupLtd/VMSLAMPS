C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1995-10-25 17:19:34.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
C                   AL01
C **********************************************************************
C ** U.S.G.S. GENERAL MAP PROJECTION PACKAGE ...... DR. A. A. ELASSAL **
C ** MODULE I                VERSION 1.0.0            NOVEMBER 1,1980 **
C **********************************************************************
C
CMOD    Converted to Fortran77 (WI/CC 1514)              TJM 25-Oct-1995
C
      SUBROUTINE AL01 (CRDIN,CRDOUT,FLAG)
C ***	IDENT   26OC95
C ***	MODULE  AL01
C
C SUBROUTINE TO COMPUTE TRANSFORMATION BETWEEN GEOGRAPHIC AND
C ALASKA STATE ZONE NO. 1.
C FLAG = 0, MEANS PLANE TO GEOGRAPHIC.
C FLAG = 1, MEANS GEOGRAPHIC TO PLANE.
C
      IMPLICIT DOUBLE PRECISION (A-Z)
C
      INTEGER FLAG
      DOUBLE PRECISION CRDIN(1),CRDOUT(1)
C
      DATA B,C,D /1.00029977273D0,0.00447599131D0,6386352.67013D0/
      DATA F,G,E /0.327015517176D0,0.945018968871D0,0.082271854223003D0/
      DATA PI,EPS /3.141592653589793D0,2.718281828459045D0/
      DATA LO /1.771754086D0/
      DATA C1,C2 /0.182880365761D0,0.243840487681D0/
      DATA C3,C4 /7000000.0D0,1000000.0D0/
      DATA ONE,TWO,FOUR /1.0D0,2.0D0,4.0D0/
      DATA MFEET,FALSE /3.280833333333D0,5000000.0D0/
C
      IF (FLAG .EQ. 0) GO TO 020
C
C GEOGRAPHIC TO STATE PLANE TRANSFORMATION.
C
      GEOG1 = DABS(CRDIN(1))
      GEOG2 = CRDIN(2)
      ESINP = E * DSIN(GEOG2)
      TANP = DTAN((PI / FOUR) + (GEOG2 / TWO))
      MU = DLOG(TANP) - (E / TWO) * DLOG((ONE + ESINP) / (ONE - ESINP))
      CON = B * MU + C
      CON1 = EPS**CON
      CON =-CON
      CON2 = EPS**CON
      P = (CON1 - CON2) / TWO
      Q = (CON1 + CON2) / TWO
      CON = B * (GEOG1 - LO)
      CON1 = DSIN(CON)
      CON2 = DCOS(CON)
      U = D * DATAN((G * P + F * CON1) / CON2)
      CON = F * P - G * CON1
      V = (D / TWO) * DLOG((Q + CON) / (Q - CON))
      CRDOUT(1) = MFEET * (-0.6D0 * U + 0.8D0 * V + FALSE)
      CRDOUT(2) = MFEET * ( 0.8D0 * U + 0.6D0 * V - FALSE)
      RETURN
C
C STATE PLANE TO GEOGRAPHIC TRANSFORMATION.
C
  020 U =-C1 * CRDIN(1) + C2 * CRDIN(2) + C3
      V = C2 * CRDIN(1) + C1 * CRDIN(2) - C4
      CON = V / D
      CON1 = EPS**CON
      CON =-CON
      CON2 = EPS**CON
      R = (CON1 - CON2) / TWO
      S = (CON1 + CON2) / TWO
      K1 = DSIN(U / D)
      K2 = DCOS(U / D)
      CON = F * R + G * K1
      MU = (ONE / (TWO * B)) * DLOG((S + CON) / (S - CON)) - (C / B)
      KI = TWO * DATAN(EPS**MU) - PI / TWO
      CON1 = DSIN(KI)
      CON2 = DCOS(KI)
      CRDOUT(2) = KI + (0.006761032571D0 + 0.000053172205D0 * CON2**2 +
     .            0.573027D-6 * CON2**4 + 0.7128D-8 * CON2**6) * CON1 *
     .            CON2
      CRDOUT(1) =-LO - (ONE / B) * DATAN((F * K1 - G * R) / K2)
      RETURN
C
      END

