;
; * This file is part of the LAMPS distribution, released as a software
; * preservation project to archive digital history for future historians.
; * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
; *
; * Timestamp of this file for the 2002 release was: 1992-07-31 11:51:32.000000000 +0100
; *
; * This program is free software: you can redistribute it and/or modify
; * it under the terms of the GNU General Public License as published by
; * the Free Software Foundation, version 3.
; *
; * This program is distributed in the hope that it will be useful, but
; * WITHOUT ANY WARRANTY; without even the implied warranty of
; * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
; * General Public License for more details.
; *
; * You should have received a copy of the GNU General Public License
; * along with this program. If not, see http://www.gnu.org/licenses/.
;
	.TITLE	LSG_LOOP
	.IDENT	/31JL92/

;Copyright Laser Scan Ltd., Cambridge, England
;Author Clarke Brunt, 16-July-1992
;
; Optimised inner loop for GKSPSLIB LSG_PIXEL_ARRAY

	.LIBRARY	/LSL$LIBRARY:LSLMACLIB/

	DATA_PSECT	LSL
ARGLST:	.LONG	2
	.BLKL	2

	CODE_PSECT	LSL

; SUBROUTINE LSG_LOOP(
;	INTEGER		NCOLS,		! number of columns to do
;	INTEGER		XIND,		! starting bit offset
;	INTEGER		PIND,		! incremented by NROWS
;	bit array	PXARR(),	! input, start at XIND
;	bit array	B(),		! output, start at PIND
;	INTEGER		BMAX )		! max bits in B

	NCOLS = 4
	XIND = 8
	PIND = 12
	PXARR = 16
	B = 20
	BMAX = 24

	.ENTRY	LSG_LOOP,^M<R2,R3,R4,R5,R6,R7>

	MOVAL	@B(AP), R6
	MOVL	R6, ARGLST+4
	MOVL	@NCOLS(AP), R7
	MOVL	@PIND(AP), R2			; PIND
	MOVAB	@PXARR(AP), R4
	MOVL	@XIND(AP), R5			; XIND
	MOVL	@BMAX(AP), R3
	MOVAL	@BMAX(AP), ARGLST+8

10$:	EXTZV	R5, #1, (R4), R1		; bit
	INSV	R1, R2, #1, (R6)
	INCL	R5				; increment XIND
	INCL	R2				; increment PIND
	CMPL	R2, R3
	BLSS	20$

	CALLG	ARGLST, PS_BIT_IMAGE
	CLRL	R2				; PIND = 0

20$:	SOBGTR	R7, 10$				; loop for NROWS
	MOVL	R2, @PIND(AP)
	RET

	.END
