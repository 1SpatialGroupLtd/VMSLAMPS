	INTEGER*4 FUNCTION DCL_REAL(CLD_LABEL,WHINGE)
C
***	MODULE	DCL_REAL
***	IDENT	27AU85
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England
C
C	Author						TJH 27-Aug-1985
C
C Function gets any real arguments associated with the DCL qualifier 
C CLD_LABEL and puts them into RARRAY(). Up to MAX_ARR arguments can be
C decoded and stored. Function returns set to SS$_NORMAL if all is well.
C Local error reporting (via condition handler) may be invoked by 
C setting WHINGE .true..
C
	IMPLICIT NONE
C
	INTEGER*4 	CLI$_ABSENT, CLI$GET_VALUE
	EXTERNAL 	CLI$_ABSENT
C
	CHARACTER*16	CHAR_ARRAY	! found string argument
	INTEGER*2	CHAR_LEN        ! length of found string arg.
	CHARACTER*(*)	CLD_LABEL	! the CLD label for qualifier
	INTEGER*4	I		! loop var.
	INTEGER*4	IERR		! for error return
	LOGICAL*4	REPORT		! .T. if local error reporting
	LOGICAL*4	WHINGE		! .T. if local error reporting
C
C condition handler
	EXTERNAL	DCL_ERRET
C
C Common block to control error reporting in condition handler
	COMMON /REPORT/ REPORT	
C
C
	INCLUDE 	'($SSDEF)'
	INCLUDE 	'($STSDEF)'
C
	INCLUDE 	'LSL$CMNLSL:CLD.CMN'
C
C ......................................................................
C
	NUMREA=0		! initialise number of args. counter
	REPORT=WHINGE		! is condition handler to scream?
C
C Establish condition handler
	CALL LIB$ESTABLISH( DCL_ERRET )
C
C Get first value for our label
	DCL_REAL= CLI$GET_VALUE (CLD_LABEL,
     &				 CHAR_ARRAY,
     &				 CHAR_LEN)
C
C ......................................................................
C
C Repeat until the list is depleted
C
	DO WHILE (DCL_REAL)
	    NUMREA=NUMREA+1
C
C Trap the potential RARRAY() overflow and issue the rather appropriate
C VMS error "result parse buffer overflow"
C
	    IF (NUMREA.GT.MAX_ARR) THEN
	        DCL_REAL='38118'X
	        CALL LIB$SIGNAL ( %VAL(DCL_REAL) ) ! signal error
C
C Revert to normal error processing
	        CALL LIB$REVERT
	        RETURN
	    ENDIF
C
C
C As we have had to use a CLD $QUOTED_STRING type qualifier to get the 
C arguments in, the CLI will not have trapped any non numeric characters
C in what we are assuming will be real numbers
C
	    DO 10 I=1,CHAR_LEN
	      IF ((CHAR_ARRAY(I:I).LT.'0'
     &		  	.OR.CHAR_ARRAY(I:I).GT.'9')
     &	  	  	.AND.CHAR_ARRAY(I:I).NE.'+'
     &	  	  	.AND.CHAR_ARRAY(I:I).NE.'-'
     &	  	  	.AND.CHAR_ARRAY(I:I).NE.'.') 
     &						THEN
C
C Since the CLI does'nt know about real numbers we will have to issue a 
C fraudulent message: "invalid syntax for value specification" here.
C
	        DCL_REAL='38088'X
	        CALL LIB$SIGNAL( %VAL(DCL_REAL) )
	        IF (WHINGE) TYPE *,' \',CHAR_ARRAY(:CHAR_LEN),'\'
C
C Revert to normal error processing
	        CALL LIB$REVERT
	        RETURN
	     ENDIF
10	   CONTINUE
C
C
C Finally read the actual number!
C
	   READ(CHAR_ARRAY,*,ERR=20) RARRAY(NUMREA)
	   DCL_REAL= CLI$GET_VALUE (CLD_LABEL,
     &				    CHAR_ARRAY,
     &				    CHAR_LEN)
	END DO
C
C ......................................................................
C
C Make sure termination status was correct
C
	IF (DCL_REAL
     &		.OR.
     &		    (DCL_REAL.EQ. %LOC(CLI$_ABSENT) )) THEN
	    DCL_REAL=SS$_NORMAL
	ELSE IF (IBITS(DCL_REAL,0,3).NE.STS$K_WARNING) THEN
     	    CALL LIB$SIGNAL( %VAL(DCL_REAL) ) ! signal error
	ENDIF
C
C Revert to normal error processing
	CALL LIB$REVERT
	RETURN	
C
C ......................................................................
C
C Error on internal read
C
20	CALL ERRSNS(,,,,DCL_REAL)
C Signal error to condition handler
	CALL LIB$SIGNAL ( %VAL(DCL_REAL) ) 
C
C Revert to normal error processing
	CALL LIB$REVERT
	RETURN 	
	END
