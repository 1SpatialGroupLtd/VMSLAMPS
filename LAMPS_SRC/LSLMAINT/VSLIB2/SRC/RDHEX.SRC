	LOGICAL FUNCTION VS_RDHEX(INT)
C
***	MODULE VS_RDHEX
***	IDENT 04OC82
C
	IMPLICIT CHARACTER*1 (A-Z)
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England
C Author R. Fairbairns
C
C Coded 31.xii.76
C Typed 4.i.77
C amended to give primitive RDHEX from RDINT for VSLIB 1 oct 82 TJI
C
C To read a (double-length) HEX integer from the command input buffer
C
	PARAMETER	ZERO = 48
	PARAMETER	NINE = 57
	PARAMETER	UPA  = 65
	PARAMETER	LOWA = 97
	PARAMETER	UPF  = 70
	PARAMETER	LOWF = 102
	PARAMETER	PLUS = 43
	PARAMETER	MINUS= 45
C
	INTEGER*4 INT
C
C INT is for returning the integer
C
C The function returns TRUE if there is no integer on the line
C
C workspace
	INTEGER CH
	LOGICAL*1 NEGTVE, HADSGN
C
C functions called
	LOGICAL VS_RDCH, VS_RDCHS
C
C initialise
	NEGTVE = .FALSE.
	HADSGN = .FALSE.
	INT = 0
C
C is there anything on the line?
	VS_RDHEX = VS_RDCHS(CH)
	IF (VS_RDHEX) RETURN
	CALL VS_BSCH
10	VS_RDHEX = VS_RDCH(CH)
	IF (VS_RDHEX) RETURN
C
C is there a number on the line?
	IF (HADSGN.OR.(CH.NE.PLUS.AND.CH.NE.MINUS)) GOTO 20
	HADSGN = .NOT.HADSGN
	IF (CH.EQ.MINUS) NEGTVE = .NOT.NEGTVE
	GOTO 10
C
C consumed all '-' signs
20	CALL VS_BSCH
	VS_RDHEX = .NOT.( (CH.GE.ZERO.AND.CH.LE.NINE) .OR.
     &			(CH.GE.LOWA.AND.CH.LE.LOWF)  .OR.
     &			(CH.GE.UPA.AND.CH.LE.UPF) )
	IF (VS_RDHEX) RETURN
C now loop to read the characters of the integer
30	IF (VS_RDCH(CH)) GOTO 50
	IF (CH.LT.ZERO.OR.CH.GT.NINE) GOTO 32
	INT = INT*16+CH-ZERO
	GOTO 30
32	IF (CH.GE.LOWA.AND.CH.LE.LOWF) CH = CH - LOWA + UPA
	IF (CH.LT.UPA.OR.CH.GT.UPF) GOTO 40
	INT = INT*16+CH-UPA+10
	GOTO 30
C
C end of integer on non-digit
40	CALL VS_BSCH
C
C end of integer at end of line
50	IF (NEGTVE) INT = -INT
	RETURN
C
	END
