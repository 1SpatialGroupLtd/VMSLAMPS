	.TITLE	HAVE_I_AN_ARG
	.IDENT	/19DE85/

; Copyright Laser-Scan Laboratories, Ltd., Cambridge, England

; Author  R. Fairbairns		Coded December 1981

;MOD	SYS$LIBRARY => LSL$LIBRARY		AJW	06-Dec-1983
;MOD	Now part of LSLLIB			RJH	19-Dec-1985 
 
 
; define call frame layout
	$SFDEF
 
 
	.LIBRARY	/LSL$LIBRARY:LSLMACLIB/
 
 
	CODE_PSECT	LSL
 
 
; FORTRAN LOGICAL FUNCTION	have_I_an_arg(<number>)
 
; Returns  TRUE if I have
;	  FALSE if not
 
 .ENTRY	HAVE_I_AN_ARG, ^M<R11>
 
	BSBW	VIO$1ST			; init argument mechanism
	BSBW	VIO$GNA			; get next (first) argument
	BCC	10$			; if CC, it was there
	MOVL	#1, R0			; assume arg 1 otherwise
	BRB	20$
 
10$:	MOVZBL	(R0), R0		; get argument number
20$:	BEQL	30$			; if EQL, meaningless - answer NO
	MOVL	SF$L_SAVE_AP(FP), R11	; get saved AP from our call frame
	MOVZBL	(R11), R1		; get calling procedure's arg count
	CMPL	R0, R1			; are we talking in the right order?
	BGTRU	30$			; if GTR, no - answer NO
	TSTL	(R11)[R0]		; was the relevant argument given?
	BEQL	30$			; if EQL, it wasn't
	MNEGL	#1, R0			; answer YES (true)
	RET
 
30$:	CLRL	R0			; answer NO (false)
	RET
 
	.END
