C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1986-07-07 18:26:38.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	INTEGER*4 FUNCTION RDYES( PROMPT, YESNO, DEFAULT, IERR )
C
***	MODULE RDYES
***	IDENT 07JL86
C
	IMPLICIT NONE
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England
C Author 		R. Fairbairns	November 1981
C Modified for LSLLIB	TJ Ibbs		6 July 1985
C
CMOD	Give sensible message, for default answer NO
C						RWR	 7-Jul-1986
CMOD	Altered to return an error code, with answer
c	in an argument				RWR	 9-May-1986
CMOD	Report an error in TTRLIN using LSL_GETFORMAT
CMOD	and WRITEF				TJI	14-Mar-1986
C
C To read a yes/no answer to a simple question
C
C arguments
C
	CHARACTER*(*)	PROMPT		! prompt string (question)
	LOGICAL		YESNO		! answer (.TRUE. for "YES")
	INTEGER*4	DEFAULT		! 0 = no default
					! 1 = default "YES"
					! 2 = default "NO"
	INTEGER*4	IERR		! error code	
C
C Function result is that returned by TTRSTR
C
C functions called
C
	INTEGER*4	TTRSTR		! read a line from the terminal
	LOGICAL		RDCHS		! read character from the line
	LOGICAL		HAVE_I_AN_ARG	! is DEFAULT there?
C
C workspace
C
	BYTE		CH		! character read
	INTEGER		DEF		! our copy of DEFAULT
	INTEGER		IER		! error code from TTRSTR
C
C
	INCLUDE	'LSL$CMNLSL:LSLLIBMSG.PAR/NOLIST'
	INCLUDE 'LSL$CMNLSL:RDYES.PAR/NOLIST'
***	COMMON/LSL$CMNLSL:EXPC/
C
C do we have a DEFAULT argument?
C
	IF ( HAVE_I_AN_ARG(3) ) THEN
	   DEF = DEFAULT
C
C make sure we have valid default 
C
	   IF (DEF.NE.ASSUME_NONE.AND.DEF.NE.ASSUME_YES
     &	                         .AND.DEF.NE.ASSUME_NO) THEN
	      DEF = ASSUME_NONE
	   ENDIF
	ELSE
	   DEF = ASSUME_NONE
	ENDIF
C
C expand the prompt a bit
C
10	CALL EXPAND('%S? ',  PROMPT)
C
C get a line of command
20	RDYES = TTRSTR(,,EXPDSC,IER)
	IF ( .NOT.RDYES ) THEN
	   IF (HAVE_I_AN_ARG(4)) IERR=IER
	   GOTO 9000
	ENDIF
C
C got some sort of answer
C
	CALL BSLN			! start at the beginning
C
	IF (RDCHS(CH)) THEN
	   IF ( DEF .EQ. ASSUME_YES ) THEN
	      YESNO = .TRUE.
	      GOTO 9000                             
	   ELSEIF ( DEF .EQ. ASSUME_NO ) THEN
	      YESNO = .FALSE.
	      GOTO 9000
	   ENDIF
	ELSEIF (CH.EQ.'Y' .OR. CH.EQ.'y') THEN
	   YESNO = .TRUE.			! answer was Yes
	   GOTO 9000
	ELSEIF (CH.EQ.'N' .OR. CH.EQ.'n') THEN
	   YESNO = .FALSE.			! answer was No
	   GOTO 9000
	ENDIF
C
C otherwise, we are giving help...
C
	IF ( DEF .EQ. ASSUME_NO) THEN
	   CALL WRITEF(
     &		'Please answer with Y for Yes, N or <return> for No')
	ELSEIF (DEF .EQ. ASSUME_YES) THEN
	   CALL WRITEF(
     &		'Please answer with N for No,  Y or <return> for Yes')
	ELSE
	   CALL WRITEF('Please answer with Y for Yes, N for No')
	ENDIF
	GOTO 10
C
9000	RETURN
	END
