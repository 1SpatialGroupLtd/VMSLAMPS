	SUBROUTINE JPINFO
C
***	MODULE	JPINFO
***	IDENT	02SE87
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Amended for LSLLIB			RJH	19-DEC-1985
C
	IMPLICIT NONE
C
C routine to print job information
C
C functions:
	INTEGER SYS$GETJPIW	! Get Job/Process Information
C
C parameters:
	INCLUDE '($JPIDEF)'
C
C workspace:
	INTEGER   IERR		! Error from queing of System service
	INTEGER*4 IOSB(2)	! status at completion of Sys service
	INTEGER*2 IOSBW(4)	! bottom word is error code
	EQUIVALENCE (IOSB,IOSBW)
C
	INTEGER   ITMLEN	! size of item list
	PARAMETER (ITMLEN=25)
	INTEGER*4 ITMLST(ITMLEN)
	INTEGER*2 ITMLST2(2*ITMLEN)
	EQUIVALENCE (ITMLST,ITMLST2)
C
	CHARACTER*128 IMAGNAME	! name of image being executed
	INTEGER	      IMAGELEN	! no. of chars
	CHARACTER*15  PRCNAM	! name of process
	INTEGER       PRCLEN	! no. of chars
	CHARACTER*7   TERMINAL	! terminal name
	INTEGER       TERMLEN	! no. of chars
	CHARACTER*15  USERNAME	! name of user
	INTEGER       USERLEN	! no. of chars
C
C code:
C
C set up the item list ( see SYSTEM SERVICES Reference Manual)
C
	ITMLST2(1)   =128
	ITMLST2(2)   =JPI$_IMAGNAME
	ITMLST (3/2+1) =%LOC(IMAGNAME)
	ITMLST (5/2+1) =%LOC(IMAGELEN)
C
	ITMLST2(7)  =7
	ITMLST2(8)  =JPI$_TERMINAL
	ITMLST (9/2+1)=%LOC(TERMINAL)
	ITMLST (11/2+1)=%LOC(TERMLEN)
C
	ITMLST2(13)  =15
	ITMLST2(14)  =JPI$_PRCNAM
	ITMLST (15/2+1)=%LOC(PRCNAM)
	ITMLST (17/2+1)=%LOC(PRCLEN)
C
	ITMLST2(19)  =15
	ITMLST2(20)  =JPI$_USERNAME
	ITMLST (21/2+1)=%LOC(USERNAME)
	ITMLST (23/2+1)=%LOC(USERLEN)
C
	ITMLST (25/2+1) =0		! to terminate it
C
C	queue the request
	IERR=SYS$GETJPIW(,		! efn
     &                   ,		! pid
     &                   ,		! prcnam
     &                   ITMLST,	! itmlst
     &                   IOSB,		! iosb
     &                   ,		! astadr
     &                   ,)		! astprm
C
	IF (.NOT.IERR) GOTO 20	! branch if failed to queue
C
C did the request complete OK ? - branch if it failed
	IF (.NOT.IOSBW(1)) GOTO 10
C
C tidy things up
	USERLEN = INDEX(USERNAME,' ') - 1
	IF (USERLEN.LE.0) USERLEN = 12
C
C print our results
	CALL EXPAND('User  %S, Process %S',USERNAME(1:USERLEN),
     &    PRCNAM(1:PRCLEN))
	IF (TERMLEN.GT.0) CALL APPEND(', on %S',TERMINAL(1:TERMLEN))
	CALL TTWSTR
	CALL WRITEF('Image %S',IMAGNAME(1:IMAGELEN))
	RETURN
C
C here if GETJPI failed to queue - should never happen so don't know 
C what to do except return
10	IERR = IOSBW(1)
20	RETURN
	END
