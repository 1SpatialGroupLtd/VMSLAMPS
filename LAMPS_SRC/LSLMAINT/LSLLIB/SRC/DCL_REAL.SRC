	INTEGER*4 FUNCTION DCL_REAL(CLD_LABEL,WHINGE)
C
***	MODULE	DCL_REAL
***	IDENT	10SE87
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England
C
CMOD	Array overflow test now uses /DCL_CLD/MAX_REAL
C	not /DCL_CLD/MAX_ARR				TJH 06-Oct-1986
CMOD	Bug fix. Now traps a '+' or '-' in middle of a
C       number, using DCL_SIGN.				TJH 16-Sep-1986
CMOD	LSL__RESPARSOVR now LSL__RESPARSOVF		RJH 29-Jan-1985
CMOD	/LSL_STATUS/ added				TJH 21-Nov-1985
C	Author						TJH 27-Aug-1985
C
C Function gets any real arguments associated with the DCL qualifier 
C CLD_LABEL and puts them into RARRAY(). Up to MAX_REAL arguments can be
C decoded and stored. Function returns set to SS$_NORMAL if all is well.
C Local error reporting (via condition handler) may be invoked by 
C setting WHINGE .true..
C
	IMPLICIT NONE
C
	INTEGER*4 	CLI$_ABSENT, CLI$GET_VALUE, DCL_SIGN
	EXTERNAL 	CLI$_ABSENT
C
	CHARACTER*16	CHAR_ARRAY	! found string argument
	INTEGER*2	CHAR_LEN        ! length of found string arg.
	CHARACTER*(*)	CLD_LABEL	! the CLD label for qualifier
	INTEGER*4	I		! loop var.
	INTEGER*4	IERR		! for error return
	LOGICAL*4	REPORT		! .T. if local error reporting
	LOGICAL*4	WHINGE		! .T. if local error reporting
C
C condition handler
	EXTERNAL	DCL_ERRET
C
C Common block to control error reporting in condition handler
	COMMON /LSL_REPORT/ REPORT	
C
	INCLUDE 	'($SSDEF)'
	INCLUDE 	'($STSDEF)'
	INCLUDE 	'LSL$CMNLSL:LSLLIBMSG.PAR'
	INCLUDE 	'LSL$CMNLSL:CLD.CMN'
	INCLUDE 	'LSL$CMNLSL:STATUS.CMN'
C
C ......................................................................
C
	NUMREA=0		! initialise number of args. counter
	REPORT=WHINGE		! is condition handler to scream?
C
C Establish condition handler
	CALL LIB$ESTABLISH( DCL_ERRET )
C
C Get first value for our label
	DCL_REAL= CLI$GET_VALUE (CLD_LABEL,
     &				 CHAR_ARRAY,
     &				 CHAR_LEN)
C
C ......................................................................
C
C Repeat until the list is depleted
C
	DO WHILE (DCL_REAL)
	    NUMREA=NUMREA+1
C
C
C Trap the potential RARRAY() overflow and issue the LSLLIB error 
C "result parse buffer overflow"
C
	    IF (NUMREA.EQ.MAX_REAL) THEN
	        DCL_REAL=LSL__RESPARSOVF
		CALL LIB$SIGNAL
     &			( %VAL(DCL_REAL) ) ! signal error
	        IF (WHINGE)
     &		           CALL WRITEF(' \%S\',CHAR_ARRAY(:CHAR_LEN))
C
C Revert to normal error processing
	        CALL LIB$REVERT
	        LSL_STATUS=DCL_REAL.OR.LSL_QUIET	! set $status 
	        RETURN
	    ENDIF
C
C
C As we have had to use a CLD $QUOTED_STRING type qualifier to get the 
C arguments in, the CLI will not have trapped any non numeric characters
C in what we are assuming will be real numbers
C
	    DO 10 I=1,CHAR_LEN
	      IF ((CHAR_ARRAY(I:I).LT.'0'
     &		  	.OR.CHAR_ARRAY(I:I).GT.'9')
     &	  	  	.AND.CHAR_ARRAY(I:I).NE.'+'
     &	  	  	.AND.CHAR_ARRAY(I:I).NE.'-'
     &	  	  	.AND.CHAR_ARRAY(I:I).NE.'.') 
     &						THEN
C
C Since the CLI does'nt know about real numbers we will have to issue a 
C LSLLIB message: "invalid syntax for value specification" here.
C
C
	        DCL_REAL=LSL__INVALSPEC
	        IF (WHINGE) THEN
	           CALL LSL_PUTMSG(DCL_REAL) 	! sets $status for you
		   CALL WRITEF(' \%S\',CHAR_ARRAY(:CHAR_LEN))
		ELSE
	           LSL_STATUS=DCL_REAL.OR.LSL_QUIET	! set $status 
		ENDIF
C
C Revert to normal error processing
	        CALL LIB$REVERT
	        RETURN
	     ENDIF
10	   CONTINUE
C
C
C Check that the user didn't specify 'n-n.n' or 'n+n.n' instead of 
C '-nn' or '+nn.n' 
	   DCL_REAL=DCL_SIGN(CHAR_ARRAY,CHAR_LEN,WHINGE)
	   IF (.NOT.DCL_REAL) GOTO 40
C
C Finally read the actual number!
C
	   READ(CHAR_ARRAY,*,ERR=20) RARRAY(NUMREA)
	   DCL_REAL= CLI$GET_VALUE (CLD_LABEL,
     &				    CHAR_ARRAY,
     &				    CHAR_LEN)
	END DO
C
C ......................................................................
C
C Make sure termination status was correct
C
	IF (DCL_REAL
     &		.OR.
     &		    (DCL_REAL.EQ. %LOC(CLI$_ABSENT) )) THEN
	    DCL_REAL=SS$_NORMAL
	ELSE IF (IBITS(DCL_REAL,0,3).NE.STS$K_WARNING) THEN
     	    CALL LIB$SIGNAL( %VAL(DCL_REAL) ) ! signal error
	ENDIF
	GOTO 40
C
C ......................................................................
C
C Error on internal read
C
20	CALL ERRSNS(,,,,DCL_REAL)
C Signal error to condition handler
	CALL LIB$SIGNAL ( %VAL(DCL_REAL) ) 
C
C Revert to normal error processing
40	CALL LIB$REVERT
	LSL_STATUS=DCL_REAL.OR.LSL_QUIET	! set $status 
	RETURN 	
	END
