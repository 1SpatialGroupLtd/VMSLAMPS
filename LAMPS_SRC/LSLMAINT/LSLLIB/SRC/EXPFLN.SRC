C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1986-05-07 11:21:12.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	INTEGER FUNCTION EXPFLN( STRING, STRLEN )
C
***	IDENT	07MY86
***	MODULE	EXPFLN
C
C	Copyright LaserScan Laboratories Ltd., Cambridge, England
C
CMOD 	Now works when compiled with bounds checking!	TJH 05-Feb-1986
CMOD	Allow null strings for all but node,device, and
CMOD	detect errors in such allowing			TJI 07-May-1986
C
C Author TJ Ibbs, 4 August 1984
C Amended for LSLLIB, 6 July 1985
C
C This routine concatenates the parts of a filename and assigns them
C to STRING
C
	IMPLICIT NONE
C
C the common block which holds the filename parts
C
***	COMMON/LSL$CMNLSL:FILENAME/
C
C and LSL error codes
C
***	PARAMETER/LSL$CMNLSL:LSLLIBMSG/
C
C and we were passed
C
	CHARACTER*(*)		STRING		! result string
	INTEGER*2		STRLEN		! length produced
C
C internal workspace
C
	CHARACTER*(C_MAX_SIZ+1)	WORKSP
	LOGICAL*4		FIRST		! .T. = first component
	LOGICAL*4		IGNORED		! .T. if ignored null field
C
C *********************************************************************
C
C now assemble the resultant string
C
	STRLEN  = 0
	FIRST   = .TRUE.
	IGNORED = .FALSE.
C
C NODE name - if present, may not be null
C
	IF (HAD_NOD) THEN
	   IF (LEN_NOD.GT.0) THEN
	      WORKSP = STR_NOD(:LEN_NOD) // '::'
	      FIRST=.FALSE.
	      STRLEN = STRLEN + LEN_NOD+2
	   ELSE
	      IGNORED = .TRUE.
	   ENDIF
	ENDIF
C
C DEVICE name - if present, may not be null
C
	IF (HAD_DEV) THEN
	   IF (LEN_DEV.GT.0) THEN
	      IF (FIRST) THEN
	         WORKSP = STR_DEV(:LEN_DEV) // ':'
	         FIRST=.FALSE.
	      ELSE
	         WORKSP = WORKSP(1:STRLEN) // STR_DEV(:LEN_DEV) // ':'
	      ENDIF
	      STRLEN = STRLEN + LEN_DEV + 1
	   ELSE
	      IGNORED = .TRUE.
	   ENDIF
	ENDIF
C
C DIRECTORY name - may be null
C
	IF (HAD_DIR) THEN
	   IF (FIRST) THEN
	      IF (LEN_DIR.GT.0) THEN
	         WORKSP = '[' // STR_DIR(:LEN_DIR) // ']'
	      ELSE
	         WORKSP = '[]'
	      ENDIF
	      FIRST=.FALSE.
	   ELSE
	      IF (LEN_DIR.GT.0) THEN
	         WORKSP = WORKSP(1:STRLEN) // '[' //STR_DIR(:LEN_DIR)// ']'
	      ELSE
	         WORKSP = WORKSP(1:STRLEN) // '[]'
	      ENDIF
	   ENDIF
 	   STRLEN = STRLEN + LEN_DIR + 2
	ENDIF
C
C file NAME - may be null, in which case we need take no action,
C	      as there is no associated punctuation
C
	IF (HAD_NAM) THEN
	   IF (LEN_NAM.GT.0) THEN
	      IF (FIRST) THEN
	         WORKSP = STR_NAM(:LEN_NAM)
	         FIRST=.FALSE.
	      ELSE
	         WORKSP = WORKSP(1:STRLEN) // STR_NAM(:LEN_NAM)
	      ENDIF
	      STRLEN = STRLEN + LEN_NAM
	   ENDIF
	ENDIF
C
C EXTENSION - may be null
C
	IF (HAD_EXT) THEN
	   IF (FIRST) THEN
	      IF (LEN_EXT.GT.0) THEN
	         WORKSP = '.' // STR_EXT(:LEN_EXT)
	      ELSE
	         WORKSP = '.'
	      ENDIF
	      FIRST=.FALSE.
	   ELSE
	      IF (LEN_EXT.GT.0) THEN
	         WORKSP = WORKSP(1:STRLEN) // '.' // STR_EXT(:LEN_EXT)
	      ELSE
	         WORKSP = WORKSP(1:STRLEN) // '.'
	      ENDIF
	   ENDIF
	   STRLEN = STRLEN + LEN_EXT + 1
	ENDIF
C
C VERSION - may be null
C
	IF (HAD_VER) THEN
	   IF (FIRST) THEN
	      IF (LEN_VER.GT.0) THEN
	         WORKSP = ';' // STR_VER(:LEN_VER)
	      ELSE
	         WORKSP = ';'
	      ENDIF
	      FIRST=.FALSE.
	   ELSE
	      IF (LEN_VER.GT.0) THEN
	         WORKSP = WORKSP(1:STRLEN) // ';' // STR_VER(:LEN_VER)
	      ELSE
	         WORKSP = WORKSP(1:STRLEN) // ';'
	      ENDIF
	   ENDIF
	   STRLEN = STRLEN + LEN_VER + 1
	ENDIF
C
C and copy this result over to the argument
C
	STRING = WORKSP
C
C do we have an error return? if so, return what happened
C
	IF ( IGNORED ) THEN
	   EXPFLN = LSL__NOFIELD		! field had no length
	ELSEIF ( STRLEN.EQ.0 ) THEN
	   EXPFLN = LSL__FILNOLEN		! bug - total length = 0
	ELSEIF ( STRLEN.GT.LEN(STRING) ) THEN
	   EXPFLN = LSL__FILTOOLONG		! name was truncated
	ELSE
	   EXPFLN = LSL__NORMAL			! all was OK
	ENDIF
C
	RETURN
	END
