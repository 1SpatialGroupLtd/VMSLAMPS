	.TITLE	VIOARG
	.IDENT	/28JA86/
;
;MOD	amended for LSLLIB				RJH	28-Jan-1986
;MOD	SYS$LIBRARY => LSL$LIBRARY			AJW	06-Dec-1983
; Copyright  Laser-Scan Laboratories Limited, Cambridge, England
; Author:	Paul Hardy
;
	.LIBRARY	/LSL$LIBRARY:LSLMACLIB/
	CODE_PSECT	LSL

; VIOLIB private FORTRAN argument handling interface
; eg 	JSB	VIO$1ST			; basic init
;	MOVAL	DEFAULT,R0		; address of default
;	JSB	VIO$GNA			; get first arg
;	PUSHL	R0			; save it
;	JSB	VIO$GNA			; and next arg (no default)
;	BCS	ERROR			; missing arg error
;	...

; VIO$1ST - basic init at start of call

VIO$1ST::CLRL	R11			; number of args so far
	RSB


; get next arg - set carry if absent
; - assumes nr of args so far left in R11
; result is address in R0 (untouched if arg missing)

VIO$GNA::ACBB	(AP),#1,R11,20$		; j if some left
10$:	SEC				; no arg found
	RSB

20$:	TSTL	(AP)[R11]		; is it there ?
	BEQL	10$			; j if this one absent
	MOVL	(AP)[R11],R0		; return pointer in R0
	CLC				; arg present
	RSB

	.PSECT
	.END
