	INTEGER*4 FUNCTION ARGSPC(NUMINT, NUMREA, LOGARG,
     &				  STRARG, CHRARG,
     &				  ARGFUL,
     &				  PRMEQU, PRMIEQ, PRMCOM)
C
***	MODULE ARGSPC
***	IDENT 06JL85
C
	IMPLICIT NONE
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England
C Author R. Fairbairns
C
C Coded August 1984
C
C To construct an argument specification word from the impossibly
C		complicated argument list
C
C Arguments:
	INTEGER*4	numint		!I number of integer args (0-7)
	INTEGER*4	numrea		!R number of real args (0-7)
	LOGICAL*1	logarg		!+ logical argument to
C					!		be recognised
	LOGICAL*1	strarg		!S string argument required
	LOGICAL*1	chrarg		!C character argument required
	LOGICAL*1	argful		!F all arguments (other than a
C					!		logical) must
C					!		be present
	LOGICAL*1	prmequ		!= permit '=' after command,
C					!		before arguments
	LOGICAL*1	prmieq		!Q permit inequalities
	LOGICAL*1	prmcom		!. permit comma between
C					!		num arguments
					!
					!*_<-this field above is the
C					!		     $CMD flag
C
C Function result is arg spec required. Note that this function
C	describes itself as an INTEGER*4, despite the documentation
C	saying it's an INTEGER*2: this is for possible upwards
C	compatibility. (It also avoids problems for users who forget
C	what's what in the argument specification field.)
C
C Workspace:
	INTEGER*2	SPEC		! our final result
	INTEGER*4	ACT_NI		! actual number of int args
	INTEGER*4	ACT_NR		! actual number of real args
C
***	PARAMETER	/LSL$CMNLSL:CMDFLG/
C
C limit integer, real, argument counts
	IF (NUMINT.LE.0) THEN
	  ACT_NI = 0
	ELSE IF (NUMINT.GE.7) THEN
	  ACT_NI = 7
	ELSE
	  ACT_NI = NUMINT
	ENDIF
	IF (NUMREA.LE.0) THEN
	  ACT_NR = 0
	ELSE IF (NUMREA.GE.7) THEN
	  ACT_NR = 7
	ELSE
	  ACT_NR = NUMREA
	ENDIF
C
C basic first stab
	SPEC = ACT_NI + ISHFT(ACT_NR, CMD$V_REALS)
C
C now the logical arguments to this routine
	IF (LOGARG) SPEC = SPEC .OR. CMD$M_LOG
	IF (STRARG) SPEC = SPEC .OR. CMD$M_STRING
	IF (CHRARG) SPEC = SPEC .OR. CMD$M_CHAR
	IF (ARGFUL) SPEC = SPEC .OR. CMD$M_FULL
	IF (PRMEQU) SPEC = SPEC .OR. CMD$M_EQUAL
	IF (PRMIEQ) SPEC = SPEC .OR. CMD$M_INEQUAL
	IF (PRMCOM) SPEC = SPEC .OR. CMD$M_COMMA
C
C return result
	ARGSPC = SPEC
C
	RETURN
	END
