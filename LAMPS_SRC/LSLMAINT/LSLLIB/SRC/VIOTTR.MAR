;
; * This file is part of the LAMPS distribution, released as a software
; * preservation project to archive digital history for future historians.
; * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
; *
; * Timestamp of this file for the 2002 release was: 1986-05-09 15:37:50.000000000 +0100
; *
; * This program is free software: you can redistribute it and/or modify
; * it under the terms of the GNU General Public License as published by
; * the Free Software Foundation, version 3.
; *
; * This program is distributed in the hope that it will be useful, but
; * WITHOUT ANY WARRANTY; without even the implied warranty of
; * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
; * General Public License for more details.
; *
; * You should have received a copy of the GNU General Public License
; * along with this program. If not, see http://www.gnu.org/licenses/.
;
	.TITLE	VIOTTR
	.IDENT	/09MY86/
;
	$SSDEF				; system error returns
	$RMSDEF				; and RMS's
;
; Copyright Laser-Scan Laboratories Ltd,Cambridge,England
;
; Author Paul Hardy
;
; TTATT, TTDET and TTATC added by TJI, 01.June.1982
; Modified for LSLLIB, R J Hulme	24-Jan-1986
;
;MOD	09-May-1986	TJ Ibbs
;MOD	correct the error handling code, to stop it access violating
;MOD	save R3 for both TTRxxx routines
;
; standard terminal input routine to go with RDCHS etc
 
; MUST be assembled with TXTC.MAR and LSLLIBMSG.MAR

	DATA_PSECT	LSL

TTRDSC:					; string descriptor for VIO$GET_INPUT
TTRLEN:	.BLKW	1			; string length
	.BYTE	0,0			; class & type
TTRPTR:	.BLKL	1			; pointer to buffer (unset)
 
	CODE_PSECT	LSL

; TTRLIN & TTRSTR read a line using VIO$GET_INPUT
; the default buffer is TXTC as used by RDCH
; RET=TTRLIN([BUF],    [NCHS], [LIM], ['prompt'], [ierr])
;							read to byte array
; RET=TTRSTR([STRING], [NCHS],        ['prompt'], [ierr])
;							read to character string
 
	.ENTRY	TTRSTR,^M<R2,R3,R11>
	JSB	VIO$1ST			; basic init
	MOVAW	TTRLEN,R2		; if explicit arg then dont want length
	JSB	VIO$GNA			; arg is string descriptor
	BCC	10$			; explicit string
	MOVAL	TXTDSC,R0		; default destination is TXTC
	MOVAW	TXTPTR,R2		; so place for resultant length
	MOVW	TXTLIM,TXTPTR		; all is available
10$:	MOVL	(R0)+,R1		; length and crud
	MOVW	R1,TTRLEN		; just the length
	MOVL	(R0),TTRPTR		; and the address
	MOVL	R2,R0			; address of default length
	JSB	VIO$GNA			; get the next argument
	MOVL	R0,R2			; save the length address
	BRB	GOTDSC			; got descriptor so common code now
 
	.ENTRY	TTRLIN,^M<R2,R3,R11>
	JSB	VIO$1ST			; basic init for arg handling
	MOVAB	TXTBUF,R0		; default buffer is TXTBUF
	JSB	VIO$GNA			; first arg is buffer to read into
	MOVL	R0,TTRPTR		; set buffer address
	MOVAW	TXTPTR,R0		; default for second arg is TXTPTR
	JSB	VIO$GNA			; get second arg (resultant length)
	MOVL	R0,R2			; save address in R2 for the moment
	MOVAW	TXTLIM,R0		; default for third arg is TXTLIM
	JSB	VIO$GNA			; third arg is buffer size
	MOVW	(R0),TTRLEN		; set in descriptor
 
; here with destination set up - look if prompt reqd
GOTDSC:	MOVL	#1,R1			; at least one arg to VIO$GET_INPUT
	JSB	VIO$GNA			; fourth arg is prompt string
	BCS	10$			; missing
	INCL	R1			; one more arg for GET_INPUT
	PUSHL	R0			; prompt string address
10$:	PUSHAQ	TTRDSC			; destination descriptor address
	CALLS	R1, G^VIO$GET_INPUT	; read from SYS$INPUT
	BLBC	R0,99$			; error in read
	MOVL	TTRPTR,R1		; address of buffer
	DECL	R1			; to allow 1st byte = 1
	MOVZWL	TTRLEN,R0		; maximum string size
20$:	CMPB	(R1)[R0],#^A/ /		; space ?
	BNEQ	30$			; jump at first non-space from end
	SOBGTR	R0,20$			; more to scan
30$:	MOVW	R0,(R2)			; no of chars read
	MOVL	#LSL__NORMAL,R0		; normal return
40$:	RET
 
; here with error in read
99$:	MOVL	R0, R3			; save the error
	CMPL	R3, #SS$_ENDOFFILE	; is it (system) end of file?
	BEQL	98$			; if so, go and set error
	CMPL	R3, #RMS$_EOF		; is it (RMS) end of file? 
	BNEQ	97$			; if not, go and deal with it
98$:	MOVL	#LSL__EOF,R0		; 'end of file error'
	BRB	40$			; common exit
97$:	BSBW	VIO$GNA			; is there an IERR argument?
	BCS	96$			; branch if not
	MOVL	R3, (R0)		; return actual error
96$:	MOVL	#LSL__SYSERR,R0		; 'any old error'
	BRB	40$			; common exit

	.PSECT
	.END
