;
; * This file is part of the LAMPS distribution, released as a software
; * preservation project to archive digital history for future historians.
; * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
; *
; * Timestamp of this file for the 2002 release was: 1987-09-10 14:00:04.000000000 +0100
; *
; * This program is free software: you can redistribute it and/or modify
; * it under the terms of the GNU General Public License as published by
; * the Free Software Foundation, version 3.
; *
; * This program is distributed in the hope that it will be useful, but
; * WITHOUT ANY WARRANTY; without even the implied warranty of
; * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
; * General Public License for more details.
; *
; * You should have received a copy of the GNU General Public License
; * along with this program. If not, see http://www.gnu.org/licenses/.
;
	.TITLE	SET_CTRLC_AST
	.IDENT	/10SE87/

;
; program for an AST to trap out-of-band characters
; i.e. control characters 0-31
;
; author TJI, 29.June.1982
; amended for LSLLIB, R J Hulme			28-Jan-1986
; tidided, CCB					 8-Jul-1987
;
	.LIBRARY/LSL$LIBRARY:LSLMACLIB/

	$IODEF				; define I/O function codes
	$QIODEF				; and QIO argument list
;
	CODE_PSECT	LSL
;
; call SET_CTRLC_AST( [AST_routine] [,input]) from Fortran
;
; AST_routine is the routine to set up. This should thus be declared
; EXTERNAL in the Fortran. If it is omitted, then the same AST as last
; time is used.
;
; input       is the name of the device to place the QIO on
; (default SYS$INPUT). This is a STRING. By default, uses the last given
; device.
;
; The AST is cancelled after use and should be re-enabled if required
;
; **************************************************************************
;
	.ENTRY	SET_CTRLC_AST, ^M<R11>
;
	JSB	VIO$1ST				; init argument reading
;
	JSB	VIO$GNA				; get the AST
	BCS	1$				; no arg
	MOVAL	(R0), QI.DPB+QIO$_P1		; remember it
;
1$:	MOVAQ	TT_DSC,R0			; default input
	JSB	VIO$GNA				; get input
	BCC	4$				; present
	TSTL	QI.DPB+QIO$_CHAN		; already got channel?
	BNEQ	5$				; yes
;
4$:	$ASSIGN_S	DEVNAM=(R0),-
			CHAN=QI.DPB+QIO$_CHAN 
	BLBC	R0,ERROR			; jump if error
;
5$:	$QIOW_G	QI.DPB
	BLBC	R0,ERROR			; jump on error
	MOVZWL	IOSB,R0
	BLBC	R0,ERROR			; jump on error
	RET
;
ERROR:	PUSHL		R0
	CALLS		#1,G^LIB$SIGNAL		; complain
	RET
;
	DATA_PSECT	LSL

IOSB:	.BLKQ	1
QI.DPB:	$QIO	CHAN=0,FUNC=IO$_SETMODE!IO$M_CTRLCAST,IOSB=IOSB,P1=0
TT_DSC:	.ASCID	/SYS$INPUT/	; default device

	.END
