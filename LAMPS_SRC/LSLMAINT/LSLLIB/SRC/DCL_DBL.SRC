C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1987-09-10 11:43:10.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	INTEGER*4 FUNCTION DCL_DBL(CLD_LABEL,WHINGE)
C
***	MODULE	DCL_DBL
***	IDENT	10SE87
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England
C
CMOD	Array overflow test now uses /DCL_CLD/MAX_REAL
C	not /DCL_CLD/MAX_ARR				TJH 06-Oct-1986
CMOD	LSL__RESPARSOVR now LSL__RESPARSOVF		RJH 29-Jan-1985
CMOD	/LSL_STATUS/ added				TJH 20-Nov-1985
C	Author						TJH 20-Sep-1985
C
C Function gets any real*8 arguments associated with the DCL qualifier 
C CLD_LABEL and puts them into DBLRAY(). Up to MAX_REAL arguments can be
C decoded and stored. Function returns set to SS$_NORMAL if all is well.
C Local error reporting (via condition handler) may be invoked by 
C setting WHINGE .true..
C
	IMPLICIT NONE
C
	INTEGER*4 	CLI$_ABSENT, CLI$GET_VALUE
	EXTERNAL 	CLI$_ABSENT
C
	CHARACTER*16	CHAR_ARRAY	! found string argument
	INTEGER*2	CHAR_LEN        ! length of found string arg.
	CHARACTER*(*)	CLD_LABEL	! the CLD label for qualifier
	INTEGER*4	I		! loop var.
	INTEGER*4	IERR		! for error return
	LOGICAL*4	REPORT	! .T.= condition handler error reporting
	LOGICAL*4	WHINGE		! .T. if local error reporting
C
C condition handler
	EXTERNAL	DCL_ERRET
C
C Common block to control error reporting in condition handler
	COMMON /LSL_REPORT/ REPORT	
C
	INCLUDE 	'($SSDEF)'
	INCLUDE 	'($STSDEF)'
	INCLUDE 	'LSL$CMNLSL:LSLLIBMSG.PAR'
	INCLUDE 	'LSL$CMNLSL:STATUS.CMN'
	INCLUDE 	'LSL$CMNLSL:CLD.CMN'
C
C ......................................................................
C
	NUMDBL=0		! initialise number of args. counter
	REPORT=WHINGE		! is condition handler to scream?
C
C Establish condition handler
	CALL LIB$ESTABLISH( DCL_ERRET )
C
C Get first value for our label
	DCL_DBL= CLI$GET_VALUE (CLD_LABEL,
     &				 CHAR_ARRAY,
     &				 CHAR_LEN)
C
C ......................................................................
C
C Repeat until the list is depleted
C
	DO WHILE (DCL_DBL)
	    NUMDBL=NUMDBL+1
C
C Trap the potential DBLRAY() overflow and issue the  LSLLIB error 
C "result parse buffer overflow"
	    IF (NUMDBL.GT.MAX_REAL) THEN
		DCL_DBL=LSL__RESPARSOVF
	 	LSL_STATUS=LSL__RESPARSOVF.OR.LSL_QUIET
	        IF (WHINGE) CALL LSL_PUTMSG
     &			( LSL__RESPARSOVF ) ! report error locally?
C
C Revert to normal error processing
	        CALL LIB$REVERT
C
	        RETURN
	    ENDIF
C
C
C As we have had to use a CLD $QUOTED_STRING type qualifier to get the 
C arguments in, the CLI will not have trapped any non numeric characters
C in what we are assuming will be real numbers
	    DO 10 I=1,CHAR_LEN
	      IF ((CHAR_ARRAY(I:I).LT.'0'
     &		  	.OR.CHAR_ARRAY(I:I).GT.'9')
     &	  	  	.AND.CHAR_ARRAY(I:I).NE.'D'
     &	  	  	.AND.CHAR_ARRAY(I:I).NE.'d'
     &	  	  	.AND.CHAR_ARRAY(I:I).NE.'+'
     &	  	  	.AND.CHAR_ARRAY(I:I).NE.'-'
     &	  	  	.AND.CHAR_ARRAY(I:I).NE.'.') 
     &						THEN
C
C Since the CLI does'nt know about real numbers we will have to issue a 
C LSLLIB message: "invalid syntax for value specification" here.
C
	        DCL_DBL=LSL__INVALSPEC
	        IF (WHINGE) THEN		! report error locally ?
	            CALL LSL_PUTMSG(DCL_DBL)
	            CALL WRITEF(' \%S\',CHAR_ARRAY(:CHAR_LEN))
	        ENDIF
C
C Revert to normal error processing
	        CALL LIB$REVERT
C
	        RETURN
	     ENDIF
10	   CONTINUE
C
C Finally read the actual number!
	   READ(CHAR_ARRAY,*,ERR=20) DBLRAY(NUMDBL)
	   DCL_DBL= CLI$GET_VALUE (CLD_LABEL,
     &				    CHAR_ARRAY,
     &				    CHAR_LEN)
	END DO
C
C ......................................................................
C
C Make sure termination status was correct
C
	IF (DCL_DBL.OR.(DCL_DBL.EQ. %LOC(CLI$_ABSENT) )) THEN
	   DCL_DBL=SS$_NORMAL
	ELSE IF (.NOT. DCL_DBL
     &		.AND.(IBITS(DCL_DBL,0,3).NE.STS$K_WARNING)) THEN
     	   CALL LIB$SIGNAL( %VAL(DCL_DBL) ) ! signal error
	ENDIF
	GOTO 30
C
C ......................................................................
C
C Error on internal read
C
20	CALL ERRSNS(,,,,DCL_DBL)
	CALL LSL_PUTMSG(DCL_DBL) 
C
C Revert to normal error processing
30	CALL LIB$REVERT
C
C Set STATUS for possible $STATUS useage on exit
	LSL_STATUS=DCL_DBL.OR.LSL_QUIET
C
	RETURN
	END
