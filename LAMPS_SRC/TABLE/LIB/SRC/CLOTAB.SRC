	LOGICAL FUNCTION CLOSE_TAB( IERR )
C
***	IDENT	16DE89
***	MODULE	CLOSE_TAB
C
C	Copyright LaserScan Laboratories Ltd., Cambridge, England
C
C Author TJ Ibbs, 4 September 1984
C
C This is the final closedown routine for the TABLE_MONITOR library
C - it puts the table monitor process back to sleep, and undoes
C everything in sight
C
C we return FALSE if it all works, and TRUE if we fail
C
	IMPLICIT NONE
C
C system parameters
C
	INCLUDE	'($SSDEF)'
C
C and our own parameters
C
***	PARAMETER/CMN:TABMON/		! names, offsets, etc
***	COMMON/CMN:SECTION/
***	COMMON/CMN:LOCK/
***	COMMON/CMN:TABLE/
C
C we were passed
C
	INTEGER*4	IERR		! error return
C
C and we call
C
	INTEGER*4	SYS$SETEF	! set the 'hibernate' flag
	INTEGER*4	SYS$DELTVA	! remove ref to section
	INTEGER*4	SYS$DEQ		! undo our lock
	INTEGER*4	SYS$DACEFC	! lose common efc
	LOGICAL		STOP_TAB	! stop the thing down
C
C assume that things go badly
C
	CLOSE_TAB = .TRUE.
C
C stop things first
C - if we fail because the TABLE_MONITOR is already hibernating
C (ie IERR returns as -2) then we carry on - a call of STOP followed by
C a call of CLOSE is legal
C
	IF ( STOP_TAB( IERR ) ) THEN
	   IF (IERR.NE.-2) GOTO 9999
	ENDIF
C
C and now clear the rest up...
C unmap the global section
C
	IERR = SYS$DELTVA( SEC_ADDR, , )
	IF ( IERR.NE.SS$_NORMAL ) GOTO 9999
C
C remove the associated event flag cluster
C
	IERR = SYS$DACEFC( %VAL(EFC_FIRST) )
	IF ( IERR.NE.SS$_NORMAL ) GOTO 9999
C
C and finally, relinquish our locks on the section and the table
C
	IERR = SYS$DEQ( %VAL(LOCKID), , , )
	IF ( IERR.NE.SS$_NORMAL ) GOTO 9999
C
	IERR = SYS$DEQ( %VAL(TABLE_LOCKID), , , )
	IF ( IERR.NE.SS$_NORMAL ) GOTO 9999
C
C so we have presumably succeeded in stopping things altogether...
C
	CLOSE_TAB = .FALSE.
C
C failure joins us here
C
9999	RETURN
	END

