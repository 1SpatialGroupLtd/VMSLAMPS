C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1988-02-08 17:23:26.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION DIGTRN( BUTTN, COORDX, COORDY, USEFUN, FUNC )
C
***	IDENT	08FE88
***	MODULE	DIGTRN
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C
	IMPLICIT NONE
C
C function to decode string from table
C
C *** return value is TRUE if fail, FALSE if succeed ***
C
	INTEGER*4	BUTTN	! button used on puck
	INTEGER*4	COORDX	! X coordinate from table
	INTEGER*4	COORDY	! Y coordinate
	LOGICAL		USEFUN	! use routine FUNC to do work
	LOGICAL		FUNC	! routine to use
C
C common, etc
C
***	COMMON/HERE:TABIFC/		! general table related stuff
C
C w/s
C
	BYTE		ICH		! character read from buffer
	INTEGER		OLD_DCPTR	! preserve current DCPTR
C
C and some relevant characters
C
	PARAMETER	DOLLAR = 36		! '$'
	PARAMETER	COMMA  = 44		! guess!
C
C functions called
C
	LOGICAL		RDCH		! read next char
	LOGICAL		RDLONG		! read a long integer
C
C *********************************************************************
C
C the Altek table returns strings of the form:
C
C	$A,12345,12345
C
C that is:
C
C	<dollar><button number><comma><X coord><comma><Y coord><CR>
C
C which gives us 14 characters in a well-formed string (we never
C actually see the <CR> - it just serves as a terminator)
C
C *********************************************************************
C
C if using supplied function, then do so
C
	IF (USEFUN) THEN
	   DIGTRN = FUNC(TABBUF,TABLEN,BUTTN,COORDX,COORDY)
	   GOTO 9999
	ENDIF
C
C otherwise do the job ourselves
C
	DIGTRN = .TRUE.			! assume failure
	IF (TABLEN.NE.14) GOTO 9999	! bad string length
C
C Now check that the commas separating things are in exactly the
C right places
C
	IF ( TABBUF(3).NE.COMMA  .OR.
     &	     TABBUF(9).NE.COMMA ) GOTO 9999
C
C save our DCPTR value
C
	CALL DCPSAV( OLD_DCPTR )
C
C and let us read from s'thing which isn't TXTC
C ie we want to read the table line from TABBUF...
C
	CALL SETAUX( TABBUF, TABLEN )	! this also moves DCPTR to
C					! the start of the buffer
C *********************************************************************
C
C now read from it
C
	IF (RDCH(ICH))     GOTO 9998	! read the DOLLAR
	IF (ICH.NE.DOLLAR) GOTO 9998	! was it a $ ?
	IF (RDCH(ICH))   GOTO 9998	! read the button
	IF (ICH.GE.'0'.AND.ICH.LE.'9') THEN
	   BUTTN = ICH - 48		! a numeral
	ELSE
	   BUTTN = ICH - 55		! a letter
	ENDIF
C
C check value of button (note H is returned when table is interrogated)
	IF ((BUTTN.LT.0.OR.BUTTN.GT.'F'X).AND.BUTTN.NE.17) GOTO 9998
	IF (RDCH(ICH))     GOTO 9998	! read the comma
	IF (ICH.NE.COMMA)  GOTO 9998	! bad separator ?
	IF (RDLONG(COORDX))GOTO 9998	! read the X coord
	IF (RDCH(ICH))     GOTO 9998	! read next comma
	IF (ICH.NE.COMMA)  GOTO 9998	! bad separator ?
	IF (RDLONG(COORDY))GOTO 9998	! read the Y coord
C
C if we are outside the ordinary area of the table (ie below or to the
C left of the origin) then we will get coordinates greater than 90000
C Apart from the fact we shouldn't really be digitising here, we can
C correct this by making the numbers negative...
C
	IF (COORDX.GT.90000) THEN
	   COORDX = COORDX - (99999+1)
	ENDIF
C
	IF (COORDY.GT.90000) THEN
	   COORDY = COORDY - (99999+1)
	ENDIF
C
	IF (.NOT.RDCH(ICH)) GOTO 9998	! spurious trailing chars ?
C
C success !
C
	DIGTRN = .FALSE.
C
9998	CALL BSLN			! side effect is to set current
C					! buffer back to TXTC again
	CALL DCPSET( OLD_DCPTR )	! and back to our place in line
C					! (set DCPTR to what it was)
C
9999	RETURN
	END
