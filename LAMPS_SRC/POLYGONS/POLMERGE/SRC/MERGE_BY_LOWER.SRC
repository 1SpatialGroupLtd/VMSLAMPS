C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1987-10-10 16:00:08.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	INTEGER*4 FUNCTION MERGE_BY_LOWER
     &                     (SUBJ_INDEX,OBJ_INDEX,REAL_NEIGH) 
C
***	MODULE	MERGE_BY_LOWER
***	IDENT	10OC87
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England
C
C Author D R CATLOW
C
C Created					DRC    26-Aug-1987
C
C Function:
C Determines using CHECK_NUMERIC_LABEL whether the label
C associated with SUBJ_INDEX polygon is numeric, and then looks 
C at all the polygons in NEIGH_BUF in turn to find a neighbour
C with a numeric code that is lower.
C When a number of neighbours have lower codes, the index of the
C neighbouring polygon with the highest value is returned.
C In the event of more than 1 neighbouring polygon having the same
C numeric value, the index of the first polygon encounterd
C in the neighbour list is returned.
C The index of the valid neighbour is returned in OBJ_INDEX.
C
C Only polygons with labels composed ENTIRELY of numerals are
C considered.  The routine CHECK_NUMERIC_LABEL is called to
C perform this check, and return the decoded number.
C The numeric label is treated as a floating point value,
C although in most circumstances it is likely to be integer.
C
C If the label of the SUBJ_INDEX polygon is not numeric the
C function returns FALSE.
C If none of the neighbouring polygons have a numeric label
C higher than that of the SUBJ_INDEX polygon,
C OBJ_INDEX will be 0, and the function will return FALSE.
C 
C 
	IMPLICIT NONE
C
	INCLUDE 	'LSL$CMNPOLYGONS:PARAMS.PAR'
C
C Common:
	INCLUDE		'LSL$CMNPOLYGONS:POLYGONS.CMN'
	INCLUDE		'LSL$CMNPOLMERGE:MERGE.CMN'
	INCLUDE		'LSL$CMNPOLMERGE:COMMAND.CMN'
C 
C Arguments
	INTEGER*4	SUBJ_INDEX	! index of the primary polygon
	INTEGER*4	OBJ_INDEX	! the index of the secondary 
C					! polygon that has the highest
C					! numeric label
	INTEGER*4	REAL_NEIGH	! real neighbour index
C
C Locals
	REAL		SUBJECT_VALUE	! numeric value of subject polygon
	REAL		OBJECT_VALUE	! numeric value of object polygon
	REAL		HIGHEST_OBJECT_VALUE	! numeric value of object
C					! polygon that is the highest valid
C					! value
	INTEGER*4	IERR		! function return
	INTEGER*4	I		! counter
	LOGICAL		FIRST		! first valid neighbour? (.T.)
C
C Functions
	INTEGER*4	CHECK_NUMERIC_LABEL	!to check if the label
C					!is composed entirely of numeric
C					!characters forming a valid number.
C					
C-------------------------------------------------------------------------
C Assume success
	MERGE_BY_LOWER=.TRUE.
C
C Initialise secondary polygon index
	OBJ_INDEX=0
C
C Test the label of the SUBJ_INDEX polygon.
	IERR=CHECK_NUMERIC_LABEL(SUBJ_INDEX,SUBJECT_VALUE)
	IF(.NOT.IERR)THEN		!not a valid numeric label
	  MERGE_BY_LOWER=.FALSE.	!signal failure
	  GOTO 999
	ENDIF
C
C Set FIRST to true to indicate that the first neighbour with a lower
C value that is found, is the first such neighbour.
	FIRST=.TRUE.
C
C Now test each neighbour polygon in turn.
	DO 10 I = 1,NUM_NEIGH
C
	   IF(NEIGH_BUF(1,I).EQ.0)GOTO 10
C
C don't count bounding polygon
	   IF (NEIGH_BUF(1,I).EQ. BOUND_POL) THEN
	      IF (HAD_BOUNDING) THEN
	          MERGE_BY_LOWER = .FALSE.
	          GOTO 999
	      ENDIF
	      GOTO 10
	   ENDIF
C
	   IERR=CHECK_NUMERIC_LABEL(ABS(NEIGH_BUF(1,I)),OBJECT_VALUE)
	   IF(.NOT.IERR)GOTO 10		!not a numeric label
C
C Is the object value greater than the subject value?
	   IF(OBJECT_VALUE.GT.SUBJECT_VALUE)GOTO 10	!no
C
C Is the object value higher than the current highest valid object value?
C If first polygon set highest value equal to the object value
	   IF(FIRST)THEN
             HIGHEST_OBJECT_VALUE=OBJECT_VALUE
	     OBJ_INDEX=NEIGH_BUF(1,I)
	     REAL_NEIGH=NEIGH_BUF(2,I)
	     FIRST=.FALSE.
	   ELSE
C Test if label value is higher than the highest valid value
	     IF(OBJECT_VALUE.GT.HIGHEST_OBJECT_VALUE)THEN
C Update lowest object value,obj_index and real_neigh
               HIGHEST_OBJECT_VALUE=OBJECT_VALUE
	       OBJ_INDEX=NEIGH_BUF(1,I)
	       REAL_NEIGH=NEIGH_BUF(2,I)
	     ENDIF
           ENDIF
C	
10	CONTINUE
C
C	If obj_index is still set to 0 (ie. no valid neighbour has 
C	been found), set function return to false.
	IF(OBJ_INDEX.EQ.0)MERGE_BY_LOWER=.FALSE.
C
999	RETURN
C
	END
