	INTEGER*4 FUNCTION REMOVE_DOUBLE_SPACES (IN_STRING, OUT_STRING)
C
***	MODULE REMOVE_DOUBLE_SPACES
***	IDENT 11AU87
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England
C
C	Coded					DRC 	11-Aug-1987
C
C	Function:
C	To search input string for multiple consecutive spaces,
C	and convert to a single space.
C	eg. "DAVE   CATLOW"  becomes "DAVE CATLOW" 
C	The function returns the length of the destination string
C	
	IMPLICIT NONE
C
C	Arguments:
	CHARACTER*(*)   IN_STRING	  !source string
	CHARACTER*(*)   OUT_STRING	  !destination string
C
C	Local
	INTEGER*4	LENGTH_IN_STRING  !length of input string
	INTEGER*4	I,J,K		  !counters
	LOGICAL		HAD_ONE_SPACE	  !had a space character
C
C-------------------------------------------------------------------------
C
C	Get length of source and destination strings
	LENGTH_IN_STRING = LEN(IN_STRING)
C
C	Deal with leading spaces (these are totally removed)
	DO 100 I=1,LENGTH_IN_STRING
	   IF(IN_STRING(I:I).NE.' ')GOTO 200
100	CONTINUE
	REMOVE_DOUBLE_SPACES=0	!output string length
	RETURN
C
200	HAD_ONE_SPACE=.FALSE.
	K = 1
	DO 300 J=I,LENGTH_IN_STRING
           IF(IN_STRING(J:J).NE.' ')THEN	   
              OUT_STRING(K:K)=IN_STRING(J:J)
	      HAD_ONE_SPACE=.FALSE.
	   ELSE
	      IF(HAD_ONE_SPACE)GOTO 300
              OUT_STRING(K:K)=IN_STRING(J:J)
	      HAD_ONE_SPACE=.TRUE.              
	   ENDIF
	   K=K+1
300	CONTINUE
C
	REMOVE_DOUBLE_SPACES=K-1
C
	RETURN
C
	END
