	LOGICAL*4 FUNCTION GET_LINE_BOXES(HADBOX,I)
C
***	MODULE	GET_LINE_BOXES
***	IDENT	27AU91
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England
C
CMOD	Modified to use LOGICAL*4 for HADBOX		AC  12-Oct-1988
C	Author						AC  29-Jun-1988
C
C	Function that implicates those boxes passed thru by a single 
C	(non horz., non vert.) vector. This vector is entry I from
C 	the coordinate data held in POLY_BUF.
C
C ......................................................................
C
	IMPLICIT NONE
C
C Parameters:
	INTEGER		X,Y		   !
	PARAMETER      (X=1,Y=2)	   !
	INTEGER		SS$_NORMAL	   !
	EXTERNAL	SS$_NORMAL	   !
	INCLUDE 	'LSL$CMNPOLYGONS:PARAMS.PAR'
C
C Common:
	INCLUDE		'LSL$CMNPOLYGONS:POLYGONS.CMN'
	INCLUDE		'LSL$CMNPOLYGONS:SECTOR.CMN'
C
C Arguments:	
C	LOGICAL		HADBOX(*)	   ! array of implicated boxes
	LOGICAL		HADBOX(BOXMAX)	   ! array of implicated boxes
	INTEGER		I		   ! Vector identifier
C
C W/s:
	REAL		X0,X1,DX,X_COORD   ! start and end points of vector, 
	REAL		Y0,Y1,DY,Y_COORD   ! vector extent and points on the
C					   ! vector
	INTEGER		X_CNT,X_LO,X_HI	   ! start and end boxes and box 
	INTEGER		Y_CNT,Y_LO,Y_HI	   ! loop counters
C
C .........................................................................
C
	GET_LINE_BOXES=%LOC(SS$_NORMAL)
C
C get X end-points and separation
	X0=POLY_BUF(X,I-1)
	X1=POLY_BUF(X,I)
	DX=X1-X0
C
C get Y end-points and separation
	Y0=POLY_BUF(Y,I-1)
	Y1=POLY_BUF(Y,I)
	DY=Y1-Y0
C
C separate steep and shallow lines
	IF (ABS(DY).GT.ABS(DX)) GOTO 1020
C
C .....................................................................
C			S H A L L O W   L I N E S
C
C arrange it so that DX is positive
	IF (DX.LT.0.0) THEN
	   X_COORD=X1
	   X1=X0
	   X0=X_COORD
	   DX=-DX
	   Y_COORD=Y1
	   Y1=Y0
	   Y0=Y_COORD
	   DY=-DY
	ENDIF
C
C get limits of line in sector space along the X axis (round down to int)
	X_LO=(X0-BOXORIX)/BOXSTPX 
	X_HI=(X1-BOXORIX)/BOXSTPX
C
C count along in X sector space
	DO 1000 X_CNT=X_LO,X_HI-1
C
C Compute X and Y coordinates of intersection of line with vertical
	   X_COORD=BOXORIX+(X_CNT+1)*BOXSTPX  
	   Y_COORD=Y0+DY*(X_COORD-X0)/DX
C
C convert into a Y count in sector space (round down to int)
	   Y_CNT=(Y_COORD-BOXORIY)/BOXSTPY
C
C now mark boxes to the left and to the right of the intersection 
	   HADBOX(Y_CNT*NUMBOX+X_CNT+1)=.TRUE.
	   HADBOX(Y_CNT*NUMBOX+X_CNT+2)=.TRUE.
1000	CONTINUE
	GOTO 1010
C
C .....................................................................
C			S T E E P    L I N E S
C
C arrange it so that DX is positive
1020	IF (DY.LT.0.0) THEN
	   X_COORD=X1
	   X1=X0
	   X0=X_COORD
	   DX=-DX
	   Y_COORD=Y1
	   Y1=Y0
	   Y0=Y_COORD
	   DY=-DY
	ENDIF
C
C get limits of line in sector space along the Y axis (round down to int)
	Y_LO=(Y0-BOXORIY)/BOXSTPY 
	Y_HI=(Y1-BOXORIY)/BOXSTPY
C
C count along in Y sector space
	DO 2000 Y_CNT=Y_LO,Y_HI-1
C
C Compute X and Y coordinates of intersection of line with horizontal
	   Y_COORD=BOXORIY+(Y_CNT+1)*BOXSTPY  
	   X_COORD=X0+DX*(Y_COORD-Y0)/DY
C
C convert into a X count in sector space (round down to int)
	   X_CNT=(X_COORD-BOXORIX)/BOXSTPX
C
C now mark boxes to the left and to the right of the intersection 
	   HADBOX(Y_CNT*NUMBOX+X_CNT+1)=.TRUE.
	   HADBOX((Y_CNT+1)*NUMBOX+X_CNT+1)=.TRUE.
2000	CONTINUE
C
C finished....	
1010	RETURN
	END
