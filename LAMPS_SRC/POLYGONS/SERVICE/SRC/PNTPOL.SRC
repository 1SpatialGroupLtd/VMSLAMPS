C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1992-05-05 15:54:10.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	INTEGER FUNCTION PNTPOL(PNTXY,POLXY,NPTS)
C
***	MODULE	PNTPOL
***	IDENT	10AP87
C
C	Copyright (C) 1987, Laser-Scan Laboratories Ltd.,
C	Cambridge, England
C
C	Created						TJH  11-Feb-1987
C
C Function to determine whether a point PNTXY(x,y) lies inside
C polygon POLXY(x,y).
C
C Function returns one of 3 values:
C
C		-1 = point outside polygon
C		 0 = point on an edge or vertex
C		 1 = point in polygon
C
C None of the variables in the argument list are changed.
C
C The vertices in POLXY may be in clockwise or anticlockwise order.
C The point PNTXT is considered inside the polygon if it is located
C in the enclosed area defined by the line forming the polygon.
C The input polygon may be a compound polygon consisting of several
C separate subpolygons. If so the first vertex of each subpolygon
C should be repeated, and when calculating NPTS must be counted twice.
C PNTPOL can handle any number of vertices defining the polygon.
C
	IMPLICIT NONE
C
C Arguments:
	INTEGER	NPTS		! no. of vertices defining the polygon
	REAL	PNTXY(2)	! the point
	REAL	XPNT,YPNT	!  "   "
	REAL	POLXY(2,NPTS)	! the polygon
	REAL	XI,YI,XJ,YJ	! distances between point and vertices
C
C Local workspace:
	INTEGER	I,J		! loop variable, counter
	LOGICAL IX,IY,JX,JY	! flags
C
C ......................................................................
C
C Assume that the point is outside
	PNTPOL=-1
C
	XPNT=PNTXY(1)
	YPNT=PNTXY(2)
	DO 40 I=1,NPTS
	   XI=POLXY(1,I)-XPNT
	   YI=POLXY(2,I)-YPNT
C
C Check whether the point lies on this vertex
	   IF (XI.EQ.0.0.AND.YI.EQ.0.0) GOTO 20
C
C J is the next vertex number of the polygon
	   J=1+MOD(I,NPTS)
	   XJ=POLXY(1,J)-XPNT
	   YJ=POLXY(2,J)-YPNT
C
C Is this line of zero length?
	   IF (XI.EQ.XJ.AND.YI.EQ.YJ) GOTO 40
	   IX=XI.GE.0.0
	   IY=YI.GE.0.0
	   JX=XJ.GE.0.0
	   JY=YJ.GE.0.0
C
C Check whether our point PNTXY(x,y) is on vertical side of polygon
	   IF (XI.EQ.0.0.AND.XJ.EQ.0.0.AND.(IY.XOR.JY)) GOTO 20
C
C Check whether our point PNTXY(x,y) is on horizontal side of polygon
	   IF (YI.EQ.0.0.AND.YJ.EQ.0.0.AND.(IX.XOR.JX)) GOTO 20
C
C Check whether both ends of this side are completely:
C		1) to the right of,
C		2) to the left of, or,
C		3) below our point PNTXY(x,y)
C
	   IF (.NOT.((IY.OR.JY).AND.(IX.XOR.JX))) GOTO 40
C
C Does this side obviously cross line rising vertically from our point
C PNTXY(x,y)?
	   IF (.NOT.((IY.AND.JY).AND.(IX.XOR.JX))) GOTO 10
C
C Yes, our point is inside the polygon
	   PNTPOL=-PNTPOL
	   GOTO 40
C
10	   IF ((YI*XJ-XI*YJ)/(XJ-XI)) 40,20,30
C
C Our point lies on the polygon boundary
20	   PNTPOL=0	   
	   GOTO 50
C
C Our point is inside the polygon
30	   PNTPOL=-PNTPOL
40	CONTINUE
C
50	RETURN
	END
