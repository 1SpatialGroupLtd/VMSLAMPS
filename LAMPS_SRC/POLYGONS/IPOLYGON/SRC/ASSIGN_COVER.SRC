C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1992-05-12 11:30:38.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
C
C-------------------------------------------------------------------------------
C				THE WRAPPER
C-------------------------------------------------------------------------------
C
C
C
C
	LOGICAL*4 FUNCTION ASSIGN_COVER()
C
***	MODULE	ASSIGN_COVER
***	IDENT	08MY92
C
	IMPLICIT NONE
C
C	Copyright Laser Scan Ltd, Cambridge, England
C
C	Author		Sunil Gupta			30-april-1992
C
C
C	........................................................................
C
	INCLUDE 'LSL$CMNPOLYGONS:ARRAY_PTRS.CMN'
C
	LOGICAL*4	ACTUAL_ASSIGN_COVER
C
C	........................................................................
C
	ASSIGN_COVER = ACTUAL_ASSIGN_COVER(
     &			POLMAX,TABMAX,POLNEST_MAX, PARTS_MAX,
     &			%VAL(PTR$_POLY_DONE),
     &			%VAL(PTR$_POLY_START),
     &			%VAL(LOC_PTR$_P_PTRS)
     &		  )
C
C	........................................................................
C
	RETURN
	END
C
C-------------------------------------------------------------------------------
C			    THE PROPER FUNCTION
C-------------------------------------------------------------------------------
C
C
C
	LOGICAL*4 FUNCTION ACTUAL_ASSIGN_COVER(
     &		POLMAX,TABMAX,POLNEST_MAX,PARTS_MAX,
     &		POLY_DONE,
     &		POLY_START,
     &		PTRS
     &	)

***	MODULE	ACTUAL_ASSIGN_COVER
***	IDENT	20NO89
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England
C
C	Author						SH   07-Nov-1989
C
C	Function to assign a set of codes to each polygon (one for each
C	coverage), from the left/right codes on the input segments, and
C	the propagation array set up in PROPAGATE_COVER
C
C ..........................................................................
C
	IMPLICIT NONE
C
C Parameters:
	INCLUDE 	'LSL$CMNPOLYGONS:PARAMS.PAR'
	INCLUDE 	'LSL$CMNIPOLYGON:IPOLYGONMSG.PAR'
	INTEGER		SS$_ABORT
	EXTERNAL	SS$_ABORT	! error
	INTEGER		SS$_NORMAL
	EXTERNAL	SS$_NORMAL	! normal successful completion
C
C Common:
	INCLUDE		'LSL$CMNIPOLYGON:COMMAND.CMN'
	INCLUDE 	'LSL$CMNPOLYGONS:COVERAGES.CMN'
	INCLUDE		'LSL$CMNPOLYGONS:FILE.CMN'
	INCLUDE		'LSL$CMNPOLYGONS:POLYGONS.CMN'
	INCLUDE		'LSL$CMNPOLYGONS:TABLES.CMN'
	INCLUDE		'LSL$CMNPOLYGONS:SECTOR.CMN'
	INCLUDE		'LSL$CMNIFF:IFFHAN.CMN'
	INCLUDE		'LSL$CMNLSL:TXTC.CMN'
C
C Arguments
	INTEGER*4	POLMAX
	INTEGER*4	TABMAX
	INTEGER*4	POLNEST_MAX
	INTEGER*4	PARTS_MAX
C
	INTEGER		POLY_DONE(POLMAX)
	INTEGER		POLY_START(3,POLMAX)
C
C W/s:
	INTEGER		NUM_SUCCESS	! # of polygons successfully labeled
	INTEGER		NUM_FAIL	! # of polygons that failed labelling
	INTEGER		NUMDUFF		! tmp variable
	INTEGER		I		! tmp variable
	INTEGER		NNEST		! number of nested polygons
	INTEGER	  	POLY_INDX	! current polygon
	LOGICAL		LASTPTR		! pointer to last netsed polygon
	INTEGER		PTRS(POLNEST_MAX)! pointers to nested polys
C					! (unused)
	LOGICAL		UNDEF		! polygon has inconsistent L/R codes
C	LOGICAL		FIRST_SEG	! first segment in entire poly boundary
C					! including INNER boundaries
C	CHARACTER*255	TEXT		! label from current segment
C	INTEGER*2	LENGTH		! length of TEXT
C	CHARACTER*255	OLD_TEXT	! label from FIRST_SEG
C	INTEGER*2	OLD_LENGTH	! length of OLD_TEXT
C
C Functions:
	LOGICAL		EXT1V		! extract 1-bit field
	INTEGER		FIRST_NEST	! gets 1st order nested polys
	INTEGER		CHECK_COVER_LABEL
C					! checks poly label
C
C ......................................................................
C
	ACTUAL_ASSIGN_COVER=%LOC(SS$_NORMAL)
C
C Tell user of progress?
	CALL FRAMED_MES('Assigning Propagated Codes',26)
C
C All IFF reading is from the segment file
	CALL IFFSEL(INLUN)
C
C Set counters
	NUM_SUCCESS=0
	NUM_FAIL=0
C
C ......................................................................
C    L O O P    F O R    E  A C H    P O L Y G O N   I N    T U R N
C
	DO 60 POLY_INDX=1,NUM_POLY
C
C Ensure that this is not a duff polygon
C	   IF (EXT1V(POLY_INDX,DUFF_POLYGON(1))) GOTO 60
C
C Is this an unwanted isolated polygon? If so ignore it.
	   IF (POLY_START(1,POLY_INDX).LT.0) GOTO 60
C
C Assume that the polygon has been defined
	   UNDEF=.FALSE.
C
C Go and get the label for the outer boundary of the polygon, and check for 
C consistency
C	   FIRST_SEG=.TRUE.
	   ACTUAL_ASSIGN_COVER=CHECK_COVER_LABEL(POLY_INDX,UNDEF)
	   IF (.NOT.ACTUAL_ASSIGN_COVER) GOTO 80
	   IF (UNDEF) GOTO 55		! undefined polygon?
C
C Note that this poly has consistent L/R coding
	   POLY_DONE(POLY_INDX)=.TRUE.
	   NUM_SUCCESS = NUM_SUCCESS+1
	   GOTO 60
C
C Note that this poly has inconsistent L/R coding
55	   POLY_DONE(POLY_INDX)=.FALSE.
C
C If /OPTIONS=UNDEFINED:DELETE then mark the polygon as duff
	   IF (.NOT.OPT_KEEP) CALL DUFF_POLY(POLY_INDX,NUMDUFF)
	   NUM_FAIL = NUM_FAIL+1
C
C Finished this polygon
60	CONTINUE
C
C ......................................................................
C
C Give summary?
	IF (HAD_LOG) THEN
	   CALL EXPAND(
     &	    'Number of polygons with consistent labelling ........ %N',
     &							NUM_SUCCESS)
	   IF (HAD_LIS) THEN
	      CALL FLWLIN
	   ELSE
	      CALL TTWLIN
	   ENDIF

	   IF (NUM_FAIL.GT.0) THEN
	      CALL EXPAND(
     &	    'Number of polygons with failed labelling ............ %N',
     &							NUM_FAIL)
	      IF (HAD_LIS) THEN
	         CALL FLWLIN
	      ELSE
	         CALL TTWLIN
	      ENDIF
	   ENDIF
	ENDIF
C
C ......................................................................
C
C finished function - if jump to here direct then function has been aborted
80	RETURN
	END
