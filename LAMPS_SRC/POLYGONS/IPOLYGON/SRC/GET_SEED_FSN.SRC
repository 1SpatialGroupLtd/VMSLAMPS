C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1992-05-12 16:10:34.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
C
C-------------------------------------------------------------------------------
C				THE WRAPPER
C-------------------------------------------------------------------------------
C
C
C
C
	LOGICAL*4 FUNCTION GET_SEED_FSN(POLY_INDX,FSN,FC)
C
***	MODULE	GET_SEED_FSN
***	IDENT	12MY92
C
	IMPLICIT NONE
C
C	Copyright Laser Scan Ltd, Cambridge, England
C
C	Author		Sunil Gupta			30-april-1992
C
C
C	........................................................................
C
	INCLUDE 'LSL$CMNPOLYGONS:ARRAY_PTRS.CMN'
C
C Arguments:
	INTEGER		POLY_INDX	! the polygon in question
	INTEGER		FSN		! FSN of seed point
	INTEGER*2	FC		! FC of seed point
C
	LOGICAL*4	ACTUAL_GET_SEED_FSN
C
C	........................................................................
C
	GET_SEED_FSN = ACTUAL_GET_SEED_FSN(POLY_INDX,FSN,FC,
     &			POLMAX,TABMAX,POLNEST_MAX, PARTS_MAX,
     &			%VAL(PTR$_POLY_DONE),
     &			%VAL(PTR$_SEED_ADDRESS)
     &		  )
C
C	........................................................................
C
	RETURN
	END
C
C-------------------------------------------------------------------------------
C			    THE PROPER FUNCTION
C-------------------------------------------------------------------------------
C
C
C
	LOGICAL*4 FUNCTION ACTUAL_GET_SEED_FSN(POLY_INDX,FSN,FC,
     &		POLMAX,TABMAX,POLNEST_MAX,PARTS_MAX,
     &		POLY_DONE,
     &		SEED_ADDRESS
     &	)
C
***	MODULE	ACTUAL_GET_SEED_FSN
***	IDENT	27FE90
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England
C
CMOD	Now also tests for polygons flagged as isolations
C 	(which should have been rejected) but have got 
C	through due to bad junction structure		TJH  27-Feb-1990
CMOD	Corrected EOF file behaviour			AC   20-Sep-1988
C	Author						AC   06-Jun-1988
C
C
C Function to get general information about the seed point that lies in polygon
C POLY_INDX. In particular the FSN and FC of the seed point are retrieved. 
C
C Function returns SS$_NORMAL if all is well or one of a variety of
C IPOLYGON error codes on failure.
C
C ......................................................................
C
	IMPLICIT NONE
C
C Arguments:
	INTEGER		POLY_INDX	! the polygon in question
	INTEGER		FSN		! FSN of seed point
	INTEGER*2	FC		! FC of seed point
C
	INTEGER*4	POLMAX
	INTEGER*4	TABMAX
	INTEGER*4	POLNEST_MAX
	INTEGER*4	PARTS_MAX
C
	INTEGER		POLY_DONE(POLMAX)
	INTEGER*4	SEED_ADDRESS(TABMAX)
C
C Parameters:
	INCLUDE 	'LSL$CMNPOLYGONS:PARAMS.PAR'
	INCLUDE 	'LSL$CMNIPOLYGON:IPOLYGONMSG.PAR'
	INTEGER		SS$_NORMAL
	EXTERNAL	SS$_NORMAL	! normal successful completion
C
C Common:
	INCLUDE		'LSL$CMNIPOLYGON:COMMAND.CMN'
	INCLUDE		'LSL$CMNPOLYGONS:FILE.CMN'
	INCLUDE		'LSL$CMNPOLYGONS:POLYGONS.CMN'
	INCLUDE		'LSL$CMNPOLYGONS:TABLES.CMN'
	INCLUDE		'LSL$CMNPOLYGONS:SECTOR.CMN'
	INCLUDE		'LSL$CMNLSL:TXTC.CMN'
C
C W/s
	INTEGER*2 IECODE,IELEN	! IFF entry code and length
C
C ......................................................................
C
	ACTUAL_GET_SEED_FSN=%LOC(SS$_NORMAL)
C
C Check for bounding polygon
	IF (POLY_INDX.EQ.BOUND_POL) THEN
	   FSN=65535
	   FC=-1
	   GOTO 30
	ENDIF
C
C Are we dealing with a duff polygon?
	IF (POLY_INDX.EQ.0.OR.POLY_INDX.EQ.-32767) GOTO 100
C
C Are we dealing with a polygon that has been kept despite a seed point 
C assignment error?
	IF (POLY_DONE(POLY_INDX).EQ.0
     &		.OR.POLY_DONE(POLY_INDX).EQ.-32767) GOTO 100
C
C Jump to seed point feature
	IF (HAD_SEED_FILE) THEN 
	   CALL IFFSEL(SEEDLUN)
	ELSE
	   CALL IFFSEL(INLUN)
	ENDIF
	CALL IFFPKE(IECODE,IELEN,SEED_ADDRESS(POLY_DONE(POLY_INDX)))
C
C Unexpected eof?
	IF (IELEN.LE.-1) GOTO 110
C
C Unexpected type of IFF entry?
	IF (IECODE.NE.'NF') THEN
	   ACTUAL_GET_SEED_FSN=IPOL__LOST
	   CALL LSL_PUTMSG(ACTUAL_GET_SEED_FSN,IECODE)
	   GOTO 30
	ENDIF
C
C Get FSN
	FSN=0			! clear longword
	CALL EIHRI(FSN,1,1)	! read feature serial no.
C
	CALL IFFNXT(IECODE,IELEN)
C
C Unexpected eof?
	IF (IELEN.LE.-1) GOTO 110
C
C Unexpected type of IFF entry?
	IF (IECODE.NE.'FS') THEN
	   ACTUAL_GET_SEED_FSN=IPOL__LOST
	   CALL LSL_PUTMSG(ACTUAL_GET_SEED_FSN,IECODE)
	   GOTO 30
	ENDIF
C
C Get FC and finish
	CALL EIHRI(FC,1,1)
	GOTO 30
C
C ......................................................................
C
C		U N D E F I N E D   P O L Y G O N 
C
100	FSN=65535
	FC=-1
	GOTO 30
C
C ......................................................................
C
C		U N E X P E C T E D   E N D   O F   F I L E
C
110	ACTUAL_GET_SEED_FSN=IPOL__UNEXPEOF
	IF (HAD_SEED_FILE) THEN
      	   CALL LSL_PUTMSG(ACTUAL_GET_SEED_FSN,SEED_FILE(:SEED_LEN))
	ELSE
      	   CALL LSL_PUTMSG(ACTUAL_GET_SEED_FSN,IN_FILE(:IN_LEN))
	ENDIF
	GOTO 30
C
30	RETURN
	END

