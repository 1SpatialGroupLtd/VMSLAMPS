C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1988-08-17 10:16:22.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION GET_PAIRED_FC(IN_FC,OUT_FC)
C
***	MODULE	GET_PAIRED_FC
***	IDENT	17AU88
C
C	Copyright (C), Laser-Scan Laboratories Ltd., Cambridge, England
C
C	Created						AC  11-Aug-1988
C
C Function to read in the FC pairs (seed point FC -> poly boundary FC)
C from file - ie set OUT_FC according to the value of IN_FC. 
C
C The routine returns .T. if IN_FC can be found otherwise it returns .F.
C
C .....................................................................
C
	IMPLICIT NONE
C
C Parameters:
	INCLUDE 	'LSL$CMNPOLYGONS:PARAMS.PAR'
C
C Common:
	INCLUDE		'LSL$CMNIPOLYGON:COMMAND.CMN'
C
C Arguments:
	INTEGER*2	IN_FC		! input FC
	INTEGER*2	OUT_FC		! output FC (if found)
C
C W/s:
	INTEGER		LO,HI,MID	! entries into array
C
C ......................................................................
C
	GET_PAIRED_FC=.FALSE.
C
C Note how many pairs have been read in - if none then nothing to do
	IF (NUM_FC_PAIR.EQ.0) GOTO 999
C
C If the value of IN_FC is not in the table because it lies outside its
C range then need do nothing
	IF (	(IN_FC.LT.FC_PAIR(1,1))	.OR.
     &		(IN_FC.GT.FC_PAIR(1,NUM_FC_PAIR))    ) GOTO 999
C
C Check that IN_FC is not the first entry ...
	IF (IN_FC.EQ.FC_PAIR(1,1)) THEN
	   OUT_FC=FC_PAIR(2,1)
	   GOTO 998
	ENDIF
C
C so if in table must have entry greater than LO - ie 1
	LO=1
C
C Check that IN_FC is not the last entry ...
	IF (IN_FC.EQ.FC_PAIR(1,NUM_FC_PAIR)) THEN
	   OUT_FC=FC_PAIR(2,NUM_FC_PAIR)
	   GOTO 998
	ENDIF
C
C so if in table must have entry less than HI - ie NUM_FC_PAIR
	HI=NUM_FC_PAIR
C
C If IN_FC can be found then it must be between LO and HI exclusive - thus
C if LO equals HI or if LO and HI are separated by one then it is not here
10	IF (HI-LO.LT.2) GOTO 999
C
C look mid way between HI and LO
	MID=(LO+HI)/2
C
C If there then have found it ...
	IF (IN_FC.EQ.FC_PAIR(1,MID)) THEN
	   OUT_FC=FC_PAIR(2,MID)
	   GOTO 998
	ENDIF
C
C otherwise update HI or LO as required ...
	IF (IN_FC.GT.FC_PAIR(1,MID)) THEN
	   LO=MID
	ELSE
	   HI=MID
	ENDIF
C
C and try again
	GOTO 10
C
C found it !!!!!!!
998	GET_PAIRED_FC=.TRUE.	
C
C Finished.....
999	RETURN
	END

