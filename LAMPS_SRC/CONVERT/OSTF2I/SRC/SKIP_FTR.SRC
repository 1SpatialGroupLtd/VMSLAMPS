C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1990-05-31 11:17:32.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION SKIP_FTR
C
***	IDENT	28JA88
***	MODULE	SKIP_FTR
C
C Copyright (C) LaserScan Laboratories Ltd, Cambridge, England
C
C Author TJ Ibbs, 7 May 1986
C
C To skip to the end of a feature
C
C returns TRUE if it reads end of file
C
	IMPLICIT NONE
C
C *********************************************************************
C functions
C
	LOGICAL		GETREC		! read the next record
C
C workspace
C
	INTEGER		ICUVAL(2)
	INTEGER	    	I,J,K,M         ! counters
	CHARACTER*8	RECORD
	CHARACTER*1	CAP_METHOD	! data capture method
	CHARACTER       CARR(4)
C
C *********************************************************************
C assume the worst
C
	SKIP_FTR = .TRUE.
C
C read the first line
C
	IF ( GETREC(RECORD) ) GOTO 9000
 500	READ (RECORD(1:4),5000) CAP_METHOD, ICUVAL(1)
5000	FORMAT(A1,I3)
C
C end of feature, so get out right away
C
	IF ( ICUVAL(1).EQ.-5 ) THEN
	   SKIP_FTR = .FALSE.
	   GOTO 9000
	ENDIF
C
C if we have a coordinate record, go straight back
C
	IF ( ICUVAL(1).GE.0 ) THEN
	   IF (GETREC(RECORD)) GOTO 9000
	   GOTO 500
	ENDIF
C
C for other record types inside the feature, we need to skip the 
C appropriate number of records
C
	J = 0				! start with none
C
C for text, work out how many lines there are
C
	IF ( ICUVAL(1).EQ.-11 ) THEN
C
	   READ (RECORD(5:8),5001) ICUVAL(2)
5001	   FORMAT(I4)
C
	   J = (ICUVAL(2)+7)/8		! number of text lines
C
C others have only one record
C
	ELSEIF (( ICUVAL(1).EQ.-9 ) .OR.	! basic grid square
     &	        ( ICUVAL(1).EQ.-10 ) .OR.	! text indicator
     &	        ( ICUVAL(1).EQ.-14 ) .OR.	! marginal grid square
     &	        ( ICUVAL(1).EQ.-23 ) .OR.	! feature history
     &	        ( ICUVAL(1).EQ.-24 )) THEN	! height
C
	   J = 1
C
C text position has two records
C
	ELSEIF ( ICUVAL(1).EQ.-18 ) THEN
C
	   J = 2
C
C for text cross reference, read the number of lines 
C
	ELSEIF ( ICUVAL(1).EQ.-22 ) THEN
C
	   READ (RECORD(5:8),5002) ICUVAL(2)
5002	   FORMAT(I4)
C
	   J = ICUVAL(2)
C
	ENDIF
C
C now skip the records
C
	DO 600 I = 1,J
	   IF ( GETREC(RECORD) ) GOTO 9000
600	CONTINUE
C
C if text then ensure that next record read contains a flag
C else simply read next record  
C
	IF (ICUVAL(1).EQ.-11) THEN
	   DO 800 K = 1,10          ! at most 80 characters in text
	      IF (GETREC(RECORD)) GOTO 9000
	      READ (RECORD(1:4), 5003) CARR
 5003	      FORMAT( 4A1 )
	      DO 700 M = 1,3
		 IF (CARR(M).NE.' ') THEN
		    IF (CARR(M).EQ.'-') GOTO 900
		    GOTO 800
		 ENDIF
 700	      CONTINUE
 800	   CONTINUE
	ELSE
	   IF (GETREC(RECORD)) GOTO 9000
C
	ENDIF
C 
 900	CONTINUE
C go back for the next line
	GO TO 500
C
C *********************************************************************
C
9000	RETURN
	END
