	PROGRAM OSTF2I
C
***	IDENT	16FE90
***	MODULE	OSTF2I
C
C Copyright (C) LaserScan Laboratories Ltd, Cambridge, England
C
C Author TJ Ibbs, 30 April 1986
C
CMOD	Introduced /INDEX qualifier			ACM 27-Apr-1989
C
C To read OSTF data from a tape or a disk file, and create IFF files
C from that data
C
	IMPLICIT NONE
C
C common files
C
	INCLUDE	'LSL$CMNLSL:CLD.CMN'		! for command line reading
	INCLUDE	'CMN:FILES.CMN'			! filenames & switches
	INCLUDE 'LSL$CMNLSL:MTIVCM.CMN'		! for MTIERR
	INCLUDE 'CMN:OSTF2IMSG.PAR'		! for message numbers
	INCLUDE 'LSL$CMNLSL:LSLLIBMSG.PAR'
C
C functions
C
 	LOGICAL		CMDLINE			! read the command line
	LOGICAL		DO_INDEX		! read index of sheets
	LOGICAL		PROCESS			! read the data
	LOGICAL		MTINIT			! initialise serial MT
	LOGICAL		FRTINI			! read FRT file
	LOGICAL		FIND_PLUS		! detect OSTF+ in input file
C
C workspace
C
	LOGICAL		OLD_DMC			! true if it is
	LOGICAL		OK
C
C **********************************************************************
C initialise the library
C
	CALL LSL_INIT
C
	CALL IFFTDY
C
C and read the command line
C
	IF ( CMDLINE() ) GOTO 9000	! exit if that fails
C
C report on what was asked for
C
	CALL REPORT
C
C now for the FRT if it was requested
C
	IF (HAD_FRT) THEN
	   IF (FRTINI(FRTFILE(1:FRTFILE_LEN))) THEN
	      CALL LSL_PUTMSG(OSTF2I__READFRT,FRTFILE(1:FRTFILE_LEN))
	      GO TO 9000
	   ENDIF
C
C otherwise set up the standard ACD definitions
C
	ELSE
	   CALL FRT_ACDINI
	ENDIF
C
C and open the input file - use a standard Fortran OPEN where possible
C - the advantage of Fortran I/O for reading the input is that it will
C   work in the same manner whether our input is a magnetic tape or a
C   disk file.
C   However, if the tape is connected via a serial line - i.e. we are
C   connected to the Kennedy magtape - this will not work, and we have
C   to use the library tape read routines to obtain the data.
C
	IF (.NOT. SERIAL_MT) THEN
	   OPEN ( FILE=INFILE(1:INFILE_LEN), UNIT=DMCLUN, STATUS='OLD',
     &	          BLOCKSIZE=BLOCKSIZE, RECORDSIZE=8, READONLY )
	ELSE
	   OK = MTINIT(INFILE(1:INFILE_LEN), .FALSE., 1 )
						! Initialise serial MT.
	   IF (.NOT. OK) THEN			! Report any error
	      CALL LSL_PUTMSG(OSTF2I__INITINP, INFILE(1:INFILE_LEN))
	      CALL LSL_ADDMSG(OK)
	      IF (OK .EQ. LSL__SYSERR) CALL LSL_ADDMSG(MTIERR)
	      GOTO 9000   			! and exit
	   ENDIF
	ENDIF
C
C If the /AUTO_PLUS qualifier is specified scan input file for
C -23 or -24 to detect OSTF+. If detected set PLUS to .TRUE.
C
	IF (HAD_AUTO_PLUS) THEN
	   IF (.NOT.FIND_PLUS(AUTO_PLUS)) GOTO 9000
	   IF (AUTO_PLUS) PLUS = .TRUE.
	ENDIF
C
C read any index from the start of the input
C
	IF ( DO_INDEX(.TRUE.,OLD_DMC) ) GOTO 9000
	IF (INDEX_ONLY) GOTO 9000
C
C now use PROCESS to read the input data, and create IFF files as
C appropriate
C
	IF ( PROCESS() ) GOTO 9000
C
C and come here to exit
C
9000	CALL LSL_EXIT
	END
