C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1984-05-23 13:42:28.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE OSNAME(NAME)
C
***	MODULE	OSNAME
***	IDENT	23MY84
C
C Copyright Laser-Scan Laboratories Ltd, Cambridge, England
C
C	Created					PGH	 14-Apr-1981
C
C OS Map name calculation (produces the OS reference name for
C a map from the grid coordinates of the SW corner)
C
	IMPLICIT NONE
C
C we were passed
C
	CHARACTER*8 	NAME		! to hold the result
C
C common block
C
***	COMMON/LSL$CMNIFF:MHDMB/	! the map header
C
C w/s:
C
	CHARACTER*1 	S500K		! 500 km letter
	CHARACTER*1	S100K		! 100 km letter
	CHARACTER*1	SQ(5,5)		! table of grid letters
	CHARACTER*1	N(2),E(2)	! N/S and E/W choice
	CHARACTER*1	QN,QE		! quadrant letters
C
	INTEGER*4   	S1KE		! 1 km square easting
	INTEGER*4	S1KN		!	      northing
	INTEGER*4	REFE		! Easting ref w/s
	INTEGER*4	REFN
	INTEGER*4	IE,IN
C
C and set up the tables we use - SQ maps the order of the OS
C letter codes over the National Grid, N and E give the quadrant
C letters for us
C
	DATA SQ/'A','B','C','D','E',
     &          'F','G','H','J','K',
     &          'L','M','N','O','P',
     &          'Q','R','S','T','U',
     &          'V','W','X','Y','Z'/
C
	DATA N/'S','N'/
C
	DATA E/'W','E'/
C
C code:
C
C for clarity, we shall use the example given in JB Harley, "OS Maps:
C a descriptive manual", page 25sqq
C
C That is, we shall consider the point with grid reference
C 538 932 East and 177 061 North, and work out the map it is in...
C
C calculate the 500km grid square -
C first we must move from the Grid false origin to the true one...
C
	REFE = PCMRCE +1 000 000	! true grid 1 538 932
	REFN = PCMRCN +  500 000	! true grid   677 061
C
C and now work out which 500 km square we are in
C
	IE = REFE/500 000 + 1		! IE = 3+1 = 4
	IN = 5 - REFN/500 000		! IN = 5-1 = 4
	S500K = SQ(IE,IN)		! => S500K = 'T'
C
C now 100km square letter
C
	REFE = MOD(REFE,500 000)	! REFE =  38 932
	REFN = MOD(REFN,500 000)	! REFN = 177 061
	IE = REFE/100 000 + 1		! IE = 0+1 = 1
	IN = 5 - REFN/100 000		! IN = 5-1 = 4
	S100K = SQ(IE,IN)		! => S100K = 'Q'
C
C Now 1km square
C
	REFE = MOD(REFE,100 000)	! REFE = 38 932
	REFN = MOD(REFN,100 000)	! REFN = 77 061
	S1KE = REFE/1000		! S1KE =     38
	S1KN = REFN/1000		! S1KN =     77
C
C and now we must assemble the actual name
C
	IF ( PCSCSC.GE.250 000 ) THEN		! ie TQ
C
	   CALL EXPAND('%WS%P0%S%S',
     &        NAME, S500K, S100K)
C
	ELSEIF ( PCSCSC.GE.25 000 ) THEN	! ie TQ 37
C
	   CALL EXPAND('%WS%P0%S%S%I1%I1',
     &        NAME, S500K, S100K, S1KE/10, S1KN/10)
C
	ELSEIF ( PCSCSC.GE.10 000 ) THEN	! ie TQ 37 NE
C
C	 so we must work out the quadrant in the 10km square
C
	   REFE = MOD( (PCMRCE + 1 000 000), 10 000 )	! REFE = 8 932
	   REFN = MOD( (PCMRCN +   500 000), 10 000 )	! REFN = 7 061
	   IE = REFE/5 000 + 1				! IE = 1+1 = 2
	   IN = REFN/5 000 + 1				! IN = 1+1 = 2
	   QN = N(IN)					! QN = 'N'
	   QE = E(IE)					! QE = 'E
C
	   CALL EXPAND('%WS%P0%S%S%I1%I1%S%S',
     &        NAME, S500K, S100K, S1KE/10, S1KN/10, QN, QE)
C
	ELSEIF ( PCSCSC.EQ.2 500 ) THEN		! ie TQ 38 77
C
	   CALL EXPAND('%WS%P0%S%S%I2%I2',
     &        NAME, S500K, S100K, S1KE, S1KN)
C
	ELSE					! ie TQ 38 77 SE
C
C	 so we must work out the quadrant in the 1km square
C
	   REFE = MOD(REFE,1000)			! REFE = 932
	   REFN = MOD(REFN,1000)			! REFN =  61
	   IE = REFE/500 + 1				! IE = 1+1 = 2
	   IN = REFN/500 + 1				! IN = 0+1 = 1
	   QN = N(IN)					! QN = 'S'
	   QE = E(IE)					! QE = 'E'
C
	   CALL EXPAND('%WS%P0%S%S%I2%I2%S%S',
     &        NAME, S500K, S100K, S1KE, S1KN, QN, QE)
C
	ENDIF	   
C
C and that is all we need to do
C
	RETURN
	END
