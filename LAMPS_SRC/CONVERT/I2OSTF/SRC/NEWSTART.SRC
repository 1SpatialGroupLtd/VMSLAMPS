	SUBROUTINE NEWSTART(XIN,YIN,XOUT,YOUT,TXTPOS,ROT,TH,
     &				N,CHRS)
C
***	IDENT	02OC84
***	MODULE	NEWSTART
C
C	Copyright LaserScan Laboratories Ltd., Cambridge, England
C
C to calculate the start position of a text string
C
C Author R.W. Russell	01-Oct-l984
C
	IMPLICIT	NONE
C
C---input arguments
C
	REAL		XIN,YIN		! coords of digitised position
	INTEGER*4	TXTPOS		! digitised position (0-8)
	REAL		ROT		! rotation of text (in radians)
	REAL		TH		! height of text (in mms)
	INTEGER*4	N		! number of characters in text
	BYTE		CHRS(N)		! text string
C
C---output arguments
C
	REAL		XOUT,YOUT	! coords of origin of text
C
C---working variables
C
	REAL		LENGTH
C
	IF (TXTPOS.EQ.0) THEN		! this is origin!
	   XOUT = XIN
	   YOUT = YIN
	   GOTO 100
	ELSE				! in case txtpos < 0 or > 8
	   XOUT=0.0
	   YOUT=0.0
	ENDIF
C
C---get length of text
C
	CALL GTTXTLNTH(N,CHRS,TH,LENGTH)
C
C---calculate corrections
C
	IF (TXTPOS.EQ.1) THEN
	   XOUT= TH/2*SIN(ROT)
	   YOUT=-TH/2*COS(ROT)
	ELSEIF (TXTPOS.EQ.2) THEN
	   XOUT= TH*SIN(ROT)
	   YOUT=-TH*COS(ROT)
	ELSEIF (TXTPOS.EQ.3) THEN
	   XOUT=-LENGTH/2*COS(ROT)
	   YOUT=-LENGTH/2*SIN(ROT)
	ELSEIF (TXTPOS.EQ.4) THEN
	   XOUT=-LENGTH/2*COS(ROT)+TH/2*SIN(ROT)
	   YOUT=-LENGTH/2*SIN(ROT)-TH/2*COS(ROT)
	ELSEIF (TXTPOS.EQ.5) THEN
	   XOUT=-LENGTH/2*COS(ROT)+TH*SIN(ROT)
	   YOUT=-LENGTH/2*SIN(ROT)-TH*COS(ROT)
	ELSEIF (TXTPOS.EQ.6) THEN
	   XOUT=-LENGTH*COS(ROT)
	   YOUT=-LENGTH*SIN(ROT)
	ELSEIF (TXTPOS.EQ.7) THEN
	   XOUT=-LENGTH*COS(ROT)+TH/2*SIN(ROT)
	   YOUT=-LENGTH*SIN(ROT)-TH/2*COS(ROT)
	ELSEIF (TXTPOS.EQ.8) THEN
	   XOUT=-LENGTH*COS(ROT)+TH*SIN(ROT)
	   YOUT=-LENGTH*SIN(ROT)-TH*COS(ROT)
	ENDIF
C
C---apply corrections to original coordinates
C
	XOUT=XIN+XOUT
	YOUT=YIN+YOUT
C
C---and exit
C
 100	CONTINUE
	RETURN
	END	
