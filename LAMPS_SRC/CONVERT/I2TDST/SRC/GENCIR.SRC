C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1989-03-16 16:17:02.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION GENCIR()
C
***	MODULE	GENCIR
***	IDENT	02AU85
C
C Copyright Laser Scan Laboratories Ltd, Cambridge, England.
C Author Clarke Brunt
C
C Created					CCB	25-February-1985
C Modified for I2TDST				ADT	13-June-1988
C
C Routine to work out required parameters for circle arcs.
C
C The graphical types we deal with are:
C
C   2 - Clockwise circle arc	  > Start pt, Centre pt, End pt.
C   3 - Anticlockwise circle arc  >    "    ,    "     ,   "   .
C   4 - Circumcircle arc	  >    "    , Any edge pt, "   .
C   5 - Full circumcircle	  > Any 3 edge pts.
C (Direction is implicit in 4 and 5).
C
	IMPLICIT NONE
C
	INCLUDE	'HERE:I2TDSTCIRC.CMN'
	INCLUDE 'HERE:I2TDST.CMN'
	INCLUDE 'HERE:I2TDSTMSG.PAR'
	INCLUDE 'LSL$CMNFRT:FRTCOM.CMN'
C
C W/s
	REAL DXS,DYS,DXE,DYE	! differences between pts on edge.
	REAL TWODEL		! temporary workspace.
	REAL RSSQD,RESQD	!	"	"
	REAL DSDE		!	"	"
	REAL XSDIF,YSDIF,XEDIF,YEDIF  ! differences (edge pts - centre).
	REAL ENDANG		! angle at centre of end point.
	REAL TWOPI		
C
	INTEGER	I

	LOGICAL*1 CLOCKW	! direction of circle/arc.
	GENCIR = .TRUE.		! assume success
	TWOPI=44.0/7.0
C
C Do we need to work out the centre point?
	IF ((GT.EQ.CLOTYP).OR.(GT.EQ.ANTTYP)) THEN
C
C Circle/arc defined by start pt, centre pt, and end pt.
	   XCENTR=XYCORD(1,2)	! x centre
	   YCENTR=XYCORD(2,2)	! y centre
C Set the direction.
	   CLOCKW=(GT.EQ.CLOTYP)
	ELSE
C
C Evaluate the centre of the circle/arc if we have got 3 points
C on the circumference. Uses fairly obscure method.
	   DXS = XYCORD(1,1)-XYCORD(1,2)	! x 2->1
	   DYS = XYCORD(2,1)-XYCORD(2,2)	! y 2->1
	   DXE = XYCORD(1,3)-XYCORD(1,2)	! x 2->3
	   DYE = XYCORD(2,3)-XYCORD(2,2)	! y 2->3
	   DSDE = DXS*DYE-DYS*DXE	! cross product DS x DE
	   IF (DSDE.EQ.0.0) GOTO 9998	! points are in a straight line
	   CLOCKW = (DSDE.GT.0.0)	! sense of circle
	   TWODEL = 0.5/DSDE
	   RSSQD = DXS*DXS+DYS*DYS
	   RESQD = DXE*DXE+DYE*DYE
C Now evaluate the centre point.
	   XCENTR=(DYE*RSSQD-DYS*RESQD)*TWODEL+XYCORD(1,2)   ! x centre
	   YCENTR=(DXS*RESQD-DXE*RSSQD)*TWODEL+XYCORD(2,2)   ! y centre
	ENDIF
C
C Now work out the radius. The correct radius is set by the 1st
C point. The 3rd point merely provides an angle rel to the centre.
	XSDIF=XYCORD(1,1)-XCENTR	! x centre->1
	YSDIF=XYCORD(2,1)-YCENTR	! y centre->1
	RADIUS=SQRT(XSDIF**2+YSDIF**2)	! radius
	IF (RADIUS.EQ.0.0) GOTO 9998	! bad circle
C
C Work out the start and end angles at the centre
	IF (GT.EQ.FULTYP) THEN	! full circle so...
	   STAANG=0.0
	   TOTANG=TWOPI
	ELSE				! arc
	   STAANG=ATAN2(YSDIF,XSDIF)
	   IF (STAANG.LT.0.0) STAANG=STAANG+TWOPI
	   XEDIF=XYCORD(1,3)-XCENTR	! x centre->3
	   YEDIF=XYCORD(2,3)-YCENTR	! y centre->3
	   IF (XEDIF.EQ.0.0.AND.YEDIF.EQ.0.0) GOTO 9998	! bad circle
	   ENDANG=ATAN2(YEDIF,XEDIF)
	   IF (ENDANG.LT.0.0) ENDANG=ENDANG+TWOPI
	   TOTANG=ENDANG-STAANG
C
C Now make sure the angles are right, positive for anti-clockwise
	   IF (TOTANG.GE.0.0) THEN
	      IF (CLOCKW) TOTANG=TOTANG-TWOPI
	   ELSE
	      IF (.NOT.CLOCKW) TOTANG=TOTANG+TWOPI
	   ENDIF
C
	ENDIF
	GOTO 9999
C
C error
9998	CALL LSL_PUTMSG(IDST__INVALARC,FSN)
	GENCIR = .FALSE.		! failed
C
9999	RETURN
	END
