	INTEGER FUNCTION CHECK_DUP_PTS(IS_LINE,IECODE,IELEN,ADDR)
C
***     MODULE CHECK_DUP_PTS
***     IDENT	12JA94
C
C	Copyright Laser-Scan Ltd, Cambridge, England
C
C	Author						ST	06-Jan-1994
C
C       Description:
C       Routine to count the number of duplicate points in a feature without
C       outputting any messages. This should only be called from POINT.SRC
C       for level 3 NTF because the order of outputting GEOMETRY records
C       is different.
C
	IMPLICIT NONE
C
C Arguments:
	LOGICAL		IS_LINE			!  Is this a line feature
	INTEGER*2 	IELEN			!  Length of record
	INTEGER*2 	IECODE			!  IFF entry
	INTEGER		ADDR			!  Start address of ST
C
C Common:
	INCLUDE	'LSL$CMNIFF:IFFHAN.CMN'		! for LPOSH
	INCLUDE	'LSL$CMNIFF:IFFJUN.CMN'		! for junction data
	INCLUDE 'COMMANDS.CMN'
	INCLUDE	'FTRCMN.CMN'
	INCLUDE	'STORE.CMN'
	INCLUDE	'NODECMN.CMN'
	INCLUDE 'LSL$CMNFRT:FRTACD.CMN'		! default attribute info
	INCLUDE	'ATTRIBUTE.CMN'
	INCLUDE	'DUPPTS.CMN'
C
C Messages:
	INCLUDE 'I2NTFMSG.PAR'
C
C Functions:
	INTEGER		COUNT_NONDUPS
C
C Workspace:
	INTEGER 	DIMENSION	!  Is it a 2D or 3D feature
	INTEGER 	STCOUNT		!  ST/ZS counter
	INTEGER 	TOTAL		!  Number of points in whole feature
	INTEGER 	TOTPTS		!  As above but after removal of dups
	INTEGER 	COUNT		!  Index
	INTEGER		NPTS		!  No of points in ST/ZS
	INTEGER*2	INPTS		!  No of points in ST/ZS
	INTEGER*2	IENDS		!  Pen up/down flag
	REAL 		STBUF(3*200)	!  Records the points of the feature
C					!  (in either 2D or 3D)
C .......................................................................
C Initialise
C
	STCOUNT     = 0				!  Initialise ST/ZS count
	TOTAL       = 0				!  and the point count
	TOTPTS      = 0				!  and the point count
C
C.............................................................................
C
10	IF (IECODE.EQ.'ST' .OR. IECODE.EQ.'ZS') THEN
	   IF (IECODE.EQ.'ST') THEN
	      DIMENSION=2   
	   ELSE
	      DIMENSION=3
	   ENDIF
C
	   CALL EIHRS(INPTS,IENDS)		! Read the descriptor
	   NPTS = INPTS
C
	   STCOUNT=STCOUNT+1			! Count this ST/ZS
	   CALL EIHRR(STBUF,INPTS*DIMENSION,1)
	   TOTPTS = TOTPTS + COUNT_NONDUPS(STCOUNT,.FALSE.,DIMENSION,NPTS,
     &					   TOTAL,0,STBUF,.FALSE.)
C
	   TOTAL = TOTAL + NPTS
C
	ELSEIF (IECODE.EQ.'EF') THEN
	   GOTO 20				! Finished feature
	ENDIF
C
	CALL IFFNXT(IECODE,IELEN)		! Read the next entry
	GOTO 10
C
20	CALL IFFPKE(IECODE,IELEN,ADDR)		! Read the next entry
	CHECK_DUP_PTS = TOTPTS
	RETURN
	END
