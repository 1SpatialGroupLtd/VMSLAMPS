	SUBROUTINE FC_TRANS (LUTABLE, FC, NTF_FC)
C
C
C Copyright Laser-Scan Ltd., Cambridge, England.
C
C Author Matt Wenham, 29 October 1996
C
C Translates IFF feature code FC into NTF feature code NTF_FC according to
C lookup table LUTABLE. If FC is not in 2nd column of lookup table, no
C change made to NTF_FC. Binary chop search code taken from frtfnd in frtlib.
C
	IMPLICIT NONE
C
C Common:
	INCLUDE 'I2NTFMSG.PAR'
	INCLUDE	'LOOKUP.CMN'
C
C Arguments:
	INTEGER		LUTABLE(2,*)	! Lookup table
	INTEGER		FC		! IFF FC to translate from
	INTEGER		NTF_FC		! NTF FC translated to
C
C Workspace:
	INTEGER I
	INTEGER LOLIM,HILIM          ! bounds for binary chop
	INTEGER BINPTR               ! binary chop pointer
C   ...............................................................
C
C as the FC lookup table is sorted, use binary chop to search
C
	LOLIM = 1
	HILIM = LUT_ENTRIES
10	BINPTR = (LOLIM+HILIM)/2		! next guess
	IF (LUTABLE(2,BINPTR).EQ.FC) GOTO 20	! found it
	IF (LOLIM.GE.HILIM) GOTO 30		! no chop room - FC not found
	IF (FC.GT.LUTABLE(2,BINPTR)) THEN	! which side ?
	    IF (LOLIM.EQ.BINPTR) BINPTR = BINPTR+1
	    LOLIM = BINPTR			! too low
	ELSE
	    HILIM = BINPTR			! too high
	ENDIF
	GOTO 10
C
C Found FC - change NTF_FC accordingly
C
20	NTF_FC = LUTABLE(1,BINPTR)		! Translate NTF feature code
C
	IF (NTF_FC .LT. 0) THEN			! Warn of duplicate IFF FC
	   NTF_FC = -NTF_FC			! Make positive
	   CALL LSL_PUTMSG( I2NTF__IFFFCDUPL, FC, NTF_FC)
	ENDIF
30	CONTINUE
	RETURN
	END
C
