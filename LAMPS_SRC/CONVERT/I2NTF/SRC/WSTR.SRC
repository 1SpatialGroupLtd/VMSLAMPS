	SUBROUTINE WSTR(OUTSTR,LENGTH)
C
C
C	Copyright Laser-Scan Ltd, Cambridge, England
C	Author						IMcL	27-Apr-1987
C
C	Modified					TJI	30-Sep-1987
C	Modified for variable length records 		IMcL	27-Jul-1989
CMOD	Suppress comment lines ('90') for CITF		JB	17-Nov-1992
C	and write a line delimiter of '%'
CMOD	BS7567 (v2.0) support added (W93/12)		ST	20-May-1993
CMOD    Implement CITF96 changes (WI/CC 1545)          TJM      20-May-1996
C
C Write out OUTSTR to the output NTF file and comments to the 
C logfile as well
C
	IMPLICIT NONE
C
C Arguments:
	CHARACTER*80	OUTSTR		!  String to be output
	INTEGER*2	LENGTH		!  of OUTSTR
C
C Common:
	INCLUDE 'LSL$CMNOSMH:OSMH_NEW_MH.CMN'
	INCLUDE 'COMMANDS.CMN'
	INCLUDE	'MAGTAPE.CMN'
	INCLUDE	'LUNS.PAR'
C
C Initialise the record number
C
	DATA		BUFFER_PTR /1/	! Point to beginning of buffer
C
C Function:
	INTEGER		MTOWRB		!  Write buffer to tape
C
C Local:
	INTEGER*4	I
C
C ......................................................................
C
C Suppress output of comments for CITF
	IF ((HAD_CITF93.OR.HAD_CITF96) .AND.
     &      OUTSTR(1:2).EQ.'90') GOTO 999
C
	IF (FIXED.AND.(LENGTH.LT.80) ) THEN
	   OUTSTR(79:79) = OUTSTR(LENGTH-1:LENGTH-1)
	   OUTSTR(LENGTH-1:LENGTH-1) = ' '
	   LENGTH = 80
	ENDIF
C
C Write a delimiting character to the end of the record
C
	IF (HAD_CITF93.OR.HAD_CITF96) THEN
	   OUTSTR(LENGTH:LENGTH) = MH_CITF_EOR		! % by default
	ELSE
	   OUTSTR(LENGTH:LENGTH) = EOR_CHAR
	ENDIF
C
C If tape output selected then write to tape
C
	IF (IS_MAGTAPE) THEN
	   IF (BUFFER_PTR + LENGTH.GT.BLOCKSIZE) THEN	! Buffer full
C
C Flush buffer
C
	      DO 100 I=BUFFER_PTR+1,BLOCKSIZE
		 BUFFER(I:I)=CHAR(0)
100	      CONTINUE
C
C Write buffer
C
	      CALL MTOWRB(%REF(BUFFER),BLOCKSIZE)
	      BUFFER_PTR = 1
	   ENDIF
	   BUFFER(BUFFER_PTR:BUFFER_PTR + LENGTH )= OUTSTR(:LENGTH)
	   BUFFER_PTR = BUFFER_PTR + LENGTH		! Update pointer
	ELSE
C
C Output to disk file
C
	   CALL FLWSEL(OUTLUN)
	   CALL FLWSTR(OUTSTR(:LENGTH))
	ENDIF
C
C If logfile requested and this is a comment then write there as well
C
999	IF (OUTPUT .AND. (OUTSTR(1:2).EQ.'90')) THEN
	   CALL FLWSEL(LOGLUN)
	   CALL FLWSTR('// '//OUTSTR(3:LENGTH - 2))
	ENDIF
C
	RETURN
	END
C
