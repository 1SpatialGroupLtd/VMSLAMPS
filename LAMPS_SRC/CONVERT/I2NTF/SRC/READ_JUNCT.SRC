	LOGICAL FUNCTION READ_JUNCT(RPOSX,RPOSY,NARMS)
C
C
C     Copyright Laser-Scan Ltd, Cambridge, England
C
C	Author		VLG		14-Feb-90
C
C Reads details of the junction in hand 
C
C Returns TRUE for sucessful completion, otherwise FALSE
C
	IMPLICIT NONE
C
C Messages:
C
	INCLUDE 'HERE:I2NTFMSG.PAR'
C
C Commons:
C
	INCLUDE	'LSL$CMNIFF:IFFHAN.CMN'		! for LPOSH
	INCLUDE	'LSL$CMNIFF:IFFJUN.CMN'		! for junction data
	INCLUDE	'HERE:COMMANDS.CMN'
	INCLUDE	'HERE:NODECMN.CMN'
	INCLUDE	'HERE:FTRCMN.CMN'
C
C Arguments:
C
	REAL		RPOSX,RPOSY	!  Position of junction
	INTEGER*2	NARMS		!  number of arms
C
C Workspace:
C
	INTEGER		I
	INTEGER 	OK		!  Success flag
	REAL		POS(2,2)	!  Coords for output
	INTEGER*2	ARMCOUNT	!  Number of arms
	INTEGER*4	ADDR		!  Address of junction
	INTEGER*2	VERT		!  Vertex number in ST
	INTEGER*4	JBPOS		!  Address of our JB
	INTEGER*2	OFFSET		!  Number of junction in JB
C
C Initialise
C
	OK=.TRUE.
C  .......................................................................
C
C Remember details about the junction
C
	NCOUNT = NCOUNT + 1		! Next node
	IF (NCOUNT.GT.MAX_NODES) THEN
	   CALL LSL_PUTMSG(I2NTF__TOOMNYNODES,MAX_NODES)
	   OK = .FALSE.
	   GOTO 999
	ENDIF
	IF (NARMS.GT.MAX_LINKS) THEN
	   CALL LSL_PUTMSG(I2NTF__TOOMNYARMS,NCOUNT,NARMS,MAX_LINKS)
	   NODES(NCOUNT,4) = NARMS
	   OK = .FALSE.
	   GOTO 999
	ENDIF
	ARMCOUNT = 0
	DO 10 I = 1,NARMS
	   CALL JIHR(I,ADDR,VERT)
	   IF (ADDR.NE.0) THEN
	      ARMCOUNT = ARMCOUNT + 1
	      IF (VERT.EQ.1) THEN		! ST starts at junction
	         ST_STORE(NCOUNT,I) = ADDR	
	      ELSE				! ST ends at junction
	         ST_STORE(NCOUNT,I) = -ADDR	! store address as negative
	      ENDIF
	   ENDIF
10	CONTINUE
	NARMS  = ARMCOUNT
C
C Store node coordinates in required format
C
	NODE_GEOMS(NCOUNT,1) = NINT(RPOSX/XY_MULT)
	NODE_GEOMS(NCOUNT,2) = NINT(RPOSY/XY_MULT)
C
	NODES(NCOUNT,1) = LPOSH		! JB address
	NODES(NCOUNT,2) = IFPOSJ	! Offset in JB
	NODES(NCOUNT,3) = 0		! Initialise GEOM_ID
	NODES(NCOUNT,4) = NARMS
	ST_STORE(NCOUNT,7) = NARMS	! Counter to check all links are found
C
C  .......................................................................
C
	OK=.TRUE.
999	READ_JUNCT = OK
	RETURN
	END	   
