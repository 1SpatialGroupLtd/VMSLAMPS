C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1990-06-21 12:50:46.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE READ_COMMAND(IFFNAME,IFFLEN,NTFNAME,OTEXT,
	1					ANNOTATE,COLLECT,RETCOD)
C
***	IDENT	21JN90
***	MODULE	READ_COMMAND
C
C	Copyright Laser-Scan Ltd., Cambridge, England.
C
C	Created 	IMcL		31-Jul-1989
CMOD	Read annotation and collection flags	VLG	21-Mar-1990
CMOD	Fix bug in setting text flag	VLG	01-Jun-1990
C
C	CONVERT I2NTF Utility - decode line from attribute file
C
C Description
C
C	Gets a command from the FRT together with its arguments
C
C	For ordinary errors, we attempt to continue with the file
C
C	Returns 0 for success
C
	IMPLICIT NONE
C
C Arguments
C
	CHARACTER*32		IFFNAME		! Name of IFF attribute
	INTEGER*2		IFFLEN		! length of IFF name read
	CHARACTER*2		NTFNAME		! Name of NTF attribute
	LOGICAL			OTEXT		! Text destination flag
	LOGICAL			ANNOTATE	! Annotation flag
	LOGICAL			COLLECT		! Collect object flag
	INTEGER*4		RETCOD		! system error code
C
C Included Parameter Files
C
	INCLUDE 'CMN:I2NTFMSG.PAR'	    ! message definitions
	INCLUDE 'LSL$CMNLSL:LSLLIBMSG.PAR'  ! LSLLIB message definitions
	INCLUDE 'LSL$CMNLSL:READSTR.PAR'    ! Parameters for READSTR
C
C Local Parameters
C
	BYTE		PLING		! comment delimiter
	PARAMETER	(PLING = '!')
C
C Common Blocks
C
	INCLUDE 'CMN:ATTRIBUTE.CMN'	! attribute description table
	INCLUDE 'HERE:COMMANDS.CMN'	! various options
	INCLUDE 'LSL$CMNLSL:CMDCOM.CMN'	! command decoding
	INCLUDE 'LSL$CMNLSL:STATUS.CMN'	! LSLLIB status flag
	INCLUDE 'LSL$CMNFRT:FRTACD.CMN'	! Attributes in FRT
C
C Functions called
C
	INTEGER		READSTR
C
C Local Variables
C
	CHARACTER*10	COMMAND		! Read from line
	CHARACTER*10	COMSTR		! Legal command
	INTEGER*2	LEN		! Of strings read
	INTEGER*2	I		! loop counter
	INTEGER		RETVAL		! return value from LSLLIB
	CHARACTER*1	FLAG		! Optional flag on command line
C
	DATA COMSTR/'ATTRIBUTE'/
C ..................................................................
C Assume that everything goes well
C
	RETCOD = 0
C
C Initialise
C
	OTEXT = .FALSE.
	ANNOTATE = .FALSE.
	COLLECT = .FALSE.
	IFFNAME = ' '
	NTFNAME = ' '
C
C Read command from line
C
	LEN = READSTR( COMMAND,,ON_SPACE,.TRUE., )
	IF ( LEN.LE.0 ) THEN
	   RETCOD = -1
	   GOTO 999
	ENDIF
C 
C Got the command, see if we recognise it
C
	IF ( COMMAND(:LEN).NE. COMSTR(:LEN) ) THEN
	   CALL LSL_PUTMSG ( I2NTF__UNKNCOM, COMMAND(:LEN))
	   RETCOD = 1
	   GOTO 999
	ENDIF
C
C Read the rest of the line - should be two attributes
C - IFF attribute name...
C 
	IFFLEN = READSTR( IFFNAME,,ON_SPACE,.TRUE., )
	IF ( IFFLEN.LE.0 ) THEN
	   CALL LSL_PUTMSG( I2NTF__NOIFFATT )	! IFF attribute name
	   RETCOD = 1				! wasn't given
	   GOTO 999
	ENDIF
C
C ...and the NTF mnemonic
C
	LEN = READSTR( NTFNAME,,ON_SPACE,.TRUE.,RETVAL )
	IF ( LEN.LE.0 ) THEN
	   CALL LSL_PUTMSG( I2NTF__NONTFATT )	! NTF attribute name
	   RETCOD = 1				! wasn't given
	   GOTO 999
	ELSEIF ( RETVAL.EQ.LSL__STRTOOLONG ) THEN
	   CALL LSL_PUTMSG ( I2NTF__STRTOOLONG,NTFNAME )
	ENDIF
C
C May be flag to say put text in optional text string rather than
C AC value
C
	LEN = READSTR( FLAG,,ON_CMD,.TRUE.,RETVAL )
	IF (LEN.LE.0) THEN
	   OTEXT = .FALSE.	! No flag - text => value
	   GOTO 999
	ELSEIF (FLAG.NE.'T') THEN
	   OTEXT = .FALSE.	! No flag - text => value
	ELSE
	   OTEXT = .TRUE.	! Flag    - text => optional text string
	ENDIF
C
C May be flag to say that this AC is to be used to indicate that a text is 
C an annotation for a particular feature - only at NTF level 3
C
	LEN = READSTR( FLAG,,ON_CMD,.TRUE.,RETVAL )
	IF (LEN.LE.0) THEN
	   ANNOTATE = .FALSE.	! No flag
	   GOTO 999
	ELSEIF (FLAG.NE.'T') THEN
	   ANNOTATE = .FALSE.	! No flag
	ELSE
	   ANNOTATE = .TRUE.		! Flag  - annotation
	   IF (LEVEL.NE.3) THEN		! not valid
	      CALL LSL_PUTMSG(I2NTF__NOANNOTATE,LEVEL)
	      ANNOTATE = .FALSE.
	   ENDIF
	ENDIF
C
C May be flag to say that this AC contains a collection id to be used to build
C a COLLECT record - i.e. a collection of features
C
	LEN = READSTR( FLAG,,ON_CMD,.TRUE.,RETVAL )
	IF (LEN.LE.0) THEN
	   COLLECT = .FALSE.	! No flag
	   GOTO 999
	ELSEIF (FLAG.NE.'T') THEN
	   COLLECT = .FALSE.	! No flag
	ELSE
	   COLLECT = .TRUE.	! Flag    - collection
	   IF (LEVEL.NE.3) THEN		! not valid
	      CALL LSL_PUTMSG(I2NTF__NOCOLLECT,LEVEL)
	      COLLECT = .FALSE.
	   ENDIF
	ENDIF
C .....................................................................
C Finish
C
	RETCOD = 0
C
C Return
C
999	RETURN
	END
