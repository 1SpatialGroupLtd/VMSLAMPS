C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1994-06-13 16:00:14.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION GETLIN
C
***	IDENT	13JN94
***	MODULE	GETLIN
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
CMOD    Don't automatically throw away attribute	ST	13-Jun-1994
C	annotation points, we may want them. Mod 1371.
C	Pinched from IMAP2CAD and modified		RJH	13-Oct-1987
C	Created						RJH	04-DEC-1985
C
C	CONVERT IMAP2I Utility - Gets a line element
C
C Description
C
C	Returns true on success
C
	IMPLICIT NONE
C
C Common Blocks
C
	INCLUDE 'CMN:FLAGS.CMN'		! flags
	INCLUDE	'CMN:TABLES.CMN'	! parameter tables
	INCLUDE 'CMN:INFDAT.CMN'	! INFORMAP data
C
C Functions called
C
	LOGICAL		NXTGRA		! gets next graphical element
C
C Local Variables
C
	REAL*8		DESAVE,DNSAVE	! saved first point of segment
	INTEGER*2	DC_ERR		! error return from DCLIP
	LOGICAL		SET_NEW		! set mode for new attributes ?
	LOGICAL		NONE		! no suitable element yet ?
	LOGICAL		FINISH		! finished ?
C
C Don't mes about with mode yet
C
	SET_NEW = .FALSE.
C
C Assume not got a suitable element
C
100	NONE = .FALSE.
C
C Get the next element
C
	FINISH = (.NOT. NXTGRA())
C
C Set mode as having just read a new attribute record, if this was the 
C case for the last segment which was ignored
C
	IF (.NOT. FINISH) THEN
	   IF (SET_NEW) THEN
	      MODE = MODE .OR. MSK_NEW
	      SET_NEW = .FALSE.
	   ENDIF
C
C Chuck out annotation points - provided we are not dealing with them
C
	   IF (.NOT.DO_ANNOTATION) NONE = ((MODE .AND. MSK_ANNO).EQ.MSK_ANNO)
C
C Are we checking the range ?
C
	   IF (CHECK_AREA .AND. (.NOT. NONE)) THEN
C
C For circles and arcs all three points should be within range
C
	      IF ((MODE .AND. MSK_ARC) .EQ. MSK_ARC) THEN
	         NONE = ((DE(1).LT.NGORIG(1)) .OR. (DE(1).GT.NGTOPR(1))
     &	            .OR. (DN(1).LT.NGORIG(2)) .OR. (DN(1).GT.NGTOPR(2))
     &	            .OR. (DE(2).LT.NGORIG(1)) .OR. (DE(2).GT.NGTOPR(1))
     &	            .OR. (DN(2).LT.NGORIG(2)) .OR. (DN(2).GT.NGTOPR(2))
     &	            .OR. (DE(3).LT.NGORIG(1)) .OR. (DE(3).GT.NGTOPR(1))
     &	            .OR. (DN(3).LT.NGORIG(2)) .OR. (DN(3).GT.NGTOPR(2)))
C
C For lines and curves, first save the last or green point
C
	      ELSE
	         DESAVE=DE(1)
	         DNSAVE=DN(1)
C
C Now clip the line segment
C
	         CALL DCLIP(NGORIG(1),NGORIG(2),NGTOPR(1),NGTOPR(2),
     &					DE(1),DN(1),DE(2),DN(2),DC_ERR)
C
C Check if the segment is completely outside
C
	         NONE = (DC_ERR .EQ. -1)
C
C If first point moved, force to be green
C
	         IF ((DE(1).NE.DESAVE) .OR. (DN(1).NE.DNSAVE)) THEN
	            MODE = MODE .OR. MSK_GREEN
	         ENDIF
	      ENDIF
	   ENDIF
C
C If we are ignoring this segment, but just read a new attribute 
C record, set the flag to say so
C
	   IF (NONE) SET_NEW = ((MODE .AND. MSK_NEW) .EQ. MSK_NEW)
	ENDIF
C
C Repeat if necessary
C
	IF (NONE) GO TO 100
C
C Assign function value
C
	GETLIN = (.NOT. FINISH)
C
C Return
C
	RETURN
	END
