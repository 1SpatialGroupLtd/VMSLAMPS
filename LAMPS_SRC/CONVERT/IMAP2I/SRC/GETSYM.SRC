	LOGICAL FUNCTION GETSYM
C
***	IDENT	13JN94
***	MODULE	GETSYM
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
CMOD    Don't automatically throw away attribute	ST	13-Jun-1994
C	annotation points, we may want them. Mod 1371.
C	Pinched from IMAP2CAD and modified		RJH	13-Oct-1987
C	Created						RJH	04-DEC-1985
C
C	CONVERT IMAP2I Utility - Gets a symbol
C
C Description
C
C	Returns true on success
C
	IMPLICIT NONE
C
C Common Blocks
C
	INCLUDE 'CMN:FLAGS.CMN'		! flags
	INCLUDE 'CMN:INFDAT.CMN'	! INFORMAP data
	INCLUDE	'CMN:TABLES.CMN'	! parameter tables
C
C Functions called
C
	LOGICAL		NXTGRA		! gets next graphical element
C
C Local Variables
C
	LOGICAL		SET_NEW		! set mode for new attributes ?
	LOGICAL		NONE		! no suitable element yet ?
	LOGICAL		FINISH		! finished ?
C
C Don't mes about with mode yet
C
	SET_NEW = .FALSE.
C
C Assume not got a suitable element
C
100	NONE = .FALSE.
C
C Get the next element
C
	FINISH = (.NOT. NXTGRA())
C
C Set mode as having just read a new attribute record, if this was the 
C case for the last segment which was ignored
C
	IF (.NOT. FINISH) THEN
	   IF (SET_NEW) THEN
	      MODE = MODE .OR. MSK_NEW
	      SET_NEW = .FALSE.
	   ENDIF
C
C Chuck out annotation points - provided were aren't dealing with them
C
	   IF (.NOT.DO_ANNOTATION) NONE = ((MODE .AND. MSK_ANNO).EQ.MSK_ANNO)
C
C Are we checking the range ?
C
	   IF (CHECK_AREA .AND. (.NOT. NONE)) THEN
C
C Check that the point is within range
C
	      NONE = ((DE(1).LT.NGORIG(1)) .OR. (DE(1).GT.NGTOPR(1))
     &	         .OR. (DN(1).LT.NGORIG(2)) .OR. (DN(1).GT.NGTOPR(2)))
	   ENDIF
C
C If we are ignoring this segment, but just read a new attribute 
C record, set the flag to say so
C
	   IF (NONE) SET_NEW = ((MODE .AND. MSK_NEW) .EQ. MSK_NEW)
	ENDIF
C
C Repeat if necessary
C
	IF (NONE) GO TO 100
C
C Assign function value
C
	GETSYM = (.NOT. FINISH)
C
C Return
C
	RETURN
	END
