        LOGICAL FUNCTION INI_FEATURE(DATA_KIND,DATA_TYPE,LINE_TYPE)
C
***     IDENT   13JL89
***     MODULE  INI_FEATURE
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created					RJH	15-Oct-1987
C
C	CONVERT IMAP2I Utility - Initialisation for a new feature
C
C Description
C
C
        IMPLICIT NONE
C
C Included Parameter Files
C
	INCLUDE 'CMN:IMAP2IMSG.PAR'	! message definitions
	INCLUDE	'LSL$CMNIFF:CB.STR'	! record structure definition
C
C Arguments
C
	INTEGER*2	DATA_KIND	! INFORMAP datakind
	INTEGER*2	DATA_TYPE	! INFORMAP datatype
	INTEGER*2	LINE_TYPE	! INFORMAP linetype
C
C Common Blocks
C
	INCLUDE 'CMN:FLAGS.CMN'		! flags
	INCLUDE 'CMN:TABLES.CMN'	! translation tables
	INCLUDE 'CMN:IFF_FEATURE.CMN'	! feature details
	INCLUDE	'CMN:IFF_AC.CMN'	! AC's in the feature
	INCLUDE	'CMN:IFF_POINTS.CMN'	! points in the feature
	INCLUDE 'CMN:TEXT.CMN'		! text data
C
C Functions called
C
	LOGICAL		FRTFND		! find feature code
C
C Local Variables
C
	INTEGER*4	I		! counter
C
C Assume failure
C
	INI_FEATURE = .FALSE.
C
C Initialise flags for type of feature (to be set later)
C
	IS_LINE   = .FALSE.
	IS_CURVE  = .FALSE.
	IS_CIRCLE = .FALSE.
	IS_ARC    = .FALSE.
	IS_SYMBOL = .FALSE.
	IS_TEXT   = .FALSE.
C
	ORI_SYM   = .FALSE.
	SCA_SYM   = .FALSE.
C
C Haven't found the FC in the FRT
C
	FOUND_FC = .FALSE.
C
C Haven't started yet
C
	DONE_START_FEATURE = .FALSE.
C
C Initialise data
C
	ANGLE    = 0.0
	STYLE    = 0
	JUSTIF   = 0
C
C and the points, just for safety
C
	STPTR = 0
	STBUF(1,0) = 0.0
	STBUF(2,0) = 0.0
C
C If we know there are no attributes, clear out any AC's remaining from 
C the previous feature - this is done elsewhere (RETATT) if there are 
C attributes to transfer
C
	IF ( DATA_KIND.NE.4 .AND. DATA_KIND.NE.5 ) NUM_AC = 0
C
C Find the feature code
C
	DO 100 I=1,TOIFF_PTR
	   IF (TOIFF_TAB(TOIFF_DTYPE,I) .EQ. DATA_TYPE) THEN
	      IF (TOIFF_TAB(TOIFF_LTYPE,I) .EQ. LINE_TYPE) THEN
	         FC = TOIFF_TAB(TOIFF_FC,I)
	         HEIGHT   = TOIFF_TAB(TOIFF_HEIGHT,I)
	         CATEGORY = TOIFF_TAB(TOIFF_CAT,I)
	         INI_FEATURE = .TRUE.
	      ENDIF
	   ENDIF
100	CONTINUE
C
C Check that we have an FC - failure means the linetype was not defined 
C in the parameter file - we know the datatype must have been as we are 
C already selected
C
	IF (.NOT. INI_FEATURE) THEN
	   CALL LSL_PUTMSG(IMAP2I__UNKLTYPE,LINE_TYPE,DATA_TYPE)
C
C If we had an FRT file, try to find the feature code - if this fails, 
C we rely on the FRTLIB warning message
C
	ELSEIF (HAD_FRT) THEN
	   FOUND_FC = (.NOT. FRTFND(FC))
	ENDIF
C
C Return
C
	RETURN
        END
