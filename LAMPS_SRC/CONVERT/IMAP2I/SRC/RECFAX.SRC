C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1987-10-09 15:37:54.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE RECFAX(X1,Y1,X2,Y2,FACET,MAXFAX,NFAX)
C
C
***	IDENT	09OC87
***	MODULE	RECFAX
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Copied and modified for IMAP2I		RJH	09-Oct-1987
C	Created for IMAP2CAD			SGH	03-DEC-1985
C
C	CONVERT IMAP2I Utility - Get facets intersecting a rectangle
C
C
	IMPLICIT NONE
C
	REAL*8 X1,Y1			! lower left of rectangle
	REAL*8 X2,Y2			! upper right of rectangle
	INTEGER*4 NFAX
	INTEGER*4 MAXFAX
	CHARACTER*9 FACET(MAXFAX)
C
	REAL*8 E
	PARAMETER (E=0.01)
C
	CHARACTER*9 FAC
	INTEGER*4 I,J,N
	INTEGER*4 II,JJ
	INTEGER*4 IR1,JR1
	INTEGER*4 IR2,JR2
	INTEGER*4 IB1,JB1
	INTEGER*4 IB2,JB2
	INTEGER*4 NXBASE,NYBASE
C
	REAL VX1,VY1
	REAL VX2,VY2
	INTEGER*4 VXSIZE,VYSIZE
	INTEGER*4 VXNUM,VYNUM
C
	INCLUDE 'CMN:FACET.CMN'
C
C get range of BASE facets covering rectangle
C
	IR1=(X1-XORIG)/XBASE
	JR1=(Y1-YORIG)/YBASE
	IR2=((X2-E)-XORIG)/XBASE
	JR2=((Y2-E)-YORIG)/YBASE
C
C check not too many facets
C
	NXBASE=IR2+1-IR1
	NYBASE=JR2+1-JR1
	NFAX=NXBASE*NYBASE
	IF(NFAX.GT.MAXFAX)THEN
	   NFAX=-1
	   GOTO 900
	ENDIF
C
C set up array of base facet names
C
	DO 100 I=IR1,IR2
	DO 90  J=JR1,JR2
	CALL BASFAC(I,J,FAC)
	FACET(NYBASE*(I-IR1)+J-JR1+1)=FAC
90	CONTINUE
100	CONTINUE
C
C process variable facets
C
	DO 500 N=1,VAR
	VX1=VX(N)
	VY1=VY(N)
	VXSIZE=VXS(N)
	VYSIZE=VYS(N)
	VXNUM =VXN(N)
	VYNUM =VYN(N)
	VX2=VX1+(VXSIZE*VXNUM*XBASE)
	VY2=VY1+(VYSIZE*VYNUM*YBASE)
C
C get facet indexes for variable facet block
C
	IB1=(VX1-XORIG)/XBASE
	JB1=(VY1-YORIG)/YBASE
	IB2=((VX2-E)-XORIG)/XBASE
	JB2=((VY2-E)-YORIG)/YBASE
C
C preliminary elimination
C
	IF(IB1.GT.IR2.OR.IB2.LT.IR1)GOTO 500
	IF(JB1.GT.JR2.OR.JB2.LT.JR1)GOTO 500
C
	DO 400 I=IB1,IB2
	DO 400 J=JB1,JB2
C
C test base facet (I,J) for being a variable facet origin
C
	IF(MOD(I-IB1,VXSIZE).EQ.0.AND.MOD(J-JB1,VYSIZE).EQ.0)THEN
C
C it is a variable facet origin:
C
C does it already appear in the list? - if so, forget it
C
	   IF(I.GE.IR1.AND.I.LE.IR2.AND.J.GE.JR1.AND.J.LE.JR2)GOTO 400
C
C is whole v. facet clear of rectangle - if so, forget this base facet
C
	   IF((I+VXSIZE-1).LT.IR1.OR.I.GT.IR2)GOTO 400
	   IF((J+VYSIZE-1).LE.JR1.OR.J.GT.JR2)GOTO 400
C
C var facet meets rectangle - add it facet to list (name=base fac name)
C
	   NFAX=NFAX+1
	   IF(NFAX.GT.MAXFAX)THEN
	      NFAX=-1
	      GOTO 900
	   ENDIF
	   CALL BASFAC(I,J,FAC)
	   FACET(NFAX)=FAC
	ELSE
C
C it is not a variable facet origin:
C
C is it in the rectangle? - if not, forget it
C
	   IF(I.LT.IR1.OR.I.GT.IR2.OR.J.LT.JR1.OR.J.GT.JR2)GOTO 400
C
C does the v. facet containing it meet the rectangle?
C if so, remove this base facet from the list
C
C first get the v. facet origin II,JJ
C
	   II=IB1+((I-IB1)/VXSIZE)*VXSIZE
	   JJ=JB1+((J-JB1)/VYSIZE)*VYSIZE
C
	   IF((II+VXSIZE-1).LT.IR1.OR.II.GT.IR2)GOTO 400
	   IF((JJ+VYSIZE-1).LT.JR1.OR.JJ.GT.JR2)GOTO 400
C
C v. fac meets rectangle
C remove this base facet, but DON'T reduce count
C
	   FACET(NYBASE*(I-IR1)+J-JR1+1)='         '
	ENDIF
C
400	CONTINUE
500	CONTINUE
C
C tidy output array
C
	I=0
	DO 600 J=1,NFAX
	IF(FACET(J).NE.'         ')THEN
	   I=I+1
	   FACET(I)=FACET(J)
	ENDIF
600	CONTINUE
	NFAX=I
	DO 700 J=NFAX+1,MAXFAX
	FACET(J)='         '
700	CONTINUE
C
900 	RETURN
	END
