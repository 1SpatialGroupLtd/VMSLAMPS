C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1994-06-13 16:48:24.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION RDADT
C
***	IDENT	13JN94
***	MODULE	RDADT
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
CMOD    Flag each attribute annotation AC as it is	ST	13-Jun-1994
C	found and set the global flag, DO_ANNOTATION
C	when they have all been defined. Mod 1371.
CMOD    New message added for when the number of	ST	17-Aug-1993
C       attributes exceeds MAX_ATTR_NUM. Mod 1276.
C	Pinched from I2IMAP and modified		RJH	22-Jun-1989
C	Implemented					RJH	 2-Jun-1989
C	Created						RJH	24-Sep-1987
C
C	CONVERT IMAP2I Utility - Read ADT entry
C
C Description
C
C	Reads an attribute description table entry
C
C	Expects the secondary command number just read to be in 
C		SECMDN    in /CMDCOM/
C	    and (if the command was ADT CODE) the AC code to be in 
C		INTARG(1) in /CMDCOM/
C
C	Returns FALSE if something goes wrong with a command line, 
C	TRUE otherwise
C
	IMPLICIT NONE
C
C Included Parameter Files
C
	INCLUDE 'CMN:IMAP2IMSG.PAR'	    ! message definitions
C
C Common Blocks
C
	INCLUDE 'CMN:TABLES.CMN'	! translation tables
	INCLUDE	'LSL$CMNFRT:FRTACD.CMN'	! FRT ACD common block
	INCLUDE 'LSL$CMNLSL:CMDCOM.CMN'	! command decoding
C
C Functions called
C
	INTEGER*4	RDCOMM		! read a command
	LOGICAL		ACDFND		! find attribute code
	LOGICAL		ACDFND_NAME	! find attribute name
	LOGICAL		READ_NAM	! read attribute name
	LOGICAL		READ_DTR	! read range of data tyeps
	LOGICAL		UP_NAM		! check attribute name
C
C Local Variables
C
	CHARACTER*(ACD_NAME_MAX) THIS_NAME	! an attribute name
	INTEGER*4		 L		! its length
	INTEGER*4		 RESULT		! command read result
C
C External declaration
C
	INTEGER*4	FLDCMD_CMD_TABLE
	EXTERNAL	FLDCMD_CMD_TABLE
C
C Assume that everything goes well
C
	RDADT = .TRUE.
C
C and get ready for a new attribute description
C
	ATTR_PTR = ATTR_PTR + 1
	IF (ATTR_PTR.GT.MAX_ATTR_NUM) THEN
	   CALL LSL_PUTMSG(IMAP2I__TOOMANYATTDES,MAX_ATTR_NUM)
	   GOTO 9999
	ENDIF
C
C Despatch according to secondary command
C
	GOTO (100,200) SECMDN
	CALL LSL_PUTMSG(IMAP2I__UNKCMD,SECNST)	! unknown command
	GOTO 9999
C
C ADT CODE - we have already read the code so check that it is in range 
C
100	IF ( INTARG(1).LT.0 .OR. INTARG(1).GT.MAX_WORD ) THEN
	   CALL LSL_PUTMSG(IMAP2I__BADATTCODE,INTARG(1))
	   GOTO 9999
	ENDIF
C
C and get its details
C
	IF (ACDFND(INTARG(1),.FALSE.)) THEN
	   CALL LSL_PUTMSG(IMAP2I__DEFAULTACD,INTARG(1))
	ENDIF
C
	GOTO 300
C
C ADT NAME - read in the attribute name
C
200	IF (.NOT. READ_NAM(THIS_NAME,L)) GOTO 9999
C
C and get its details
C
	IF (ACDFND_NAME(THIS_NAME(1:L))) THEN
	   CALL LSL_PUTMSG(IMAP2I__BADATTNAME,THIS_NAME(1:L))
	   GOTO 9999
	ENDIF
C
CMOD	.......................................................................
C
C	remember the attributes for Annotation and its number
C
	IF (THIS_NAME(1:L).EQ.'STARTX') THEN
	  NSX_ANNO_PTS = 1
	  ATTR_SX = ACD_CODE
	ELSE IF (THIS_NAME(1:L).EQ.'STARTY') THEN
	  NSY_ANNO_PTS = 1
	  ATTR_SY = ACD_CODE
	ELSE IF (THIS_NAME(1:L).EQ.'ENDX') THEN
	  NEX_ANNO_PTS = 1
	  ATTR_EX = ACD_CODE
	ELSE IF (THIS_NAME(1:L).EQ.'ENDY') THEN
	  NEY_ANNO_PTS = 1
	  ATTR_EY = ACD_CODE
	ENDIF
C
C..............................................................................
C
C Now save the appropriate things
C
300	ATTR_TAB(ATTR_COD,ATTR_PTR) = ACD_CODE
	ATTR_TAB(ATTR_TYP,ATTR_PTR) = ACD_DATA_TYPE
C
C Read the command describing the field in the IFF entry
C
	RESULT = RDCOMM(FLDCMD_CMD_TABLE)
	IF (RESULT .LE. 0) GOTO 9999
C
	GOTO (400,500) RESULT			! and despatch
	CALL LSL_PUTMSG(IMAP2I__UNKCMD,CMDNST)	! unknown command
	GOTO 9999
C
400	GOTO (410,420) SECMDN			! despatch again
	CALL LSL_PUTMSG(IMAP2I__UNKCMD,SECNST)	! unknown command
	GOTO 9999
C
C AC VALUE - just set the field indicator
C
410	ATTR_TAB(ATTR_FLD,ATTR_PTR) = AC_VAL
	GOTO 600
C
C AC TEXT - set the field indicator, and leave the type unset
C
420	ATTR_TAB(ATTR_FLD,ATTR_PTR) = AC_TEXT
	ATTR_TAB(ATTR_TYP,ATTR_PTR) = UNSET_VAL
	GOTO 600
C
C CB - just set the field indicator
C
500	ATTR_TAB(ATTR_FLD,ATTR_PTR) = CB_COL
C
C Read in the INFORMAP attribute name
C
600	IF (.NOT. READ_NAM(ATTR_NAM(ATTR_PTR),L)) GOTO 9999
C
C and make sure we can use it
C
	IF (.NOT. UP_NAM(ATTR_NAM(ATTR_PTR)(1:L))) GOTO 9999
C
C Now read the range of datatypes for this attribute
C
	IF (.NOT. READ_DTR(ATTR_DTR(MIN_DTYPE,ATTR_PTR),
     &				MIN_DTYPE,MAX_DTYPE)) GOTO 9999
C
C and finish
C
	GOTO 9000			! success
C
C Errors
C
9999	RDADT = .FALSE.			! failure
	ATTR_PTR = ATTR_PTR - 1		! no need to unset anything else
C
C..............................................................................
C
C	check that all the required ACD names were found for Attr annotation
C
9000	IF ((NSX_ANNO_PTS.GT.0).AND.(NSY_ANNO_PTS.GT.0).AND.
     &	   (NEX_ANNO_PTS.GT.0).AND.(NEY_ANNO_PTS.GT.0))
     &	   DO_ANNOTATION = .TRUE.
C
	RETURN
	END
