	LOGICAL FUNCTION RDTET
C
***	IDENT	06MR89
***	MODULE	RDTET
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created, based on IFF2IMAP RDFDT	ADT	03-Feb-1989
C
C Description
C
C	Reads a TRANS_ERR_TEXT entry in the parameter file
C
C	Expects the datatype just read to be in INTARG(1) in /CMDCOM/
C		the linetype just read to be in INTARG(2) in /CMDCOM/
C
C	Returns FALSE if something goes wrong with a command line, 
C	TRUE otherwise
C
	IMPLICIT NONE
C
C Included Parameter Files
C
	INCLUDE 'CMN:CHECK.PAR'		    ! input checks
	INCLUDE 'CMN:I2IMAPMSG.PAR'	    ! message definitions
	INCLUDE 'LSL$CMNLSL:LSLLIBMSG.PAR'  ! LSLLIB message definitions
C
C Common Blocks
C
	INCLUDE 'CMN:CALOG.CMN'		! flags
	INCLUDE 'CMN:CAINT.CMN'		! feature description table
	INCLUDE 'LSL$CMNLSL:CLD.CMN'	! DCL command decoding
	INCLUDE 'LSL$CMNLSL:CMDCOM.CMN'	! command decoding
	INCLUDE 'LSL$CMNLSL:EXCEPTION.CMN'	! number reading errors
C
C Functions called
C
	LOGICAL		RDINT		! reads integer
C
C Local Variables
C
	INTEGER*4	I		! counter
	INTEGER*4	PENNUM		! pen number
	INTEGER*4	LOW		! lowest height in range
	INTEGER*4	HIGH		! highest height in range
	INTEGER*4	CAT		! text category
	LOGICAL		NOTHING		! not a symbol ?
C
C Assume that everything goes well
C
	RDTET = .TRUE.
C
C Check that the datatype is within the permissible range
C
	IF ( INTARG(1).LT.MIN_DTYPE .OR. INTARG(1).GT.MAX_DTYPE ) THEN
	   CALL LSL_PUTMSG(I2IMAP__BADDTYPE,INTARG(1))
	   GOTO 9999
	ELSE
	   DEF_TEXT(3) = INTARG(1)
	ENDIF
C
C Do the same for the linetype
C
	IF ( INTARG(2).LT.MIN_LTYPE .OR. INTARG(2).GT.MAX_LTYPE ) THEN
	   CALL LSL_PUTMSG(I2IMAP__BADLTYPE,INTARG(2))
	   GOTO 9999
	ELSE
	   DEF_TEXT(4) = INTARG(2)
	ENDIF
C
C Assume we have a height range
C
	NOTHING = RDINT(LOW)
	IF (NOTHING) THEN
	   IF ((ERRNUM .NE. LSL__UNEXPEOL) .AND. 
     &	       (ERRNUM .NE. LSL__NONUM)) THEN
	      GOTO 410
	   ENDIF
	ELSE
	   NOTHING = RDINT(HIGH)
C
C Check for the high specification
C
	   IF (NOTHING) THEN
	            CALL LSL_PUTMSG(I2IMAP__INCTETDEF)
	            GOTO 9999
C
C Otherwise we have a height range, but check it first
C
	   ELSEIF (LOW .LT. 0) THEN
	      CALL LSL_PUTMSG(I2IMAP__BADTEXTHT,LOW)
	      GOTO 9999
	   ELSEIF (HIGH .LT. 0) THEN
	      CALL LSL_PUTMSG(I2IMAP__BADTEXTHT,HIGH)
	      GOTO 9999
	   ELSE
	      DEF_TEXT(LF_LOW) = LOW
	      DEF_TEXT(LF_HIGH) = HIGH
C
C Now try for the category
C
	      NOTHING = RDINT(CAT)
	      IF (NOTHING) THEN
	         IF ((ERRNUM .NE. LSL__UNEXPEOL) .AND. 
     &	             (ERRNUM .NE. LSL__NONUM)) THEN
	            GOTO 410
	         ENDIF
	      ELSEIF ( CAT.LT.MIN_CAT .OR. CAT.GT.MAX_CAT ) THEN
	         CALL LSL_PUTMSG(I2IMAP__BADTEXTCAT,CAT)
	         GOTO 9999
	      ELSE
		 DEF_TEXT(LF_CAT) = CAT
	      ENDIF
	   ENDIF
	ENDIF
	GOTO 9000
C
C Error reading integer value
C
410	CALL LSL_PUTMSG(ERRNUM)
	IF (ERRNUM .EQ. LSL__HADEXCP) CALL LSL_ADDMSG(LSL_EXCP)
	GOTO 9999
C
C Errors
C
9999	RDTET = .FALSE.		! failure
C
C Return
C
9000	RETURN
	END
