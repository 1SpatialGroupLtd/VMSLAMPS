	SUBROUTINE FC_TRANS (LUTABLE, NTF_FC, IFF_FC)
C
***	MODULE	FC_TRANS
***	IDENT	28NO96
C
C Copyright Laser-Scan Ltd., Cambridge, England.
C
C Author Matt Wenham, 29 October 1996
C
C Translates NTF feature code NTF_FC into IFF feature code IFF_FC according to
C lookup table LUTABLE. If NTF_FC is not in 1st column of lookup table, no
C change made to NTF_FC. Binary chop search code taken from frtfnd in frtlib.
C
	IMPLICIT NONE
C
C Messages:
	INCLUDE 'HERE:NTF2IMSG.PAR'
	INCLUDE 'LSL$CMNLSL:LSLLIBMSG.PAR'

C
C Common:
	INCLUDE	'LOOKUP.CMN'
C
C Arguments:
	INTEGER		LUTABLE(2,*)	! Lookup table
	INTEGER		NTF_FC		! NTF FC to translate from
	INTEGER		IFF_FC		! IFF FC translated to
C
C Workspace:
	INTEGER I
	INTEGER LOLIM,HILIM          ! bounds for binary chop
	INTEGER BINPTR               ! binary chop pointer
C   ...............................................................
C
C as the FC lookup table is sorted, use binary chop to search
C
	LOLIM = 1
	HILIM = LUT_ENTRIES
10	BINPTR = (LOLIM+HILIM)/2		! next guess
	IF (LUTABLE(1,BINPTR).EQ.NTF_FC) GOTO 20 ! found it
	IF (LOLIM.GE.HILIM) GOTO 30		! no chop room - FC not found
	IF (NTF_FC.GT.LUTABLE(1,BINPTR)) THEN	! which side ?
	    IF (LOLIM.EQ.BINPTR) BINPTR = BINPTR+1
	    LOLIM = BINPTR			! too low
	ELSE
	    HILIM = BINPTR			! too high
	ENDIF
	GOTO 10
C
C Found NTF FC - change IFF_FC accordingly
C
20	IFF_FC = LUTABLE(2,BINPTR)		! Translate NTF feature code
C
	IF (IFF_FC .LT. 0) THEN			! Warn of duplicate NTF FC
	   IFF_FC = -IFF_FC			! Make positive
	   CALL LSL_PUTMSG( NTF2I__NTFFCDUPL, NTF_FC, IFF_FC)
	ENDIF
30	CONTINUE
	RETURN
	END
C
