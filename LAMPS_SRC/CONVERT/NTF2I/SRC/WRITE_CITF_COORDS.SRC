	SUBROUTINE WRITE_CITF_COORDS(NCOORDS,GRAPH)
C
***	IDENT	02FE93
***	MODULE	WRITE_CITF_COORDS
C
C Copyright (C) LaserScan Laboratories Ltd, Cambridge, England
C
C Author TJ Ibbs, 7 October 1987
C
C To write out the coordinates in our array
C
CMOD	write AC with height of feature(ZMAX) if HAD_Z was set	SG 31-jul-1991
C
CMOD	bug fix, The value of IENDS was used but never set, removed duplicate 
C	code							SG 12-sep-1991
C
C
	IMPLICIT NONE
C
C Arguments:
	INTEGER		NCOORDS			! number of coordinate pairs
	INTEGER		GRAPH			! grpahical type of feature
C
C Includes
	INCLUDE		'NTFDATA.CMN'
	INCLUDE		'CITF.CMN'
	INCLUDE		'SUPERSET.CMN'
C
C Workspace:
	INTEGER		I,J,K			! loop counters
	INTEGER		POSN			! location in COORDS
	INTEGER		NUM			! how many to write
	INTEGER*2	IENDS			! pen up/down flag
C
	INTEGER*2	LCODE
	REAL		RCODE
	EQUIVALENCE	(LCODE,RCODE)
C
C----------------------------------------------------------------------------
C if the buffers are empty, then RETURN at once, doing nothing
C
	IF ( NCOORDS.LE.0 ) GOTO 999
C
C set up the CB header - 
C
	CBH.FLAGS = 0		! pen up to the CB
C
C set up the graphical type
C
	CBH.GTYPE = GRAPH
C
C no fixed attributes yet
C
	CBH.NATT = 0
C
C Fill CBH columns with AC codes
C
	CBH.NCOL = OUTBUFCOL+NUM_POINTATTS
	DO 400 I = 1,CBH.NCOL
	   CBH.COLH(I) = OUTBUFC(I)
400	CONTINUE
C
C Write lines of data in blocks of 200s
C
	DO 500 I=1,NCOORDS
	   J = MOD(I-1,200)+1		! prevent J from ever being zero
	   CALL IFS_CB_WRITE(CBH,CBD,OUTBUF(1,I),CBH.NCOL,CBH.COLH(1),J,J)
	   IF (J.EQ.200 .OR. I.EQ.NCOORDS) THEN
	      CBH.NROW = J
C
C compress the CB
C
	      CALL IFS_CB_COMPRESS(CBH,CBD)
C
C and write it out
C
	      CALL IFFCB(CBH,CBD)
C
C IFS_CB_COMPRESS can compact the CBH.COLH array so the next CB will be wrong
C so we must rebuild the list AFTER the've been written with IFFCB
C
	      IF (J.EQ.200) THEN
		 CBH.NCOL = OUTBUFCOL+NUM_POINTATTS
		 DO 450 K = 1,CBH.NCOL
		    CBH.COLH(K) = OUTBUFC(K)
450		 CONTINUE
	      ENDIF
C
C Set the pen down
C
	      CBH.FLAGS = 1
	   ENDIF
C
500	CONTINUE
C
999	RETURN
	END
