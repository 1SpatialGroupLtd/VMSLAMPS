	LOGICAL FUNCTION READ_DBHREC
C
***	IDENT	13JN96
***	MODULE	READ_DBHREC
C
C Copyright (C) LaserScan Laboratories Ltd, Cambridge, England
C
C Author TJ Ibbs, 13 October 1987
C
C	Read the data-base header records - for information only
C
CMOD	added meaningful variable names			SG 24-June-1991
C
CMOD	added new function IS_CONTINUATION() which checks if current record
C	has continuation mark (checks for implied carriage return)
C							SG 25-June-1991
CMOD	BS7567 (v2.0) support added (W93/13)		ST 02-Jun-1993
C
CMOD    Implement CITF96 changes (WI/CC 1545)           TJM     13-Jun-1996
C
	IMPLICIT NONE
C Common files
C
	INCLUDE	'NTF2IMSG.PAR'
	INCLUDE	'CITF.CMN'
	INCLUDE	'COMMANDS.CMN'
	INCLUDE	'NTFDATA.CMN'
	INCLUDE	'RECORD.CMN'
C
C Functions
C
	LOGICAL		READ_CONTINUE
	LOGICAL		IS_CONTINUATION
C
C Workspace
C
	LOGICAL		OK
	CHARACTER*20	DBNAME
	CHARACTER*20	DDNAME
	CHARACTER*8	DDATE
	CHARACTER*20	FCNAME
	CHARACTER*8	FCDATE
	CHARACTER*20	DQNAME
	CHARACTER*8	DQDATE
	CHARACTER*2	DMODEL
C
	CHARACTER*2	DAY,MONTH
	CHARACTER*4 	YEAR
	INTEGER*4	I
C
C...........................................................................
C
	DBNAME= STRING(3:22)
	DDNAME= STRING(23:42)
	DDATE = STRING(43:50)
	DAY   = DDATE(7:8)
	MONTH = DDATE(5:6)
	YEAR  = DDATE(1:4)
C
C...........................................................................
C At the moment, the record(s) are just reflected to the log file
C
	IF ( OUTPUT ) THEN
	   CALL FLWSTR('Database header data:')
	   CALL FLWRTF('	Database name         "%S"',DBNAME)
	   CALL FLWRTF('	Data dictionary       "%S"',DDNAME)
	   CALL FLWRTF('	Database date         "%S" (ie %S/%S/%S)',
     &			DDATE,DAY,MONTH,YEAR)
	ENDIF
	IF (.NOT.(HAD_CITF93.OR.HAD_CITF96)) THEN
	   IF (DDATE.NE.NTFDATE .AND. DDATE.NE.NTFDATEV2)
     &	       CALL LSL_PUTMSG( NTF2I__NTFDATE,DDATE)
	ENDIF
C
	IF (HAD_CITF93.OR.HAD_CITF96) THEN
	   DO 100 I = 1,20
	      IF (DDNAME(I:I).NE.' ') GOTO 110
100	   CONTINUE
110	   IF (INDEX(DDNAME,'CITF').EQ.0) THEN
	      CALL LSL_PUTMSG( NTF2I__NOTCITF,DDNAME(I:20))
	      OK = HADCONT
	      IF (.NOT.OK) GOTO 999	      ! give up if /CONTINUE not given
	   ELSE
	      IF (INDEX(DDNAME,'1.0').EQ.0) THEN
		 CALL LSL_PUTMSG( NTF2I__BADCITFVER,DDNAME(I:20))
		 OK = HADCONT
		 IF (.NOT.OK) GOTO 999	      ! give up if /CONTINUE not given
	      ENDIF
	   ENDIF
	ENDIF
C
C...........................................................................
C This is followed by a mandatory continuation record.
C
	OK = .FALSE.
	IF ( IS_CONTINUATION(STRING,LENGTH) ) THEN		! continua.
	  OK = READ_CONTINUE('DBQUAL')				! read contin..
	ELSE							! no cont. mark
	  CALL LSL_PUTMSG(NTF2I__NOTCONTINUE,'DBHREC',SEC_REC)	! error mesg
	ENDIF
	IF (.NOT.OK) GOTO 999
C
C...........................................................................
C
	FCNAME = STRING(3:22)
	FCDATE = STRING(23:30)
	DQNAME = STRING(31:50)
	DQDATE = STRING(51:58)
	DMODEL = STRING(59:60)		! only exists for NTF level 2 and CITF
	DAY    = FCDATE(7:8)
	MONTH  = FCDATE(5:6)
	YEAR   = FCDATE(1:4)
C
C Check Data Model number for CITF only
C
	IF (HAD_CITF93.OR.HAD_CITF96) THEN
	   IF (DMODEL(2:2).GE.'0'.AND.DMODEL(2:2).LE.'9') THEN
	      DATAMODEL = ICHAR(DMODEL(2:2))-ICHAR('0')
	      IF (DATAMODEL.NE.2) THEN
		 CALL LSL_PUTMSG( NTF2I__DATAMODEL,DMODEL(1:2))
		 OK = HADCONT
		 IF (.NOT.OK) GOTO 999
	      ENDIF
	   ELSE
	      CALL LSL_PUTMSG( NTF2I__DATAMODEL,DMODEL(1:2))
	      OK = HADCONT
	      IF (.NOT.OK) GOTO 999
	      DATAMODEL = 2		! default to 2 and try to continue
	   ENDIF
	ENDIF
C
C
	IF ( OUTPUT ) THEN
	  CALL FLWRTF('	Classification scheme "%S"',FCNAME)
	  CALL FLWRTF('	Classification date   "%S" (ie %S/%S/%S)',
     &						FCDATE, DAY,MONTH,YEAR)
	  DAY    = DQDATE(7:8)
	  MONTH  = DQDATE(5:6)
	  YEAR   = DQDATE(1:4)

	  CALL FLWRTF('	Data quality report   "%S"',DQNAME)
	  CALL FLWRTF('	Data quality date     "%S" (ie %S/%S/%S)',
     &						DQDATE, DAY,MONTH,YEAR)
	  IF (HAD_CITF93.OR.HAD_CITF96)
     &	       CALL FLWRTF('	Data Model   %N',DATAMODEL)
	ENDIF
C
C...........................................................................
C
999	READ_DBHREC = OK
	RETURN
	END
