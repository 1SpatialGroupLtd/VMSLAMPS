
	INTEGER FUNCTION READ_COMMAND( 	IFFNAME, IFFLEN,NTFNAME,OTEXT,RETCOD)
C
***	IDENT	17SP91
***	MODULE	READ_COMMAND
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Created 	IMcL		31-Jul-1989
C
C	CONVERT NTF2I Utility - decode line from attribute file
C
C Description
C
C	Gets a command from the FRT together with its arguments
C
C	For ordinary errors, we attempt to continue with the file
C
C	Returns 0 for success
C
CMOD	tidied source in order to track down bug in reading routines SG 17-9-91
C
	IMPLICIT NONE
C
C Arguments
C
	CHARACTER*32		IFFNAME		! Name of IFF attribute
	INTEGER*2		IFFLEN		! length of IFF name read
	CHARACTER*2		NTFNAME		! Name of NTF attribute
	LOGICAL			OTEXT		! Text destination flag
	INTEGER*2		RETCOD		! Error flag
C
C Included Parameter Files
C
	INCLUDE 'HERE:NTF2IMSG.PAR'		! message definitions
	INCLUDE 'LSL$CMNLSL:LSLLIBMSG.PAR'	! LSLLIB message definitions
	INCLUDE 'LSL$CMNLSL:READSTR.PAR'	! Parameters for READSTR
	INCLUDE 'LSL$CMNLSL:TXTC.CMN'
C
C Local Parameters
C
	BYTE		PLING			! comment delimiter
	PARAMETER	(PLING = '!')
C
C Common Blocks
C
	INCLUDE 'HERE:ATTRIBUTE.CMN'		! attribute description table
	INCLUDE 'LSL$CMNLSL:CMDCOM.CMN'		! command decoding
	INCLUDE 'LSL$CMNLSL:STATUS.CMN'		! LSLLIB status flag
	INCLUDE 'LSL$CMNFRT:FRTACD.CMN'		! Attributes in FRT
C
C Functions called
C
	INTEGER		READSTR
C
C Local Variables
C
	CHARACTER*10	COMMAND			! Read from line
	CHARACTER*10	COMSTR			! Legal command
	INTEGER*2	LEN			! Of strings read
	INTEGER*2	I			! loop counter
	INTEGER		RETVAL			! return value from LSLLIB
	CHARACTER*2	FLAG			! Optional flag on command line
C
	DATA COMSTR/'ATTRIBUTE'/
C
C###############################################################################
C
	RETCOD = 0					!assume success
C
C-------------------------------------------------------------------------------
C Read command from line
C
	LEN = READSTR( COMMAND,,ON_SPACE,.TRUE., )	!read command
	IF ( LEN.NE.0 ) THEN				!got something
	  IF ( COMMAND(:LEN).NE. COMSTR(:LEN) ) THEN	!compare
	     CALL LSL_PUTMSG ( NTF2I__UNKNCOM, COMMAND(:LEN))
	     RETCOD = 1					!not recognised
	  ENDIF
	ELSE						!didnt get anything
	   RETCOD = -1					!badly formed command
	ENDIF
C
	IF (RETCOD.NE.0) GOTO 9999
C
C-------------------------------------------------------------------------------
C Read the rest of the line - should be two attributes
C
C	........................................................................
C	- IFF attribute name
C
	IFFLEN = READSTR( IFFNAME,,ON_SPACE,.TRUE., )	!read IFF name
	IF ( IFFLEN.LE.0 ) THEN			!OOOPS!
	  CALL LSL_PUTMSG( NTF2I__NOIFFATT )		! no IFF attribute name
	  RETCOD = 1	
	  GOTO 9999
	ENDIF
C
C	........................................................................
C	and the NTF mnemonic
C
	LEN = READSTR( NTFNAME,,ON_SPACE,.TRUE.,RETVAL) !get NTF mnemonic
	IF ( LEN.LE.0 ) THEN				!OOOPS!
	  CALL LSL_PUTMSG( NTF2I__NONTFATT )		! no NTF attribute name
	  RETCOD = 1
	ELSEIF ( RETVAL.EQ.LSL__STRTOOLONG ) THEN	!name too long
	  CALL LSL_PUTMSG ( NTF2I__STRTOOLONG,NTFNAME )
	  RETCOD = -1
	ENDIF
	IF (RETCOD.NE.0) GOTO 9999
C
C-------------------------------------------------------------------------------
C May be flag to say put text in optional text string instead of AC value
C
	LEN = READSTR( FLAG,,ON_SPACE,.TRUE.,RETVAL )	!read flag
	OTEXT = (LEN.NE.0).AND.(FLAG(1:1).EQ.'T')
C
C###############################################################################
C
9999	READ_COMMAND = RETCOD
	RETURN
	END
