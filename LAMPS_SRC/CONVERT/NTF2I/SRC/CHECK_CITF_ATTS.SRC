	SUBROUTINE CHECK_CITF_ATTS()
C
***     MODULE CHECK_CITF_ATTS
***     IDENT  14JN96
C
C Copyright Laser-Scan Laboratories Ltd, Cambridge, England
C
C       Author					S Townrow 5-Nov-1992
C
C Description
C
C Checks that the ACD part of the FRT contains the default feature level
C and per-point attributes required for CITF.
C Also checks that there is a definition of the default attribtues in the
C ACD
C
CMOD    Implement CITF96 changes (WI/CC 1545)           TJM     14-Jun-1996
C
	IMPLICIT NONE
C
	INCLUDE 'NTF2IMSG.PAR'
C
C Common files
C
	INCLUDE 'LSL$CMNFRT:FRTACD.CMN'
	INCLUDE 'CITF.CMN'
C
C Functions:
C
	LOGICAL		ACDFND_NAME	! Get ACD
C
C Workspace
C
	INTEGER*4	I			! Loop counter
	INTEGER*4	DUMMY			! Unused argument
C
C------------------------------------------------------------------------------
C
C Check all feature level attributes and get their values
C
	DO 50 I = 1, CITF_FEAT_NUMATT 
	   CITF_FEAT_ACD_DEF(I) = .NOT.ACDFND_NAME(CITF_FEAT_ACD_NAME(I),DUMMY)
	   IF (CITF_FEAT_ACD_DEF(I)) THEN
	      CITF_FEAT_ACD_CODE(I) = ACD_CODE
	      CITF_FEAT_ACD_TYPE(I) = ACD_DATA_TYPE
	   ELSE
	      CALL LSL_PUTMSG(NTF2I__FEATATT,CITF_FEAT_ACD_NAME(I)
     &		                             (1:CITF_FEAT_ACD_NAMLEN(I)))
	   ENDIF
50	CONTINUE
C
C Check all per-point attributes and get their values
C
	DO 60 I = 1, CITF_POINT_NUMATT 
	   CITF_POINT_ACD_DEF(I)=.NOT.ACDFND_NAME(CITF_POINT_ACD_NAME(I),DUMMY)
	   IF (CITF_POINT_ACD_DEF(I)) THEN
	      CITF_POINT_ACD_CODE(I) = ACD_CODE
	      CITF_POINT_ACD_TYPE(I) = ACD_DATA_TYPE
	   ELSE
	      IF (I.EQ.POINT_X) THEN
		 CITF_POINT_ACD_CODE(I) = IFF_C_X
		 CITF_POINT_ACD_DEF(I) = .TRUE.
	      ELSEIF (I.EQ.POINT_Y) THEN
		 CITF_POINT_ACD_CODE(I) = IFF_C_Y
		 CITF_POINT_ACD_DEF(I) = .TRUE.
	      ELSEIF (I.EQ.POINT_Z) THEN
		 CITF_POINT_ACD_CODE(I) = IFF_C_Z
		 CITF_POINT_ACD_DEF(I) = .TRUE.
	      ELSE
		 CALL LSL_PUTMSG(NTF2I__POINTATT,CITF_POINT_ACD_NAME(I)
     &		                                 (1:CITF_POINT_ACD_NAMLEN(I)))
	      ENDIF
	   ENDIF
60	CONTINUE
C
C Suppress the CITF93 per-point attributes not used by CITF96
C
	IF (HAD_CITF96) THEN
	   CITF_POINT_ACD_DEF(POINT_XY_ACQ_METH) = .FALSE.
	   CITF_POINT_ACD_DEF(POINT_QPLAN)       = .FALSE.
	   CITF_POINT_ACD_DEF(POINT_Z_ACQ_METH)  = .FALSE.
	   CITF_POINT_ACD_DEF(POINT_QHT)         = .FALSE.
	   CITF_POINT_ACD_DEF(POINT_QDIFF_HT)    = .FALSE.
	ENDIF
C
C------------------------------------------------------------------------------
C And return
C
9999	RETURN
	END
