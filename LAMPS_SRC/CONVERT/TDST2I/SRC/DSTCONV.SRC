	INTEGER*4  FUNCTION DSTCONV
C
***	MODULE DSTCONV
***	IDENT 28MR91
C
C	Copyright Laser-Scan Laboratories Ltd., Cambridge, England.
C
C	Author  A.D.Trigg 23 May 1988
C
CMOD739 Set MHDESC(1)-(4) instead of MHARR(1)-(4)   RJH 03-Nov-1989
C
CMOD879 Put object sections back in to read through them, and 
C	standardise error returns		J Barber 26-Apr-1990
C
CMOD914 Update IFF range if the coordinates disagree with the DST range
C						J Barber 24-Jul-1990
C
C	Farms out the conversion for different feature types and initialises
C	the IFF file
C
	IMPLICIT NONE
C
	INCLUDE 	'LSL$CMNIFF:MHDMB.CMN'
	INCLUDE 	'LSL$CMNIFF:MD2DES.CMN'
	INCLUDE		'LSL$CMNIFF:IFFHAN.CMN'
	INCLUDE		'LSL$CMNLSL:READSTR.PAR'	!For ON_EOS
	INCLUDE		'LSL$CMNDSTPAR:LOOKUP.CMN'
	INCLUDE		'HERE:TDST2I.CMN'
	INCLUDE 	'HERE:TDST2IMSG.PAR'
C
C  Declarations
	REAL		CFT(10,2)
	REAL		CPTS(4,4)
	INTEGER		LPOSNO
	INTEGER		LPOSRA
	INTEGER*2	IELEN,IECODE
	CHARACTER*256	STRING
	INTEGER 	LEN
C
C  Functions
	LOGICAL		DSTPOLY
	LOGICAL		DSTTEXT
	LOGICAL		DSTPOINT
	LOGICAL		DSTMULTIP
	LOGICAL		DSTARC
	LOGICAL		DSTAREA
	LOGICAL		DSTOBJEC
C
	LOGICAL		RDREAL
	INTEGER		FLRLIN,FLRSEL
	INTEGER		READSTR
C
	INTEGER		SS$_NORMAL
	EXTERNAL	SS$_NORMAL
	INTEGER		SS$_ABORT
	EXTERNAL	SS$_ABORT
C
	DATA CFT/0.0,1.0,10*0.0,1.0,7*0.0/
	DATA CPTS/0.0, 1000.0, 0.0, 1000.0, 0.0, 0.0, 0.0, 0.0,
     &	1000.0, 0.0, 1000.0, 0.0, 1000.0, 1000.0, 1000.0, 1000.0/
C
C *******************************************************************
C Set count of lines to zero
C
	DSTCONV = FLRSEL(DSTLUN)		!Define file to read
	IF (.NOT.DSTCONV) GOTO 1000
C
C Master section
C
	NUMLIN = 0
	DSTCONV = FLRLIN()
	IF (.NOT.DSTCONV) GOTO 999
C
	CALL BSLN
	NUMLIN=NUMLIN+1
C
	LEN=READSTR(STRING,,ON_EOL)
	IF(STRING(1:LEN).NE.'MASTER') THEN
	   DSTCONV = DSTI__NOMASSEC
	   GOTO 1000
	ENDIF
C
C Read header name line - not needed
C
	DSTCONV = FLRLIN()		
	IF (.NOT.DSTCONV) GOTO 999
C
	NUMLIN = NUMLIN+1
C
C Determine range and write to IFF file
C
	DSTCONV = FLRLIN()		
	IF (.NOT.DSTCONV) GOTO 999
C
	NUMLIN = NUMLIN+1
	CALL BSLN
C
C Read range info
C
	IF(RDREAL(MINMAX(1))) GOTO 900
	IF(RDREAL(MINMAX(3))) GOTO 900
	IF(RDREAL(MINMAX(2))) GOTO 900
	IF(RDREAL(MINMAX(4))) GOTO 900
C
	LPOSRA = LPOSE		! Save the posn of range entry
	CALL IFFRA(MINMAX)
C
C Look for EOS indicating the end of master section
C
	DSTCONV = FLRLIN()
	IF (.NOT.DSTCONV) GOTO 999
C
	CALL BSLN
	NUMLIN=NUMLIN+1
C
	LEN=READSTR(STRING,,ON_EOL)
	IF(STRING(1:LEN).NE.'EOS') THEN
	   DSTCONV = DSTI__NOMASEOS	
	   GOTO 1000
	ENDIF
C
C Fill in rest of IFF entries before we deal with individual features
C
	CALL IFFHI
C
C Set default parameters before inserting a map header to the IFF file
C
	MHDESC(1)=174			! length
	MHDESC(2)=0			! unset map header
	MHDESC(3)=0
	MHDESC(4)=0
	CALL IFFMH(MHARR,MHLEN)
C
C Set default parameters before inserting a map descriptor into 
C the IFF file
C
	MD2LOC(1) = MINMAX(1)
	MD2LOC(2) = MINMAX(3)
	MATV2     = 2
	MD2ARR(1) = -1
C
	CALL IFFMD(MD2ARR,MD2LEN)
	CALL IFFNS(' IFF data created from input DST file')
	CALL IFFCC(CFT)
	CALL IFFCP(CPTS)
	LPOSNO=LPOSE		! Save the pos of start of overlay
	CALL IFFNO(1,0,0)
C
C  Logic sections
C
300	DSTCONV = FLRLIN()
	IF (.NOT.DSTCONV) GOTO 999
C
	CALL BSLN
	NUMLIN=NUMLIN+1
C
	LEN=READSTR(STRING,,ON_EOL)
	IF(STRING(1:LEN).EQ.'EOI') GOTO 850
C
	IF(STRING(1:LEN).NE.'LOGIC') THEN
	   DSTCONV = DSTI__NOLOGSEC
	   CALL LSL_PUTMSG(DSTI__NOLOGSEC,NUMLIN)
	   GOTO 1001
	ENDIF
C
C Account for unneeded info
C
	DSTCONV = FLRLIN()		
	IF(.NOT.DSTCONV)GOTO 999
C
	NUMLIN=NUMLIN+1
C
500	DSTCONV = FLRLIN()
	IF(.NOT.DSTCONV)GOTO 999
C
	NUMLIN=NUMLIN+1
	CALL BSLN
C
C Find logic block type and  direct to appropriate conversion routine
C
	LEN=READSTR(STRING,,ON_EOL)
C
	IF(STRING(1:LEN).EQ.'AREA') THEN
	   DSTCONV = DSTAREA()
	   IF(.NOT.DSTCONV)GOTO 1001
	   GOTO 500
C
	ELSEIF(STRING(1:LEN).EQ.'OBJECT') THEN
	   DSTCONV = DSTOBJEC()
	   IF(.NOT.DSTCONV)GOTO 1001
	   GOTO 500
C
	ELSEIF(STRING(1:LEN).EQ.'POLYGON') THEN
	   DSTCONV = DSTPOLY()
	   IF(.NOT.DSTCONV)GOTO 1001
	   GOTO 500
C
	ELSEIF(STRING(1:LEN).EQ.'ARC') THEN
	   DSTCONV = DSTARC()
	   IF(.NOT.DSTCONV)GOTO 1001
	   GOTO 500
C
	ELSEIF(STRING(1:LEN).EQ.'TEXT') THEN
	   DSTCONV = DSTTEXT()
	   IF(.NOT.DSTCONV)GOTO 1001
	   GOTO 500
C
	ELSEIF(STRING(1:LEN).EQ.'POINT') THEN
	   DSTCONV = DSTPOINT()
	   IF(.NOT.DSTCONV)GOTO 1001
	   GOTO 500
C
	ELSEIF(STRING(1:LEN).EQ.'MULTIPOINT') THEN
	   DSTCONV = DSTMULTIP()
	   IF(.NOT.DSTCONV)GOTO 1001
	   GOTO 500
	ENDIF
C
	IF (STRING(1:LEN).EQ.'EOS') GOTO 300
C
	DSTCONV = DSTI__UNKGEOIDN
	CALL LSL_PUTMSG(DSTI__UNKGEOIDN,NUMLIN)
	GOTO 1001
C
850     CONTINUE
C
C Close IFF files and write EO ,EM, EJ etc entries here
C
 	CALL IFFUNO(LPOSNO)
C
C Update Range entry
C
	CALL IFFPKE(IECODE,IELEN,LPOSRA)
	CALL EIHWR(RANGE,4,1)
C
	CALL IFFEO
	CALL IFFEM
	CALL IFFEJ
	CALL IFFCLO(IFFLUN)
C
	GOTO 1001
C
C Error exit reading a line element
C
900	DSTCONV = DSTI__ERRINTLIN
	CALL LSL_PUTMSG(DSTCONV,NUMLIN)
	GOTO 1001
C
999	CALL LSL_PUTMSG(DSTCONV)
	CALL LSL_ADDMSG(DSTI__ERRINTLIN,NUMLIN)
	GOTO 1001
C
C General error exit
C
1000 	CALL LSL_PUTMSG(DSTCONV)
C
1001	IF (DSTCONV) THEN
	   DSTCONV=%LOC(SS$_NORMAL)
	ELSE
C
C Delete the IFF file
C
	   CALL IFFCDL(IFFLUN)
	   DSTCONV=%LOC(SS$_ABORT)
	ENDIF
C
	RETURN
	END
C
