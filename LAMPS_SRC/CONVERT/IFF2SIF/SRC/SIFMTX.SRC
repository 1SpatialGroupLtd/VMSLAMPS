	SUBROUTINE SIFMTX( MIRROR, ANGLE, XSCALE, YSCALE)
C
***	IDENT	07OC86
***	MODULE	SIFMTX
C
C	Copyright LaserScan Laboratories Ltd., Cambridge, England
C
C Author TJ Ibbs, 18 Jan 1984
C
C Outputs a GENERATE TRANSFORMATION MATRIX command -
C
C	MTX/MX, AN=angle, XS=xscale, YS=yscale
C	MTX/MY, AN=angle, XS=xscale, YS=yscale
C
C This command generates the transformation matrix for a following
C GENERATE SYMBOL command (see SIFSYM)
C
	IMPLICIT NONE
C
***	COMMON/HERE:SIF/		! state common block
C
C Arguments are:
C
	INTEGER		MIRROR
	REAL		ANGLE
	REAL		XSCALE, YSCALE
C
C MIRROR	states whether the symbol is to be reflected
C
C		0 => don't reflect
C		1 => reflect in X axis
C		2 => reflect in Y axis
C
C ANGLE		is the angle to rotate the symbol by, and is given to
C		us in radians, widdershins from X (standard notation)
C
C XSCALE	is the X scale factor, from 0 to 10 (generally 1)
C YSCALE	is the Y scale factor, from 0 to 10 (generally 1)
C
C function called
C
	REAL	DEGREE		! convert radians to degrees
C
C
C start the expansion
C
	CALL EXPAND('MTX/')
C
C check that our arguments make sense as we go along
C - first our mirroring
C
	IF (MIRROR.LT.0 .OR. MIRROR.GT.2) THEN
	   CALL MOAN( MSG__MTXMIRROR, MIRROR, FSN )
	ELSEIF (MIRROR.EQ.1) THEN
	   CALL APPEND('MX,')			! reflect in X
	ELSEIF (MIRROR.EQ.2) THEN
	   CALL APPEND('MY,')			! reflect in Y
	ENDIF
C
C - now for the angle of rotation
C
	CALL APPEND('%^PAN=%F5.3,',DEGREE(ANGLE))
C
C and finally the scale factors for the symbol cell
C
	IF (XSCALE.LT.0.0 .OR. XSCALE.GT.10.0) THEN
	   CALL MOAN(MSG__SYMSCALE,'X',XSCALE,FSN)
	ELSE
	   CALL CHECK_ROOM( 9 )
	   CALL APPEND('%^PXS=%F5.3,',XSCALE)
	ENDIF
C
	IF (YSCALE.LT.0.0 .OR. YSCALE.GT.10.0) THEN
	   CALL MOAN(MSG__SYMSCALE,'Y',YSCALE,FSN)
	ELSE
	   CALL CHECK_ROOM( 8 )
	   CALL APPEND('%^PYS=%F5.3',YSCALE)
	ENDIF
C
C and output this line
C
	CALL SIFOUT
C
	RETURN
	END
