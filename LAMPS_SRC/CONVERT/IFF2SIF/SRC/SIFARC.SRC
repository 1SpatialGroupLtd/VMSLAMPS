	SUBROUTINE SIFARC( SIGN, DIM, BUFF )
C
***	IDENT	25NO86
***	MODULE	SIFARC
C
C	Copyright LaserScan Laboratories Ltd., Cambridge, England
C
C Author TJ Ibbs, 23 January 1985
C
C This routine outputs the SIF command to generate an arc
C
	IMPLICIT NONE
C
C Arguments
C
	INTEGER		SIGN		! plus for clockwise
	INTEGER		DIM		! the dimension of the data
	REAL		BUFF(DIM,3)	! the three defining points
C
***	COMMON/HERE:SIF/
C
C workspace
C
	INTEGER		SAME_CNT	! count of samenesses
	INTEGER		I  		! DO loop variable
	INTEGER		Q		! ditto
	INTEGER		P		! which point to output
C
C function
C
	INTEGER		UORX,UORY,UORZ	! convert map space to UORs
C
C check for a null arc
C
	DO 200 P = 1,3
C
 	   Q = P+1			! ie  P = 1,2,3
	   IF (Q.GT.3) Q=1		! and Q = 2,3,1
C
	   SAME_CNT = 0
	   DO 100 I = 1,DIM
	      IF ( BUFF(I,P).EQ.BUFF(I,Q) ) SAME_CNT = SAME_CNT+1
100	   CONTINUE
	   IF ( SAME_CNT.EQ.3 ) GOTO 9999
C
200	CONTINUE
C
C otherwise, just output it
C - for a clockwise or anticlockwise arc, we output CE,P1,P2
C - for a circumcircle arc, we output P1,P2,P3
C
	IF (SIGN.GT.0) THEN
	   CALL EXPAND('ARC/CL,CE=')	! clockwise arc
	   P = 2		       	! output centre first
	ELSEIF (SIGN.LT.0) THEN
	   CALL EXPAND('ARC/CC,CE=')	! counterclockwise arc
	   P = 2		       	! output centre first
	ELSE
	   CALL EXPAND('ARC/P1=')	! circumcircle arc
	   P = 1			! point 1 first
	ENDIF
	CALL APPEND('%^L%U%N,%U%N,', UORX(BUFF(1,P)), UORY(BUFF(2,P)) )
	IF (DIM.EQ.3) CALL APPEND('%^L%U%N,',UORZ(BUFF(3,P)) )
C
	CALL CHECK_ROOM( DIM*COORD_MAX + 5 )
	IF (SIGN.EQ.0) THEN
	   CALL APPEND('P2=')
	   P = 2
	ELSE
	   CALL APPEND('P1=')
	   P = 1
	ENDIF
	CALL APPEND('%^L%U%N,%U%N,', UORX(BUFF(1,P)), UORY(BUFF(2,P)) )
	IF (DIM.EQ.3) CALL APPEND('%^L%U%N,',UORZ(BUFF(3,P)) )
C
	CALL CHECK_ROOM( DIM*COORD_MAX + 4 )
	IF (SIGN.EQ.0) THEN
	   CALL APPEND('P3=')
	   P = 3
	ELSE
	   CALL APPEND('P2=')
	   P = 3
	ENDIF
	CALL APPEND('%^L%U%N,%U%N', UORX(BUFF(1,P)), UORY(BUFF(2,P)) )
	IF (DIM.EQ.3) CALL APPEND('%^L,%U%N',UORZ(BUFF(3,P)) )
C
    	CALL SIFOUT
C
	GOTO 9000
C
C **********************************************************************
9999	CALL MOAN( MSG__BADARC, FSN )
C
9000	RETURN
	END
