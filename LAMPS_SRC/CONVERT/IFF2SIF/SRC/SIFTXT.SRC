C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1992-03-20 18:18:36.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE SIFTXT( DIM, MIRROR, ORI , HEIGHT , STRLEN , ANGLE , TEXT )
C
***	IDENT	20MR92
***	MODULE	SIFTXT
C
C	Copyright LaserScan Laboratories Ltd., Cambridge, England
C
C Author TJ Ibbs, 23 January 1985
C
C Routine to output a SIF generate text line command
C
C NB - assumed to have been prefixed by an appropriate TX3 command,
C generated by SIFTX3
C
	IMPLICIT NONE
C
C Common blocks
C
	INCLUDE 'HERE:CONTROL.CMN'
C
C Functions called
C
	REAL		DEGREE		! convert radians to degrees
	INTEGER		UORX			! convert map units to UORs
	INTEGER		UORY
	INTEGER		UORZ
C
C Arguments
C
	INTEGER		DIM
	INTEGER		MIRROR			
	REAL		ORI(DIM)		! text origin from  IFF
	REAL		HEIGHT			! text height
	REAL		STRLEN			! text string length
	REAL		ANGLE			! text angle from IFF
	CHARACTER*(*)	TEXT			! text string
C
C Workspace
C
	REAL		THETA			! angle for calculation
	REAL		 PI			! value for PI
	PARAMETER	(PI = 3.141593)
	REAL		X1			! text origin coord
	REAL		Y1
	REAL		X2			! text bottom right coord
	REAL		Y2
	REAL		X3			! text top left coord
	REAL		Y3
	REAL		Z
C
C MIRROR	states whether the text is to be reflected
C
C		0 => don't reflect
C		1 => reflect in X axis
C		2 => reflect in Y axis
C
C TEXT		is the text to be output
C
***	COMMON/SIF/
C
C SCITEX option can't use TX3 and does not allow text mirroring.
C
	IF (HAD_SCITEX) THEN
	   X1 = ORI(1) 
	   Y1 = ORI(2) 
	   IF (DIM.EQ.3) Z = ORI(3)
C
C
C Calculate top left, bottom left, and bottom right coords of text
C (using equation of a circle). Must be in the order TL, BL, BR.
C
	   THETA = ANGLE + (PI/2)
	   X3 = X1 + (HEIGHT*COS(THETA))
	   Y3 = Y1 + (HEIGHT*SIN(THETA))
	   THETA = ANGLE
	   X2 = X1 + (STRLEN*COS(THETA))
	   Y2 = Y1 + (STRLEN*SIN(THETA))
C
	   CALL EXPAND('TXT/') 			! start off the line
	   CALL APPEND('%^LTL=%U%N,%U%N,',UORX(X3),UORY(Y3))
	   IF (DIM.EQ.3) CALL APPEND('%^L%U%N,',UORZ(Z) )
C
	   CALL CHECK_ROOM( DIM*COORD_MAX + 5 )
	   CALL APPEND('%^LBL=%U%N,%U%N,',UORX(X1),UORY(Y1))
	   IF (DIM.EQ.3) CALL APPEND('%^L%U%N,',UORZ(Z) )
C
	   CALL CHECK_ROOM( DIM*COORD_MAX + 5 )
	   CALL APPEND('%^LBR=%U%N,%U%N,',UORX(X2),UORY(Y2))
	   IF (DIM.EQ.3) CALL APPEND('%^L%U%N,',UORZ(Z) )
	ELSE
C
C No SCITEX option - proceed for standard SIF
C
	   CALL EXPAND('TXT/')			! start off the line
C
C now see if we need to add on a mirroring request
C
	   IF (MIRROR.LT.0 .OR. MIRROR.GT.2) THEN
	      CALL MOAN( MSG__TXTMIRROR, MIRROR, FSN )
	   ELSEIF (MIRROR.EQ.1) THEN
	      CALL APPEND('MX,')			! reflect in X
	   ELSEIF (MIRROR.EQ.2) THEN
	      CALL APPEND('MY,')			! reflect in Y
	   ENDIF
	ENDIF
C
C and the text itself
C
	CALL CHECK_CHAR( TEXT )
	CALL SIFOUT
C
	RETURN
	END
