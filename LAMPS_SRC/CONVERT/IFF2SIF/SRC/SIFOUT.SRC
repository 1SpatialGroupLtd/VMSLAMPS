	SUBROUTINE SIFOUT
C
***	IDENT	02JL90
***	MODULE	SIFOUT
C
C	Copyright LaserScan Laboratories Ltd., Cambridge, England
C
C Author TJ Ibbs, 4 February 1985
C
C Subroutine to add the current record to the output buffer, and
C output that if necessary
C
	IMPLICIT NONE
C
***	COMMON/HERE:SIF/
***	COMMON/HERE:CONTROL/
***	COMMON/HERE:SIFMT/
***	COMMON/HERE:SIFFILE/
C***	COMMON/LSL$CMNLSL:EXPC/
***	COMMON/LSL$CMNLSL:MTOVCM/
C
C workspace
C
	INTEGER		LENGTH		! amount to pad buffer with
	INTEGER		I		! DO loop counter
C
C SCITEX requires that records on disk are not padded with blanks, so 
C do so only for magtape.
C
	IF (HAD_SCITEX) THEN
C
C if we are outputting to a file, do so now
C
	   IF ( OUTFILE_OPEN ) THEN
	     CALL FLWLIN
	   ENDIF
C
	   IF (USE_MT) THEN
C
C pad the record out to RECSIZ characters - note that we assume that we
C will not have more than RECSIZ characters in it already
C
	      DO 20 I = EXPLEN+1,RECSIZ
	         EXPBUF(I) = ' '
20	      CONTINUE
	      EXPLEN = RECSIZ
	   ENDIF
	ELSE
C
C pad the record out to RECSIZ characters - note that we assume that we
C will not have more than RECSIZ characters in it already
C
	   DO 30 I = EXPLEN+1,RECSIZ
	      EXPBUF(I) = ' '
30	   CONTINUE
	   EXPLEN = RECSIZ
C
C if we are outputting to a file, do so now
C
	   IF ( OUTFILE_OPEN ) THEN
	     CALL FLWLIN
	   ENDIF
C
	ENDIF
C if we are using the magtape, can we fit the record into the
C output buffer?
C
	IF ( USE_MT ) THEN
C
	   LENGTH = (BUFSIZ-BYTCNT)	! working length left
C
	   IF ( LENGTH.LT.EXPLEN ) THEN
C
C	    no room - force the buffer out to the magtape,
C	    padded with spaces to the full buffer size
C
	      CALL OUTBLK
C
	   ENDIF
C
C	 now, regardless, transfer the new data into the buffer
C
	   DO 200 I = 1,EXPLEN
	      BYTBUF(BYTCNT+I) = EXPBUF(I)
200	   CONTINUE
C
	   BYTCNT = BYTCNT+EXPLEN
C
	ENDIF
C
C and we have output all of the record, so
C
	NEED_SIFOUT = .FALSE.
C
	RETURN
	END
