C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1988-08-19 12:38:34.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
        INTEGER*4 FUNCTION CMDLIN()
C
***	MODULE	CMDLIN
***	IDENT	19AU88
C
C 	Copyright Laser-Scan Laboratories Ltd.,England
C
C	Created.					FJB 	15-August-1988
C                                     
C	The command decoder for I2MGD
C
	IMPLICIT NONE
C
C Parameters:
	INCLUDE 'LSL$CMNI2MGD:I2MGDMSG.PAR'
	INCLUDE 'LSL$CMNI2MGD:PARAMS.PAR'
	INCLUDE 'LSL$CMNLSL:LSLLIBMSG.PAR'
	INCLUDE '($SSDEF)/NOLIST'
C
C Common:
	INCLUDE 'LSL$CMNLSL:CLD.CMN'
	INCLUDE 'LSL$CMNLSL:FILENAME.CMN'
	INCLUDE 'LSL$CMNI2MGD:COMMAND.CMN'
C
C Command Language Definitions:
	EXTERNAL	I2MGD_CLD
C
C Longword:
	INTEGER		I	   ! tmp var.
	INTEGER		TMP_LEN	   ! length of tmp. file-spec
C
C Logical*4                                   
	LOGICAL		ABSENT	   ! .T. if file param. not found
C	LOGICAL		HAD_DEV	   ! had /DEVICE qualifier
	LOGICAL		HAD_FC	   ! had /FC qualifier
	LOGICAL		HAD_FRT	   ! had /FRT qualifier
 	LOGICAL		LOCAL      ! .T. if qual. is local
	LOGICAL		LOCNEG     ! set .T. if qual. locally negated
        LOGICAL		WHINGE     ! .T. - local error reporting
C
C Functions called:
	INTEGER*4	DCL_FILE   ! reads & parses file title from DCL 
C			           ! command line
	INTEGER*4	DCL_INT	   ! looks for qualifier integer args.
	INTEGER*4	DCL_QUAL   ! looks for specified qualifier
	INTEGER*4	DCL_REAL   ! looks for qualifier real args.
        INTEGER*4       DCL_STARTUP! looks for command line
        INTEGER*4       DCL_STR	   ! looks for qualifier string args.
C
C Character:
	CHARACTER*256	TMP_FIL	   ! temp file-spec for parsing
C
C...................................................................
C
C	    I N I T I A L I S E   T H I N G S 
C
	WHINGE=.TRUE.		 ! local error reporting
C
C....................................................................
C
C	    G E T   C O M M A N D   L I N E
C
	CMDLIN=DCL_STARTUP('I2MGD',.FALSE.,I2MGD_CLD,WHINGE)
C
C DCL_STARTUP will return set to SS$_NORMAL if all well.
C
        IF (.NOT.CMDLIN) GOTO 10 		! return
C
C....................................................................
C
C	     C H E C K   F O R   Q U A L I F I E R S
C
C We could have /DEVICE=?, /FC=?, /FRT=?, 
C		/DIAGNOSTICS, /REWIND
C
C Try /DIAGNOSTICS
	CMDLIN=DCL_QUAL('DIAGNOSTICS',HAD_DIAG,LOCAL,LOCNEG,WHINGE)
	IF (.NOT.CMDLIN) GOTO 10 		! return
C
C Try /REWIND
	CMDLIN=DCL_QUAL('REWIND',HAD_REW,LOCAL,LOCNEG,WHINGE)
	IF (.NOT.CMDLIN) GOTO 10 		! return
C
C Try /DEVICE
	CMDLIN=DCL_QUAL('DEVICE',HAD_DEV,LOCAL,LOCNEG,WHINGE)
	IF (.NOT.CMDLIN) GOTO 10 		! return
C
C Try for /DEVICE argument
	IF (HAD_DEV) THEN
	   CMDLIN=DCL_STR('DEVICE',WHINGE)
	   IF (CMDLIN) THEN
	      DEV_NAM=CARRAY(1)(:STR_LEN(1))
	      DEV_LEN=STR_LEN(1)
	   ELSE
	      GOTO 10				! return
	   ENDIF
	ENDIF
C
C Try /FC
	CMDLIN=DCL_QUAL('FC',HAD_FC,LOCAL,LOCNEG,WHINGE)
	IF(.NOT. CMDLIN) GOTO 10		! return
C    
C Get /FC argument
	FCTYPE=10
	IF (HAD_FC) THEN
	   CMDLIN=DCL_INT('FC',WHINGE)
	   IF (CMDLIN) THEN
	      IF((IARRAY(1).LT.0).OR.(IARRAY(1).GT.32767))THEN
		CMDLIN=I2MGD__INVALFC
		CALL LSL_PUTMSG(CMDLIN,IARRAY(1))
		GOTO 10
	      ENDIF     
	      FCTYPE=IARRAY(1)
	   ELSE
	      GOTO 10				! return
	   ENDIF
	ENDIF
C
C Check for /FRT=filespec
	CMDLIN=DCL_QUAL('FRT',HAD_FRT,LOCAL,LOCNEG,WHINGE)
	IF(.NOT.CMDLIN) GOTO 10			! return
C
	IF(HAD_FRT) THEN
	   CMDLIN=DCL_FILE('FRT','LSL$FRT:MGD77_ACD_TABLE.FRT;0',
     &			ABSENT,.FALSE.,WHINGE)
	   IF(.NOT. CMDLIN)GOTO 10		! return
	   FRT_FIL=FILARY(1)(1:FIL_LEN(1))
	   FRT_LEN=FIL_LEN(1)
	ENDIF
C                                       
C Get input file
	CMDLIN=DCL_FILE('P1','LSL$IF:IFF.IFF;0',ABSENT,.FALSE.,WHINGE)
C
C Transfer file-spec to a local variable
	IF (CMDLIN) THEN
	  P1_FIL=FILARY(1)(1:FIL_LEN(1))
	  P1_LEN=FIL_LEN(1)
	ENDIF
C			
10	IF (CMDLIN) CMDLIN=SS$_NORMAL
C
C Finish
	RETURN
	END
