C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1994-12-06 17:37:04.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL*4 FUNCTION DXF_POINT()
C
***	MODULE DXF_POINT
***	IDENT 06DE94
C
C ©Copyright Laser-Scan Ltd, Cambridge, England.
C
C Author  : P Pan 					13 Mar 1990
C
C To read and translate DXF POINT entity to IFF feature
C
C Modified: P. Pan to set symbol bit			23 Mar 1990
C
CMOD961     J Barber					30 Jan 1991
C	    Add 3d coord reading into ZS entries
C	    and group 38 heights into AC type 3
C	    Change program action on DXF2I__MISENTCOL error to
C	    continue to transfer the coordinate data regardless
C
CMOD1004    J Barber 					13 Jun 1991
C	    Convert coordinates with any scale factors and x,y offsets
C	    from the parameter file if an EXPAND 'symbol' entry exists
C	    there and this routine was called from within the BLOCKS 
C	    section
C
CMOD1156    J Barber 					13 Oct 1992
C	    Add ABS coord changes
C
CMOD1226    J Barber 					10 Jun 1993
C	    Add /SCAN facility
C
CMOD1439    Jon Barber				 	06 Dec 1994
C	    Use IFF_ABSENT as a 'real' number in its absence from
C	    the DXF file
C
	IMPLICIT NONE
C
	INCLUDE  'LSL$CMNIFF:CB.STR'		! for IFF_ABSENT
	INCLUDE  'LSL$CMNLSL:LSLLIBMSG.PAR'
	INCLUDE  'LSL$CMNLSL:READSTR.PAR'
	INCLUDE  'LSL$CMNLSL:TXTC.CMN'
	INCLUDE  'LSL$CMNIFF:IFFHAN.CMN'
	INCLUDE  'LSL$CMNIFF:MD2DES.CMN'
	INCLUDE  'LSL$CMNFRT:FRTCOM.CMN'
	INCLUDE  'LSL$CMNDXFPAR:LOOKUP.CMN'
	INCLUDE  'HERE:DXF2I.CMN'
	INCLUDE  'HERE:DXF2IMSG.PAR'
C
C Functions
	INTEGER		RDREAL		! Get a real
	INTEGER		RDDBLE		! Get a real
	INTEGER		RDINT		! Get an integer
	INTEGER		FLRLIN		! Read a line from file
	INTEGER		FLWSTR		! Write a line to file
	INTEGER		READSTR		! Get a string
	INTEGER*2	EXPAND
	LOGICAL		FRTFGT		! Get FC for GT
C
C Workspace
	CHARACTER*255	STRING		! Any old string
	INTEGER*4	LEN		! Length of character string
	CHARACTER*255	ENTITY_OUT	! output string
	INTEGER*2	EXPLEN		! expanded string length
	CHARACTER*20	STYLE_NAME	! output string
C
	CHARACTER*255	LAST_ENTITY_OUT	! last output string
	INTEGER*2	LAST_EXPLEN	! last expanded string length
	SAVE		LAST_ENTITY_OUT	! keep last output string 
	SAVE		LAST_EXPLEN	! keep last expanded string length
C
	INTEGER*2       FC(4)           ! Feature code etc.
	INTEGER*2	COLNUM		! Feature colour
	REAL		HEIGHT
	REAL		SCL
C
	LOGICAL		HAD_LAY		! Had layer value?
	LOGICAL		HAD_COL 	! Had colour? 
	LOGICAL		HAD_X		! Had x coord?
	LOGICAL		HAD_Y		! Had y coord?
	LOGICAL		HAD_Z		! Had z coord?
	LOGICAL		HAD_3D		! Had 3d coord?
	LOGICAL		HAD_HT		! Had AC height?
C
	INTEGER		INT_EQUIV	! for IFF_ABSENT
	REAL		REAL_EQUIV
	EQUIVALENCE	(INT_EQUIV,REAL_EQUIV)
C
	LOGICAL		ERRCOD		! ok?
	INTEGER		IERR
        INTEGER		I,COUNTER	! local counter
	CHARACTER*255	LAYERNAM	! layer name
	INTEGER		LAYERLEN	! layer name's length
	INTEGER		PNTNUMLIN	! line position for POINT
C
C---------------------------------------------------------------------
C DXF POINT
C---------------------------------------------------------------------
C Set defaults
C
	SELENTTYP = DXFPOINT
	HAD_LAY = .FALSE.
	HAD_COL = .FALSE.
	HAD_X   = .FALSE.
	HAD_Y   = .FALSE.
	HAD_Z   = .FALSE.
	HAD_3D  = .FALSE.
	HAD_HT  = .FALSE.
C
C---------------------------------------------------------------------
C read the entity
C
	PNTNUMLIN = NUMLIN
C
99	DXF_POINT = FLRLIN()
	IF (.NOT.DXF_POINT) GOTO 999
C
	CALL BSLN
	NUMLIN = NUMLIN+1
	LEN = READSTR(STRING,,ON_EOL)
C
C...............................................................................
C '8' Layer 
C...............................................................................
C
	IF (STRING(1:LEN).EQ.'8') THEN
	   DXF_POINT = FLRLIN()
	   IF (.NOT.DXF_POINT) GOTO 999
C
	   CALL BSLN
	   NUMLIN = NUMLIN+1
	   LAYERLEN = READSTR(LAYERNAM,,ON_EOL)
C
C if an empty layer name is supplied, output error message
C
	   IF (LAYERLEN.EQ.0) THEN
	      CALL LSL_PUTMSG(DXF2I__ERRLAYNAM,NUMLIN)
C
C and use default from $CLAYER if present
C
	      IF (HAD_LAYER_NAME) THEN
	         LAYERLEN = DEF_LAYER_NAME_LEN
	         LAYERNAM = DEF_LAYER_NAME(1:LAYERLEN)
		 IF (.NOT.HAD_QUIET) CALL LSL_ADDMSG(DXF2I__DEFLAYNAM,
     &				    LAYERNAM(1:LAYERLEN),PNTNUMLIN)
	      ELSE
		 DXF_POINT = DXF2I__UNACONT
		 IF (.NOT.HAD_SCAN) GOTO 1000
	      ENDIF
	   ENDIF
C
	   HAD_LAY = .TRUE.
	   GOTO 99
C
C..............................................................................
C '38' - CONTOUR HEIGHT
C..............................................................................
C
	ELSEIF (STRING(1:LEN).EQ.'38') THEN
	   DXF_POINT = FLRLIN()
	   IF (.NOT.DXF_POINT) GOTO 999
C
	   CALL BSLN
	   NUMLIN = NUMLIN+1
	   ERRCOD = RDREAL(HEIGHT)
           IF (ERRCOD) THEN
	      DXF_POINT = DXF2I__UNACONT
	      CALL LSL_PUTMSG(DXF2I__ERRENTACVAL,NUMLIN)
	      GOTO 1000
	   ENDIF
	   HAD_HT = .TRUE.
	   GOTO 99
C
C...............................................................................
C '62' - COLOUR
C...............................................................................
C
	ELSEIF (STRING(1:LEN).EQ.'62') THEN
	   DXF_POINT = FLRLIN()
	   IF (.NOT.DXF_POINT) GOTO 999
C
C if colour is by layer then colour entry is irrelevant
C
	   IF (BY_LAYER) THEN
	      NUMLIN = NUMLIN+1
	      GOTO 99
	   ENDIF
C
	   CALL BSLN
	   NUMLIN = NUMLIN+1
	   ERRCOD = RDINT(COLNUM)
           IF (ERRCOD) THEN
	      DXF_POINT = DXF2I__UNACONT
	      CALL LSL_PUTMSG(DXF2I__ERRENTCOL,NUMLIN)
	      GOTO 1000
	   ENDIF
	   HAD_COL = .TRUE.
	   SELCOLOUR = COLNUM
	   GOTO 99
C
C...............................................................................
C '10' = X1
C...............................................................................
C
	ELSEIF (STRING(1:LEN).EQ.'10') THEN
	   DXF_POINT = FLRLIN()
	   IF (.NOT.DXF_POINT) GOTO 999
C
	   CALL BSLN
	   NUMLIN = NUMLIN+1
	   IF (RDDBLE(DST(1,1))) THEN
	      DXF_POINT = DXF2I__UNACONT
	      CALL LSL_PUTMSG(DXF2I__ERRENTCOOR,NUMLIN)
	      GOTO 1000
	   ENDIF
C
	   IF (HAD_ABS) THEN
	      ST(1,1) = DST(1,1)-MD2LOC(1)
	   ELSE
	      ST(1,1) = DST(1,1)
	   ENDIF
C
C Initialise the third coord in case it's missing
	   INT_EQUIV = IFF_ABSENT
	   ZS(3,1)   = REAL_EQUIV
C
	   HAD_X = .TRUE.
	   GOTO 99
C
C...............................................................................
C '20' = Y1
C...............................................................................
C
	ELSEIF (STRING(1:LEN).EQ.'20') THEN
	   DXF_POINT = FLRLIN()
	   IF (.NOT.DXF_POINT) GOTO 999
C
	   CALL BSLN
	   NUMLIN = NUMLIN+1
	   IF (RDDBLE(DST(2,1))) THEN
	      DXF_POINT = DXF2I__UNACONT
	      CALL LSL_PUTMSG(DXF2I__ERRENTCOOR,NUMLIN)
	      GOTO 1000
	   ENDIF
C
	   IF (HAD_ABS) THEN
	      ST(2,1) = DST(2,1)-MD2LOC(2)
	   ELSE
	      ST(2,1) = DST(2,1)
	   ENDIF
C
C Initialise the third coord in case it's missing
	   INT_EQUIV = IFF_ABSENT
	   ZS(3,1)   = REAL_EQUIV
C
	   HAD_Y = .TRUE.
	   GOTO 99
C
C...............................................................................
C '30' = Z1
C...............................................................................
C
	ELSEIF (STRING(1:LEN).EQ.'30') THEN
	   DXF_POINT = FLRLIN()
	   IF (.NOT.DXF_POINT) GOTO 999
C
	   CALL BSLN
	   NUMLIN = NUMLIN+1
	   IF (RDREAL(ZS(3,1))) THEN
	      DXF_POINT = DXF2I__UNACONT
	      CALL LSL_PUTMSG(DXF2I__ERRENTCOOR,NUMLIN)
	      GOTO 1000
	   ENDIF
C
	   HAD_Z  = .TRUE.
	   HAD_3D = .TRUE.
	   GOTO 99
C
C...............................................................................
C '0'- write feature
C...............................................................................
C
	ELSEIF (STRING(1:LEN).EQ.'0') THEN	! end of entity
C
C Check that we have had all of the necessary bits and bobs
C had layer?
C
	   IF (.NOT.HAD_LAY) THEN
	      CALL LSL_PUTMSG(DXF2I__MISENTLAY,PNTNUMLIN)
C
C and use default from $CLAYER if present
C
	      IF (HAD_LAYER_NAME) THEN
	         LAYERLEN = DEF_LAYER_NAME_LEN
	         LAYERNAM = DEF_LAYER_NAME(1:LAYERLEN)
		 IF (.NOT.HAD_QUIET) CALL LSL_ADDMSG(DXF2I__DEFLAYNAM,
     &				    LAYERNAM(1:LAYERLEN),PNTNUMLIN)
	      ELSE
		 DXF_POINT = DXF2I__UNACONT
		 IF (.NOT.HAD_SCAN) GOTO 1000
	      ENDIF
	   ENDIF
C
C if it's not empty, go and look for the corresponding IFF layer no
C in the PARAMETER lookup table
C
	   DO 4000 COUNTER = 1,CURRLAYIND
	      IF (LAYERNAM(1:LAYERLEN).EQ.
     &		    DXFLAYNAM(COUNTER)(1:DXFLAYLEN(COUNTER))) THEN
		 SELLAYIND = COUNTER
		 GOTO 4001
	      ENDIF
4000	   CONTINUE
C
C output error message when there is no match and give if not had SCAN
C
	   IF (.NOT.HAD_SCAN) THEN
	     DXF_POINT = DXF2I__UNACONT
	     CALL LSL_PUTMSG(DXF2I__ERRMATLAY,
     &				LAYERNAM(1:LAYERLEN),PNTNUMLIN)
	     GOTO 1000
	   ELSE
	     CALL LSL_PUTMSG(DXF2I__ERRSCANLAY,
     &				LAYERNAM(1:LAYERLEN),PNTNUMLIN)
	   ENDIF
C
4001	   CONTINUE
C
C now get the colour with priority
C BY LAYER will have higher preference than '62' entry
C
	   IF (BY_LAYER) THEN
              SELCOLOUR = DXFLAYCOL(SELLAYIND)
              HAD_COL = .TRUE.
           ENDIF
C
C had colour?
C
	   IF (.NOT.HAD_COL) THEN
C	      DXF_POINT = DXF2I__UNACONT
	      IF (.NOT.HAD_QUIET) 
     &			CALL LSL_PUTMSG(DXF2I__MISENTCOL,PNTNUMLIN)
C
C Allow coord entries to be read even if colour entry missing
C
	   ENDIF
C
	   IF (.NOT.HAD_X .OR. .NOT.HAD_Y) THEN 
	      DXF_POINT = DXF2I__UNACONT
	      CALL LSL_PUTMSG (DXF2I__MISCOOR,PNTNUMLIN)
	      IF (.NOT.HAD_SCAN) GOTO 1000
	   ENDIF
C
C Don't match FC or anything if SCAN
C
	   IF (HAD_SCAN) THEN		! write entity entry to scanfil
C
C Get the first suitable FC from FRT file
C
	      IF (.NOT.HAD_POI_FC) THEN
		IF (.NOT.FRTFGT(UNOTYP,POI_FC)) THEN	! unoriented symbol
		   HAD_POI_FC = .TRUE.
		ENDIF
	      ENDIF
C
	      IF (.NOT.HAD_POI_FC) THEN
		IF (.NOT.FRTFGT(ORITYP,POI_FC)) THEN	! oriented symbol
		   HAD_POI_FC = .TRUE.
		ENDIF
	      ENDIF
C
	      IF (.NOT.HAD_POI_FC) THEN
		IF (.NOT.FRTFGT(SCATYP,POI_FC)) THEN	! scaled symbol
		   HAD_POI_FC = .TRUE.
		ENDIF
	      ENDIF
C
C Write out entity name to SCAN parameter file if required
C
	      EXPLEN = EXPAND(
     &		'%WS% ENTITY POINT                          %N%  %^W%N',
     &				ENTITY_OUT,
     &				SELCOLOUR,POI_FC)
C
C Check same as last one output
C
	      IF (EXPLEN.EQ.LAST_EXPLEN) THEN
		 IF (ENTITY_OUT(1:EXPLEN) .EQ.
     &			LAST_ENTITY_OUT(1:LAST_EXPLEN)) GOTO 1000
	      ENDIF
C
C Not the same as last one output, so search the entity list for a match
C
	     DO 4002 I = 1,ENTITY_CNT
		IF (EXPLEN.EQ.ENTITY_LEN(I)) THEN	! matched length
		   IF (ENTITY_OUT(1:EXPLEN) .EQ.	! match entity
     &				 ENTITY(I)(1:ENTITY_LEN(I)) ) THEN
			GOTO 1000
		   ENDIF
		ENDIF
4002	     CONTINUE
C
C No match, so output it
C
	      DXF_POINT = FLWSTR(ENTITY_OUT(1:EXPLEN),IERR)
C
	      IF (.NOT.DXF_POINT) THEN
		 CALL LSL_PUTMSG(DXF_POINT)
		 IF (DXF_POINT.EQ.LSL__SYSERR) CALL LSL_ADDMSG(IERR)
		 GOTO 1000
	      ENDIF
C
	      IF (ENTITY_CNT.EQ.MAXSCAN) THEN
		 CALL LSL_PUTMSG(DXF2I__TOOMNYENTS,MAXSCAN)
		 GOTO 1000
	      ENDIF
C
	      ENTITY_CNT = ENTITY_CNT + 1
C
	      ENTITY_LEN(ENTITY_CNT) 	   = EXPLEN
	      ENTITY(ENTITY_CNT)(1:EXPLEN) = ENTITY_OUT(1:EXPLEN)
C
	      LAST_EXPLEN 		     = EXPLEN
	      LAST_ENTITY_OUT(1:LAST_EXPLEN) = ENTITY_OUT(1:EXPLEN)
C
	      GOTO 1000
	   ENDIF
C
C now get hold of the FC from parameter lookup table
C
	   DO 4003 COUNTER = 1,CURRENTIND
	      IF (SELENTTYP.EQ.DXFENTTYP(COUNTER)) THEN
		 IF (HAD_COL .AND. SELCOLOUR.NE.ENTCOLOUR(COUNTER)) 
     &							GOTO 4003
		 FC(1) = IFFFCCODE(COUNTER)
		 GOTO 4005
	      ENDIF
4003	   CONTINUE
C
	   DXF_POINT = DXF2I__UNACONT
	   CALL LSL_PUTMSG(DXF2I__ERRMATFC,PNTNUMLIN)
           GOTO 1000
C
4005	   CONTINUE
C
C final setting for output
C
	   IF (IN_LAYER .AND. IFFOLAYNO(SELLAYIND).NE.LSTIFFLAY) THEN
	      CALL IFFUNO(LPOSNO)
	      CALL IFFEO
	   ENDIF
C
	   IF (.NOT.IN_LAYER .OR. IFFOLAYNO(SELLAYIND).NE.LSTIFFLAY) THEN
	      LSTIFFLAY = IFFOLAYNO(SELLAYIND)
	      LPOSNO = LPOSE
	      CALL IFFNO(LSTIFFLAY,0,0)
	      IN_LAYER = .TRUE.
	   ENDIF
C
	   NF = NF+1
	   CALL IFFNF(NF,NF)
	   CALL LIB$INSV(1,14,2,FC(3))  ! insert correct value into
C					! right bits for call to ifffs
	   CALL IFFFS(FC)		! write the FC
	   FC(3) = 0			! reset symbol bit
	   IF (HAD_HT) CALL IFFAC (3,HEIGHT)
C
	   IF (FROM_BLOCKS) THEN
	      IF (EXPAND_XSCL(EXPAND_INDEX) .NE. 1) ST(1,1) = 
     &				ST(1,1) * EXPAND_XSCL(EXPAND_INDEX) 
	      IF (EXPAND_YSCL(EXPAND_INDEX) .NE. 1) ST(2,1) = 
     &				ST(2,1) * EXPAND_YSCL(EXPAND_INDEX) 
	      IF (HAD_Z .AND. EXPAND_ZSCL(EXPAND_INDEX).NE.1) ZS(3,1) = 
     &				ZS(3,1) * EXPAND_ZSCL(EXPAND_INDEX) 
C
	      IF (EXPAND_XOFF(EXPAND_INDEX) .NE. 0) ST(1,1) = 
     &				ST(1,1) + EXPAND_XOFF(EXPAND_INDEX)
	      IF (EXPAND_YOFF(EXPAND_INDEX) .NE. 0) ST(2,1) = 
     &				ST(2,1) + EXPAND_YOFF(EXPAND_INDEX)
	      IF (HAD_Z .AND. EXPAND_ZOFF(EXPAND_INDEX).NE.0) ZS(3,1) = 
     &				ZS(3,1) + EXPAND_ZOFF(EXPAND_INDEX)
	   ENDIF
C
	   CALL CHECK_RANGE(ST,1)	! check the range
C
	   IF (HAD_3D) THEN
	      ZS(1,1) = ST(1,1)
	      ZS(2,1) = ST(2,1)
C
	      CALL IFFZS(ZS,1,0) 	! write ZS entry
	   ELSE
	      CALL IFFST(ST,1,0) 	! write ST entry
	   ENDIF
	   CALL IFFEF			! and end feature
	   GOTO 1000
	ENDIF
C
	GOTO 99
C...............................................................................
C
999	CALL LSL_PUTMSG(DXF2I__RDLINERR,NUMLIN)
C
1000	RETURN
	END
C
