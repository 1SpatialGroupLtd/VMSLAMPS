C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1994-12-06 17:35:42.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL*4 FUNCTION DXF_INSERT()
C
***	MODULE DXF_INSERT
***	IDENT 06DE94
C
C ©Copyright Laser-Scan Ltd, Cambridge, England.
C
C Author  : P. Pan 					13 Mar 1990
C
C To read and translate DXF INSERT entity to IFF feature
C
C Modified: P. Pan to set the correct bit		23 Mar 1990
C	    
CMOD961     J Barber					30 Jan 1991
C	    Add 3d coord reading into ZS entries
C	    and group 38 heights into AC type 3
C	    Change program action on DXF2I__MISENTCOL error to
C	    continue to transfer the coordinate data regardless
C
CMOD1004    J Barber 					13 Jun 1991
C	    Convert coordinates with any scale factors and x,y offsets
C	    from the parameter file if an EXPAND 'symbol' entry exists
C	    there and this routine was called from within the BLOCKS 
C	    section. This will occur for an INSERT entity in a BLOCK, 
C	    ie. a nested BLOCK hierarchy. 
C	    If the insertion points are all non-zero, or the scalings all
C	    not equal to 1, the final coordinates may be in error, as the
C	    parameter file EXPAND offsets and scaling must include the 
C	    final combination of all acting offsets, and the warning 
C	    messages DIFFEXPSCL/DIFFEXPOFF will be output as only the final 
C	    INSERT points will be compared with the parameter file entries.
C
C	    Read DXF EXPAND entries for the presence of the symbol name
C	    in the parameter file to get scale factors and x,y offsets,
C	    and compare with the supplied values already used for the
C	    BLOCK entity output, and output a warning if there are any
C	    discrepencies
C
CMOD1156    J Barber 					13 Oct 1992
C	    Add ABS coord changes
C	    Use FRTGTTYPE(I) instead of FC(I) for graphical type
C
CMOD1172    J Barber 					03 Jan 1993
C	    Change IFF feature entry order to:
C		NF FS AC TH ST RO TX EF
C
CMOD1226    J Barber 					10 Jun 1993
C	    Add /SCAN facility
C
CMOD1439    Jon Barber				 	06 Dec 1994
C	    Use IFF_ABSENT as a 'real' number in its absence from
C	    the DXF file
C
	IMPLICIT NONE
C
	INCLUDE  'LSL$CMNIFF:CB.STR'		! for IFF_ABSENT
	INCLUDE  'LSL$CMNLSL:LSLLIBMSG.PAR'
	INCLUDE  'LSL$CMNLSL:READSTR.PAR'
	INCLUDE  'LSL$CMNLSL:TXTC.CMN'
	INCLUDE  'LSL$CMNIFF:IFFHAN.CMN'
	INCLUDE  'LSL$CMNIFF:MD2DES.CMN'
	INCLUDE  'LSL$CMNFRT:FRTCOM.CMN'
	INCLUDE  'LSL$CMNDXFPAR:LOOKUP.CMN'
	INCLUDE  'HERE:DXF2I.CMN'
	INCLUDE  'HERE:DXF2IMSG.PAR'
C
C Functions
	INTEGER		RDREAL		! Get a real
	INTEGER		RDDBLE		! Get a real
	INTEGER		RDINT		! Get an integer
	INTEGER		READSTR		! Get a string
	INTEGER		FLRLIN		! Read a line from file
	INTEGER		FLWSTR		! Write a line to file
	INTEGER*2	EXPAND
	LOGICAL		FRTFGT		! Get FC for GT
C
C Workspace
	CHARACTER*255	STRING		! Any old string
	INTEGER*4	LEN		! Length of character string
	CHARACTER*255	ENTITY_OUT	! output string
	INTEGER*2	EXPLEN		! expanded string length
	CHARACTER*20	STYLE_NAME	! output string
C
	CHARACTER*255	LAST_ENTITY_OUT	! last output string
	INTEGER*2	LAST_EXPLEN	! last expanded string length
	SAVE		LAST_ENTITY_OUT	! keep last output string 
	SAVE		LAST_EXPLEN	! keep last expanded string length
C
	INTEGER*2       FC(4)           ! Feature code
	INTEGER*2	GT		! and graphical type
C
	INTEGER*4	NCOORX		! Number of x coords in string
	INTEGER*4	NCOORY		! Number of y coords in string
	INTEGER*2	COLNUM		! Feature colour
C
	LOGICAL		HAD_LAY		! Had layer value?
	LOGICAL		HAD_COL 	! Had colour? 
	LOGICAL		HAD_RO 		! Had rotation?
	LOGICAL		USE_RO 		! Use rotation?
	LOGICAL		HAD_XSC		! Had scale factor?
	LOGICAL		HAD_YSC		! Had scale factor?
	LOGICAL		HAD_ZSC		! Had scale factor?
	LOGICAL		HAD_X		! Had x coord?
	LOGICAL		HAD_Y		! Had y coord?
	LOGICAL		HAD_Z		! Had z coord?
	LOGICAL		HAD_3D		! Had 3d coord?
	LOGICAL		HAD_HT		! Had AC height?
	LOGICAL		HAD_SYM		! Had symbol?
C
	LOGICAL		ERRCOD		! ok?
	INTEGER		IERR
        INTEGER		I,COUNTER	! local counter
	INTEGER		INSNUMLIN	! line position for insert
	CHARACTER*255	SYMBOLNAM	! symbol name
	INTEGER		SYMBOLLEN	! symbol name's length
	CHARACTER*255	LAYERNAM	! layer name
	INTEGER		LAYERLEN	! layer name's length
	REAL*4		RO,RO_RAD
	REAL*4		XSCALE,YSCALE	! scale factors
	REAL*4		ZSCALE		! scale factors
	REAL*4		HEIGHT
	REAL*4		X,Y,Z,SCL
C
	INTEGER		INT_EQUIV	! for IFF_ABSENT
	REAL		REAL_EQUIV
	EQUIVALENCE	(INT_EQUIV,REAL_EQUIV)
C
C---------------------------------------------------------------------
C INSERT
C---------------------------------------------------------------------
C Set defaults
C
	SELENTTYP = DXFINSERT
	SELENTIND = 1
	HAD_LAY = .FALSE.
	HAD_COL = .FALSE.
	HAD_X   = .FALSE.
	HAD_Y   = .FALSE.
	HAD_Z   = .FALSE.
	HAD_3D  = .FALSE.
	HAD_HT  = .FALSE.
	HAD_RO  = .FALSE.
	USE_RO  = .FALSE.
	HAD_XSC = .FALSE.
	HAD_YSC = .FALSE.
	HAD_ZSC = .FALSE.
	HAD_SYM = .FALSE.
	XSCALE = 1
	YSCALE = 1
	ZSCALE = 1
	RO     = 0
C
C---------------------------------------------------------------------
C read the entity
C
	INSNUMLIN = NUMLIN
C
99	DXF_INSERT = FLRLIN()
	IF (.NOT.DXF_INSERT) GOTO 999
C
	CALL BSLN
	NUMLIN = NUMLIN+1
	LEN = READSTR(STRING,,ON_EOL)
C
C..............................................................................
C '8' Layer 
C..............................................................................
C
	IF (STRING(1:LEN).EQ.'8') THEN
	   DXF_INSERT = FLRLIN()
	   IF (.NOT.DXF_INSERT) GOTO 999
C
	   CALL BSLN
	   NUMLIN = NUMLIN+1
	   LAYERLEN = READSTR(LAYERNAM,,ON_EOL)
C
C if an empty layer name is supplied, output error message
C
	   IF (LAYERLEN.EQ.0) THEN
	      CALL LSL_PUTMSG(DXF2I__ERRLAYNAM,NUMLIN)
C
C and use default from $CLAYER if present
C
	      IF (HAD_LAYER_NAME) THEN
	         LAYERLEN = DEF_LAYER_NAME_LEN
	         LAYERNAM = DEF_LAYER_NAME(1:LAYERLEN)
		 IF (.NOT.HAD_QUIET) CALL LSL_ADDMSG(DXF2I__DEFLAYNAM,
     &				    	LAYERNAM(1:LAYERLEN),INSNUMLIN)
	      ELSE
		 DXF_INSERT = DXF2I__UNACONT
		 IF (.NOT.HAD_SCAN) GOTO 1000
	      ENDIF
	   ENDIF
C
	   HAD_LAY = .TRUE.
	   GOTO 99
C
C..............................................................................
C '6' Linetype (unused)
C..............................................................................
C
	ELSEIF (STRING(1:LEN).EQ.'6') THEN
	   DXF_INSERT = FLRLIN()
	   IF (.NOT.DXF_INSERT) GOTO 999
	   NUMLIN = NUMLIN+1
	   GOTO 99
C
C..............................................................................
C '2' block name
C..............................................................................
C
	ELSEIF (STRING(1:LEN).EQ.'2') THEN
	   HAD_SYM = .TRUE.
	   DXF_INSERT = FLRLIN()
	   IF (.NOT.DXF_INSERT) GOTO 999
C
	   CALL BSLN
	   NUMLIN = NUMLIN+1
	   SYMBOLLEN = READSTR(SYMBOLNAM,,ON_EOL)
C
C if an empty line type is supplied, output error message and leave
C
	   IF (SYMBOLLEN.EQ.0) THEN
	      DXF_INSERT = DXF2I__UNACONT
	      CALL LSL_PUTMSG(DXF2I__ERRSYMNAM,NUMLIN)
	      GOTO 1000
	   ENDIF
C
C if it's not empty, go and look for the first appearence within the 
C PARAMETER lookup table, colour not matched yet
C
	   DO 5001 COUNTER = 1,CURRENTIND
	      IF (SELENTTYP.EQ.DXFENTTYP(COUNTER)) THEN
		 IF (SYMBOLNAM(1:SYMBOLLEN) .EQ. 
     &		      DXFENTNAM(COUNTER)(1:DXFENTLEN(COUNTER))) THEN
		    SELENTIND = COUNTER
		    GOTO 99
		 ENDIF
	      ENDIF
5001	   CONTINUE
C
C output error message when there is no match
C
	   IF (.NOT.HAD_SCAN) THEN
	      DXF_INSERT = DXF2I__UNACONT
	      CALL LSL_PUTMSG(DXF2I__ERRMATBLK,
     &				SYMBOLNAM(1:SYMBOLLEN),INSNUMLIN)
	      GOTO 1000
	   ENDIF
C
	   GOTO 99
C..............................................................................
C '38' - CONTOUR HEIGHT
C..............................................................................
C
	ELSEIF (STRING(1:LEN).EQ.'38') THEN
	   DXF_INSERT = FLRLIN()
	   IF (.NOT.DXF_INSERT) GOTO 999
C
	   CALL BSLN
	   NUMLIN = NUMLIN+1
	   ERRCOD = RDREAL(HEIGHT)
           IF (ERRCOD) THEN
	      DXF_INSERT = DXF2I__UNACONT
	      CALL LSL_PUTMSG(DXF2I__ERRENTACVAL,NUMLIN)
	      GOTO 1000
	   ENDIF
	   HAD_HT = .TRUE.
	   GOTO 99
C
C..............................................................................
C '62' - COLOUR
C..............................................................................
C
	ELSEIF (STRING(1:LEN).EQ.'62') THEN
	   DXF_INSERT = FLRLIN()
	   IF (.NOT.DXF_INSERT) GOTO 999
C
C if colour is by layer then colour entry is irrelevant
C
	   IF (BY_LAYER) THEN
	      NUMLIN = NUMLIN+1
	      GOTO 99
	   ENDIF
C
	   CALL BSLN
	   NUMLIN = NUMLIN+1
	   ERRCOD = RDINT(COLNUM)
           IF (ERRCOD) THEN
	      DXF_INSERT = DXF2I__UNACONT
	      CALL LSL_PUTMSG(DXF2I__ERRENTCOL,NUMLIN)
	      GOTO 1000
	   ENDIF
	   HAD_COL = .TRUE.
	   SELCOLOUR = COLNUM
	   GOTO 99
C
C...............................................................................
C '10' = X1
C...............................................................................
C
	ELSEIF (STRING(1:LEN).EQ.'10') THEN
	   DXF_INSERT = FLRLIN()
	   IF (.NOT.DXF_INSERT) GOTO 999
C
	   CALL BSLN
	   NUMLIN = NUMLIN+1
	   ERRCOD = RDDBLE(DST(1,1))
	   IF (ERRCOD) THEN
	      DXF_INSERT = DXF2I__UNACONT
	      CALL LSL_PUTMSG(DXF2I__ERRENTCOOR,NUMLIN)
	      GOTO 1000
	   ENDIF
C
	   IF (HAD_ABS) THEN
	      ST(1,1) = DST(1,1)-MD2LOC(1)
	   ELSE
	      ST(1,1) = DST(1,1)
	   ENDIF
C
C Initialise the third coord in case it's missing
	   INT_EQUIV = IFF_ABSENT
	   ZS(3,1)   = REAL_EQUIV
C
	   HAD_X = .TRUE.
	   GOTO 99
C
C...............................................................................
C '20' = Y1
C...............................................................................
C
	ELSEIF (STRING(1:LEN).EQ.'20') THEN
	   DXF_INSERT = FLRLIN()
	   IF (.NOT.DXF_INSERT) GOTO 999
C
	   CALL BSLN
	   NUMLIN = NUMLIN+1
	   ERRCOD = RDDBLE(DST(2,1))
	   IF (ERRCOD) THEN
	      DXF_INSERT = DXF2I__UNACONT
	      CALL LSL_PUTMSG(DXF2I__ERRENTCOOR,NUMLIN)
	      GOTO 1000
	   ENDIF
C
	   IF (HAD_ABS) THEN
	      ST(2,1) = DST(2,1)-MD2LOC(2)
	   ELSE
	      ST(2,1) = DST(2,1)
	   ENDIF
C
C Initialise the third coord in case it's missing
	   INT_EQUIV = IFF_ABSENT
	   ZS(3,1)   = REAL_EQUIV
C
	   HAD_Y = .TRUE.
	   GOTO 99
C
C...............................................................................
C '30' = Z1
C...............................................................................
C
	ELSEIF (STRING(1:LEN).EQ.'30') THEN
	   DXF_INSERT = FLRLIN()
	   IF (.NOT.DXF_INSERT) GOTO 999
C
	   CALL BSLN
	   NUMLIN = NUMLIN+1
	   ERRCOD = RDREAL(ZS(3,1))
	   IF (ERRCOD) THEN
	      DXF_INSERT = DXF2I__UNACONT
	      CALL LSL_PUTMSG(DXF2I__ERRENTCOOR,NUMLIN)
	      GOTO 1000
	   ENDIF
C
	   ZS(3,2) = ZS(3,1)		! in case
	   HAD_Z  = .TRUE.
	   HAD_3D = .TRUE.
	   GOTO 99
C
C...............................................................................
C '41' = X scale factor
C...............................................................................
C
	ELSEIF (STRING(1:LEN).EQ.'41') THEN
	   DXF_INSERT = FLRLIN()
	   IF (.NOT.DXF_INSERT) GOTO 999
C
	   CALL BSLN
	   NUMLIN = NUMLIN+1
	   ERRCOD = RDREAL(XSCALE)
	   IF (ERRCOD) THEN
	      DXF_INSERT = DXF2I__UNACONT
	      CALL LSL_PUTMSG(DXF2I__ERRSYMSCA,NUMLIN)
	      GOTO 1000
	   ENDIF
	   HAD_XSC = .TRUE.
	   GOTO 99
C
C...............................................................................
C '42' = Y scale factor
C...............................................................................
C
	ELSEIF (STRING(1:LEN).EQ.'42') THEN
	   DXF_INSERT = FLRLIN()
	   IF (.NOT.DXF_INSERT) GOTO 999
C
	   CALL BSLN
	   NUMLIN = NUMLIN+1
	   ERRCOD = RDREAL(YSCALE)
	   IF (ERRCOD) THEN
	      DXF_INSERT = DXF2I__UNACONT
	      CALL LSL_PUTMSG(DXF2I__ERRSYMSCA,NUMLIN)
	      GOTO 1000
	   ENDIF
	   HAD_YSC = .TRUE.
	   GOTO 99
C
C...............................................................................
C '43' = Z scale factor
C...............................................................................
C
	ELSEIF (STRING(1:LEN).EQ.'43') THEN
	   DXF_INSERT = FLRLIN()
	   IF (.NOT.DXF_INSERT) GOTO 999
C
	   CALL BSLN
	   NUMLIN = NUMLIN+1
	   ERRCOD = RDREAL(ZSCALE)
	   IF (ERRCOD) THEN
	      DXF_INSERT = DXF2I__UNACONT
	      CALL LSL_PUTMSG(DXF2I__ERRSYMSCA,NUMLIN)
	      GOTO 1000
	   ENDIF
	   HAD_ZSC = .TRUE.
	   GOTO 99
C
C...............................................................................
C '50' = RO, please note that RO is still in DEGREES
C...............................................................................
C
	ELSEIF (STRING(1:LEN).EQ.'50') THEN
	   DXF_INSERT = FLRLIN()
	   IF (.NOT.DXF_INSERT) GOTO 999
C
	   CALL BSLN
	   NUMLIN = NUMLIN+1
	   ERRCOD = RDREAL(RO)
	   IF (ERRCOD) THEN
	      DXF_INSERT = DXF2I__UNACONT
	      CALL LSL_PUTMSG(DXF2I__ERRTEXROT,NUMLIN)
	      GOTO 1000
	   ENDIF
	   HAD_RO = .TRUE.
	   GOTO 99
C
C...............................................................................
C '0'- write feature
C...............................................................................
C
	ELSEIF (STRING(1:LEN).EQ.'0') THEN		! end of entity
C
C Check that we have had all of the necessary bits and bobs
C had layer?
C
	   IF (.NOT.HAD_LAY) THEN
	      CALL LSL_PUTMSG(DXF2I__MISENTLAY,INSNUMLIN)
C
C and use default from $CLAYER if present
C
	      IF (HAD_LAYER_NAME) THEN
	         LAYERLEN = DEF_LAYER_NAME_LEN
	         LAYERNAM = DEF_LAYER_NAME(1:LAYERLEN)
		 IF (.NOT.HAD_QUIET) CALL LSL_ADDMSG(DXF2I__DEFLAYNAM,
     &				    	LAYERNAM(1:LAYERLEN),INSNUMLIN)
	      ELSE
		 DXF_INSERT = DXF2I__UNACONT
		 IF (.NOT.HAD_SCAN) GOTO 1000
	      ENDIF
	   ENDIF
C
C if it's not empty, go and look for the corresponding IFF layer no
C in the PARAMETER lookup table
C
	   DO 4000 COUNTER = 1,CURRLAYIND
	      IF (LAYERNAM(1:LAYERLEN).EQ.
     &		       DXFLAYNAM(COUNTER)(1:DXFLAYLEN(COUNTER))) THEN
                 SELLAYIND = COUNTER
		 GOTO 4001
	      ENDIF
4000	   CONTINUE
C
C output error message when there is no match, and give up if not had SCAN
C
	   IF (.NOT.HAD_SCAN) THEN
	     DXF_INSERT = DXF2I__UNACONT
	     CALL LSL_PUTMSG(DXF2I__ERRMATLAY,
     &				LAYERNAM(1:LAYERLEN),INSNUMLIN)
	     GOTO 1000
	   ELSE
	     CALL LSL_PUTMSG(DXF2I__ERRSCANLAY,
     &				LAYERNAM(1:LAYERLEN),INSNUMLIN)
	   ENDIF
C
4001	   CONTINUE
C
C now get the colour: BY LAYER will have higher preference than '62' entry, 
C (or 0 (BYBLOCK) - not supported)
C
	   IF (BY_LAYER) THEN
              SELCOLOUR = DXFLAYCOL(SELLAYIND)
              HAD_COL = .TRUE.
           ENDIF
C
C had block name?
C
	   IF (.NOT.HAD_SYM) THEN
	      DXF_INSERT = DXF2I__UNACONT
	      CALL LSL_PUTMSG(DXF2I__MISENTBLK,INSNUMLIN)
              GOTO 1000
	   ENDIF
C
C had colour?
C
	   IF (.NOT.HAD_COL) THEN
	      IF (.NOT.HAD_QUIET) 
     &			CALL LSL_PUTMSG(DXF2I__MISENTCOL,INSNUMLIN)
C
C Allow coord entries to be read even if colour entry missing
C
	   ENDIF
C
C had x, y?
C
	   IF (.NOT.HAD_X .OR. .NOT.HAD_Y) THEN 
	      DXF_INSERT = DXF2I__UNACONT
	      CALL LSL_PUTMSG (DXF2I__MISCOOR,INSNUMLIN)
	      IF (.NOT.HAD_SCAN) GOTO 1000
	   ENDIF
C
C Don't match FC or anything if SCAN
C
	   IF (HAD_SCAN) THEN		! write entity entry to scanfil
C
C Write out entity name to SCAN parameter file if required
C
C Get the first suitable FC from FRT file
C
	     IF (.NOT.HAD_INS_FC) THEN
		IF (.NOT.FRTFGT(ORITYP,INS_FC)) THEN	! oriented symbol
		   HAD_INS_FC = .TRUE.
		ENDIF
	     ENDIF
C
	     IF (.NOT.HAD_INS_FC) THEN
		IF (.NOT.FRTFGT(SCATYP,INS_FC)) THEN	! scaled symbol
		   HAD_INS_FC = .TRUE.
		ENDIF
	     ENDIF
C
	     IF (.NOT.HAD_INS_FC) THEN
		IF (.NOT.FRTFGT(UNOTYP,INS_FC)) THEN	! unoriented symbol
		   HAD_INS_FC = .TRUE.
		ENDIF
	     ENDIF
C
C Format layers shorter then 20 characters
C
	      IF (SYMBOLLEN.LE.20) THEN
		 CALL EXPAND('%WS% %S20',STYLE_NAME,SYMBOLNAM(1:SYMBOLLEN))
C
		 EXPLEN = EXPAND('%WS% ENTITY INSERT    %S%  %N%  %^W%N',
     &				ENTITY_OUT,
     &				STYLE_NAME(1:20),
     &				SELCOLOUR,INS_FC)
C
	      ELSE
		 EXPLEN = EXPAND('%WS% ENTITY INSERT    %S%  %N%  %^W%N',
     &				ENTITY_OUT,
     &				SYMBOLNAM(1:SYMBOLLEN),
     &				SELCOLOUR,INS_FC)
	      ENDIF
C
C Check same as last one output
C
	      IF (EXPLEN.EQ.LAST_EXPLEN) THEN
		 IF (ENTITY_OUT(1:EXPLEN) .EQ.
     &			LAST_ENTITY_OUT(1:LAST_EXPLEN)) GOTO 1000
	      ENDIF
C
C Not the same as last one output, so search the entity list for a match
C
	     DO 4002 I = 1,ENTITY_CNT
		IF (EXPLEN.EQ.ENTITY_LEN(I)) THEN	! matched length
		   IF (ENTITY_OUT(1:EXPLEN) .EQ.	! match entity
     &				 ENTITY(I)(1:ENTITY_LEN(I)) ) THEN
			GOTO 1000
		   ENDIF
		ENDIF
4002	     CONTINUE
C
C No match, so output it
C
	      DXF_INSERT = FLWSTR(ENTITY_OUT(1:EXPLEN),IERR)
C
	      IF (.NOT.DXF_INSERT) THEN
		 CALL LSL_PUTMSG(DXF_INSERT)
		 IF (DXF_INSERT.EQ.LSL__SYSERR) CALL LSL_ADDMSG(IERR)
		 GOTO 1000
	      ENDIF
C
	      IF (ENTITY_CNT.EQ.MAXSCAN) THEN
		 CALL LSL_PUTMSG(DXF2I__TOOMNYENTS,MAXSCAN)
		 GOTO 1000
	      ENDIF
C
	      ENTITY_CNT = ENTITY_CNT + 1
C
	      ENTITY_LEN(ENTITY_CNT) 	   = EXPLEN
	      ENTITY(ENTITY_CNT)(1:EXPLEN) = ENTITY_OUT(1:EXPLEN)
C
	      LAST_EXPLEN 		     = EXPLEN
	      LAST_ENTITY_OUT(1:LAST_EXPLEN) = ENTITY_OUT(1:EXPLEN)
C
	      GOTO 1000
	   ENDIF
C
C now get hold of the FC from parameter lookup table
C
	   DO 4003 COUNTER = SELENTIND,CURRENTIND
	      IF (SELENTTYP.EQ.DXFENTTYP(COUNTER)) THEN
		 IF (SYMBOLNAM(1:SYMBOLLEN).EQ.DXFENTNAM(COUNTER)
     &			      (1:DXFENTLEN(COUNTER))) THEN
		    IF (HAD_COL .AND. SELCOLOUR.NE.ENTCOLOUR(COUNTER))
     &							 GOTO 4003
		    FC(1) = IFFFCCODE(COUNTER)
		    GT    = FRTGTTYPE(COUNTER)
		    GOTO 4005
                 ENDIF
              ENDIF
4003	   CONTINUE
C
	   DXF_INSERT = DXF2I__UNACONT
	   CALL LSL_PUTMSG(DXF2I__ERRMATFC,INSNUMLIN)
           GOTO 1000
C
4005	   CONTINUE
C
C Search EXPAND entries for the symbol name to check on offset and scale
C given to EXPAND this nested BLOCK hierarchy
C NB. If the insertion points are all non-zero, or the scalings all not 
C equal to 1, the final coordinates may be in error, as the parameter file 
C EXPAND offsets and scalings must include the final combination of all 
C acting offsets, and the warning messages DIFFEXPSCL/DIFFEXPOFF will be 
C output as only the final INSERT points will be compared with the parameter 
C file entries.
C
C	   IF (HAD_QUIET) GOTO 7004
C
	   DO 7003 I = 1,CURREXPIND
	      IF (EXPANDLEN(I).EQ.SYMBOLLEN) THEN
		 IF (EXPANDNAM(I)(1:SYMBOLLEN) .EQ.
     &				SYMBOLNAM(1:SYMBOLLEN)) THEN
C
C Compare INSERT coords with EXPAND offsets and INSERT scales with 
C EXPAND scales
C
		    IF (EXPAND_XOFF(I).NE.ST(1,1) .OR. 
     &				EXPAND_YOFF(I).NE.ST(2,1))
     &			CALL LSL_PUTMSG(DXF2I__DIFFEXPOFF,
     &			ST(1,1),ST(2,1),SYMBOLNAM(1:SYMBOLLEN),INSNUMLIN)
C
		    IF (HAD_Z .AND. EXPAND_ZOFF(I).NE.ZS(3,1) )
     &			CALL LSL_PUTMSG(DXF2I__DIFFEXPZOFF,
     &			ZS(3,1),SYMBOLNAM(1:SYMBOLLEN),INSNUMLIN)
C
		    IF (EXPAND_XSCL(I).NE.XSCALE .OR. 
     &			EXPAND_YSCL(I).NE.YSCALE)
     &			CALL LSL_PUTMSG(DXF2I__DIFFEXPSCALE,
     &			XSCALE,YSCALE,SYMBOLNAM(1:SYMBOLLEN),INSNUMLIN)
C
		    IF (HAD_Z .AND. EXPAND_ZSCL(I).NE.ZSCALE) 
     &			CALL LSL_PUTMSG(DXF2I__DIFFEXPZSCALE,
     &			ZSCALE,SYMBOLNAM(1:SYMBOLLEN),INSNUMLIN)
C
		    GOTO 7004
		 ENDIF
	      ENDIF
7003	   CONTINUE
C
C check what sort of symbol it is in IFF
C if it is either oriented or scaled symbol then apply the rotation
C
C GT7 - Unoriented symbol; size in FRT, ignore scaling and orientation
C GT8 - Oriented symbol: size in FRT, ignore scaling, use RO instead of sec pnt
C GT9 - Scaled symbol: orient and size from second point (or RO or FRT size)
C
7004	   CONTINUE
C
	   USE_RO = .FALSE.
	   NCOORX = 1
	   RO_RAD = RO * PI / 180.0
C
	   IF (GT.EQ.8) THEN
	      USE_RO = .TRUE.
C
	   ELSEIF (GT.EQ.9) THEN
	      ST(1,2) = ST(1,1) + XSCALE * COS(RO_RAD)
	      ST(2,2) = ST(2,1) + XSCALE * SIN(RO_RAD)
	      NCOORX  = 2
           ENDIF
C
C If a nested INSERT, rescale the coordinates
C
	   IF (FROM_BLOCKS) THEN
	      IF (EXPAND_XSCL(EXPAND_INDEX) .NE. 1) THEN
		 SCL = EXPAND_XSCL(EXPAND_INDEX)
		 DO 122 I = 1,NCOORX
		    ST(1,I) = ST(1,I) * SCL
122		 CONTINUE
	      ENDIF
C
	      IF (EXPAND_YSCL(EXPAND_INDEX) .NE. 1) THEN
		 SCL = EXPAND_YSCL(EXPAND_INDEX)
		 DO 123 I = 1,NCOORX
		    ST(2,I) = ST(2,I) * SCL
123		 CONTINUE
	      ENDIF
C
	      IF (HAD_Z .AND. EXPAND_ZSCL(EXPAND_INDEX) .NE. 1) THEN
		 SCL = EXPAND_ZSCL(EXPAND_INDEX)
		 DO 126 I = 1,NCOORX
		    ZS(3,I) = ZS(3,I) * SCL
126		 CONTINUE
	      ENDIF
C
	      IF (EXPAND_XOFF(EXPAND_INDEX) .NE. 0) THEN
		 X = EXPAND_XOFF(EXPAND_INDEX)
		 DO 124 I = 1,NCOORX
		    ST(1,I) = ST(1,I) + X
124		 CONTINUE
	      ENDIF
C
	      IF (EXPAND_YOFF(EXPAND_INDEX) .NE. 0) THEN
		 Y = EXPAND_YOFF(EXPAND_INDEX)
		 DO 125 I = 1,NCOORX
		    ST(2,I) = ST(2,I) + Y
125		 CONTINUE
	      ENDIF
C
	      IF (EXPAND_ZOFF(EXPAND_INDEX) .NE. 0) THEN
		 Z = EXPAND_ZOFF(EXPAND_INDEX)
		 DO 127 I = 1,NCOORX
		    ZS(3,I) = ZS(3,I) + Z
127		 CONTINUE
	      ENDIF
	   ENDIF
C
	   CALL CHECK_RANGE(ST,NCOORX)	! check the range
C
C final setting for output
C
	   IF (IN_LAYER .AND. IFFOLAYNO(SELLAYIND).NE.LSTIFFLAY) THEN
	      CALL IFFUNO(LPOSNO)
	      CALL IFFEO
	   ENDIF
C
	   IF (.NOT.IN_LAYER .OR. IFFOLAYNO(SELLAYIND).NE.LSTIFFLAY) THEN
	      LSTIFFLAY = IFFOLAYNO(SELLAYIND)
	      LPOSNO = LPOSE
	      CALL IFFNO(LSTIFFLAY,0,0)
	      IN_LAYER = .TRUE.
	   ENDIF
C
	   NF = NF+1
	   CALL IFFNF(NF,NF)
C
	   CALL LIB$INSV(1,14,2,FC(3))	! insert correct value into
C					! right bits for call to IFFFS
	   CALL IFFFS(FC)		! write the FC
C
	   IF (HAD_HT) CALL IFFAC(3,HEIGHT)
	   FC(3) = 0			! reset symbol bit
C
	   IF (HAD_3D) THEN
	      DO 130 I = 1,NCOORX
		 ZS(1,I) = ST(1,I)
		 ZS(2,I) = ST(2,I)
130	      CONTINUE
C
	      CALL IFFZS(ZS,NCOORX,0)		! write ZS entry
	   ELSE
	      CALL IFFST(ST,NCOORX,0)		! write ST entry
	   ENDIF
C
	   IF (USE_RO) CALL IFFRO(RO_RAD)
C
	   CALL IFFEF			! and end feature
	   GOTO 1000
	ENDIF
C
	GOTO 99
C...............................................................................
C
999	CALL LSL_PUTMSG(DXF2I__RDLINERR,NUMLIN)
C
1000	RETURN
	END
C
