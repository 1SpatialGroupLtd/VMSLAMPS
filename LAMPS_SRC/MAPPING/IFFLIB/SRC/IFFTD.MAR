	.TITLE	$IFFTD
	.IDENT	'30OC86'
;
; Copyright  Laser-Scan Laboratories Limited, Cambridge, England.
;
; Author: Paul Hardy after PDP11 version by P. Cross.

; if IERCD is even (internal error), signal
; #40 (%SYSTEM-W-ABORT)				AJW/PGH	31-May-1983
; Does not clear IERCD after error		AJW	21-May-1981

;
; IFF tidy-up after errors

	DATA_PSECT	IFF

TRNARG:	$TRNLNM	TABNAM=TABDSC,LOGNAM=LOGDSC
TABDSC:	.ASCID	/LNM$DCL_LOGICAL/
LOGDSC:	.ASCID	/LSL$DEBUG_TRACE/

	CODE_PSECT	IFF

; IFFTDY - establish tidy-up routes for IFF errors

	.ENTRY	IFFTDY,^M<R11>
	JSB	IF$1ST			; basic init
	CLRL	TDYRT			; assume no routine
	JSB	IF$GNA			; get first arg
	BCS	10$			; missing
	MOVAL	(R0),TDYRT		; routine to call
10$:	RET

; IF$TDY - called from IFFERR

IF$TDY::
	MOVAB	@INAST,R0		; if in AST status
	BEQL	10$
; should arrange for recall in mainline here
	RSB

10$:	MOVAB	@TDYRT,R0		; tidy-up routine supplied ?
	BEQL	15$			; no
	CALLS	#0,(R0)			; call users routine

; check if a traceback is required
15$:	$TRNLNM_G	TRNARG		; translate LSL$DEBUG_TRACE
	BLBC	R0,40$			; no translation, no traceback

	BLBC	IERNM,20$		; jump if VMS error
	PUSHL	#40			; if internal error, signal with
					; "warning abort"
	BRB	30$
20$:	PUSHL	IERNM			; error code
30$:	CALLS #1,G^LIB$SIGNAL		; die noisily or...
40$:	RSB				; ... continue quietly

	.PSECT
	.END
