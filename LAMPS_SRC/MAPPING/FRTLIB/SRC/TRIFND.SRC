C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1995-04-04 14:23:54.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	LOGICAL FUNCTION TRIFND(CHARNO,THS_FONT)
C
***	MODULE	TRIFND
***	IDENT	04AP95
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author  Paul Hardy
C
C Created					PGH	11-Oct-1983
C
C returns TRUE if it fails to find character in the TRI table,
C otherwise the character is made current in COMMON/FRTTRI/.
C see FRTTRI.CMN for details
C
	IMPLICIT NONE
C
	INTEGER*2	CHARNO		! character number
	INTEGER*2	THS_FONT	! font number (-ve for italic)
C
***	COMMON/LSL$CMNFRT:FRTTRI/
C
C functions called
	LOGICAL		TRIFND_SUB
C
	TRIFND = TRIFND_SUB(CHARNO,THS_FONT,
     &		%VAL(TRITAB_LOC),%VAL(TRIFNT_LOC),%VAL(TRIFNP_LOC))
C
	RETURN
	END
C
	LOGICAL FUNCTION TRIFND_SUB(CHARNO,THS_FONT,
     &		TRITAB,TRIFNT,TRIFNP)
C
***	MODULE	TRIFND_SUB
***	IDENT	19JA88
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C
	IMPLICIT NONE
C
	INTEGER*2	CHARNO		! character number
	INTEGER*2	THS_FONT	! font number (-ve for italic)
	INTEGER*2	TRITAB(*)	! characters and SCT numbers 
	INTEGER*2	TRIFNT(*)	! font numbers
	INTEGER*4	TRIFNP(*)	! font pointers
C
***	COMMON/HERE:FRTTRI_PRIVATE/
C
C w/s
	INTEGER*4	I
	INTEGER*2	FONT
	INTEGER*4	SYMNO,FOUNDC	! symbol index numbers
	INTEGER*4	HILIM,LOLIM	! binary chop pointers
	INTEGER*4	BINPTR,GUESS	! ditto
	INTEGER*4	TRIFNI		! index into font table
C
C init things
C
	TRIFND_SUB = .TRUE.	! assume will fail
	GUESS = TRIIND		! current guess is same as now
	FONT = ABS(THS_FONT)	! ignore italic texts
C
C check valid font
C
	DO 5 I=1,TRIFNC
	   TRIFNI = I
	   IF (TRIFNT(I).EQ.FONT) GOTO 7
5	CONTINUE
	CALL FRTWRT('Font %^W%N is not defined.',FONT)
	GOTO 999
C
C calculate symbol number from font and char number
C char is 0 to 127 for ASCII, 128 to 255 for escape chars
C font is 1 to TRIMXF
C
7	SYMNO = CHARNO + FONT*256	! in font groups of 256
C
C nothing to do if currently selected etc
C
	IF (CHARNO.EQ.0) GOTO 100	! null char requires no work
	IF (CHARNO.EQ.32) GOTO 100	! neither does a space
	IF (SYMNO.EQ.TRISEL) GOTO 100	! same as previous
C
C as the TRI table is sorted, we can use binary chop
C
	IF (TRITOP.LE.0) GOTO 70
	LOLIM = TRIFNP(TRIFNI)+1	! start of this font
	IF (TRIFNI.EQ.TRIFNC) THEN
	   HILIM = TRITOP		! the last font
	ELSE
	   HILIM = TRIFNP(TRIFNI+1)	! the start of the next font
	ENDIF
C
C start chopping
C
10	   GUESS = (LOLIM+HILIM)/2		! next guess
	   BINPTR = GUESS			! working pointer
20	   IF ( TRITAB(BINPTR).LT.0.AND.
     &		TRITAB(BINPTR).NE.-32768) GOTO 30 ! are we at a start ?
	   BINPTR = BINPTR + 1			! no - keep looking
	   IF (BINPTR.LE.HILIM) GOTO 20		! unless at end
	   IF (GUESS.EQ.HILIM) GOTO 70		! no more chopping space
	   HILIM = GUESS			! new end
	   GOTO 10				! try again
C
C here having found start of something
30	   FOUNDC = -TRITAB(BINPTR)		! what have we found ?
	   IF (FOUNDC.EQ.SYMNO) GOTO 80		! found right one
	   IF (LOLIM.GE.HILIM) GOTO 70		! no more chopping space
	   IF (SYMNO.GT.FOUNDC) THEN		! which side of chop ?
	      IF (LOLIM.EQ.BINPTR) BINPTR = BINPTR + 1
	      LOLIM = BINPTR			! too low
	   ELSE
	      HILIM = GUESS			! too high
	   ENDIF
	   GOTO 10
C
C failed
C set I to be the character, adding $ for escaped characters
70	I = CHARNO.AND.127
	IF (I.LT.32) I = 256*(I+64)+ICHAR('^')
	IF (CHARNO.GE.128) THEN
	   IF (CHARNO.LE.159.OR.TRIESC) I = 256*I+ICHAR('$')
	ENDIF
	CALL FRTWRT(
     &	   'Character %^W%N ("%^L%C") not found in font %^W%N of TRI'
     &               ,CHARNO,I,FONT)
	GOTO 999		! failed - FC not in table
C
C found it - get back to start of it
C
80	GUESS = BINPTR		! first guess is here
	DO 90 I=BINPTR-1,1,-1
	   IF (TRITAB(I).GE.0.OR.TRITAB(I).EQ.-32768) GOTO 90
	   IF (TRITAB(I).NE.-SYMNO) GOTO 100
	   GUESS = I		! found another bit of it
90	CONTINUE
C
C all set up
C
100	TRIIND = GUESS
	TRISEL = SYMNO
	TRIFND_SUB = .FALSE.		! success
C
C that's all
C
999	RETURN
	END
