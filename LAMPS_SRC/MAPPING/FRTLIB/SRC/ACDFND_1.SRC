	LOGICAL FUNCTION ACDFND_1(AC,ERR,ACD)
C
***	MODULE	ACDFND_1
***	IDENT	25AU88
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author  Ron Russell
C
C Created					RWR	12-Oct-1987
C
C returns TRUE if it fails to find Attribute Code AC in the ACD,
C otherwise the attributes of AC are set up in COMMON/FRTACD/.
C see FRTACD.CMN for details
C
	IMPLICIT NONE
C
***	COMMON/LSL$CMNFRT:FRTACD_PRIVATE/
C
C arguments
	INTEGER*2	AC		! Attribute Code
	LOGICAL		ERR		! output error message?
	RECORD/ACD/	ACD(*)		! main ACD structure
C
C functions called
	LOGICAL		IS_REAL_AC
	INTEGER		EXPAND
C
C w/s
	INTEGER    I
	INTEGER	   LOLIM,HILIM		! bounds for binary chop
	INTEGER    BINPTR		! binary chop pointer
	INTEGER	   INDEX
	INTEGER	   LEN			! number of digits to output
	REAL	   VAL
C
C init things
C
	ACDFND_1 = .TRUE.		! assume will fail
C
C as the ACD table is sorted, we can use binary chop
C
	IF (ACDCNT.LE.0) GOTO 19
	LOLIM = 1
	HILIM = ACDCNT
10	   BINPTR = (LOLIM+HILIM)/2		! next guess
	   IF (ACD(BINPTR).CODE.EQ.AC) THEN
	      INDEX = BINPTR			! found it
	      GOTO 20
	   ENDIF
	   IF (LOLIM.GE.HILIM) GOTO 19		! no chopping room
	   IF (AC.GT.ACD(BINPTR).CODE) THEN	! which side ?
	      IF (LOLIM.EQ.BINPTR) BINPTR = BINPTR+1
	      LOLIM = BINPTR			! too low
	   ELSE
	      HILIM = BINPTR			! too high
	   ENDIF
	   GOTO 10
C
19	IF (ERR) CALL FRTWRT('Attribute Code %^W%N not found in ACD',AC)
C
C set up defaults for an integer or real type (for user convenience)
	ACD_CODE         = -1			! an invalid value
	IF (IS_REAL_AC(AC)) THEN
	   ACD_DATA_TYPE    = ACD_DATATYPE_R	! real type
	   ACD_MIN_MAX_R(1) = ACD_DEF_MINR	! with defaults
	   ACD_MIN_MAX_R(2) = ACD_DEF_MAXR
	   VAL = MAX(ABS(ACD_DEF_MINR),ABS(ACD_DEF_MAXR))
	   IF (VAL.LE.0) THEN			! use default
	      LEN = 8
	   ELSE
	      LEN = INT(LOG10(VAL)) + 1
	   ENDIF
	   LEN = MIN(LEN,8)
	   ACD_FORMAT_LEN   = EXPAND('%WS%%G0.%N',ACD_FORMAT,LEN)
	ELSE
	   ACD_DATA_TYPE    = ACD_DATATYPE_I	! integer type
	   ACD_MIN_MAX_I(1) = ACD_DEF_MINI	! with defaults
	   ACD_MIN_MAX_I(2) = ACD_DEF_MAXI
	   VAL = MAX(ABS(ACD_DEF_MINI),ABS(ACD_DEF_MAXI))
	   IF (VAL.EQ.0) THEN			! use default
	      LEN = 1
	   ELSE
	      LEN = INT(LOG10(VAL)) + 1
	   ENDIF
C
C add 1 if possibility of -ve number
	   IF (ACD_DEF_MINI.LT.0.OR.ACD_DEF_MAXI.LT.0) LEN=LEN+1
	   ACD_FORMAT_LEN   = EXPAND('%WS%%I%N',ACD_FORMAT,LEN)
	ENDIF
	ACD_NAME         = '?'
	ACD_NAME_LEN     = 1
	ACD_INTERVAL     = 0.0
	GOTO 999		! failed - AC not in table
C
C found the AC in table - set things up
C
20	CALL FRT_LOADACD(ACD(INDEX))
C
	ACDFND_1 = .FALSE.		! success
C
C that's all
C
999	RETURN
	END
