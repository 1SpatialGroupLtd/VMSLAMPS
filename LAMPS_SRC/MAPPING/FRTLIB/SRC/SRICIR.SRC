	LOGICAL FUNCTION SRICIR(XCOORD,YCOORD,GT)
C
***	MODULE	SRICIR
***	IDENT	27OC94
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author  Paul Hardy
C
C remove line width control			PGH	29-Nov-1983
C Created 					PGH	22-Sep-1983
C
C returns FALSE if it succeeds in plotting a circle or circle arc
C of graphical type GT, using positions in (XCOORD,YCOORD)
C 
C Which points we have to work from (in X/YCOORD) depends on GT
C
C   2 - Clockwise circle arc	  > Start pt, Centre pt, End pt.
C   3 - Anticlockwise circle arc  >    "    ,    "     ,   "   .
C   4 - Circumcircle arc	  >    "    , Any edge pt, "   .
C   5 - Full circumcircle	  > Any 3 edge pts.
C
	IMPLICIT NONE
C
***	COMMON/LSL$CMNFRT:FRTCOM/	! for GT definitions
C
C args
	INTEGER*2	GT		! graphical type
	REAL		XCOORD(3)	! coordinate buffer
	REAL		YCOORD(3)	! ditto
C
C w/s
	REAL TWOPI		! constant.
	REAL XCENTRE,YCENTRE	! coords of circle/arc centre.
	REAL DXS,DYS,DXE,DYE	! differences between pts on edge.
	REAL TWODEL		! temporary workspace.
	REAL RSSQD,RESQD	!	"	"
	REAL DSDE		!	"	"
	REAL XSDIF,YSDIF,XEDIF,YEDIF  ! differences (edge pts - centre).
	REAL RADIUS		! circle/arc radius. 
	REAL STAANG		! angle at centre of 1st point.
	REAL ENDANG		! angle at centre of end point.
	REAL TOTANG		! total angle of circle/arc.
	LOGICAL CLOCKW	! direction of circle/arc.
C
	DATA TWOPI/6.283185307/
C
	SRICIR = .FALSE.		! assume success
C
C Do we need to work out the centre point?
	IF ((GT.EQ.CLOTYP).OR.(GT.EQ.ANTTYP)) THEN
C
C Circle/arc defined by start pt, centre pt, and end pt.
	   XCENTRE=XCOORD(2)
	   YCENTRE=YCOORD(2)
C Set the direction.
	   CLOCKW=(GT.EQ.CLOTYP)
	ELSE
C
C Evaluate the centre of the circle/arc if we have got 3 points
C on the circumference. Uses fairly obscure method.
	   DXS = XCOORD(1)-XCOORD(2)
	   DYS = YCOORD(1)-YCOORD(2)
	   DXE = XCOORD(3)-XCOORD(2)
	   DYE = YCOORD(3)-YCOORD(2)
	   DSDE = DXS*DYE-DYS*DXE	! cross product DSxDE
	   IF (DSDE.EQ.0.0) GOTO 97	! bad circle
	   CLOCKW = (DSDE.GT.0.0)	! sense of circle
	   TWODEL = 0.5/DSDE
	   RSSQD = DXS*DXS+DYS*DYS
	   RESQD = DXE*DXE+DYE*DYE
C Now evaluate the centre point.
	   XCENTRE=(DYE*RSSQD-DYS*RESQD)*TWODEL+XCOORD(2)
	   YCENTRE=(DXS*RESQD-DXE*RSSQD)*TWODEL+YCOORD(2)
	ENDIF
C
C Now work out the radius. The correct radius is set by the 1st
C point. The 3rd point merely provides an angle rel to the centre.
	XSDIF=XCOORD(1)-XCENTRE
	YSDIF=YCOORD(1)-YCENTRE
	RADIUS=SQRT(XSDIF**2+YSDIF**2)
	IF (RADIUS.EQ.0.0) GOTO 97	! bad circle
C
C Work out the start and end angles at the centre
	IF (GT.EQ.FULTYP) THEN		! full circle so...
	   STAANG=0.0
	   TOTANG=TWOPI
	ELSE				! arc
	   STAANG=ATAN2(YSDIF,XSDIF)
	   IF (STAANG.LT.0.0) STAANG=STAANG+TWOPI
	   XEDIF=XCOORD(3)-XCENTRE
	   YEDIF=YCOORD(3)-YCENTRE	
	   IF (XEDIF.EQ.0.0.AND.YEDIF.EQ.0.0) GOTO 97	! bad circle
	   ENDANG=ATAN2(YEDIF,XEDIF)
	   IF (ENDANG.LT.0.0) ENDANG=ENDANG+TWOPI
	   TOTANG=ENDANG-STAANG
C
C Now make sure the angles are right, positive for anti-clockwise
	   IF (TOTANG.GE.0.0) THEN
	      IF (CLOCKW) TOTANG=TOTANG-TWOPI
	   ELSE
	      IF (.NOT.CLOCKW) TOTANG=TOTANG+TWOPI
	   ENDIF
C
C If arc is so short that a full circle was probably meant,
C then set the angle explicitly
	   IF ((GT.NE.CIRTYP).AND.(ABS(TOTANG).LT.1.0E-5)) TOTANG=TWOPI
C
	ENDIF
C
C Now draw the circle/arc
	CALL SRISTC(XCENTRE,YCENTRE,RADIUS,STAANG,TOTANG)
	GOTO 100
C
C Errors
97	CALL FRTWRT('Bad circle arc - points collinear or coincident')
	SRICIR = .TRUE.			! failed
C
100	RETURN
	END
