	SUBROUTINE CLIPGFA(N,X,Y,Z,NPOL,POLXY)
C
***	MODULE	CLIPGFA
***	IDENT	26OC94
C
C	Copyright (C) 1987, Laser-Scan Laboratories Ltd.,
C	Cambridge, England
C
C	Created					RWR	 4-Oct-1988
C
C Subroutine to clip a poly-line to a polygon. The results are output
C by calls to CLIPAREA
C
C the polygon (POLXY) must be closed - ie first point = last point
C
	IMPLICIT NONE
C
C Arguments:
	INTEGER	N		! number of points
	REAL	X(N)		! x coords
	REAL	Y(N)		! y coords
	REAL	Z(N)		! z coords
	INTEGER	NPOL		! number of points in polygon
	REAL	POLXY(2,NPOL)	! points is polygon; point 1 = point NPOL
C
***	COMMON/LSL$CMNFRT:FFABUF/
***	COMMON/LSL$CMNFRT:SRIBFC/
***	COMMON/LSL$CMNFRT:SRIVEC/
C
C Local workspace:
	INTEGER		WSPACE
	INTEGER		I
	REAL		AREA
	LOGICAL		SAME
	REAL		MINX,MAXX,MINY,MAXY
C
C use FFABUF as workspace to pass to CLIPAREA. POLXY is already in PTS,
C CLIP so make PTS WXY, as CLIPAREA just copies PTS into WXY;
C the bottom half is used for PTRS
C
C ......................................................................
C
	WSPACE = FFAMAX
C
	IF (N.GE.3) THEN
C
C calculate areas of two polygons, to see if they go the same way
	   IF (FILLCLIP_AREA.EQ.0.0) THEN	! (may already have
C						!  been done)
	      DO 10, I=1,NPOL-1
	            FILLCLIP_AREA = FILLCLIP_AREA +
     &	                            POLXY(1,I)*POLXY(2,I+1) - 
     &	                            POLXY(1,I+1)*POLXY(2,I)
10	      CONTINUE
	      FILLCLIP_AREA = FILLCLIP_AREA*0.5
	   ENDIF
C
C
C get area and limits of this fill area
	   MINX = X(N)
	   MINY = Y(N)
	   MAXX = MINX
	   MAXY = MINY
	   AREA = 0.0
	   DO 20, I = 1,N-1
	      MINX = MIN(MINX,X(I))
	      MAXX = MAX(MAXX,X(I))
	      MINY = MIN(MINY,Y(I))
	      MAXY = MAX(MAXY,Y(I))
	      AREA = AREA + X(I)*Y(I+1) - X(I+1)*Y(I)
20	   CONTINUE
	   IF (X(1).NE.X(N).OR.Y(1).NE.Y(N)) THEN
	      AREA = AREA + X(N)*Y(1) - X(1)*Y(N)
	   ENDIF
	   AREA = AREA*0.5
C
C areas are digitised same way if their areas have the same sign
	   SAME = AREA*FILLCLIP_AREA .GT. 0.0
C
C don't bother trying to clip if the areas don't impinge
	   IF (MAX(MINX,MAXX).GE.FILLCLIP_XMIN.AND.
     &	       MIN(MINX,MAXX).LE.FILLCLIP_XMAX.AND.
     &	       MAX(MINY,MAXY).GE.FILLCLIP_YMIN.AND.
     &	       MIN(MINX,MAXY).LE.FILLCLIP_YMAX) THEN
C
C supply parts of FFABUF as working space
	      CALL CLIPAREA(N,X,Y,Z,NPOL,POLXY,SAME,WSPACE,
     &			%VAL(PTS_ADDR),%VAL(SIDPTR_ADDR),
     &			%VAL(SIDES_ADDR),2*FFASCT,%VAL(INTSCT_ADDR),
     &			SRIBFM,SRIBFX,SRIBFY,SRIBFZ,
     &			%VAL(SRIVEC(XSRIGFA)))
	   ENDIF
	ENDIF
C
	RETURN
	END
