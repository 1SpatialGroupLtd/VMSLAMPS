	LOGICAL FUNCTION SRISCN(SYMNO,XMIN,XMAX,YMIN,YMAX)
C
***	MODULE	SRISCN
***	IDENT	12FE91
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C Author  Paul Hardy
C
C Created					PGH	28-Feb-1984
C
C returns FALSE if it succeeds in scanning symbol SYMNO from the SRI
C table, returning symbol extent in XMIN,XMAX,YMIN,YMAX
C N.B. no special attention is paid to circle arcs - beware
C 
C
	IMPLICIT NONE
C
C args
C
	INTEGER*2	SYMNO		! Symbol number
	REAL		XMIN,XMAX	! extent in X
	REAL		YMIN,YMAX	! extent in Y
C
***	COMMON/LSL$CMNFRT:FRTSRI/
C
C functions called
C
	LOGICAL		SRISCN_SUB
C
	SRISCN = SRISCN_SUB(SYMNO,XMIN,XMAX,YMIN,YMAX,
     &				%VAL(SRIBUF_LOC),%VAL(SRITAB_LOC))
C
	RETURN
	END
C
	LOGICAL FUNCTION SRISCN_SUB(SYMNO,XMIN,XMAX,YMIN,YMAX,SRIBUF,SRITAB)
C
***	MODULE	SRISCN_SUB
***	IDENT	30MY84
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England.
C
	IMPLICIT NONE
C
C args
C
	INTEGER*2	SYMNO		! Symbol number
	REAL		XMIN,XMAX	! extent in X
	REAL		YMIN,YMAX	! extent in Y
	REAL		SRIBUF(2,*)	! coord array
	INTEGER*2	SRITAB(*)	! SCTs etc.
C
***	COMMON/LSL$CMNFRT:FRTSRI/
C
C functions called
C
	LOGICAL		SRIFND		! find symbol
C
C w/s
	INTEGER*4	I
C
C init things
C
	SRISCN_SUB = .TRUE.		! assume will fail
C
C check symbol currently selected
C
	IF (SRIFND(SYMNO)) GOTO 999
C
C initialise the maxima and minima
C
	XMIN = 1E38
	YMIN = XMIN
	XMAX = -XMIN
	YMAX = -XMIN
C
C scan all coords until start of a new symbol, or end of table
C
	DO 10 I=SRIIND,SRITOP
	   IF ( SRITAB(I).LT.0.AND.
     &		SRITAB(I).NE.-SYMNO.AND.
     &		SRITAB(I).NE.-32768 ) GOTO 20
	   IF (SRIBUF(1,I).GT.XMAX) XMAX = SRIBUF(1,I)
	   IF (SRIBUF(1,I).LT.XMIN) XMIN = SRIBUF(1,I)
	   IF (SRIBUF(2,I).GT.YMAX) YMAX = SRIBUF(2,I)
	   IF (SRIBUF(2,I).LT.YMIN) YMIN = SRIBUF(2,I)
10	CONTINUE
C
20	SRISCN_SUB = .FALSE.		! success
C
C that's all
C
999	RETURN
	END
