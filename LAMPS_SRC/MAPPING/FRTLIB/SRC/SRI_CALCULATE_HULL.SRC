	SUBROUTINE SRI_CALCULATE_HULL( MAXPOINTS, POINTS, NPOINTS,
     &                                 MAXHULLXY, HULLXY, NHULLXY )
C
***	IDENT	10NO94
***	MODULE	SRI_CALCULATE_HULL
C
C Copyright Laser-Scan Laboratories Ltd., Cambridge, England
C
C Author: Tim Mulcahy, 10-Nov-1994
C
C Find the convex hull of the point set given in the array POINTS(2,MAXPOINTS)
C
C Algorithm: see Data Structures and Algorithms 3,
C            Multi-dimensional Searching and Computational Geometry
C            Kurt Mehlhorn, Springer Verlag 1984, section VIII. 2., p93
C
C Error code: return NHULLXY = -1
C
	IMPLICIT NONE
C
	INTEGER MAXPOINTS		! Max. size of point set work array
	REAL POINTS(2,MAXPOINTS)	! Point set work array
	INTEGER NPOINTS		        ! Accumulated size of point set
C
	INTEGER MAXHULLXY		! Max. size of convex hull result array
	REAL HULLXY(2,MAXHULLXY)	! Convex hull result array
	INTEGER NHULLXY			! Actual size of convex hull array
C
C Declare convex hull `compare' and `swap' functions for SHELL_SORT
C
	EXTERNAL SRI_HULL_CF, SRI_HULL_SWAP
C
	INTEGER I, N1, N2
C
C Set error condition as default return code
C
	NHULLXY = -1
C
C Check for empty source point set
C
	IF ( NPOINTS.LE.0 ) THEN
	   NHULLXY = 0
	   GOTO 999
	ENDIF
C
C If no. of source points is 2 or less, then nothing much to do!
C
	IF ( NPOINTS.LE.2 ) THEN
	   IF ( MAXHULLXY.LT.NPOINTS ) GOTO 999
	   DO 10 I = 1, NPOINTS
	      HULLXY(1,I) = POINTS(1,I)
	      HULLXY(2,I) = POINTS(2,I)
 10	   CONTINUE
	   NHULLXY = NPOINTS
	   GOTO 999
	ENDIF
C
C Use SHELL_SORT (LSLLIB Ref. 1.13) to perform lexicographical sort in X to
C create a `simple' polygon for the convex hull algorithm.
C
        CALL SHELL_SORT(POINTS, NPOINTS, SRI_HULL_CF, SRI_HULL_SWAP)
C
C Find the UPPER convex hull
C
	CALL SRI_UPPER_HULL(NPOINTS, POINTS, MAXHULLXY, HULLXY, NHULLXY)
	IF ( NHULLXY.LT.0 ) GOTO 999
	N1 = NHULLXY
C
C Find the LOWER convex hull
C
	CALL SRI_LOWER_HULL(NPOINTS, POINTS, MAXHULLXY, HULLXY, NHULLXY)
	IF ( NHULLXY.LT.0 ) GOTO 999
	N2 = NHULLXY
C
C Finally, merge the upper and lower convex hulls, i.e. eliminate the upper
C and lower dividing edge of the hull.
C
	NHULLXY = 0
	DO 20 I = 2, N1 - 1
	   NHULLXY = NHULLXY + 1
	   HULLXY(1,NHULLXY) = HULLXY(1,I)
	   HULLXY(2,NHULLXY) = HULLXY(2,I)
 20	CONTINUE
C
	DO 30 I = N1 + 2, N2 - 1
	   NHULLXY = NHULLXY + 1
	   HULLXY(1,NHULLXY) = HULLXY(1,I)
	   HULLXY(2,NHULLXY) = HULLXY(2,I)
 30	CONTINUE
C
 999	RETURN
	END

