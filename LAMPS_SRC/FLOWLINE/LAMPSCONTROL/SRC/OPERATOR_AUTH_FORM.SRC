C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1988-12-22 11:00:44.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	INTEGER FUNCTION OPERATOR_AUTH_FORM()
C
***	MODULE OPERATOR_AUTH_FORM
***	IDENT 22DE88
C
C Copyright Laser-Scan Laboratories Ltd, Cambridge, England
C Author: J.M. Billing			11 May 1988
C
C Routine to allow entry or modification of operator authorisation
C and current project, job, task details.
C
C
	IMPLICIT NONE
C
C Common blocks and parameter files
C
	INCLUDE 'LSL$CMNFLOWLINE:FLOWLINEMSG.PAR'
	INCLUDE 'LSL$CMNFLOWLINE:FLOW.STR'
	INCLUDE 'LSL$CMNFLOWLINE:FLOW.PAR'
	INCLUDE 'LSL$LAMPSCONTROL:LAMPSCONTROLMSG.PAR'
	INCLUDE 'LSL$LAMPSCONTROL:CURRENT_ITEMS.CMN'
	INCLUDE 'LSL$LAMPSCONTROL:NEXT_FUNCTION.CMN'
	INCLUDE 'LSL$LAMPSCONTROL:INTERACTIVE_IO.CMN'
C
C w/s
	INTEGER		TERM
	CHARACTER*20	FVAL
	CHARACTER*20	FID
	INTEGER		STATUS
	LOGICAL		EXISTS			! .T. operator exists
	INTEGER		ERROR_INDEX		! auth rec in error
	INTEGER		I			! temp index
	INTEGER		J			! temp index
C
	INTEGER		CURRENT_REC		! current auth entry 
	INTEGER		NUM_REC			! num auth entries
	RECORD /FLN_AUTH/ AUTH_REC(FLN_MAX_AUTH)! auth entries
C
	CHARACTER*20	AUTH_OPER		! operator being auth
	CHARACTER*20	CURR_PROJ		! current project
	CHARACTER*20	CURR_PROJ_STATUS	! project status
	CHARACTER*20	CURR_JOB		! current job
	CHARACTER*20	CURR_JOB_ID		! current job id
	CHARACTER*20	CURR_JOB_STATUS		! current job status
	CHARACTER*20	CURR_ELEM		! current element
C
	CHARACTER*100	REPLY			! reply to prompt
	INTEGER		REPLY_TERM		! prompt terminator
C
	LOGICAL		HAD_OPER_ID		! .T. if operator ident
						! has already been 
						! entered on form
C
C function
	INTEGER		FORM_GETLINE
	INTEGER		GET_AUTH_AREA
	INTEGER		DISPLAY_AUTH_AREA
	INTEGER		FLN_RETRIEVE_OPERATOR_AUTH
	INTEGER		FLN_AUTHORISE_OPERATOR
	INTEGER		FLN_SET_CURRENT_PROJECT
	INTEGER		FLN_CHECK_OPERATOR
	INTEGER		FLN_CREATE_OPERATOR
	INTEGER		FLN_DELETE_OPERATOR
	INTEGER		FLN_RETRIEVE_CURRENT
	INTEGER		FLN_SET_CURRENT_PJE
C
C Assume success
C
	OPERATOR_AUTH_FORM = LSC__NORMAL
C
C Clear the screen and display the form
C
	CALL FDV$CLRSH('OP_AUTH_FORM')
C
C Set flag to indicate screen does not have a reusable menu on it
C
	PREVIOUS_MENU_USABLE = .FALSE.
C
C Clear flag to indicate operator identifier has not yet been entered
C
	HAD_OPER_ID = .FALSE.
C
C Now set the first field to be entered
C
	FID  = 'OPERATOR_ID'
C
C Initialiase operator authorisation count
C
	NUM_REC = 0
C
C Initialise current authorisation entry record 
C i.e. which one is current line of scrolled area
C
	CURRENT_REC = 1
C
C Initialise current project, job and element variables to null strings
C
	CURR_PROJ = '  '
	CURR_JOB  = '  '
        CURR_ELEM = '  '
C
C Start of loop dealing with entry of each field on form
C
C
C Deal with entry to scrolled area
C
10	IF ((FID.EQ.'AUTH_TYPE').OR.(FID.EQ.'NEGATABLE').OR.
     &		(FID.EQ.'AUTH_ID')) THEN
	   OPERATOR_AUTH_FORM = GET_AUTH_AREA(
     &		AUTH_OPER,
     &		FLN_MAX_AUTH,
     &		AUTH_REC,
     &		NUM_REC,
     &		CURRENT_REC,
     &		TERM)
C
C If not scrolled area, read next normal field
C
	ELSE
	   CALL FDV$GET(FVAL,TERM,FID)
	ENDIF
C
C Clear any messages which may be on last lines of screen
C
	CALL CLEAR_MESSAGE_LINES()
C
C Check if abort form terminator used, exit if it is, indicating
C that previous menu to be redisplayed
C
	IF (TERM.EQ.ABORT_FORM) THEN
	   NEXT_FUNCTION = DO_MOVE_UP_LEVEL
	   GOTO 999
	ENDIF
C
C Now determine which field was entered
C
C Operator id
C
	IF (FID.EQ.'OPERATOR_ID') THEN
	   AUTH_OPER = FVAL
C
C Current Project id
C
	ELSEIF (FID.EQ.'PROJECT_ID') THEN
	   CURR_PROJ = FVAL
C
C Current Job name
C
	ELSEIF (FID.EQ.'JOB_NAME') THEN
	   CURR_JOB = FVAL
C
C Current element id
C
	ELSEIF (FID.EQ.'ELEMENT_ID') THEN
	   CURR_ELEM = FVAL
	ENDIF
C
C Check if we want to delete the entire operator entry - ie GOLD D
C was used
C
	IF ((TERM.EQ.GOLD_BASE+ICHAR('D')).OR.
     &	    (TERM.EQ.GOLD_BASE+ICHAR('d'))) THEN
C
C Ensure that the operator exists
C
	   OPERATOR_AUTH_FORM = 
     &		FLN_CHECK_OPERATOR(AUTH_OPER,EXISTS)
C
C Check for errors
C
	   IF (.NOT.OPERATOR_AUTH_FORM) THEN
	      CALL LCONT_PUTMSG(OPERATOR_AUTH_FORM,0)
	      GOTO 999
	   ENDIF
C
C If the operator does not exist, tell user and loop
C
	   IF (.NOT.EXISTS) THEN
	      CALL FORM_PUTMSG('Operator does not exist',-1,0)
	      GOTO 10
	   ENDIF
C
C Prompt the user to make sure
C
	   CALL FORM_GETLINE(
     &		REPLY,
     &		REPLY_TERM,
     &		0,
     &		'Delete operator entry (Y/N) ? ')
C
C If the reply starts with 'Y' or 'y'
C
	   IF ((REPLY.EQ.'Y').OR.(REPLY.EQ.'y')) THEN
C
C Then delete the operator
C
	      OPERATOR_AUTH_FORM = FLN_DELETE_OPERATOR(AUTH_OPER)
C
C Check for errors, report problem and loop to allow re-entry on form
C
	      IF (.NOT.OPERATOR_AUTH_FORM) THEN
	         CALL LCONT_PUTMSG(OPERATOR_AUTH_FORM,0)
	         GOTO 10
C
C Otherwise, we have deleted the operator, so exit from form
C
	      ELSE
	         GOTO 999
	      ENDIF
C
C Otherwise, have replied 'NO' to prompt, so loop to allow re-entry
C of same field as before
C
	   ELSE
	      GOTO 10
	   ENDIF
	ENDIF
C
C If the operator id has just been entered for the first time...
C
	IF ((.NOT.HAD_OPER_ID).AND.(FID.EQ.'OPERATOR_ID')) THEN
C
C Set flag to indicate operator identifier has been entered
C
	   HAD_OPER_ID = .TRUE.
C
C Check if any information already exists for this operator
C
	   OPERATOR_AUTH_FORM = FLN_CHECK_OPERATOR(AUTH_OPER,EXISTS)
C
C Check for errors
C
	   IF (.NOT.OPERATOR_AUTH_FORM) THEN
	      CALL LCONT_PUTMSG(OPERATOR_AUTH_FORM,0)
	      GOTO 999
	   ENDIF
C
C If the operator exists, retrieve current project etc 
C and authorisations
C
	   IF (EXISTS) THEN
C
C Retrieve authorisations for user
C
	      OPERATOR_AUTH_FORM = FLN_RETRIEVE_OPERATOR_AUTH(
     &			AUTH_OPER,
     &			AUTH_REC,
     &			NUM_REC)
C
C Check for errors
C
	      IF (.NOT.OPERATOR_AUTH_FORM) THEN
	         CALL LCONT_PUTMSG(OPERATOR_AUTH_FORM,0)
	         GOTO 999
	      ENDIF
C
C Output the authorisations to the scrolled area
C
	      OPERATOR_AUTH_FORM = 
     &			DISPLAY_AUTH_AREA(AUTH_REC,NUM_REC,CURRENT_REC)
C
C Deal with any errors
C
	      IF (.NOT.OPERATOR_AUTH_FORM) THEN
	         CALL LCONT_PUTMSG(OPERATOR_AUTH_FORM,0)
	         GOTO 999
	      ENDIF
C
C Retrieve current project, job and element
C
	      OPERATOR_AUTH_FORM = FLN_RETRIEVE_CURRENT(
     &			AUTH_OPER,
     &			CURR_PROJ,
     &			CURR_PROJ_STATUS,
     &			CURR_JOB_ID,
     &			CURR_JOB,
     &			CURR_JOB_STATUS,
     &			CURR_ELEM)
C
C Now put current job, project and element into correct fields
C Check for case where it failed to find one of the project,
C job or element
C
	      IF (OPERATOR_AUTH_FORM.NE.FLN__NOSUCHPROJ) THEN
	         CALL FDV$PUT(CURR_PROJ,'PROJECT_ID')
	         IF (OPERATOR_AUTH_FORM.NE.FLN__NOSUCHJOB) THEN
	            CALL FDV$PUT(CURR_JOB ,'JOB_NAME')
	            IF (OPERATOR_AUTH_FORM.NE.FLN__NOSUCHELMNT) THEN
	               CALL FDV$PUT(CURR_ELEM,'ELEMENT_ID')
C
C Deal with error other than non-existent project, job and element
C
	               IF (.NOT.OPERATOR_AUTH_FORM) THEN
	                  CALL LCONT_PUTMSG(OPERATOR_AUTH_FORM,0)
	                  CURR_PROJ = '  '
	                  CURR_JOB  = '  '
                          CURR_ELEM = '  '
	                  CALL FDV$PUT('   ','PROJECT_ID')
	                  CALL FDV$PUT('   ','JOB_NAME')
	                  CALL FDV$PUT('   ','ELEMENT_ID')
	               ENDIF
	            ENDIF
	         ENDIF
	      ENDIF
C
C Tell the user that the operator already exists
C
	      CALL FORM_PUTMSG('Operator already exists',-1,0)
C
C Reset the current field to be the one following the operator one,
C and loop for the next field
C
	      FID = 'PROJECT_ID'
	      GOTO 10
C
C Otherwise the operator does not already exist
C
	   ELSE
C
C Initialise the authorisations
C
	      NUM_REC = 0
	      CURRENT_REC = 1
C
C Display the authorisations
C
	      OPERATOR_AUTH_FORM = 
     &	         DISPLAY_AUTH_AREA(AUTH_REC,NUM_REC,CURRENT_REC)
C
C Deal with any errors
C
	      IF (.NOT.OPERATOR_AUTH_FORM) THEN
	         CALL LCONT_PUTMSG(OPERATOR_AUTH_FORM,0)
	         GOTO 999
	      ENDIF
C
C Initialise the project, job and element variables
C
	      CURR_PROJ = '  '
	      CURR_JOB  = '  '
	      CURR_ELEM = '  '
C
C Display the project, job and element
C
	      CALL FDV$PUT(CURR_PROJ,'PROJECT_ID')
	      CALL FDV$PUT(CURR_JOB,'JOB_NAME')
	      CALL FDV$PUT(CURR_ELEM,'ELEMENT_ID')
C
C Tell the user that operator does not yet exist
C
CCC	      CALL FORM_PUTMSG('Operator does not yet exist',-1,0)
C
C Reset the current field to be the one following the operator one,
C and loop for the next field
C
	      FID = 'PROJECT_ID'
	      GOTO 10
	   ENDIF
	ENDIF
C
C Process the terminator
C
	CALL FDV$PFT(TERM)
C
C Find the status, i.e. is everything ok
C
	CALL FDV$STAT(STATUS)
C
C Determine the next field to be entered
C
	CALL FDV$GCF(FID)
C
C If not RETURN, loop again
C
	IF (TERM.NE.RETURN_KEY) GOTO 10
C
C If status not ok (i.e. form not complete), loop again
C
	IF (STATUS.NE.1) GOTO 10
C
C About to exit, so we must output all the stuff we've gone
C to so much trouble to collect
C
C First, make sure that every entry in the authorisation
C list has operator id in it
C
500	DO 520 I = 1,NUM_REC
	   DO 510 J=1,MIN(LEN(AUTH_REC(1).OP_WORK),LEN(AUTH_OPER))
	      AUTH_REC(I).OP_WORK(J:J) = AUTH_OPER(J:J)
510	   CONTINUE
520	CONTINUE
C
C If the operator does not exist, create him/her
C
	IF (.NOT.EXISTS) THEN
	   OPERATOR_AUTH_FORM = FLN_CREATE_OPERATOR(AUTH_OPER)
C
C Check for errors
C
	   IF (.NOT.OPERATOR_AUTH_FORM) THEN
	      CALL LCONT_PUTMSG(OPERATOR_AUTH_FORM,0)
	      GOTO 999
	   ENDIF
C
C Set flag to show it now exists
C
	   EXISTS = .TRUE.
	ENDIF
C
C Now set current project, job and element names
C
	OPERATOR_AUTH_FORM =
     &	     FLN_SET_CURRENT_PJE(AUTH_OPER,CURR_PROJ,CURR_JOB,CURR_ELEM)
C
C Test for errors and move to required field
C
	IF (OPERATOR_AUTH_FORM.EQ.FLN__NOSUCHPROJ) THEN
	   FID = 'PROJECT_ID'
	ELSEIF (OPERATOR_AUTH_FORM.EQ.FLN__NOSUCHJOB) THEN
	   FID = 'JOB_NAME'
	ELSEIF (OPERATOR_AUTH_FORM.EQ.FLN__NOSUCHELMNT) THEN
	   FID = 'ELEMENT_ID'
	ELSE
	   FID = 'OPERATOR_ID'
	ENDIF
C
	IF (.NOT.OPERATOR_AUTH_FORM) THEN
	   CALL LCONT_PUTMSG(OPERATOR_AUTH_FORM,0)
	   CALL FORM_PUTMSG('Check project, job and element specified'
     &				,-1,0)
	   GOTO 10
	ENDIF
C
C Now can write out the operator authorisations
C
	OPERATOR_AUTH_FORM = 
     &		FLN_AUTHORISE_OPERATOR(NUM_REC,AUTH_REC,ERROR_INDEX)
C
C Act on any errors in authorisations
C
	IF (.NOT.OPERATOR_AUTH_FORM) THEN
C
C Redisplay authorisation areas
C
	   IF ((ERROR_INDEX.GT.0).AND.(ERROR_INDEX.LE.NUM_REC)) THEN
C
C Set current line of area to be the problem line
C
	      CURRENT_REC = ERROR_INDEX
C
C Display the auth area
C
	      CALL DISPLAY_AUTH_AREA(AUTH_REC,NUM_REC,CURRENT_REC)
C
C Set the current field to point at the scrolled area
C
	      FID = 'AUTH_TYPE'
C
C Now display the error message and loop to do the entry again
C
	      CALL LCONT_PUTMSG(OPERATOR_AUTH_FORM,0)
	      CALL FORM_PUTMSG('Re-enter this authorisation',-1,0)
	      GOTO 10
C
C Otherwise, error line was 0, so problem not in authorisations
C
	   ELSE
	      CALL LCONT_PUTMSG(OPERATOR_AUTH_FORM,0)
	      GOTO 999
	   ENDIF
	ENDIF
C
C Write out current project, job and element id
C
	OPERATOR_AUTH_FORM = 
     &		FLN_SET_CURRENT_PROJECT(AUTH_OPER,CURR_PROJ)
C
C Check for errors
C
	IF (.NOT.OPERATOR_AUTH_FORM) THEN
	   CALL LCONT_PUTMSG(OPERATOR_AUTH_FORM,0)
	   GOTO 999
	ENDIF
C
C Indicate nothing is wrong
C
999	NEXT_FUNCTION = DO_MOVE_UP_LEVEL
	OPERATOR_AUTH_FORM = LSC__NORMAL
C
C Exit
C
	RETURN
	END
