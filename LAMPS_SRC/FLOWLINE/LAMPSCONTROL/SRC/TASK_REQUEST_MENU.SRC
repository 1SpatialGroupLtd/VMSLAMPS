C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1992-04-15 17:22:06.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	INTEGER FUNCTION TASK_REQUEST_MENU()
C
***	MODULE TASK_REQUEST_MENU
***	IDENT 15AP92
C
C Copyright Laser-Scan Laboratories Ltd, Cambridge, England
C Author: J.M. Billing			13 April 1988
C
C Routine to put up menu of task options, and allow user to select one
C
C
	IMPLICIT NONE
C
C Common blocks and parameter files
C
	INCLUDE 'LSL$CMNFLOWLINE:FLOW.PAR'
	INCLUDE 'LSL$LAMPSCONTROL:LAMPSCONTROLMSG.PAR'
	INCLUDE 'LSL$LAMPSCONTROL:NEXT_FUNCTION.CMN'
	INCLUDE 'LSL$LAMPSCONTROL:CURRENT_ITEMS.CMN'
	INCLUDE 'LSL$LAMPSCONTROL:INTERACTIVE_IO.CMN'
	INCLUDE 'LSL$LAMPSCONTROL:LARGE_ARRAYS.CMN'
C
C w/s
	INTEGER		TERM		! field terminator found
	LOGICAL		REDO		! .T. if to redisplay menu
	INTEGER		OPT_NUM		! number of options
	INTEGER		CURRENT_OPT	! current option
	INTEGER		INV_NUM		! number of invariants
	INTEGER		CURRENT_INV	! current invariant
	CHARACTER*60	DESCRIPTION	! menu description line
C
C
C The following arrays have been transfered to 'LARGE_ARRAYS.CMN', which allows
C the program's storage requirements to be reduced by sharing the larger arrays
C among a number of routines.
C Since these arrays can be used by other routines, care must be taken that 
C their values are not overwritten by any of the routines called by this
C routine.
C
CCC	INTEGER		A_TASK_ID(FLN_MAX_TASK)	! task idents
CCC	CHARACTER*20	A_ELEM_ID(FLN_MAX_TASK)	! element idents
C
C functions
	INTEGER		DISPLAY_MENU
	INTEGER		FLN_RETRIEVE_TASKS
	INTEGER		GET_MENU_OPTION
C
C Assume success
C
	TASK_REQUEST_MENU = LSC__NORMAL
C
C First, set up the menu description line
C
	DESCRIPTION = ' '
C
	CALL WRITEF('%WProject %S20  Job %S20',
     &		%REF(DESCRIPTION),PROJECT_ID,JOB_NAME)
C
C Now cause the menu form to be displayed
C
	TASK_REQUEST_MENU = DISPLAY_MENU(
     &		'                   Task Request Menu',
     &		DESCRIPTION,
     &		'Authorised options are :')
C
C Check for errors
C
	IF (.NOT.TASK_REQUEST_MENU) GOTO 900
C
C Now set up the invariant options list (go top and move up to jobs)
C
	INV_NUM = 3
	INV_LIST(1)  = 'Move up to job request menu'
	INV_LIST(2)  = 'Move up to project request menu'
	INV_LIST(3)  = 'Jump to top level menu'
C
C Now attempt to read the current task list
C
	TASK_REQUEST_MENU = FLN_RETRIEVE_TASKS(
     &			OPERATOR_ID,		! id of operator
     &			WORKSTATION_ID,		! current workstation
     &			JOB_ID,			! current job
     &			A_TASK_ID,		! list of task ids
     &			A_ELEM_ID,		! list of returned 
     &						! element names
     &			OPT_NUM)
C
C Check for errors
C
	IF (.NOT.TASK_REQUEST_MENU) THEN
	   CALL LCONT_PUTMSG(LSC__UNRETTASK,0)
	   CALL LCONT_ADDMSG(TASK_REQUEST_MENU,0)
	   GOTO 900
	ENDIF
C
C Set display flag to indicate doing for first time
C
	REDO = .TRUE.
C
C Set current option to be first item on menu
C
	CURRENT_OPT = 1
C
C Get required option
C
20	TASK_REQUEST_MENU = GET_MENU_OPTION(
     &		A_ELEM_ID,		! option list
     &		OPT_NUM,		! number of options
     &		INV_LIST,		! invariant list
     &		INV_NUM,		! number of invariants
     &		CURRENT_OPT,		! current option
     &		CURRENT_INV,	! current invariant option
     &		OPT_AREA_LEN,		! number of lines in scroll area
     &		OPT_FIELD,		! field name
     &		TERM,			! field terminator found
     &		REDO)			! .T. if to redisplay menu
C
C Check for errors
C
	IF (.NOT.TASK_REQUEST_MENU) GOTO 900
C
C Set display flag to indicate don't need to redisplay
C
	REDO = .FALSE.
C
C Check if have required field terminator
C
	IF (TERM.NE.RETURN_KEY) GOTO 20
C
C Check for errors
C
	IF (.NOT.TASK_REQUEST_MENU) GOTO 900
C
C Translate selected option number into some sort of task_id which
C the rest of the program will apreciate
C
C
C First check if an invariant option was selected
C
C For move up to job menu
C
	IF ((CURRENT_OPT.LE.0).AND.(CURRENT_INV.EQ.1)) THEN
	   NEXT_FUNCTION = DO_MOVE_UP_LEVEL
C
C For move up to project menu
C
	ELSEIF ((CURRENT_OPT.LE.0).AND.(CURRENT_INV.EQ.2)) THEN
	   NEXT_FUNCTION = DO_PROJECT
C
C For go top
C
	ELSEIF ((CURRENT_OPT.LE.0).AND.(CURRENT_INV.EQ.3)) THEN
	   NEXT_FUNCTION = DO_GO_TOP
C
C Now deal with task options, setting next task id and element name
C for rest of program to play with
C
	ELSE
	   TASK_ID = A_TASK_ID(CURRENT_OPT)	! task id number
	   ELEMENT_ID = A_ELEM_ID(CURRENT_OPT)	! element name
	   NEXT_FUNCTION = DO_TASK
	ENDIF
C
C Exit
C
900	RETURN
	END
