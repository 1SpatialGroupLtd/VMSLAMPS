	INTEGER FUNCTION OPERATOR_STATISTICS_MENU()
C
***	MODULE OPERATOR_STATISTICS_MENU
***	IDENT 22DE88
C
C Copyright Laser-Scan Laboratories Ltd, Cambridge, England
C Author: J.M. Billing			13 September 1988
C
C Routine to put up menu of operators, and allow user to request 
C output of statistics on all operators, or to select one operator
C to examine its statistics broken down by workstation or flowlines
C and elements.
C
C
	IMPLICIT NONE
C
	INCLUDE 'LSL$CMNFLOWLINE:FLOW.PAR'
	INCLUDE 'LSL$LAMPSCONTROL:LAMPSCONTROLMSG.PAR'
	INCLUDE 'LSL$LAMPSCONTROL:NEXT_FUNCTION.CMN'
	INCLUDE 'LSL$LAMPSCONTROL:INTERACTIVE_IO.CMN'
	INCLUDE 'LSL$LAMPSCONTROL:LARGE_ARRAYS.CMN'
C
C w/s
	INTEGER		OPER_NUM	! number of operators
	CHARACTER*80	INVARIANTS(10)	! menu invariant options
	INTEGER		INV_NUM		! number of invariants
C
	INTEGER		TERM		! field terminator found
	LOGICAL		REDO		! .T. if to redisplay menu
	INTEGER		CURRENT_OPT	! current option
	INTEGER		CURRENT_INV	! current invariant
	LOGICAL		PRINT		! .T. if report to be printed
	LOGICAL		ABORT		! .T. if report to be aborted
C
C The following arrays have been transfered to 'LARGE_ARRAYS.CMN', which allows
C the program's storage requirements to be reduced by sharing the larger arrays
C among a number of routines.
C Since these arrays can be used by other routines, care must be taken that 
C their values are not overwritten by any of the routines called by this
C routine.
C
CCC	CHARACTER*20	A_OPER_ID(FLN_MAX_OPER)	! operator ids
C
C functions
C
	INTEGER		DISPLAY_MENU
	INTEGER		GET_MENU_OPTION
	INTEGER		OPEN_REPORT_FILE
	INTEGER		CLOSE_REPORT_FILE
	INTEGER		PER_OPERATOR_STATISTICS_MENU
	INTEGER		OPER_TOTAL_STATISTICS_REPORT
	INTEGER		FLN_RETRIEVE_ALL_OPER_IDS
	INTEGER		WIDE_SCREEN
C
C Assume success
C
	OPERATOR_STATISTICS_MENU = LSC__NORMAL
C
C Now cause the menu form to be displayed with title etc
C
10	OPERATOR_STATISTICS_MENU = DISPLAY_MENU(
     &		'                   Operator Statistics Menu',
     &		'Selection of operator for statistical reports',
     &		'Available options are :')
C
C Check for errors
C
	IF (.NOT.OPERATOR_STATISTICS_MENU) GOTO 900
C
C Now set up the invariant options list (go top, move up and output
C report on all operators)
C
	INV_NUM = 4
	INVARIANTS(1)  = 'Display operator statistics report on screen'
	INVARIANTS(2)  = 'Print operator statistics report'
	INVARIANTS(3)  = 'Move up to system statistics report menu'
	INVARIANTS(4)  = 'Jump to top level menu'
C
C Now attempt to read all the operators in the system
C
	OPERATOR_STATISTICS_MENU = FLN_RETRIEVE_ALL_OPER_IDS(
     &			A_OPER_ID,	! operator names found
     &			OPER_NUM)	! number of operators
C
C Check for errors
C
	IF (.NOT.OPERATOR_STATISTICS_MENU) THEN
	   CALL LCONT_PUTMSG(LSC__UNRETOPER,0)
	   CALL LCONT_ADDMSG(OPERATOR_STATISTICS_MENU,0)
	   GOTO 900
	ENDIF
C
C Set display flag to indicate doing for first time
C
	REDO = .TRUE.
C
C And set current option to be first item on menu
C
	CURRENT_OPT = 1
C
C Get required option
C
20	OPERATOR_STATISTICS_MENU = GET_MENU_OPTION(
     &		A_OPER_ID,		! operator list
     &		OPER_NUM,		! number of operators
     &		INVARIANTS,		! invariant list
     &		INV_NUM,		! number of invariants
     &		CURRENT_OPT,		! current option
     &		CURRENT_INV,		! current invariant option
     &		OPT_AREA_LEN,		! number of lines in scroll area
     &		OPT_FIELD,		! field name
     &		TERM,			! field terminator found
     &		REDO)			! .T. if to redisplay menu
C
C Check for errors
C
	IF (.NOT.OPERATOR_STATISTICS_MENU) GOTO 900
C
C Set display flag to indicate don't need to redisplay
C
	REDO = .FALSE.
C
C Check if have required field terminator, that is, the return key
C
	IF (TERM.NE.RETURN_KEY) GOTO 20
C
C Decide which option was actually selected
C
C First check if an invariant option was selected
C
C Compile and output report
C If invariant 1 was chosen, report is to be displayed on screen
C If invariant 2 was chosen, report is to be printed
C
	IF ((CURRENT_OPT.LE.0).AND.
     &	    ((CURRENT_INV.EQ.1).OR.(CURRENT_INV.EQ.2))) THEN
C
C Check if report is to be printed, and set flag accordingly
C If report is to be displayed on screen, set flag to indicate
C that the current menu has been destroyed and needs redisplaying,
C and set the screen to 132 columns
C
	   IF (CURRENT_INV.EQ.2) THEN
	      PRINT = .TRUE.
	   ELSE
	      PRINT = .FALSE.
	      REDO = .TRUE.	! menu on screen needs redisplaying
C
C Set screen to wide (132 columns)
C
	      OPERATOR_STATISTICS_MENU = WIDE_SCREEN(.TRUE.)
C
C Check for errors - if any are found, redisplay the menu, including
C the error message.
C
	      IF (.NOT.OPERATOR_STATISTICS_MENU) THEN
	         CALL WIDE_SCREEN(.FALSE.)
	         CALL LCONT_PUTMSG(OPERATOR_STATISTICS_MENU,0)
	         GOTO 20
	      ENDIF
	   ENDIF
C
C Set flag to indicate that, if close report file procedure is reached, 
C before the flag can be cleared, the report is to be aborted
C
	   ABORT = .TRUE.
C
C Open the report file, for screen or printing depending on argument
C
	   OPERATOR_STATISTICS_MENU = OPEN_REPORT_FILE(PRINT)
C
C Check for errors - if any are found, jump to closing the report file
C
	   IF (.NOT.OPERATOR_STATISTICS_MENU) GOTO 30
C
C Output the operator statistics report
C
	   OPERATOR_STATISTICS_MENU = OPER_TOTAL_STATISTICS_REPORT()
C
C If report has been completed successfully, clear flag to 
C indicate we do not want to abort the report
C
	   IF (OPERATOR_STATISTICS_MENU) ABORT = .FALSE.
C
C Close the report file - ignore any errors which arise
C
30	   CALL CLOSE_REPORT_FILE(PRINT,ABORT)
C
C Set screen to be 80 columns, if report was output to screen
C
	   IF (.NOT.PRINT) THEN
	      CALL WIDE_SCREEN(.FALSE.)
C
C Check for any errors which occured during output of report, and,
C if report was output on screen, display them again, since the screen
C will have been cleared by resetting to 80 columns.
C Set semaphore to indicate redisplay of operator statistics menu is
C required.
C
	      IF (ABORT.AND.(.NOT.OPERATOR_STATISTICS_MENU)) THEN
C	         CALL LCONT_PUTMSG(OPERATOR_STATISTICS_MENU,0)
	         NEXT_FUNCTION = DO_MOVE_UP_LEVEL
	      ENDIF
	   ENDIF
C
C Move up to higher level menu
C
	ELSEIF ((CURRENT_OPT.LE.0).AND.
     &		(CURRENT_INV.EQ.3)) THEN
	   NEXT_FUNCTION = DO_MOVE_UP_LEVEL
	   GOTO 900
C
C Go top
C
	ELSEIF ((CURRENT_OPT.LE.0).AND.
     &		(CURRENT_INV.EQ.4)) THEN
	   NEXT_FUNCTION = DO_GO_TOP
C
C Move down to element selection menu - for all other options
C
	ELSE
	   OPERATOR_STATISTICS_MENU = 
     &		PER_OPERATOR_STATISTICS_MENU(A_OPER_ID(CURRENT_OPT))
C
C Check for errors - if any are found, redisplay the menu
C
	   IF (.NOT.OPERATOR_STATISTICS_MENU) GOTO 10
	ENDIF
C
C Loop to repeat this menu, if the next function semaphore has
C been set to indicate that this is required
C
	IF (NEXT_FUNCTION.EQ.DO_MOVE_UP_LEVEL) THEN
	   NEXT_FUNCTION = DO_ERROR
	   GOTO 10
	ENDIF
C
C Exit
C
900	RETURN
	END
