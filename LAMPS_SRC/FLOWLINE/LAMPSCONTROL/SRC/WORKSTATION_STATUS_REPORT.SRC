C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1988-12-23 11:21:08.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	INTEGER FUNCTION WORKSTATION_STATUS_REPORT()
C
***	MODULE WORKSTATION_STATUS_REPORT
***	IDENT 23DE88
C
C Copyright Laser-Scan Laboratories Ltd, Cambridge, England
C Author: J.M. Billing			3 May 1988
C
C Routine to produce listing on lun for current report file
C of the workstations using the flowline control database
C The information displayed will include:
C	Workstation identifier
C	Current operator(s)
C	Authorisation record(s) :	Type
C					Allowed (Yes/No)
C					Identifier
C
	IMPLICIT NONE
C
C Common blocks and parameter files
C
	INCLUDE 'LSL$CMNFLOWLINE:FLOW.STR'
	INCLUDE 'LSL$CMNFLOWLINE:FLOW.PAR'
	INCLUDE 'LSL$LAMPSCONTROL:LAMPSCONTROLMSG.PAR'
	INCLUDE 'LSL$LAMPSCONTROL:NEXT_FUNCTION.CMN'
	INCLUDE 'LSL$LAMPSCONTROL:FILES.CMN'
	INCLUDE 'LSL$LAMPSCONTROL:INTERACTIVE_IO.CMN'
	INCLUDE 'LSL$LAMPSCONTROL:LARGE_ARRAYS.CMN'
	INCLUDE 'LSL$LAMPSCONTROL:AUTHORISATION_STRUCTURE.CMN'
C
C w/s
	INTEGER		NUM_WORKSTAT		! number of workstations
	INTEGER		I,J			! temporary count varble
	INTEGER		NUM_OPER(FLN_MAX_WORK)	! number of operators
	INTEGER		NUM_AUTH(FLN_MAX_WORK)	! number of auth rec
C
C The following arrays have been transfered to 'LARGE_ARRAYS.CMN', which allows
C the program's storage requirements to be reduced by sharing the larger arrays
C among a number of routines.
C Since these arrays can be used by other routines, care must be taken that 
C their values are not overwritten by any of the routines called by this
C routine.
C
CCC	CHARACTER*20	A_WORK_ID(FLN_MAX_WORK)	! list of wkstation ids
C
C The following array is equivalenced onto (occupies the same area of storage
C as) several other square identifier arrays, and so should be used with
C caution. See the 'LARGE_ARRAYS.CMN' common block file for details.
C
CCC	CHARACTER*20	A_OPER_ID2(FLN_MAX_WORK,FLN_MAX_OPER)
C
C The data structure for storing authorisation records is now contained in
C 'AUTHORISATION_STRUCTURE.CMN'.
C
CCC	RECORD /FLN_AUTH/ AUTH_REC(FLN_MAX_WORK,FLN_MAX_AUTH) ! auth recs
C
C functions
	INTEGER		FLN_RETRIEVE_ALL_WORKSTATIONS
C
C Assume success
C
	WORKSTATION_STATUS_REPORT = LSC__NORMAL
C
C Call the routine to get list of information on all WORKSTATIONs
C in the system
C
	WORKSTATION_STATUS_REPORT = FLN_RETRIEVE_ALL_WORKSTATIONS(
     &		A_WORK_ID,	! WORKSTATION identifiers
     &		A_OPER_ID2,	! workstation identifiers
     &		AUTH_REC,	! authorisation record arrays
     &		NUM_WORKSTAT,	! number of workstations
     &		NUM_OPER,	! number of operators on each wks
     &		NUM_AUTH)	! number of auth recs for each wks
C
C Check for errors in reading list of WORKSTATIONs
C
	IF (.NOT.WORKSTATION_STATUS_REPORT) THEN
	   CALL LCONT_PUTMSG(WORKSTATION_STATUS_REPORT,0)
C
C Specify that you just want to repeat the menu from which call
C originated
C
	   NEXT_FUNCTION = DO_MOVE_UP_LEVEL
	   GOTO 999
	ENDIF
C
C Select report list file
C
	CALL FLWSEL(REPORTFIL_LUN)
C
C Mark the FMS screen menu as not reusable
C
	PREVIOUS_MENU_USABLE = .FALSE.
C
C Now output the title lines of the WORKSTATION listing
C
	CALL FLWRTF('LAMPSCONTROL WORKSTATION Status Report')
	CALL FLWRTF(' ')
C
C Output the header line, with column titles
C
	CALL EXPAND('Workstation Identifier   ')
	CALL APPEND('Operator Identifiers     ')
	CALL APPEND('     Authorisations')
	CALL FLWSTR()
C
C Output the next header line
C
	CALL EXPAND('                         ')
	CALL APPEND('                         ')
	CALL APPEND('Type  Allowed  ')
	CALL APPEND('Identifier               ')
	CALL FLWSTR()
C
C Now output a blank line to make output look really beautiful
C
	CALL FLWRTF()
C
C Now output the actual workstation details, in a loop
C
	DO 10 I = 1, NUM_WORKSTAT
C
C Format the line - start with the workstation identifier
C
	   CALL EXPAND('%S     ',A_WORK_ID(I))
C
C Now deal with all the possible operators and authorisation records
C Go round at least once to make sure you actually output the line
C with the workstation identifier on it.
C
	   DO 15 J = 1, MAX(1,NUM_AUTH(I),NUM_OPER(I))
C
C If this is not the first line, expand a string to fill workstation
C id column
C
	      IF (J.NE.1) CALL EXPAND('                         ')
C
C Append the operator id if there are any left
C
	      IF (J.LE.NUM_OPER(I)) THEN
	         CALL APPEND('%S     ',A_OPER_ID2(I,J))
C
C Otherwise output a blank part in the operator column
C
	      ELSE
	         CALL APPEND('                         ')
	      ENDIF
C
C Now check if there are any authorisation records left, and output
C them if there are
C
	      IF (J.LE.NUM_AUTH(I)) THEN
C
C Output the authorisation type
C
	         CALL APPEND('%S    ',AUTH_REC(I,J).TYPE)
C
C Output whether it is allowed or not
C
	         IF (AUTH_REC(I,J).YES_NO.EQ.'Y') THEN
	            CALL APPEND('Yes        ')
	         ELSEIF (AUTH_REC(I,J).YES_NO.EQ.'E') THEN
	            CALL APPEND('Everything ')
	         ELSE
	            CALL APPEND('No         ')
	         ENDIF
C
C	         IF (AUTH_REC(I,J).YES_NO) THEN
C	            CALL APPEND('Yes      ')
C	         ELSE
C	            CALL APPEND('No       ')
C	         ENDIF
C
C And finally output the authorisation identifier
C
	         CALL APPEND('%S',AUTH_REC(I,J).ID)
C
	      ENDIF
C
C And output the finished line to the report file lun
C
	      CALL FLWSTR()
C
15	   CONTINUE
C
C Blank line to separate workstation entries
C
	   CALL FLWRTF()
C
10	CONTINUE
C
C Output blank line
C
	CALL FLWRTF(' ')
C
C Tell the user how many WORKSTATIONs were found
C
	IF (NUM_WORKSTAT.GT.0) THEN
	   CALL FLWRTF('Total number of workstations is %N',
     &			NUM_WORKSTAT)
C
C Deal with the case where no WORKSTATION details are found
C
	ELSE
	   CALL FLWRTF('No workstations found')
	ENDIF
C
C Set semaphore to indicate that report status menu should be re-entered
C
	NEXT_FUNCTION = DO_MOVE_UP_LEVEL
C
C Exit
C
999	RETURN
	END
