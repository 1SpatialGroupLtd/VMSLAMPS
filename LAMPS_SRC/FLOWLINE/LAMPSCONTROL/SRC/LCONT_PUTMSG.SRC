	INTEGER FUNCTION LCONT_PUTMSG(ARG_ERRNUM,TIME)
C
***	MODULE LCONT_PUTMSG
***	IDENT 13AP88
C
C Copyright Laser-Scan Laboratories Ltd, Cambridge, England
C Author: J.M. Billing			13 April 1988
C
C Routine to output error code to penultimate line of a form
C or SYS$OUTPUT depending on whether using menus or not in
C LAMPSCONTROL
C
C
	IMPLICIT NONE
C
	INCLUDE 'LSL$LAMPSCONTROL:QUALIFIERS.CMN'
	INCLUDE 'LSL$LAMPSCONTROL:INTERACTIVE_IO.CMN'
	INCLUDE 'LSL$LAMPSCONTROL:LAMPSCONTROLMSG.PAR'
	INCLUDE 'LSL$CMNFLOWLINE:FLOWLINEMSG.PAR'
C
C arguments
	INTEGER		TIME		! duration of display in FMS
	INTEGER		ARG_ERRNUM	! error code
C
C w/s
	CHARACTER*200	MSG		! message text
	INTEGER		MSGLEN		! message length
	INTEGER		I		! temporary index
	INTEGER		ERRNUM		! variable error number
C
C Assume success
C
	LCONT_PUTMSG = LSC__NORMAL
C
C Transfer argument, which may be constant, to local variable
C
	ERRNUM = ARG_ERRNUM
C
C Retrieve message text
C
10	CALL LSL_GETMSG(ERRNUM,MSG,MSGLEN)
C
C Test that some text has been found and give appropriate LAMPSCONTROL
C message if not found
C
	IF (MSGLEN.LT.1) THEN
	   ERRNUM = LSC__UNRECERR
	   GOTO 10
	ENDIF
C
C Make sure length is acceptable
C
	MSGLEN = MIN(MSGLEN,LEN(MSG))
C
C Ensure any formatting % characters are removed from text
C
	DO 20 I = 1,MSGLEN
	  IF (MSG(I:I).EQ.'%') MSG(I:I) = ' '
20	CONTINUE
C
C Test if using menus
C
	IF (MENU) THEN
C
C Now output it to the FMS screen
C
	   CALL FORM_PUTMSG(MSG(:MSGLEN),(LAST_LINE-2),TIME)
C
C Otherwise, just output the error message as normal
C
	ELSE
	   CALL LSL_PUTMSG(ERRNUM)
	ENDIF
C
C Now send it to the error log file
C
	CALL LOG_ENTRY(ERRNUM,MSG(:MSGLEN))
C
C Exit
C
	RETURN
	END
