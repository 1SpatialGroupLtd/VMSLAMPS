C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1988-12-22 16:03:24.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	INTEGER FUNCTION ELEM_WORK_STATISTICS_REPORT(FLN_ID,ELEM_ID)
C
***	MODULE ELEM_WORK_STATISTICS_REPORT
***	IDENT 22DE88
C
C Copyright Laser-Scan Laboratories Ltd, Cambridge, England
C Author: J.M. Billing			12 September 1988
C
C Routine to produce listing on lun for current report file
C of the statistics relating to the element in the flowline, passed
C as arguments, broken down by workstations which have been used to
C perform the sessions for the element.
C The information displayed will include:
C	Workstation id
C	Total connect time
C	Average connect time per session
C	Total CPU time
C	Average CPU time per session
C	Number of sessions by workstation
C
C Times will be displayed in hh:mm:ss.cc format
C
C The connect and CPU times are recovered from the database in integer
C hundredths of seconds.
C
C
	IMPLICIT NONE
C
C Common blocks and parameter files
C
	INCLUDE 'LSL$CMNFLOWLINE:FLOW.PAR'
	INCLUDE 'LSL$LAMPSCONTROL:LAMPSCONTROLMSG.PAR'
	INCLUDE 'LSL$LAMPSCONTROL:NEXT_FUNCTION.CMN'
	INCLUDE 'LSL$LAMPSCONTROL:FILES.CMN'
	INCLUDE 'LSL$LAMPSCONTROL:INTERACTIVE_IO.CMN'
	INCLUDE 'LSL$LAMPSCONTROL:LARGE_ARRAYS.CMN'
C
C arguments
	CHARACTER*(*)	FLN_ID			! flowline of interest
	CHARACTER*(*)	ELEM_ID			! element of interest
C
C w/s
	INTEGER		NUM_WORK		! number of workstations
	INTEGER		I,J			! temporary count varble
C
	CHARACTER*20	CPU			! cpu time string
	INTEGER		CPU_LENGTH		! length of cpu string
	CHARACTER*20	CONNECT			! connect time string
	INTEGER		CONNECT_LENGTH		! length of connect str
	CHARACTER*20	MEAN_CPU		! mean cpu time string
	INTEGER		MEAN_CPU_LENGTH		! length mean cpu string
	CHARACTER*20	MEAN_CONNECT		! mean connect time str
	INTEGER		MEAN_CONNECT_LENGTH	! len mean connect str
C
	INTEGER		CPU_TIME(FLN_MAX_WORK) 	! cpu time
	INTEGER		CONNECT_TIME(FLN_MAX_WORK)! connect time
	INTEGER		NUM_SESS(FLN_MAX_WORK)	! number of sessions
C
C The following arrays have been transfered to 'LARGE_ARRAYS.CMN', which allows
C the program's storage requirements to be reduced by sharing the larger arrays
C among a number of routines.
C Since these arrays can be used by other routines, care must be taken that 
C their values are not overwritten by any of the routines called by this
C routine.
C
CCC	CHARACTER*20	A_WORK_ID(FLN_MAX_WORK)	! workstations
C
C functions
	INTEGER		FLN_RETRIEVE_ELEM_WORK_TIMINGS
	INTEGER		SIGCHS
	INTEGER		CONVERT_TIME_TO_STRING
C
C Assume success
C
	ELEM_WORK_STATISTICS_REPORT = LSC__NORMAL
C
C Call the routine to get statistics on all workstations which have 
C been used to perform sessions for this element
C
	ELEM_WORK_STATISTICS_REPORT = FLN_RETRIEVE_ELEM_WORK_TIMINGS(
     &		FLN_ID,		! flowline 
     &		ELEM_ID,	! element identifier
     &		A_WORK_ID,	! WORKSTATION identifiers
     &		CPU_TIME,	! cpu time used by workstation 
     &		CONNECT_TIME,	! connect time used for workstation
     &		NUM_SESS,	! number of sessions by workstation
     &		NUM_WORK)	! number of workstations
C
C Check for errors in reading list of workstationS
C
	IF (.NOT.ELEM_WORK_STATISTICS_REPORT) THEN
	   CALL LCONT_PUTMSG(LSC__UNRETELEM,0)
	   CALL LCONT_ADDMSG(ELEM_WORK_STATISTICS_REPORT,0)
C
C Specify that you just want to repeat the menu from which call
C originated
C
	   NEXT_FUNCTION = DO_MOVE_UP_LEVEL
	   GOTO 999
	ENDIF
C
C Select report list file
C
	CALL FLWSEL(REPORTFIL_LUN)
C
C Mark the FMS screen menu as not reusable
C
	PREVIOUS_MENU_USABLE = .FALSE.
C
C Now output the title lines of the element by workstation listing
C
	CALL FLWRTF('LAMPSCONTROL Statistics Report Broken Down By '//
     &		    'Workstation For Element %S in Flowline %S',
     &		    ELEM_ID(1:SIGCHS(ELEM_ID)),FLN_ID)
C
C Output blank line for neatness
C
	CALL FLWRTF(' ')
C
C Output the header line, with column titles
C
	CALL EXPAND('Workstation Ident     ')
	CALL APPEND('Connect time          ')
	CALL APPEND('Connect/session       ')
	CALL APPEND('CPU time              ')
	CALL APPEND('CPU/session           ')
	CALL APPEND('No. sessions')
	CALL FLWSTR()
C
C Now output a blank line to make output look really beautiful
C
	CALL FLWRTF()
C
C Now output the actual workstation details, in a loop
C
	DO 10 I = 1, NUM_WORK
C
C Convert the times and things into strings for output
C Allow for unset values in each of the quantities
C
C Deal with total connect time
C
	   CALL CONVERT_TIME_TO_STRING(
     &			CONNECT_TIME(I),CONNECT_LENGTH,CONNECT)
C
C Deal with mean connect time
C
	   IF (NUM_SESS(I).LE.0) THEN
	      MEAN_CONNECT = 'Unset'
	      MEAN_CONNECT_LENGTH = SIGCHS(MEAN_CONNECT)
	   ELSE
	      CALL CONVERT_TIME_TO_STRING(
     &			(CONNECT_TIME(I)/NUM_SESS(I)),
     &			MEAN_CONNECT_LENGTH,
     &			MEAN_CONNECT)
	   ENDIF
C
C Deal with total cpu time
C
	   CALL CONVERT_TIME_TO_STRING(CPU_TIME(I),CPU_LENGTH,CPU)
C
C Deal with mean CPU time
C
	   IF (NUM_SESS(I).LE.0) THEN
	      MEAN_CPU = 'Unset'
	      MEAN_CPU_LENGTH = SIGCHS(MEAN_CPU)
	   ELSE
	      CALL CONVERT_TIME_TO_STRING(
     &		   	(CPU_TIME(I)/NUM_SESS(I)),
     &		   	MEAN_CPU_LENGTH,
     &		   	MEAN_CPU)
	   ENDIF
C
C Format the line
C
	   CALL EXPAND('%S20  ',A_WORK_ID(I))
	   CALL APPEND('%S%[ %]',CONNECT(1:CONNECT_LENGTH),
     &				 (22-CONNECT_LENGTH))
	   CALL APPEND('%S%[ %]',MEAN_CONNECT(1:MEAN_CONNECT_LENGTH),
     &				 (22-MEAN_CONNECT_LENGTH))
	   CALL APPEND('%S%[ %]',CPU(1:CPU_LENGTH),
     &				 (22-CPU_LENGTH))
	   CALL APPEND('%S%[ %]',MEAN_CPU(1:MEAN_CPU_LENGTH),
     &				 (22-MEAN_CPU_LENGTH))
	   CALL APPEND('%I10',NUM_SESS(I))
C
C And output line to the report file lun
C
	   CALL FLWSTR()
C
10	CONTINUE
C
C Output blank line
C
	CALL FLWRTF(' ')
C
C Tell the user how many workstations were found
C
	IF (NUM_WORK.GT.0) THEN
	   CALL FLWRTF('Total number of workstations is %N',NUM_WORK)
C
C Deal with the case where no workstation details are found
C
	ELSE
	   CALL FLWRTF('No workstations found')
	ENDIF
C
C Set semaphore to indicate that report status menu should be re-entered
C
	NEXT_FUNCTION = DO_MOVE_UP_LEVEL
C
C Exit
C
999	RETURN
	END
