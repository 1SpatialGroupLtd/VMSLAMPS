C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1991-09-05 14:11:00.000000000 +0100
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	INTEGER FUNCTION JOB_STATUS_MENU(CONT_PROJ_ID)
C
***	MODULE JOB_STATUS_MENU
***	IDENT 05SE91
C
C Copyright Laser-Scan Laboratories Ltd, Cambridge, England
C Author: J.M. Billing			13 April 1988
C
C Menu to examine/modify the status of the jobs in the
C system
C
	IMPLICIT NONE
C
C Common blocks and parameter files
C
	INCLUDE 'LSL$CMNFLOWLINE:FLOWLINEMSG.PAR'
	INCLUDE 'LSL$CMNFLOWLINE:FLOW.PAR'
	INCLUDE 'LSL$LAMPSCONTROL:LAMPSCONTROLMSG.PAR'
	INCLUDE 'LSL$LAMPSCONTROL:CURRENT_ITEMS.CMN'
	INCLUDE 'LSL$LAMPSCONTROL:NEXT_FUNCTION.CMN'
	INCLUDE 'LSL$LAMPSCONTROL:INTERACTIVE_IO.CMN'
	INCLUDE 'LSL$LAMPSCONTROL:LARGE_ARRAYS.CMN'
C
C arguments
	CHARACTER*(*)	CONT_PROJ_ID
C
C w/s
	INTEGER		NUM_JOB				! number of jobs
	INTEGER		CURRENT_OPT			! current option
	INTEGER		CURRENT_INV			! current inv'nt
	INTEGER		NUM_INV				! num invariants
C
	CHARACTER*80	DESCRIPTION_LINE		! for menu descr
C
	INTEGER		TERM				! fms terminator
	INTEGER		REDISPLAY			! .T. redo menu
	CHARACTER*50	FVAL				! field value
	INTEGER		FLEN				! length
C
	INTEGER		I				! tempar'y count
C
C
C The following arrays have been transfered to 'LARGE_ARRAYS.CMN', which allows
C the program's storage requirements to be reduced by sharing the larger arrays
C among a number of routines.
C Since these arrays can be used by other routines, care must be taken that 
C their values are not overwritten by any of the routines called by this
C routine.
C
CCC	INTEGER		A_JOB_ID(FLN_MAX_JOB)		! job ids
CCC	INTEGER		A_JOB_PRI(FLN_MAX_JOB)		! job pri'ties
CCC	CHARACTER*255	A_COMPROC(FLN_MAX_JOB)		! command procs
CCC	CHARACTER*20	A_JOB_NAME(FLN_MAX_JOB)		! job names
CCC	CHARACTER*20	A_JOB_STATUS(FLN_MAX_JOB)	! job status
CCC	CHARACTER*80	A_MENU(FLN_MAX_JOB)		! lines on menu
C
C functions
	INTEGER		FLN_RETRIEVE_JOBS_PROJECT
	INTEGER		FLN_CHANGE_JOB_NAME
	INTEGER		FLN_CHANGE_JOB_PRI
	INTEGER		FLN_DELETE_JOB
	INTEGER		FLN_HOLD_JOB
	INTEGER		FLN_RELEASE_JOB
	INTEGER		FLN_REMOVE_SPACES
	INTEGER		DISPLAY_MENU
	INTEGER		GET_MENU_OPTION
	LOGICAL		RDINT
	INTEGER		TASK_STATUS_MENU
	INTEGER		JOB_MOVE_ELEMENT_MENU
	INTEGER		EDIT_FILE
C
C Assume success
C
5	JOB_STATUS_MENU = LSC__NORMAL
C
C Set up defaults for menu display etc
C
	CURRENT_OPT = 1
	CURRENT_INV = 0
	REDISPLAY   = .TRUE.
C
C Set up invariant menu list options
C
	INV_LIST(1) = 'Move up to project status modification menu'
	INV_LIST(2) = 'Jump to top level menu'
	NUM_INV = 2
C
C Read the list of jobs from database
C
	JOB_STATUS_MENU = FLN_RETRIEVE_JOBS_PROJECT(
     &		CONT_PROJ_ID,	! containing project id
     &		A_JOB_ID,	! array of job ids
     &		A_JOB_NAME,	! array of job names
     &		A_JOB_STATUS,   ! array of job statuses
     &		A_JOB_PRI,	! array of job priorities
     &		A_COMPROC,	! array of job command procedures
     &		NUM_JOB)	! number of jobs
C
C Deal with any errors
C
	IF (.NOT.JOB_STATUS_MENU) THEN
	   CALL LCONT_PUTMSG(JOB_STATUS_MENU,0)
	   NEXT_FUNCTION = DO_MOVE_UP_LEVEL
	   GOTO 999
	ENDIF
C
C Assemble list including job_id, status and priority on same line
C
	DO 10 I = 1,NUM_JOB
C
C Ensure menu list array is empty
C
	   A_MENU(I) = '     '
C
C Now expand the required text line into the menu option list string
C
	   CALL EXPAND('%W%S20     %S20     %^W%N5',
     &			%REF(A_MENU(I)),
     &			A_JOB_NAME(I),
     &			A_JOB_STATUS(I),
     &			A_JOB_PRI(I))
C
10	CONTINUE
C
C Construct the menu description line
C
	DESCRIPTION_LINE = '   '
C
	CALL EXPAND('%WProject %S20',
     &			%REF(DESCRIPTION_LINE),CONT_PROJ_ID)
C
C Display the menu page
C
	JOB_STATUS_MENU = DISPLAY_MENU(
     &		'                Job Status Modification Menu',
     &		DESCRIPTION_LINE,
     &		'Job Name                 Status                   '//
     &							'Priority')
C
C Deal with errors
C
	IF (.NOT.JOB_STATUS_MENU) THEN
	   NEXT_FUNCTION = DO_MOVE_UP_LEVEL
	   GOTO 999
	ENDIF
C
C Set the flag to indicate menu must be redisplayed
C
	REDISPLAY = .TRUE.
C
C Now display the menu
C
20	JOB_STATUS_MENU = GET_MENU_OPTION(
     &		A_MENU,	! list of main options
     &		NUM_JOB,	! number of main options
     &		INV_LIST,	! list of invariant options
     &		NUM_INV,	! number of invariant options
     &		CURRENT_OPT,	! current main option
     &		CURRENT_INV,	! current invariant option
     &		OPT_AREA_LEN,	! number of lines in scrolled area
     &		OPT_FIELD,	! option field name
     &		TERM,		! fms field terminator used to exit
     &		REDISPLAY)	! .T. if to redisplay menu on entry
C
C Deal with errors
C
	IF (.NOT.JOB_STATUS_MENU) THEN
	   NEXT_FUNCTION = DO_MOVE_UP_LEVEL
	   GOTO 999
	ENDIF
C
C---------------------------------------------------------------------
C Check the terminators and act accordingly
C
C---------------------------------------------------------------------
C Deal with RETURN
C
	IF (TERM.EQ.RETURN_KEY) THEN
C
C If job selected, call job status menu for JOB selected
C
	   IF (CURRENT_OPT.GT.0) THEN
	      JOB_STATUS_MENU = 
     &		TASK_STATUS_MENU(
     &			A_JOB_ID(CURRENT_OPT),
     &			A_JOB_NAME(CURRENT_OPT),
     &			CONT_PROJ_ID)
C
C If next operation is to move up a level, then reset next function
C semaphore and repeat this menu
C
	      IF (NEXT_FUNCTION.EQ.DO_MOVE_UP_LEVEL) THEN
	         NEXT_FUNCTION = DO_ERROR
	         GOTO 5
	      ENDIF
C
C Deal with error which do not require simple move up to higher
C level menu
C
	      IF (.NOT.JOB_STATUS_MENU) THEN
	         GOTO 999
	      ENDIF
C
C Otherwise it is an invariant option
C
C---------------------------------------------------------------------
C Deal with move up option
C
	   ELSEIF (CURRENT_INV.EQ.1) THEN
	      NEXT_FUNCTION = DO_MOVE_UP_LEVEL
	      GOTO 999
C
C---------------------------------------------------------------------
C Deal with go top
C
	   ELSEIF (CURRENT_INV.EQ.2) THEN
	      NEXT_FUNCTION = DO_GO_TOP
	      GOTO 999
	   ENDIF
C
C---------------------------------------------------------------------
C Deal with edit job command procedure i.e. GOLD E
C Will call routine to take care of editing file
C
	ELSEIF (((TERM.EQ.(GOLD_BASE + ICHAR('E'))).OR.
     &		 (TERM.EQ.(GOLD_BASE + ICHAR('e')))).AND.
     &		(CURRENT_OPT.GT.0)) THEN
C
C Call the file editing routine
C
	   JOB_STATUS_MENU = EDIT_FILE(A_COMPROC(CURRENT_OPT))
C
C Check for, and report, any errors
C
	   IF (.NOT.JOB_STATUS_MENU) THEN
	      CALL LCONT_PUTMSG(JOB_STATUS_MENU,0)
	   ENDIF
C
C Mark menu as not displayed, refresh it to cure damage done by EDT
C to the display
C
	   CALL FDV$NDISP
	   CALL FDV$RFRSH
C
C Repeat menu, redisplaying it because EDT will have destroyed screen
C
	   GOTO 20
C
C---------------------------------------------------------------------
C Deal with job move i.e. GOLD M
C Will call element selection for job move menu
C
	ELSEIF (((TERM.EQ.(GOLD_BASE + ICHAR('M'))).OR.
     &		 (TERM.EQ.(GOLD_BASE + ICHAR('m')))).AND.
     &		(CURRENT_OPT.GT.0)) THEN
C
C Call the element selection menu
C
	   JOB_STATUS_MENU = 
     &		JOB_MOVE_ELEMENT_MENU(
     &			A_JOB_ID(CURRENT_OPT),
     &			A_JOB_NAME(CURRENT_OPT),
     &			CONT_PROJ_ID)
C
C Check if to simply move up level on exit
C i.e. redo this menu
C
	   IF (NEXT_FUNCTION.EQ.DO_MOVE_UP_LEVEL) THEN
	      NEXT_FUNCTION = DO_ERROR
	      GOTO 5
	   ENDIF
C
C Deal with errors which cannot be dealt with by repeating the job
C status menu
C
	   IF (.NOT.JOB_STATUS_MENU) THEN
	      GOTO 999
	   ENDIF
C
C---------------------------------------------------------------------
C Deal with job deletion i.e. GOLD D
C
	ELSEIF (((TERM.EQ.(GOLD_BASE + ICHAR('D'))).OR.
     &		 (TERM.EQ.(GOLD_BASE + ICHAR('d')))).AND.
     &		(CURRENT_OPT.GT.0)) THEN
C
C Prompt the user to make sure
C
	   CALL FORM_GETLINE(FVAL,TERM,0,'Are you sure (Y/N) ? ')
C
C Test the value returned
C
	   IF ((FVAL(1:1).EQ.'Y').OR.(FVAL(1:1).EQ.'y')) THEN
C
C Delete the job
C
	      JOB_STATUS_MENU = FLN_DELETE_JOB(
     &		A_JOB_ID(CURRENT_OPT))
C
C Deal with errors
C
	      IF (.NOT.JOB_STATUS_MENU) THEN
	         CALL LCONT_PUTMSG(JOB_STATUS_MENU,0)
	         REDISPLAY = .FALSE.
	         GOTO 20
	      ENDIF
C
C The job has now been deleted, so must get new list of jobs
C and display it.
C
	      GOTO 5
C
C Otherwise, just loop without redisplaying
C
	   ELSE
	      REDISPLAY = .FALSE.
	      GOTO 20
	   ENDIF
C
C---------------------------------------------------------------------
C Deal with job held i.e. GOLD H
C
	ELSEIF (((TERM.EQ.(GOLD_BASE + ICHAR('H'))).OR.
     &		 (TERM.EQ.(GOLD_BASE + ICHAR('h')))).AND.
     &		(CURRENT_OPT.GT.0)) THEN
C
C Put the job to held in database
C
	   JOB_STATUS_MENU = 
     &			FLN_HOLD_JOB(A_JOB_ID(CURRENT_OPT))
C
C Deal with errors
C
	   IF (.NOT.JOB_STATUS_MENU) THEN
	      CALL LCONT_PUTMSG(JOB_STATUS_MENU,0)
	      REDISPLAY = .FALSE.
	      GOTO 20
	   ENDIF
C
C Now amend the local status array
C
	   A_JOB_STATUS(CURRENT_OPT) = 'Held'
C
C And update the menu item on display
C Ensure menu list array entry is empty
C
	   A_MENU(CURRENT_OPT) = '     '
C
C Now expand the text line into the menu option list string
C
	   CALL EXPAND('%W%S20     %S20     %^W%N5',
     &			%REF(A_MENU(CURRENT_OPT)),
     &			A_JOB_NAME(CURRENT_OPT),
     &			A_JOB_STATUS(CURRENT_OPT),
     &			A_JOB_PRI(CURRENT_OPT))
C
C And loop for next option
C
	   REDISPLAY = .TRUE.
	   GOTO 20
C
C---------------------------------------------------------------------
C Deal with job release i.e. GOLD R
C
	ELSEIF (((TERM.EQ.(GOLD_BASE + ICHAR('R'))).OR.
     &		 (TERM.EQ.(GOLD_BASE + ICHAR('r')))).AND.
     &		(CURRENT_OPT.GT.0)) THEN
C
C Release the job in database (change to 'Active' status)
C
	   JOB_STATUS_MENU = 
     &			FLN_RELEASE_JOB(A_JOB_ID(CURRENT_OPT))
C
C Deal with errors
C
	   IF (.NOT.JOB_STATUS_MENU) THEN
	      CALL LCONT_PUTMSG(JOB_STATUS_MENU,0)
	      REDISPLAY = .FALSE.
	      GOTO 20
	   ENDIF
C
C Now amend the local status array
C
	   A_JOB_STATUS(CURRENT_OPT) = 'Active'
C
C And update the menu item on display
C Ensure menu list array entry is empty
C
	   A_MENU(CURRENT_OPT) = '     '
C
C Now expand the new text line into the menu option list string
C
	   CALL EXPAND('%W%S20     %S20     %^W%N5',
     &			%REF(A_MENU(CURRENT_OPT)),
     &			A_JOB_NAME(CURRENT_OPT),
     &			A_JOB_STATUS(CURRENT_OPT),
     &			A_JOB_PRI(CURRENT_OPT))
C
C And loop for next option
C
	   REDISPLAY = .TRUE.
	   GOTO 20
C
C---------------------------------------------------------------------
C Deal with job priority alteration i.e. GOLD P
C
	ELSEIF (((TERM.EQ.(GOLD_BASE + ICHAR('P'))).OR.
     &		 (TERM.EQ.(GOLD_BASE + ICHAR('p')))).AND.
     &		(CURRENT_OPT.GT.0)) THEN
C
C Prompt the user determine the required priority
C
30	   CALL FORM_GETLINE(FVAL,TERM,0,'New priority (1-16) : ')
C
C Determine the value entered, setting and resetting buffers and
C testing for errors
C
	   CALL SETAUX(%REF(FVAL),20)
C
	   IF (RDINT(A_JOB_PRI(CURRENT_OPT))) THEN
	      CALL FORM_PUTMSG('Illegal priority',-1,0)
	      CALL BSLN
	      GOTO 30
	   ENDIF
C
	   CALL BSLN
C
C Test that priority is in legal range
C
	   IF ((A_JOB_PRI(CURRENT_OPT).GT.16).OR.
     &	       (A_JOB_PRI(CURRENT_OPT).LT.01)) THEN
	      CALL FORM_PUTMSG('Illegal priority',-1,0)
	      GOTO 30
	   ENDIF
C
C Reset the priority
C
	      JOB_STATUS_MENU = FLN_CHANGE_JOB_PRI(
     &		A_JOB_ID(CURRENT_OPT),
     &		A_JOB_PRI(CURRENT_OPT))
C
C Deal with errors
C
	   IF (.NOT.JOB_STATUS_MENU) THEN
	      CALL LCONT_PUTMSG(JOB_STATUS_MENU,0)
	      REDISPLAY = .FALSE.
	      GOTO 20
	   ENDIF
C
C Ensure menu list array is empty
C
	   A_MENU(CURRENT_OPT) = '     '
C
C Now expand the new text line into the menu option list string
C
	   CALL EXPAND('%W%S20     %S20     %^W%N5',
     &			%REF(A_MENU(CURRENT_OPT)),
     &			A_JOB_NAME(CURRENT_OPT),
     &			A_JOB_STATUS(CURRENT_OPT),
     &			A_JOB_PRI(CURRENT_OPT))
C
C And loop for next option
C
	   REDISPLAY = .TRUE.
	   GOTO 20
C
C---------------------------------------------------------------------
C Deal with job name alteration i.e. GOLD N
C
	ELSEIF (((TERM.EQ.(GOLD_BASE + ICHAR('N'))).OR.
     &		 (TERM.EQ.(GOLD_BASE + ICHAR('n')))).AND.
     &		(CURRENT_OPT.GT.0)) THEN
C
C Prompt the user determine the new job name
C
40	   CALL FORM_GETLINE(FVAL,TERM,0,'New job name : ')
C
	   JOB_STATUS_MENU = FLN_REMOVE_SPACES(FVAL,FVAL,FLEN)
	   IF (.NOT.JOB_STATUS_MENU) THEN
	      CALL FORM_PUTMSG('Illegal job name',-1,0)
	      GOTO 40
	   ENDIF
	   CALL STR$UPCASE(FVAL,FVAL)
C
C Change the name
C
	   JOB_STATUS_MENU = FLN_CHANGE_JOB_NAME(
     &		A_JOB_ID(CURRENT_OPT),
     &		FVAL(:FLEN))
C
C Deal with errors
	   IF (.NOT.JOB_STATUS_MENU) THEN
	      IF (JOB_STATUS_MENU.EQ.FLN__DUPJOB) THEN
	         CALL FORM_PUTMSG('Duplicate job name',-1,0)
	      ELSE
	         CALL LCONT_PUTMSG(JOB_STATUS_MENU,0)
	      ENDIF
	      REDISPLAY = .FALSE.
	      GOTO 20
	   ENDIF
C
C The job has now been renamed, so must get new list of jobs
C and display it.
C
	   GOTO 5
C
C---------------------------------------------------------------------
C Deal with unrecognised key sequence - just ignore it and loop
C back to allow entry of next option without rediplaying menu
C
	ELSE
	   REDISPLAY = .FALSE.
	   GOTO 20
	ENDIF
C
C Exit
C
999	RETURN
	END
