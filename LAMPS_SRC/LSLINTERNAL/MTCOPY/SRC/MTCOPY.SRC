	PROGRAM MTCOPY
C
***	IDENT	18AP89
***	MODULE	MTCOPY
C
C	Copyright : Laser-Scan Laboratories Ltd., Cambridge, England

C	Created   : Andrew C Morris			14-Apr-86
C	starting from MTTY and MTRY, by WJJ

C Description
C	DMT is a file format for holding magtape images.
C	MTCOPY copies DMT files to tape, tape to DMTfile,
C	 tape to tape, but NOT DMT file to DMT file)
C
	IMPLICIT NONE
C
	INCLUDE '($SSDEF)'
	INCLUDE 'LSL$CMNLSL:LSLLIBMSG.PAR'
	INCLUDE 'LSL$CMNMTCOPY:MTCOPYMSG.PAR'
	INCLUDE 'LSL$CMNMTCOPY:MTCOPY.PAR'
C
	INCLUDE 'LSL$CMNMTCOPY:MTCOPY.CMN'
C
C Workspace
	INTEGER*4	STATUS		! exit status
	LOGICAL*4	OK		! logical function return
C
C Functions
	LOGICAL*4	GET_COMMANDS
	LOGICAL*4	IDENTIFY_IO_DEVICES
	LOGICAL*4	FILE_TO_TAPE
	LOGICAL*4	TAPE_TO_TAPE
	LOGICAL*4	TAPE_TO_FILE
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
	STATUS = SS$_ABORT		! assume failure

C Initialise LSLLIB
C
	CALL LSL_INIT(.FALSE.)
C
C Get command line (i/o dev specs, etc)
C
	OK = GET_COMMANDS()
C
	IF (.NOT.OK) THEN
	   CALL LSL_PUTMSG(MTCOPY__BADCMDLIN)
	   GOTO 999
	ENDIF
C
C Find whether input and output devices are magtape or diskfile
C
	OK = IDENTIFY_IO_DEVICES()
	IF (.NOT.OK) GOTO 999
C
C Do the copying
C
	IF (INPUT_DEV.EQ.FILE.AND.OUTPUT_DEV.EQ.TAPE) THEN
C
	   OK = FILE_TO_TAPE()		! copy DMT file to magtape
	   IF (OK) CALL LSL_PUTMSG(MTCOPY__NFILES,NFILES,
     &	      'DMT file',INPUT_DEV_SPEC(:INPUT_DEV_LEN),
     &	      'magtape',OUTPUT_DEV_SPEC(:OUTPUT_DEV_LEN))
C
	ELSEIF (INPUT_DEV.EQ.TAPE.AND.OUTPUT_DEV.EQ.TAPE) THEN
C
	   OK = TAPE_TO_TAPE()		! copy magtape to magtape
	   IF (OK) CALL LSL_PUTMSG(MTCOPY__NFILES,NFILES,
     &	      'magtape',INPUT_DEV_SPEC(:INPUT_DEV_LEN),
     &	      'magtape',OUTPUT_DEV_SPEC(:OUTPUT_DEV_LEN))
C
	ELSEIF (INPUT_DEV.EQ.TAPE.AND.OUTPUT_DEV.EQ.FILE) THEN
C
	   OK = TAPE_TO_FILE()		! copy magtape to diskfile
	   IF (OK) CALL LSL_PUTMSG(MTCOPY__NFILES,NFILES,
     &	      'magtape',INPUT_DEV_SPEC(:INPUT_DEV_LEN),
     &	      'DMT file',OUTPUT_DEV_SPEC(:OUTPUT_DEV_LEN))
	ELSE
	   OK = .TRUE.
	   CALL LSL_PUTMSG(MTCOPY__USEDCLCOPY)
	ENDIF
C
	IF (OK) STATUS = SS$_NORMAL
999	CALL LSL_EXIT(STATUS)
	END
