	LOGICAL*4 FUNCTION TAPE_TO_TAPE
C
***	IDENT	14SE90
***	MODULE	TAPE_TO_TAPE

C	Copyright : Laser-Scan Laboratories Ltd., Cambridge, England

C	Allow more than 32767 blocks			WJJ	13-Mar-1992
C	Allow blocks up to 64000 bytes			WJJ	14-Sep-1990
C	Created   : Andrew C Morris			14-Apr-86

C Description
C	Copies data from magtape to magtape
C
	IMPLICIT NONE
C
	INCLUDE 'LSL$CMNLSL:LSLLIBMSG.PAR'
	INCLUDE 'LSL$CMNMTCOPY:MTCOPYMSG.PAR'
C
	INCLUDE 'LSL$CMNLSL:MTIVCM.CMN'
	INCLUDE 'LSL$CMNLSL:MTOVCM.CMN'
	INCLUDE 'LSL$CMNMTCOPY:MTCOPY.CMN'
C
C Workspace
	INTEGER*4	STATUS,IRECNO

	INTEGER*2	NBYTES
C
C Functions
	INTEGER*4	MTINIT,MTONIT,MTIRDB,MTOWRB,MTOEOV,MTOEOF
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
	TAPE_TO_TAPE = .FALSE.
C
C Set up input magtape
C
	STATUS = MTINIT(INPUT_DEV_SPEC(:INPUT_DEV_LEN),.FALSE.)
	IF (.NOT.STATUS) GOTO 80
C
C
C Set up output magtape
C
	IF (APPEND) THEN
	   STATUS = MTONIT(OUTPUT_DEV_SPEC(:OUTPUT_DEV_LEN),,
     &	      .NOT.REWIND_OUTPUT)
	   IF (.NOT.STATUS) GOTO 60
	   IF (REWIND_OUTPUT) THEN
	     STATUS = MTOEOV()
	     IF (.NOT.STATUS) GOTO 70
	   ENDIF
	ELSE
	   STATUS = MTONIT(OUTPUT_DEV_SPEC(:OUTPUT_DEV_LEN),
     &	      DENSITY,.FALSE.)
	   IF (.NOT.STATUS) GOTO 60
	ENDIF
C
	NFILES = 0
5	IRECNO = 0
C
10	STATUS = MTIRDB(BUFF(1),BUFLEN,NBYTES)
	IF (.NOT.STATUS) GOTO 40
	IF (NBYTES.EQ.0) GOTO 40
	IF (IRECNO.EQ.0) NFILES = NFILES+1	! update file count
C
C
30	STATUS = MTOWRB(BUFF(1),NBYTES)
	IF (.NOT.STATUS) GOTO 90
C
	IRECNO = IRECNO+1
	GOTO 10
C
C Some input error (may be file_mark...)
C
40	IF (STATUS.NE.LSL__EOF) THEN
	   CALL LSL_PUTMSG(MTIERR)
	   GOTO 50
	ENDIF
	IF (IRECNO.EQ.0) GOTO 50		! end of tape
	STATUS = MTOEOF()			! tape-mark
	IF (.NOT.STATUS) GOTO 90
	GOTO 5
C
C write end-of-tape
50	IF (IRECNO.NE.0) STATUS = MTOEOF()	! end of tape
	STATUS = MTOEOF()
	IF (.NOT.STATUS) GOTO 90
C
C Back over last tape-mark
	CALL MTOBCK
C
	GOTO 900
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C Error opening output magtape
C
60	CALL LSL_PUTMSG(MTCOPY__CANTOPNOUTPUT,
     &	   OUTPUT_DEV_SPEC(:OUTPUT_DEV_LEN))
	CALL LSL_ADDMSG(STATUS)
	CALL LSL_ADDMSG(MTOERR)
	GOTO 999
C
C Error in finding EOV
C
70	CALL LSL_PUTMSG(MTCOPY__EOVERR)
	CALL LSL_ADDMSG(STATUS)
	CALL LSL_ADDMSG(MTOERR)
	GOTO 999
C
C Error opening input magtape
C
80	CALL LSL_PUTMSG(MTCOPY__CANTOPNINPUT,
     &	   INPUT_DEV_SPEC(:INPUT_DEV_LEN))
	GOTO 999
C
C Error in write
C
90	CALL LSL_PUTMSG(MTCOPY__WRITEERR)
	CALL LSL_ADDMSG(STATUS)
	CALL LSL_ADDMSG(MTOERR)
	GOTO 999
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
900	TAPE_TO_TAPE = .TRUE.
999	RETURN
	END

