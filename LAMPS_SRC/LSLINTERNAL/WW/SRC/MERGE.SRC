C--------------------------------------------------------
C
C	MERGE
C
C	Remove duplicates by merging nets.
C--------------------------------------------------------
	SUBROUTINE MERGE(NMAX)
C
***	MODULE MERGE
***	IDENT 30OC84
C
	IMPLICIT NONE
C
C Search ISHED for duplicated occurrences of same IC pin.
C
C D R Griffiths 23-Dec-80
	INCLUDE 'WIREWRAP.HDR'
C
***	COMMON/LSL$CMNVIO:EXPC/
C
C argument
	INTEGER		NMAX		! extent of data in ISHED
C
C workspace
	INTEGER		I, J		! loop variables
	INTEGER		IS		! an element of ISHED
	INTEGER		JHI, JLO	! limits of target of merge
	INTEGER		K, L, M		! other convenient integers
	INTEGER		THIS,THAT
	INTEGER		STPIN		! start pin of 'current node'
	BYTE		PINNAM(10)	! 1 more than is needed by FORM
C
C function called
	INTEGER		FORM		! form pin name
C
	DO 60 I=1,NMAX-1
	  THIS = ISHED(I)
	  IF (THIS.LT.0) STPIN = I+1
	  IF (THIS.LE.0 .OR. THIS.EQ.GND .OR. THIS.EQ.VCC) GOTO 60
C
C Here's a pin. Anywhere else ?
	  DO 60 J=I+1,NMAX-1
	    THAT = ISHED(J)
	    IF (THAT.NE.THIS) GOTO 60
C
C It's twice. Find entire net containing latter pin.
	    DO 10 JHI = J,NMAX-1
	      IF (ISHED(JHI).EQ.-1) GOTO 20
10	    CONTINUE
	    GOTO 60
20	    DO 30 JLO = J,I,-1
	      IF (ISHED(JLO).EQ.-1) GOTO 40
30	    CONTINUE
	    GOTO 60
C
40	    JLO = JLO+1
	    JHI = JHI-1
C
C Now  I -> THIS
C     JLO -> first pin  )
C      J  -> THAT       )
C     JHI -> last  pin  )
C
C for peace of mind of program maintainer, rather than that of the
C  engineers who use the thing, we now inform the engineer that we
C  are about to merge these nodes
	    CALL EXPAND('Merging node ')
	    K = STPIN
	    DO WHILE (ISHED(K).NE.-1)
	      L = FORM(PINNAM, K)
	      PINNAM(L+1) = 0
	      CALL APPEND('%AZ9', PINNAM)
	      IF (EXPLEN.GE.50) THEN
		CALL TTWSTR
		CALL EXPAND('< ')
	      ENDIF
	      K = K+1
	    ENDDO
	    IF (EXPLEN.GT.2) THEN
	      CALL APPEND('  with node ')
	    ELSE
	      CALL EXPAND('with node ')
	    ENDIF
	    DO K = JLO,JHI
	      L = FORM(PINNAM, K)
	      PINNAM(L+1) = 0
	      CALL APPEND('%AZ9', PINNAM)
	      IF (EXPLEN.GE.50) THEN
		CALL TTWSTR
		CALL EXPAND('< ')
	      ENDIF
	    ENDDO
	    IF (EXPLEN.GT.2) CALL TTWSTR
C
C Move entire JLO to JHI inclusive down over I+1.
	    DO K=JLO,JHI
	      IS = ISHED(K)
	      L = I+1+(K-JLO)
	      DO M=K,L,-1
		ISHED(M) = ISHED(M-1)
	      ENDDO
	      ISHED(L) = IS
	    ENDDO
60	CONTINUE
	RETURN
C
	END
