C------------------------------------------------------
C
C	OUTPUT
C
C Output schedule listing to stream 'ISTRM'
C------------------------------------------------------
	SUBROUTINE OUTPUT(ISTRM,NMAX)
C
***	MODULE OUTPUT
***	IDENT 02MY84
C
	IMPLICIT NONE
C
	INCLUDE 'WIREWRAP.HDR'
C
C arguments:
	INTEGER		ISTRM		! FORTRAN unit for output
	INTEGER		NMAX		! limit of data in ISHED
C
C w/s:
	INTEGER		I, II		! loop variables
	INTEGER		J		! index on output buffer
	INTEGER		LENGTH		! FORM's function result
	INTEGER		N		! pin-in-hand index
C
	BYTE		LINB(9)		! pin-output buffer
	BYTE		LINE(80)	! line output buffer
C
	LOGICAL		DATA		! TRUE iff 'real' data in LINE
C
C function called:
	INTEGER		FORM		! form pin name for output
C
C output comments before anything else
	IF (COMLIM.GT.0) THEN
	   I = 0
	   DO WHILE (I.LT.COMLIM)
	      J = COMBUF(I+1)
	      WRITE (ISTRM,5) (COMBUF(I+II+1),II=1,J)
	      I = I+J+1
	   ENDDO
	   WRITE (ISTRM,6)
	ENDIF
C
C formats for the above
5	FORMAT (' ','| ',<J>A1)
6	FORMAT (' ')
C
C initialise
	N=0
	J=1
	DATA = .FALSE.
C
C signal Eurocard or Multibus if it is, otherwise Dec rack
	IF (EUROCD) THEN
	   WRITE (ISTRM,7)
	ELSE IF (MLTBUS) THEN
	   WRITE (ISTRM,8)
	ELSE
	   WRITE (ISTRM,9)
	ENDIF
7	FORMAT(' ','Eurocard',/)
8	FORMAT(' ','Multibus',/)
9	FORMAT(' ','DEC rack',/)
C
C look for another pin
10	IF(J.GT.42)GOTO 70
	N=N+1
	IF(N.GT.NMAX)GOTO 90
	IF(ISHED(N).EQ.-1)GOTO 40
	LENGTH = FORM(LINB,N)
	DATA = .TRUE.
C
C copy one pin's output into LINE
20	DO 30 I=1,LENGTH
	   LINE(J)=LINB(I)
	   J=J+1
30	CONTINUE
	GOTO 10
C
C end of node - complete line out
40	J=J-2					! trim trailing space
	IF (.NOT.(J.GT.1 .AND. DATA)) GOTO 60
	WRITE(ISTRM,50)(LINE(I),I=1,J)
50	FORMAT(' ',<J>A1)
C
C mark line empty again for new node
60	J=1
	DATA = .FALSE.
	GOTO 10
C
C over-full line - force it out and prepare for a continuation
70	J=J-1
	WRITE(ISTRM,50)(LINE(I),I=1,J)
	ENCODE(LENGTH,80,LINB)
80	FORMAT('   <  ')
	J=1
	DATA = .FALSE.
	GOTO 20
C
C done the schedule: now for the IC listing
90	WRITE(ISTRM,100)
100	FORMAT(' ',/,'IC Listing',/)
C
C output all IC positions in order
	DO 120 I=1,MAXICS
	   IF(ICLIST(I,1).EQ.0)GOTO 120		! no IC at this posn
	   DO LENGTH = 1,9
	      IF (ICLIST(I,LENGTH+1).EQ.0) GOTO 105 ! find blank
	   ENDDO
	   LENGTH = 10				! no blanks to find
105	   WRITE(ISTRM,110)I,(ICLIST(I,II),II=1,LENGTH)
110	   FORMAT(' ',I3,' = ',<LENGTH>A1)
120	CONTINUE
	RETURN
	END
