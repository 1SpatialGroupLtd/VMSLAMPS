C
C * This file is part of the LAMPS distribution, released as a software
C * preservation project to archive digital history for future historians.
C * Copyright (c) 1980-2002 Laser-Scan Ltd, 1Spatial Group Ltd
C *
C * Timestamp of this file for the 2002 release was: 1986-02-10 13:06:42.000000000 +0000
C *
C * This program is free software: you can redistribute it and/or modify
C * it under the terms of the GNU General Public License as published by
C * the Free Software Foundation, version 3.
C *
C * This program is distributed in the hope that it will be useful, but
C * WITHOUT ANY WARRANTY; without even the implied warranty of
C * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
C * General Public License for more details.
C *
C * You should have received a copy of the GNU General Public License
C * along with this program. If not, see http://www.gnu.org/licenses/.
C
	SUBROUTINE OCCURS(NMAX)
C
***	MODULE OCCURS
***	IDENT 10FE86
C
	IMPLICIT NONE
C
C Copyright Laser Scan Laboratories Ltd., Cambridge, England
C Author R. Fairbairns
C
C Coded February 1986
C
C To say where a pin occurs in the schedule (an interactive command)
C
	INCLUDE	'WIREWRAP.HDR'
C
***	COMMON/LSL$CMNVIO:CMDCOM/
***	COMMON/LSL$CMNVIO:EXPC/
C
C argument:
	INTEGER		NMAX		! extent of data in ISHED
C
C workspace:
	BYTE		CH		! character read from line
	LOGICAL		FIRST		! first time this pin occurs?
	INTEGER		I		! loop variable
	INTEGER		L		! length of expanded name
	INTEGER		NODE_POINTER	! -> start of current node in search
	INTEGER		PIN		! pin number
	INTEGER		PIN_DESC	! pin descriptor
	BYTE		PIN_NAME(10)	! buffer for expanding pin name
C
C functions called:
	INTEGER		FORM		! lay out a pin name
	LOGICAL		RDCHS		! read a character
	LOGICAL		RDINT		! read an integer from the command line
C
C
C first of all, get the pin descriptor
	IF (RDCHS(CH)) GOTO 999			! get '.' - moan if not there
	IF (CH.NE.'.') GOTO 999			! or if it ain't '.'
	IF (RDINT(PIN)) GOTO 999		! get the pin number, or moan
	PIN_DESC = INTARG(1)*256 + PIN		! there we are (neat, isn't it?)
C
C now search the table for this pin
	NODE_POINTER = 1
	FIRST = .TRUE.
	DO I = 1,NMAX
C
	  IF (ISHED(I).EQ.-1) THEN
	    NODE_POINTER = I+1			! found a start of node
	  ELSE IF (ISHED(I).EQ.PIN_DESC) THEN
C
C here's the node that has this pin
	    IF (FIRST) THEN
	      CALL EXPAND('%^L%N.%N occurs in: ', INTARG(1), PIN)
	      FIRST = .FALSE.
	    ELSE
	      CALL EXPAND('... and in: ')
	    ENDIF
	    DO WHILE (ISHED(NODE_POINTER).NE.-1)
	      L = FORM(PIN_NAME, NODE_POINTER)
	      PIN_NAME(L) = 0
	      CALL APPEND('%AZ9', PIN_NAME)
	      IF (EXPLEN.GE.50) THEN
		CALL TTWSTR
		CALL EXPAND('< ')
	      ENDIF
	      NODE_POINTER = NODE_POINTER+1
	    ENDDO
	    IF (EXPLEN.GT.2) CALL TTWSTR
	  ENDIF
	ENDDO
C
	IF (FIRST)				! it never appeared
     &		CALL WRITEF('%^L%N.%N doesn''t appear', INTARG(1), PIN)
	RETURN
C
C bad format for pin name
999	CALL TTWSTR('*** Bad format to pin name')
	RETURN
C
	END
