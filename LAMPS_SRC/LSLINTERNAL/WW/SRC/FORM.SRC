C--------------------------------------
C
C	FORM
C
C	FORM A PIN NUMBER FOR OUTPUT
C---------------------------------------
	INTEGER FUNCTION FORM(LINBN,I)
C
***	MODULE FORM
***	IDENT 02MY84
C
	IMPLICIT NONE
C
	INCLUDE 'WIREWRAP.HDR'
C
C arguments:
	BYTE		LINBN(9)	! buffer for output chars
	INTEGER		I		! index of pin in ISHED
C
C function result is length of what's written into LINBN
C
C w/s:
	INTEGER		I1, I2, I3	! for decomposition
	INTEGER		PIN		! extracted from ISHED
	LOGICAL		LONG		! IF eurocd OR mltbus OR pos3dg
C
C evaluate length of operation
	LONG = EUROCD.OR.MLTBUS.OR.POS3DG
C
C make early decision on what our answer will be
	IF (LONG) THEN
	   FORM = 9
	ELSE
	   FORM = 8
	ENDIF
C
C get pin descriptor, make early decisions
	PIN = ISHED(I)
	IF(PIN.EQ.GND)GOTO 20
	IF(PIN.EQ.VCC)GOTO 40
	IF(PIN.LE.255)GOTO 60
	I1=PIN/256
	I2=MOD(PIN,256)
	IF (LONG) GOTO 11
C
C ordinary pin (short)
	ENCODE(8,10,LINBN)I1,I2
10	FORMAT(I2,'.',I2,' - ')
	RETURN
C
C extraordinary pin (long)
11	ENCODE(9,12,LINBN)I1,I2
12	FORMAT(I3,'.',I2,' - ')
	RETURN
C
C GND pin
20	IF (LONG) GOTO 30
	ENCODE(8,25,LINBN)
25	FORMAT(' GND  - ')
	RETURN
C
C ditto (long)
30	ENCODE(9,35,LINBN)
35	FORMAT('  GND  - ')
	RETURN
C
C VCC pin
40	IF (LONG) GOTO 50
	ENCODE(8,45,LINBN)
45	FORMAT(' VCC  - ')
	RETURN
C
C ditto (long)
50	ENCODE(9,55,LINBN)
55	FORMAT('  VCC  - ')
	RETURN
C
C edge pin
60	I1=1+PIN/128
	IF (EUROCD) GOTO 100
	IF (MLTBUS) GOTO 120
C
C Dec-rack edge-pin
	I2=64+MOD(PIN,64)/2
	I3=1+MOD(PIN,2)
	IF (LONG) GOTO 80
	ENCODE(8,70,LINBN)I1,I2,I3
70	FORMAT(' P',I1,A1,I1,' - ')
	RETURN
C
C ditto (long)
80	ENCODE(9,90,LINBN)I1,I2,I3
90	FORMAT('  P',I1,A1,I1,' - ')
	RETURN
C
C Eurocard edge pin (always long)
100	I2 = 65+MOD(PIN,128)/32			! A, B, or C
	I3 = 1+MOD(PIN,32)			! 1-32
	ENCODE (9,110,LINBN) I1, I2, I3
110	FORMAT (' P',I1,A1,I2,' - ')
	RETURN
C
C Multibus edge pin (likewise always long)
120	I2 = 1+MOD(PIN,128)			! 1-86 or 1-60
	ENCODE (9,130,LINBN) I1, I2
130	FORMAT (' P',I1,'.',I2,' - ')
	RETURN
C
	END
